!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019, 2020, 2021, 2022, 2023, 2024
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!!{
Contains a module which implements an ISM metallicity output analysis property extractor class.
!!}

  !![
  <nodePropertyExtractor name="nodePropertyExtractorMetallicityStellar">
   <description>A stellar metallicity output analysis property extractor class.</description>
  </nodePropertyExtractor>
  !!]
  type, extends(nodePropertyExtractorScalar) :: nodePropertyExtractorMetallicityStellar
     !!{
     A stellar metallicity output analysis property extractor class.
     !!}
     private
     integer          :: indexElement
     character(len=3) :: element
   contains
     procedure :: extract     => metallicityStellarExtract
     procedure :: name        => metallicityStellarName
     procedure :: description => metallicityStellarDescription
     procedure :: unitsInSI   => metallicityStellarUnitsInSI
  end type nodePropertyExtractorMetallicityStellar

  interface nodePropertyExtractorMetallicityStellar
     !!{
     Constructors for the ``metallicityStellar'' output analysis class.
     !!}
     module procedure metallicityStellarConstructorParameters
     module procedure metallicityStellarConstructorInternal
  end interface nodePropertyExtractorMetallicityStellar

contains

  function metallicityStellarConstructorParameters(parameters) result(self)
    !!{
    Constructor for the ``metallicityStellar'' output analysis property extractor class which takes a parameter set as input.
    !!}
    use :: Abundances_Structure, only : Abundances_Index_From_Name
    use :: Input_Parameters    , only : inputParameter            , inputParameters
    use :: Error               , only : Error_Report
    implicit none
    type     (nodePropertyExtractorMetallicityStellar)                :: self
    type     (inputParameters                        ), intent(inout) :: parameters
    integer                                                           :: indexElement
    character(len=3                                  )                :: element

    !![
    <inputParameter>
      <name>element</name>
      <source>parameters</source>
      <description>The atomic symbol for the element to use to define metallicity.</description>
    </inputParameter>
    !!]
    indexElement=Abundances_Index_From_Name(element)
    if (indexElement < 0) call Error_Report('element "'//trim(element)//'" is not being tracked'//{introspection:location})
    self=nodePropertyExtractorMetallicityStellar(indexElement)
    !![
    <inputParametersValidate source="parameters"/>
    !!]
    return
  end function metallicityStellarConstructorParameters

  function metallicityStellarConstructorInternal(indexElement) result(self)
    !!{
    Internal constructor for the ``metallicityStellar'' output analysis property extractor class.
    !!}
    use :: Abundances_Structure, only : Abundances_Names
    implicit none
    type(nodePropertyExtractorMetallicityStellar)                :: self
    integer                                      , intent(in   ) :: indexElement
    !![
    <constructorAssign variables="indexElement"/>
    !!]

    self%element=Abundances_Names(indexElement)
    return
  end function metallicityStellarConstructorInternal

  double precision function metallicityStellarExtract(self,node,instance)
    !!{
    Extracts the metallicity (defined as the mass ratio of a specified element to hydrogen) in the stellar component.
    !!}
    use :: Abundances_Structure, only : abundances
    use :: Galacticus_Nodes    , only : nodeComponentDisk, nodeComponentSpheroid, treeNode
    implicit none
    class           (nodePropertyExtractorMetallicityStellar), intent(inout), target       :: self
    type            (treeNode                               ), intent(inout), target       :: node
    type            (multiCounter                           ), intent(inout), optional     :: instance
    class           (nodeComponentDisk                      ), pointer                     :: disk
    class           (nodeComponentSpheroid                  ), pointer                     :: spheroid
    double precision                                         , allocatable  , dimension(:) :: massElementsDisk, massElementsSpheroid
    type            (abundances                             )                              :: abundancesDisk  , abundancesSpheroid
    integer                                                                                :: countElements
    !$GLC attributes unused :: self, instance

    disk               => node    %disk             ()
    spheroid           => node    %spheroid         ()
    abundancesDisk     =  disk    %abundancesStellar()
    abundancesSpheroid =  spheroid%abundancesstellar()
    if (disk%massStellar() > 0.0d0 .or. spheroid%massStellar() > 0.0d0) then
       countElements=abundancesDisk%serializeCount()
       allocate(massElementsDisk    (countElements))
       allocate(massElementsSpheroid(countElements))
       call abundancesDisk    %serialize(massElementsDisk    )
       call abundancesSpheroid%serialize(massElementsSpheroid)
       metallicityStellarExtract=+(                                                                  &
            &                      +massElementsDisk    (self%indexElement)                          &
            &                      +massElementsSpheroid(self%indexElement)                          &
            &                     )                                                                  &
            &                    /(                                                                  &
            &                      +disk    %massStellar()*abundancesDisk    %hydrogenMassFraction() &
            &                      +spheroid%massStellar()*abundancesSpheroid%hydrogenMassFraction() &
            &                     )
    else
       metallicityStellarExtract=0.0d0
    end if
    return
  end function metallicityStellarExtract

  function metallicityStellarName(self)
    !!{
    Return the name of the metallicityStellar property.
    !!}
    use :: Abundances_Structure, only : Abundances_Names
    implicit none
    type (varying_string                         )                :: metallicityStellarName
    class(nodePropertyExtractorMetallicityStellar), intent(inout) :: self
    !$GLC attributes unused :: self

    metallicityStellarName=var_str('metallicityStellar')//Abundances_Names(self%indexElement)
    return
  end function metallicityStellarName

  function metallicityStellarDescription(self)
    !!{
    Return a description of the metallicityStellar property.
    !!}
    use :: Abundances_Structure, only : Abundances_Names
    implicit none
    type (varying_string                         )                :: metallicityStellarDescription
    class(nodePropertyExtractorMetallicityStellar), intent(inout) :: self
    !$GLC attributes unused :: self

    metallicityStellarDescription=var_str('The metallicity ')//Abundances_Names(self%indexElement)//'/H (by mass) of the stellar component.'
    return
  end function metallicityStellarDescription

  double precision function metallicityStellarUnitsInSI(self)
    !!{
    Return the units of the metallicityStellar property in the SI system.
    !!}
    implicit none
    class(nodePropertyExtractorMetallicityStellar), intent(inout) :: self
    !$GLC attributes unused :: self

    metallicityStellarUnitsInSI=0.0d0
    return
  end function metallicityStellarUnitsInSI
