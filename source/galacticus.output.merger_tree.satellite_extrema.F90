!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which handles outputting of satellite orbital extremum data to the \glc\ output file.

module Galacticus_Output_Trees_Satellite_Extremum
  !% Handles outputting of satellite orbital extremum data to the \glc\ output file.
  implicit none
  private
  public :: Galacticus_Output_Tree_Satellite_Extremum, Galacticus_Output_Tree_Satellite_Extremum_Property_Count,&
       & Galacticus_Output_Tree_Satellite_Extremum_Names

  ! Number of orbital properties.
  integer :: satelliteExtremumPropertyCount

  ! Flag indicating whether or not satellite orbital extremum information is to be output.
  logical :: outputSatelliteApocenterData                  , outputSatellitePericenterData

  ! Flag indicating whether or not this module has been initialized.
  logical :: outputSatelliteExtremumDataInitialized=.false.

contains

  subroutine Galacticus_Output_Tree_Satellite_Extremum_Initialize
    !% Initializes the module by determining whether or not satellite extremum data should be output.
    use Input_Parameters
    implicit none

    if (.not.outputSatelliteExtremumDataInitialized) then
       !$omp critical(Galacticus_Output_Tree_Satellite_Extremum_Initialize)
       if (.not.outputSatelliteExtremumDataInitialized) then
          !# <inputParameter>
          !#   <name>outputSatellitePericenterData</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not satellite orbital pericenter data (radius, velocity) should be included in the output.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          !# <inputParameter>
          !#   <name>outputSatelliteApocenterData</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not satellite orbital apocenter data (radius, velocity) should be included in the output.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          ! Count number of properties to output.
          satelliteExtremumPropertyCount=0
          if (outputSatellitePericenterData) satelliteExtremumPropertyCount=satelliteExtremumPropertyCount+2
          if (outputSatelliteApocenterData ) satelliteExtremumPropertyCount=satelliteExtremumPropertyCount+2
          ! Flag that module is now initialized.
          outputSatelliteExtremumDataInitialized=.true.
       end if
       !$omp end critical(Galacticus_Output_Tree_Satellite_Extremum_Initialize)
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Extremum_Initialize

  !# <mergerTreeOutputNames>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Extremum_Names</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Extremum</sortName>
  !# </mergerTreeOutputNames>
  subroutine Galacticus_Output_Tree_Satellite_Extremum_Names(node,integerProperty,integerPropertyNames,integerPropertyComments,integerPropertyUnitsSI,doubleProperty&
       &,doublePropertyNames,doublePropertyComments,doublePropertyUnitsSI,time)
    !% Set the names of satellite orbital extremum properties to be written to the \glc\ output file.
    use Galacticus_Nodes
    use Numerical_Constants_Astronomical
    implicit none
    type            (treeNode)              , intent(inout) :: node
    double precision                        , intent(in   ) :: time
    integer                                 , intent(inout) :: doubleProperty         , integerProperty
    character       (len=*   ), dimension(:), intent(inout) :: doublePropertyComments , doublePropertyNames   , &
         &                                                     integerPropertyComments, integerPropertyNames
    double precision          , dimension(:), intent(inout) :: doublePropertyUnitsSI  , integerPropertyUnitsSI
    !GCC$ attributes unused :: node, time, integerProperty, integerPropertyComments, integerPropertyNames, integerPropertyUnitsSI
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Extremum_Initialize

    ! Return property names if we are outputting satellite orbital pericenter data.
    if (outputSatellitePericenterData) then
       doubleProperty=doubleProperty+1
       doublePropertyNames   (doubleProperty)='satellitePericenterRadius'
       doublePropertyComments(doubleProperty)='Pericenteric radius of satellite orbit [Mpc].'
       doublePropertyUnitsSI (doubleProperty)=megaParsec
       doubleProperty=doubleProperty+1
       doublePropertyNames   (doubleProperty)='satellitePericenterVelocity'
       doublePropertyComments(doubleProperty)='Pericenteric velocity of satellite orbit [km/s].'
       doublePropertyUnitsSI (doubleProperty)=kilo
    end if
    ! Return property names if we are outputting satellite orbital apocenter data.
    if (outputSatelliteApocenterData) then
       doubleProperty=doubleProperty+1
       doublePropertyNames   (doubleProperty)='satelliteApocenterRadius'
       doublePropertyComments(doubleProperty)='Apocenteric radius of satellite orbit [Mpc].'
       doublePropertyUnitsSI (doubleProperty)=megaParsec
       doubleProperty=doubleProperty+1
       doublePropertyNames   (doubleProperty)='satelliteApocenterVelocity'
       doublePropertyComments(doubleProperty)='Apocenteric velocity of satellite orbit [km/s].'
       doublePropertyUnitsSI (doubleProperty)=kilo
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Extremum_Names

  !# <mergerTreeOutputPropertyCount>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Extremum_Property_Count</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Extremum</sortName>
  !# </mergerTreeOutputPropertyCount>
  subroutine Galacticus_Output_Tree_Satellite_Extremum_Property_Count(node,integerPropertyCount,doublePropertyCount,time)
    !% Account for the number of satellite orbital extremum properties to be written to the \glc\ output file.
    use Galacticus_Nodes
    implicit none
    type            (treeNode), intent(inout) :: node
    double precision          , intent(in   ) :: time
    integer                   , intent(inout) :: doublePropertyCount, integerPropertyCount
    !GCC$ attributes unused :: node, integerPropertyCount, time
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Extremum_Initialize

    ! Increment property count if we are outputting satellite orbital extremum data.
    doublePropertyCount=doublePropertyCount+satelliteExtremumPropertyCount
    return
  end subroutine Galacticus_Output_Tree_Satellite_Extremum_Property_Count

  !# <mergerTreeOutputTask>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Extremum</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Extremum</sortName>
  !# </mergerTreeOutputTask>
  subroutine Galacticus_Output_Tree_Satellite_Extremum(node,integerProperty,integerBufferCount,integerBuffer,doubleProperty&
       &,doubleBufferCount,doubleBuffer,time,instance)
    !% Store satellite orbital extremum properties in the \glc\ output file buffers.
    use Galacticus_Nodes
    use Kind_Numbers
    use Kepler_Orbits
    use Satellite_Orbits
    use Multi_Counters
    implicit none
    double precision                        , intent(in   ) :: time
    type            (treeNode              ), intent(inout) :: node
    integer                                 , intent(inout) :: doubleBufferCount     , doubleProperty , integerBufferCount, &
         &                                                     integerProperty
    integer         (kind=kind_int8        ), intent(inout) :: integerBuffer    (:,:)
    double precision                        , intent(inout) :: doubleBuffer     (:,:)
    type            (multiCounter          ), intent(inout) :: instance
    type            (treeNode              ), pointer       :: nodeHost
    class           (nodeComponentSatellite), pointer       :: satellite
    type            (keplerOrbit           )                :: orbit
    double precision                                        :: orbitalRadius         , orbitalVelocity
    !GCC$ attributes unused :: time, integerBufferCount, integerProperty, integerBuffer, instance
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Extremum_Initialize

    ! Store property data if we are outputting satellite orbital pericenter data.
    if (outputSatellitePericenterData) then
       ! Test for satellite.
       if (node%isSatellite()) then
          ! Find the host node.
          nodeHost  => node     %parent
          ! Get the satellite component.
          satellite => node     %satellite  ()
          ! Get the orbit for this node.
          orbit     =  satellite%virialOrbit()
          ! Get the orbital radius and velocity at pericenter.
          call Satellite_Orbit_Extremum_Phase_Space_Coordinates(nodeHost,orbit,extremumPericenter,orbitalRadius,orbitalVelocity)
       else
          orbitalRadius  =0.0d0
          orbitalVelocity=0.0d0
       end if
       ! Store the orbital properties.
       doubleProperty=doubleProperty+1
       doubleBuffer(doubleBufferCount,doubleProperty)=orbitalRadius
       doubleProperty=doubleProperty+1
       doubleBuffer(doubleBufferCount,doubleProperty)=orbitalVelocity
    end if
    ! Store property data if we are outputting satellite orbital apocenter data.
    if (outputSatelliteApocenterData) then
       ! Test for satellite.
       if (node%isSatellite()) then
          ! Find the host node.
          nodeHost  => node     %parent
          ! Get the satellite component.
          satellite => node     %satellite   ()
          ! Get the orbit for this node.
          orbit     =  satellite%virialOrbit()
          ! Get the orbital radius and velocity at apocenter.
          call Satellite_Orbit_Extremum_Phase_Space_Coordinates(nodeHost,orbit,extremumApocenter,orbitalRadius,orbitalVelocity)
       else
          orbitalRadius  =0.0d0
          orbitalVelocity=0.0d0
       end if
       ! Store the orbital properties.
       doubleProperty=doubleProperty+1
       doubleBuffer(doubleBufferCount,doubleProperty)=orbitalRadius
       doubleProperty=doubleProperty+1
       doubleBuffer(doubleBufferCount,doubleProperty)=orbitalVelocity
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Extremum

end module Galacticus_Output_Trees_Satellite_Extremum
