!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015 Andrew Benson <abenson@obs.carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a program which computes various quantities related to the excursion set formalism and stores them in an output file.

program Tests_Excursion_Sets
  !% Computes various quantities related to the excursion set formalism and stores them in an output file.
  use Input_Parameters
  use ISO_Varying_String
  use Memory_Management
  use Cosmology_Functions
  use Cosmology_Parameters
  use Excursion_Sets_Barriers
  use Excursion_Sets_First_Crossings
  use Halo_Mass_Functions
  use Galacticus_Display
  use Galacticus_Error
  use Numerical_Ranges
  use Power_Spectra
  use Cosmological_Mass_Variance
  use Numerical_Constants_Math
  use IO_HDF5
  implicit none
  integer                                                                              , parameter :: massCount                =200
  double precision                                                                     , parameter :: massMaximum              =1.0d16, massMinimum             =1.0d6
  integer                                                                              , parameter :: fileNameLengthMaximum    =1024
  double precision                                        , allocatable, dimension(:  )            :: barrier                         , firstCrossingProbability      , &
       &                                                                                              haloMass                        , haloMassFunctionDifferential  , &
       &                                                                                              powerSpectrum                   , variance                      , &
       &                                                                                              wavenumber
  double precision                                        , allocatable, dimension(:,:)            :: firstCrossingRate
  integer                                                                                          :: iMass                       , jMass                          , &
       &                                                                                              verbosityLevel
  class           (cosmologyParametersClass     ), pointer                                         :: cosmologyParameters_
  class           (cosmologyFunctionsClass      ), pointer                                         :: cosmologyFunctions_
  class           (cosmologicalMassVarianceClass), pointer                                         :: cosmologicalMassVariance_
  class           (haloMassFunctionClass        ), pointer                                         :: haloMassFunction_
  double precision                                                                                 :: time                            , varianceProgenitor
  character       (len=fileNameLengthMaximum    )                                                  :: fileCharacter
  type            (varying_string               )                                                  :: outputFileName                  , parameterFile
  type            (hdf5Object                   )                                                  :: outputFile

  ! Read in basic code memory usage.
  call Code_Memory_Usage('Excursion_Sets.size')

  ! Get the name of the parameter file from the first command line argument.
  call Get_Command_Argument(1,fileCharacter)
  if (len_trim(fileCharacter) == 0) call Galacticus_Error_Report(message="Usage: Excursion_Sets.exe <parameterFile> <outputFile>")
  parameterFile=fileCharacter
  call Get_Command_Argument(2,fileCharacter)
  if (len_trim(fileCharacter) == 0) call Galacticus_Error_Report(message="Usage: Excursion_Sets.exe <parameterFile> <outputFile>")
  outputFileName=fileCharacter

  ! Open the output file.
  call outputFile%openFile(char(outputFileName),overWrite=.true.,objectsOverwritable=.false.)

  ! Read the parameter file.
  call Input_Parameters_File_Open(parameterFile,outputFile,allowedParametersFile='Excursion_Sets.parameters.xml')

  ! Set verbosity level.
  call Get_Input_Parameter('verbosityLevel',verbosityLevel,1)
  call Galacticus_Verbosity_Level_Set(verbosityLevel)

  ! Get required objects.
  cosmologyFunctions_       => cosmologyFunctions      ()
  cosmologicalMassVariance_ => cosmologicalMassVariance()
  haloMassFunction_         => haloMassFunction        ()
  
  ! Get the current time.
  time=cosmologyFunctions_%cosmicTime(1.0d0)

  ! Allocate arrays.
  call Alloc_Array(haloMass                    ,[massCount          ])
  call Alloc_Array(variance                    ,[massCount          ])
  call Alloc_Array(barrier                     ,[massCount          ])
  call Alloc_Array(firstCrossingProbability    ,[massCount          ])
  call Alloc_Array(haloMassFunctionDifferential,[massCount          ])
  call Alloc_Array(wavenumber                  ,[massCount          ])
  call Alloc_Array(powerSpectrum               ,[massCount          ])
  call Alloc_Array(firstCrossingRate           ,[massCount,massCount])

  ! Create a grid of masses.
  haloMass=Make_Range(massMinimum,massMaximum,massCount,rangeType=rangeTypeLogarithmic)

  ! Set first crossing rates to unphysical values.
  firstCrossingRate=-1.0d0

  ! Get the default cosmology.
  cosmologyParameters_ => cosmologyParameters()

  ! Loop over masses.
  do iMass=1,massCount
     wavenumber                  (iMass)=(3.0d0*haloMass(iMass)/4.0d0/Pi/cosmologyParameters_%densityCritical()/cosmologyParameters_%OmegaMatter())**(-1.0d0/3.0d0)
     powerSpectrum               (iMass)=Power_Spectrum                           (wavenumber(iMass)                     )
     variance                    (iMass)=cosmologicalMassVariance_%rootVariance   (                       haloMass(iMass))**2
     barrier                     (iMass)=Excursion_Sets_Barrier                   (variance  (iMass),time                )
     firstCrossingProbability    (iMass)=Excursion_Sets_First_Crossing_Probability(variance  (iMass),time                )
     haloMassFunctionDifferential(iMass)=haloMassFunction_%differential           (                  time,haloMass(iMass))

     ! Compute halo branching rates.
     do jMass=1,iMass-1
        varianceProgenitor=cosmologicalMassVariance_%rootVariance(haloMass(jMass))**2
        firstCrossingRate(iMass,jMass)=Excursion_Sets_First_Crossing_Rate(variance(iMass),varianceProgenitor,time)
     end do

  end do

  ! Write results to the output file.
  call outputFile%writeDataset(haloMass                    ,'haloMass'                ,'The mass of the halo [M☉]'                       )
  call outputFile%writeDataset(wavenumber                  ,'wavenumber'              ,'The wavenumber associated with this mass [Mpc⁻¹]')
  call outputFile%writeDataset(powerSpectrum               ,'powerSpectrum'           ,'The power spectrum at this wavenumber [Mpc³]'    )
  call outputFile%writeDataset(variance                    ,'variance'                ,'The variance on this mass scale'                 )
  call outputFile%writeDataset(barrier                     ,'barrier'                 ,'The excursion set barrier for this variance'     )
  call outputFile%writeDataset(firstCrossingProbability    ,'firstCrossingProbability','The first crossing probability'                  )
  call outputFile%writeDataset(haloMassFunctionDifferential,'haloMassFunction'        ,'The halo mass function [Mpc⁻³ M☉⁻¹]'             )
  call outputFile%writeDataset(firstCrossingRate           ,'firstCrossingRate'       ,'The first crossing rate [Gyr⁻¹]'                 )

  ! Close the parameters file.
  call Input_Parameters_File_Close()

  ! Close the output file.
  call outputFile%close()

end program Tests_Excursion_Sets
