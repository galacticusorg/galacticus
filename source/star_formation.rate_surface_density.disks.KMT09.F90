!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements the \cite{krumholz_star_2009} star formation rate surface density law for galactic disks.

module Star_Formation_Rate_Surface_Density_Disks_KMT09
  !% Implements the \cite{krumholz_star_2009} star formation rate surface density law for galactic disks.
  use Galacticus_Nodes
  use Kind_Numbers
  use Math_Exponentiation
  implicit none
  private
  public :: Star_Formation_Rate_Surface_Density_Disks_KMT09_Reset,&
       & Star_Formation_Rate_Surface_Density_Disks_KMT09_Initialize

  ! Record of unique ID of node which we last computed results for.
  integer         (kind=kind_int8  )            :: lastUniqueID                       =-1
  !$omp threadprivate(lastUniqueID)
  ! Record of whether or not factors have been precomputed.
  logical                                       :: factorsComputed                    =.false.
  !$omp threadprivate(factorsComputed)
  ! Precomputed factors.
  double precision                              :: chi                                        , diskScaleRadius            , &
       &                                           gasMass                                    , hydrogenMassFraction       , &
       &                                           metallicityRelativeToSolar                 , sNormalization             , &
       &                                           sigmaMolecularComplexNormalization
  type            (treeNode        ), pointer   :: nodeActive
  !$omp threadprivate(hydrogenMassFraction,gasMass,diskScaleRadius,metallicityRelativeToSolar,nodeActive)
  !$omp threadprivate(chi,sigmaMolecularComplexNormalization,sNormalization)
  ! Parameters of the model.
  double precision                              :: molecularComplexClumpingFactorKMT09        , starFormationFrequencyKMT09

  ! Parameters controlling optimization assumptions.
  logical                                       :: assumeMonotonicSurfaceDensityKMT09
  
  ! Pointer the the molecular fraction function that is to be used.
  procedure       (double precision), pointer   :: KMT09_Molecular_Fraction           =>null()

  ! Minimum fraction of molecular hydrogen allowed.
  double precision                  , parameter :: molecularFractionMinimum           =1.0d-4

  ! Fast exponentiator.
  type(fastExponentiator) :: surfaceDensityExponentiator
  
contains

  !# <calculationResetTask>
  !# <unitName>Star_Formation_Rate_Surface_Density_Disks_KMT09_Reset</unitName>
  !# </calculationResetTask>
  subroutine Star_Formation_Rate_Surface_Density_Disks_KMT09_Reset(thisNode)
    !% Reset the extended Schmidt relation calculation.
    implicit none
    type(treeNode), intent(inout) :: thisNode

    factorsComputed=.false.
    lastUniqueID   =thisNode%uniqueID()
    return
  end subroutine Star_Formation_Rate_Surface_Density_Disks_KMT09_Reset

  !# <starFormationRateSurfaceDensityDisksMethod>
  !#  <unitName>Star_Formation_Rate_Surface_Density_Disks_KMT09_Initialize</unitName>
  !# </starFormationRateSurfaceDensityDisksMethod>
  subroutine Star_Formation_Rate_Surface_Density_Disks_KMT09_Initialize(starFormationRateSurfaceDensityDisksMethod&
       &,Star_Formation_Rate_Surface_Density_Disk_Get,Star_Formation_Rate_Surface_Density_Disk_Intervals_Get)
    !% Initializes the ``KMT09'' disk star formation rate surface density.
    use ISO_Varying_String
    use Input_Parameters
    implicit none
    type     (varying_string                                          ), intent(in   )          :: starFormationRateSurfaceDensityDisksMethod
    procedure(Star_Formation_Rate_Surface_Density_Disk_KMT09          ), intent(inout), pointer :: Star_Formation_Rate_Surface_Density_Disk_Get
    procedure(Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09), intent(inout), pointer :: Star_Formation_Rate_Surface_Density_Disk_Intervals_Get
    logical                                                                                     :: molecularFractionFastKMT09

    if (starFormationRateSurfaceDensityDisksMethod == 'KMT09') then
       Star_Formation_Rate_Surface_Density_Disk_Get           => Star_Formation_Rate_Surface_Density_Disk_KMT09
       Star_Formation_Rate_Surface_Density_Disk_Intervals_Get => Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09
       ! Get parameters of our model.
       !@ <inputParameter>
       !@   <name>starFormationFrequencyKMT09</name>
       !@   <defaultValue>$0.385$ \citep{krumholz_star_2009}</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     The star formation frequency (in units of Gyr$^{-1}$) in the ``Krumholz-McKee-Tumlinson'' star formation timescale calculation.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('starFormationFrequencyKMT09',starFormationFrequencyKMT09,defaultValue=0.385d0)
       !@ <inputParameter>
       !@   <name>molecularComplexClumpingFactorKMT09</name>
       !@   <defaultValue>5</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     The density enhancement (relative to mean disk density) for molecular complexes in the ``Krumholz-McKee-Tumlinson'' star formation timescale calculation.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('molecularComplexClumpingFactorKMT09',molecularComplexClumpingFactorKMT09,defaultValue=5.0d0)
       !@ <inputParameter>
       !@   <name>molecularFractionFastKMT09</name>
       !@   <defaultValue>false</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@      Selects whether the fast (but less accurate) fitting formula for molecular hydrogen should be used in the ``Krumholz-McKee-Tumlinson'' star formation timescale calculation.
       !@   </description>
       !@   <type>boolean</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('molecularFractionFastKMT09',molecularFractionFastKMT09,defaultValue=.false.)
       !@ <inputParameter>
       !@   <name>assumeMonotonicSurfaceDensityKMT09</name>
       !@   <defaultValue>false</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@      If true, assume that the surface density in disks is always monotonically decreasing.
       !@   </description>
       !@   <type>boolean</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('assumeMonotonicSurfaceDensityKMT09',assumeMonotonicSurfaceDensityKMT09,defaultValue=.false.)
       ! Set a pointer to the molecular hydrogen fraction fitting function to be used.
       select case (molecularFractionFastKMT09)
       case (.true.)
          KMT09_Molecular_Fraction => KMT09_Molecular_Fraction_Fast
       case(.false.)
          KMT09_Molecular_Fraction => KMT09_Molecular_Fraction_Slow
       end select
       ! Initialize exponentiator.
       surfaceDensityExponentiator=fastExponentiator(1.0d0,100.0d0,0.33d0,100.0d0,.false.)
    end if
    return
  end subroutine Star_Formation_Rate_Surface_Density_Disks_KMT09_Initialize

  subroutine KMT09_Compute_Factors(thisNode)
    !% Compute constant factors needed in the \cite{krumholz_star_2009} star formation rule.
    use Abundances_Structure
    use Numerical_Constants_Prefixes
    implicit none
    type(treeNode), intent(inout) :: thisNode
    class           (nodeComponentDisk), pointer       :: thisDiskComponent
    type            (abundances       ), save          :: fuelAbundances
    !$omp threadprivate(fuelAbundances)

    ! Check if node differs from previous one for which we performed calculations.
    if (thisNode%uniqueID() /= lastUniqueID) call Star_Formation_Rate_Surface_Density_Disks_KMT09_Reset(thisNode)
    ! Check if factors have been precomputed.
    if (.not.factorsComputed) then
       ! Get the disk properties.
       thisDiskComponent => thisNode       %disk   ()
       gasMass           =thisDiskComponent%massGas()
       diskScaleRadius   =thisDiskComponent%radius ()
       ! Find the hydrogen fraction in the disk gas of the fuel supply.
       fuelAbundances=thisDiskComponent%abundancesGas()
       call fuelAbundances%massToMassFraction(gasMass)
       hydrogenMassFraction=fuelAbundances%hydrogenMassFraction()
       ! Get the metallicity in Solar units, and related quantities.
       metallicityRelativeToSolar=fuelAbundances%metallicity(metallicityTypeLinearByMassSolar)
       if (metallicityRelativeToSolar > 0.0d0) then
          chi                               =0.77d0*(1.0d0+3.1d0*metallicityRelativeToSolar**0.365d0)
          sigmaMolecularComplexNormalization=hydrogenMassFraction*molecularComplexClumpingFactorKMT09/mega**2
          sNormalization                    =log(1.0d0+0.6d0*chi+0.01d0*chi**2)/(0.04d0*metallicityRelativeToSolar)
       end if
       ! Record that factors have now been computed.
       factorsComputed=.true.
    end if
    return
  end subroutine KMT09_Compute_Factors
  
  double precision function Star_Formation_Rate_Surface_Density_Disk_KMT09(thisNode,radius)
    !% Returns the star formation rate surface density (in $M_\odot$ Gyr$^{-1}$ Mpc$^{-2}$) for star formation
    !% in the galactic disk of {\normalfont \ttfamily thisNode}. The disk is assumed to obey the
    !% \cite{krumholz_star_2009} star formation rule.
    implicit none
    type            (treeNode), intent(inout) :: thisNode
    double precision          , intent(in   ) :: radius
    double precision                          :: surfaceDensityFactor, molecularFraction             , &
         &                                       s                   , sigmaMolecularComplex         , &
         &                                       surfaceDensityGas   , surfaceDensityGasDimensionless

    ! Compute factors.
    call KMT09_Compute_Factors(thisNode)
    ! Check if the disk is physical.
    if (gasMass <= 0.0d0 .or. diskScaleRadius <= 0.0d0) then
       ! It is not, so return zero rate.
       Star_Formation_Rate_Surface_Density_Disk_KMT09=0.0d0
    else
       ! Get surface density and related quantities.
       call KMT09_Surface_Density_Factors(thisNode,radius,surfaceDensityGas,surfaceDensityGasDimensionless)
        ! Check for non-positive gas mass.
       if (surfaceDensityGas <= 0.0d0) then
          Star_Formation_Rate_Surface_Density_Disk_KMT09=0.0d0
       else
          ! Compute the molecular fraction.
          if (metallicityRelativeToSolar > 0.0d0) then
             sigmaMolecularComplex=sigmaMolecularComplexNormalization*surfaceDensityGas
             s                    =sNormalization/sigmaMolecularComplex
             molecularFraction    =KMT09_Molecular_Fraction(s)
          else
             molecularFraction    =molecularFractionMinimum
          end if
          ! Compute the cloud density factor.
          if (surfaceDensityGasDimensionless < 1.0d0) then
             surfaceDensityFactor=surfaceDensityExponentiator%exponentiate(1.0d0/surfaceDensityGasDimensionless)
          else
             surfaceDensityFactor=surfaceDensityExponentiator%exponentiate(      surfaceDensityGasDimensionless)
          end if
          ! Compute the star formation rate surface density.
          Star_Formation_Rate_Surface_Density_Disk_KMT09=+starFormationFrequencyKMT09 &
               &                                         *surfaceDensityGas           &
               &                                         *surfaceDensityFactor        &
               &                                         *molecularFraction
       end if
    end if
    return
  end function Star_Formation_Rate_Surface_Density_Disk_KMT09
  
  subroutine KMT09_Surface_Density_Factors(thisNode,radius,surfaceDensityGas,surfaceDensityGasDimensionless)
    !% Compute surface density and related quantities needed for the \cite{krumholz_star_2009} star formation rate model.
    use Galactic_Structure_Surface_Densities
    use Galactic_Structure_Options
    implicit none
    type            (treeNode), intent(inout) :: thisNode
    double precision          , intent(in   ) :: radius
    double precision          , intent(  out) :: surfaceDensityGas               , surfaceDensityGasDimensionless
    double precision          , parameter     :: surfaceDensityTransition=85.0d12                                 !   M☉/Mpc²

    ! Get gas surface density.
    surfaceDensityGas=Galactic_Structure_Surface_Density(                                                            &
         &                                                                 thisNode                                , &
         &                                                                [radius                     ,0.0d0,0.0d0], &
         &                                               coordinateSystem= coordinateSystemCylindrical             , &
         &                                               componentType   = componentTypeDisk                       , &
         &                                               massType        = massTypeGaseous                           &
         &                                              )
    ! Compute the cloud density factor.
    surfaceDensityGasDimensionless=+hydrogenMassFraction     &
         &                         *surfaceDensityGas        &
         &                         /surfaceDensityTransition
    return
  end subroutine KMT09_Surface_Density_Factors

  double precision function KMT09_Molecular_Fraction_Slow(s)
    !% Slow (but more accurate at low molecular fraction) fitting function from \cite{krumholz_star_2009} for the molecular
    !% hydrogen fraction.
    implicit none
    double precision, intent(in   ) :: s
    double precision, parameter     :: sMinimum=1.0d-6
    double precision, parameter     :: sMaximum=8.0d0
    double precision                :: delta

    ! Check if s is below maximum. If not, simply truncate to the minimum fraction that we allow. Also use a simple series
    ! expansion for cases of very small s.
    if      (s <  sMinimum) then
       KMT09_Molecular_Fraction_Slow=1.0d0-0.75d0*s
    else if (s >= sMaximum) then
       KMT09_Molecular_Fraction_Slow=                                                               molecularFractionMinimum
    else
       delta                        =0.0712d0/((0.1d0/s+0.675d0)**2.8d0)
       KMT09_Molecular_Fraction_Slow=max(1.0d0-1.0d0/((1.0d0+(((1.0d0+delta)/0.75d0/s)**5))**0.2d0),molecularFractionMinimum)
    end if
    return
  end function KMT09_Molecular_Fraction_Slow

  double precision function KMT09_Molecular_Fraction_Fast(s)
    !% Fast (but less accurate at low molecular fraction) fitting function from \cite{mckee_atomic--molecular_2010} for the
    !% molecular hydrogen fraction.
    implicit none
    double precision, intent(in   ) :: s

    ! Check that s is below 2 - if it is, compute the molecular fraction, otherwise truncate to the minimum.
    if (s < 2.0d0) then
       KMT09_Molecular_Fraction_Fast=max(1.0d0-0.75d0*s/(1.0d0+0.25d0*s),molecularFractionMinimum)
    else
       KMT09_Molecular_Fraction_Fast=                                    molecularFractionMinimum
    end if
    return
  end function KMT09_Molecular_Fraction_Fast

  function Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09(thisNode,radiusInner,radiusOuter)
    !% Returns intervals to use for integrating the \cite{krumholz_star_2009} star formation rate over a galactic disk.
    use Root_Finder
    implicit none
    double precision            , allocatable  , dimension(:,:) :: Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09
    type            (treeNode  ), intent(inout), target         :: thisNode
    double precision            , intent(in   )                 :: radiusInner      , radiusOuter
    type            (rootFinder), save                          :: finder
    !$omp threadprivate(finder)
    double precision                                            :: surfaceDensityGas, surfaceDensityGasDimensionless, &
         &                                                         radiusCritical

    ! Check if we can assume a monotonic surface density.
    if (assumeMonotonicSurfaceDensityKMT09) then
       ! Compute factors.
       call KMT09_Compute_Factors(thisNode)
       ! Test if the inner radius is below the surface density threshold.
       call KMT09_Surface_Density_Factors(thisNode,radiusInner,surfaceDensityGas,surfaceDensityGasDimensionless)
       if (surfaceDensityGasDimensionless <= 1.0d0) then
          ! The entire disk is below the critical surface density so use a single interval.
          allocate(Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09(2,1))
          Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09=reshape([radiusInner,radiusOuter],[2,1])
       else
          ! Test the surface density at the outer radius.
          call KMT09_Surface_Density_Factors(thisNode,radiusOuter,surfaceDensityGas,surfaceDensityGasDimensionless)
          if (surfaceDensityGasDimensionless >= 1.0d0) then
             ! Entire disk is above the critical surface density threshold so use a single interval.
             allocate(Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09(2,1))
             Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09=reshape([radiusInner,radiusOuter],[2,1])
          else
             ! The disk transitions the critical surface density - attempt to locate the radius at which this happens and use two
             ! intervals split at this point.
             if (.not.finder%isInitialized()) then
                call finder%rootFunction(KMT09_Critical_Density_Root)
                call finder%tolerance   (toleranceAbsolute=0.0d0,toleranceRelative=1.0d-4)
                call finder%rangeExpand(                                                             &
                     &                  rangeExpandUpward            =2.0d0                        , &
                     &                  rangeExpandDownward          =0.5d0                        , &
                     &                  rangeExpandUpwardSignExpect  =rangeExpandSignExpectNegative, &
                     &                  rangeExpandDownwardSignExpect=rangeExpandSignExpectPositive, &
                     &                  rangeExpandType              =rangeExpandMultiplicative      &
                     &                 )
             end if
             nodeActive => thisNode
             radiusCritical=finder%find(rootRange=[radiusInner,radiusOuter])
             allocate(Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09(2,2))
             Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09=reshape([radiusInner,radiusCritical,radiusCritical,radiusOuter],[2,2])
             call KMT09_Surface_Density_Factors(thisNode,radiusCritical,surfaceDensityGas,surfaceDensityGasDimensionless)
          end if
       end if
    else
       ! Disk surface density can not be assumed to be monotonic - use a single interval.
       allocate(Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09(2,1))
       Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09=reshape([radiusInner,radiusOuter],[2,1])
    end if
    return
  end function Star_Formation_Rate_Surface_Density_Disk_Intervals_KMT09

  double precision function KMT09_Critical_Density_Root(radius)
    !% Root function used in finding the radius in a disk where the surface density equals the critical surface density in the
    !% \cite{krumholz_star_2009} star formation rate model.
    implicit none
    double precision, intent(in   ) :: radius
    double precision                :: surfaceDensityGas, surfaceDensityGasDimensionless

    call KMT09_Surface_Density_Factors(nodeActive,radius,surfaceDensityGas,surfaceDensityGasDimensionless)
    KMT09_Critical_Density_Root=surfaceDensityGasDimensionless-1.0d0
    return
  end function KMT09_Critical_Density_Root
  
end module Star_Formation_Rate_Surface_Density_Disks_KMT09
