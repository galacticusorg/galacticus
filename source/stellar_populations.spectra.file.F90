!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !+    Contributions to this file made by: Alex Merson.

  !% Implements a file-based stellar population spectra class.

  use FGSL

  type spectralTable
     !% Structure to hold tabulated stellar population  data.
     ! The spectra tables.
     integer                                                            :: agesCount                     , metallicityCount              , &
          &                                                                wavelengthsCount
     double precision                   , allocatable, dimension(:    ) :: ages                          , metallicities                 , &
          &                                                                wavelengths
     double precision                   , allocatable, dimension(:,:,:) :: table
     ! Interpolation structures.
     logical                                                            :: resetAge               =.true., resetMetallicity       =.true., &
          &                                                                resetWavelength        =.true.
     type            (fgsl_interp_accel)                                :: acceleratorAge                , acceleratorMetallicity        , &
          &                                                                acceleratorWavelength
   contains
     final :: spectralTableDestructorScalar, spectralTableDestructor1D
  end type spectralTable
  
  !# <stellarPopulationSpectra name="stellarPopulationSpectraFile">
  !#  <description>
  !#   Provides stellar population spectra via interpolation in a tabulation read from file. This should be an HDF5 file with the following structure:
  !#   \begin{verbatim}
  !#    ages                     Dataset {ageCount}
  !#    metallicities            Dataset {metallicityCount}
  !#    spectra                  Dataset {metallicityCount, ageCount, metallicityCount}
  !#    wavelengths              Dataset {wavelengthCount}
  !#   \end{verbatim}
  !#   where the datasets contain the tabulated ages (in Gyr), metallicities (logarithmic, relative to Solar), wavelengths (in \AA) and spectra (in $L_\odot$ Hz$^{-1}$).
  !#
  !#   Currently, the following pre-computed stellar spectra files are available as a separate download from \href{http://users.obs.carnegiescience.edu/abenson/galacticus/data/Galacticus_SSP_Data.tar.bz2}{\normalfont \ttfamily http://users.obs.carnegiescience.edu/abenson/galacticus/data/Galacticus\_SSP\_Data.tar.bz2}:
  !#   \begin{description}
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.0\_imfSalpeter.hdf5}] Corresponds to a Salpeter IMF computed using v2.0 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.1\_imfSalpeter.hdf5}]  Corresponds to a Salpeter IMF computed using v2.1 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.1\_imfChabrier.hdf5}]  Corresponds to a Chabrier IMF computed using v2.1 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.2\_imfChabrier.hdf5}]  Corresponds to a Chabrier IMF computed using v2.2 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.2\_imfKennicutt.hdf5}]  Corresponds to a Kennicutt IMF computed using v2.2 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Conroy-et-al\_v2.2\_imfBaugh2005TopHeavy.hdf5}]  Corresponds to the top-heavy IMF of \cite{baugh_can_2005} computed using v2.2 of the {\normalfont \ttfamily FSPS} code;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Maraston\_hbMorphologyRed\_imfKroupa.hdf5}] The spectra from \cite{maraston_evolutionary_2005} for a Kroupa IMF and a red horizontal branch morphology;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Maraston\_hbMorphologyRed\_imfSalpeter.hdf5}] The spectra from \cite{maraston_evolutionary_2005} for a Salpeter IMF and a red horizontal branch morphology; 
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_BC2003\_highResolution\_imfChabrier.hdf5}] The (high resolution) spectra from \cite{bruzual_stellar_2003} for a Chabrier IMF, using Padova 1994 tracks;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_BC2003\_highResolution\_imfSalpeter.hdf5}] The (high resolution) spectra from \cite{bruzual_stellar_2003} for a Salpeter IMF, using Padova 1994 tracks;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_BC2003\_lowResolution\_imfChabrier.hdf5}] The (low resolution) spectra from \cite{bruzual_stellar_2003} for a Chabrier IMF, using Padova 1994 tracks;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_BC2003\_lowResolution\_imfSalpeter.hdf5}] The (low resolution) spectra from \cite{bruzual_stellar_2003} for a Salpeter IMF, using Padova 1994 tracks;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Grasil\_gkn15rd\_ken.hdf5}] The spectra used by {\normalfont \scshape Grasil} for a Kennicutt IMF;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Grasil\_gkn1rd\_ken.hdf5}] The spectra used by {\normalfont \scshape Grasil} for a Kennicutt IMF;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Grasil\_gsrdk0b\_sal.hdf5}] The spectra used by {\normalfont \scshape Grasil} for a Salpeter IMF;
  !#    \item [{\normalfont \ttfamily stellarPopulations/SSP\_Spectra\_Grasil\_imf27\_kro.hdf5}] Spectra used by {\normalfont \scshape Grasil}.
  !#   \end{description}
  !#   Note that the high resolution spectra from \cite{bruzual_stellar_2003} may require you to adjust the {\normalfont \ttfamily [stellarPopulationLuminosityIntegrationToleranceRelative]} parameter to a larger value\footnote{Or, alternatively, set {\normalfont \ttfamily [stellarPopulationLuminosityIntegrationToleranceDegrade]}$=${\normalfont \ttfamily true}. This will cause \glc\ to increase the tolerance as necessary to get the integrals to converge---issuing warnings each time the tolerance is increased.}. The sharp features in these high resolution spectra can be difficult to integrate. Scripts to convert the data provided by \cite{maraston_evolutionary_2005} and \cite{bruzual_stellar_2003} into \glc's format are provided in the {\normalfont \ttfamily scripts/ssps} folder.
  !#  </description>
  !#  <stateStorable>
  !#   <exclude variables="spectra, forceZeroMetallicity, fileName, fileRead"/>
  !#  </stateStorable>
  !# </stellarPopulationSpectra>
  type, extends(stellarPopulationSpectraClass) :: stellarPopulationSpectraFile
     !% A stellar population spectra class which interpolates spectra given in a file.
     private
     type   (spectralTable ):: spectra
     logical                :: forceZeroMetallicity, fileRead
     type   (varying_string):: fileName
   contains
     !@ <objectMethods>
     !@   <object>stellarPopulationSpectraFile</object>
     !@   <objectMethod>
     !@     <method>readFile</method>
     !@     <type>void</type>
     !@     <arguments>\textcolor{red}{\textless char(len=*)\textgreater} fileName\argin</arguments>
     !@     <description>Read the named stellar population spectra file.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     procedure :: readFile           => fileReadFile
     procedure :: luminosity         => fileLuminosity
     procedure :: tabulation         => fileTabulation
     procedure :: wavelengths        => fileWavelengths
     procedure :: wavelengthInterval => fileWavelengthInterval
  end type stellarPopulationSpectraFile

  interface stellarPopulationSpectraFile
     !% Constructors for the file stellar spectra class.
     module procedure fileConstructorParameters
     module procedure fileConstructorInternal
  end interface stellarPopulationSpectraFile

  ! The current file format version.
  integer, parameter :: fileFormatVersionCurrent=1

contains

  function fileConstructorParameters(parameters) result(self)
    !% Constructor for the file stellar spectra class which takes a parameter set as input.
    use Galacticus_Paths
    implicit none
    type   (stellarPopulationSpectraFile)                :: self
    type   (inputParameters             ), intent(inout) :: parameters
    type   (varying_string              )                :: fileName
    logical                                              :: forceZeroMetallicity

    !# <inputParameter>
    !#   <name>forceZeroMetallicity</name>
    !#   <defaultValue>.false.</defaultValue>
    !#   <source>parameters</source>
    !#   <description>Force the use of zero metallicity (or lowest metallicity available) for all stellar populations.</description>
    !#   <type>boolean</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>fileName</name>
    !#   <source>parameters</source>
    !#   <description>The name of the file from which to read spectra.</description>
    !#   <type>string</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    self=stellarPopulationSpectraFile(forceZeroMetallicity,char(fileName))
    !# <inputParametersValidate source="parameters"/>
    return
  end function fileConstructorParameters
  
  function fileConstructorInternal(forceZeroMetallicity,fileName) result(self)
    !% Internal constructor for the file stellar spectra class.
    use Galacticus_Error
    implicit none
    type     (stellarPopulationSpectraFile)                :: self
    logical                                , intent(in   ) :: forceZeroMetallicity
    character(len=*                       ), intent(in   ) :: fileName
    !# <constructorAssign variables="forceZeroMetallicity, fileName"/>

    self%fileRead=.false.
    return
  end function fileConstructorInternal
  
  subroutine spectralTableDestructorScalar(self)
    !% Destructor for the stellar spectra table class.
    use Numerical_Interpolation
    implicit none
    type(spectralTable), intent(inout) :: self

    ! Free all FGSL objects.
    call Interpolate_Done(                                                      &
         &                interpolationAccelerator=self%acceleratorAge        , &
         &                reset                   =self%      resetAge          &
         &               )
    call Interpolate_Done(                                                      &
         &                interpolationAccelerator=self%acceleratorWavelength , &
         &                reset                   =self%      resetWavelength   &
         &               )
    call Interpolate_Done(                                                      &
         &                interpolationAccelerator=self%acceleratorMetallicity, &
         &                reset                   =self%      resetMetallicity  &
         &               )
    return
  end subroutine spectralTableDestructorScalar

  subroutine spectralTableDestructor1D(self)
    !% Destructor for the stellar spectra table class.
    use Numerical_Interpolation
    implicit none
    type   (spectralTable), intent(inout), dimension(:) :: self
    integer                                             :: i

    do i=1,size(self)
       call spectralTableDestructorScalar(self(i))
    end do
    return
  end subroutine spectralTableDestructor1D
  
  double precision function fileLuminosity(self,abundancesStellar,age,wavelength,status)
    !% Return the luminosity (in units of $L_\odot$ Hz$^{-1}$) for a stellar population with composition {\normalfont \ttfamily abundances}, of the
    !% given {\normalfont \ttfamily age} (in Gyr) and the specified {\normalfont \ttfamily wavelength} (in Angstroms). This is found by interpolating in tabulated
    !% spectra.
    use, intrinsic :: ISO_C_Binding
    use               Abundances_Structure
    use               Numerical_Interpolation
    use               Galacticus_Error
    implicit none
    class           (stellarPopulationSpectraFile), intent(inout)            :: self
    type            (abundances                  ), intent(in   )            :: abundancesStellar
    double precision                              , intent(in   )            :: age                        , wavelength
    integer                                       , intent(  out) , optional :: status
    double precision                              , dimension(0:1)           :: hAge                       , hMetallicity, &
         &                                                                      hWavelength
    double precision                              , parameter                :: metallicityTolerance=0.01d0
    integer         (c_size_t                    )                           :: iAge                       , iMetallicity, &
         &                                                                      iWavelength
    integer                                                                  :: jWavelength                , jMetallicity, &
         &                                                                      jAge
    double precision                                                         :: metallicity
    type            (varying_string              )                           :: message
    character       (len=12                      )                           :: metallicityLabel           , label

    ! Ensure that the file has been read.
    call self%readFile()
    ! Set status code if present.
    if (present(status)) status=errorStatusSuccess
    ! Check for out of range conditions.
    if (age > self%spectra%ages(self%spectra%agesCount)) then
       write (label,'(e12.4)') age 
       message='age ['//trim(label)//'] exceeds the maximum tabulated ['
       write (label,'(e12.4)') self%spectra%ages(self%spectra%agesCount)
       message=message//trim(label)//']'
       call Galacticus_Error_Report(message//{introspection:location})
    end if
    if (self%forceZeroMetallicity) then
       metallicity=logMetallicityZero
    else
       metallicity=Abundances_Get_Metallicity(abundancesStellar,metallicityType=metallicityTypeLogarithmicByMassSolar)
    end if
    if (metallicity > self%spectra%metallicities(self%spectra%metallicityCount)+metallicityTolerance) then
       if (present(status)) then
          metallicity=self%spectra%metallicities(self%spectra%metallicityCount)
          status     =errorStatusInputDomain
       else
          write (metallicityLabel,'(f12.6)') metallicity
          message='metallicity ['//trim(adjustl(metallicityLabel))//'] exceeds the maximum tabulated ['
          write (metallicityLabel,'(f12.6)') self%spectra%metallicities(self%spectra%metallicityCount)
          message=message//trim(adjustl(metallicityLabel))//']'
          call Galacticus_Error_Report(message//{introspection:location})
       end if
    end if
    ! Assume zero flux outside of the tabulated wavelength range.
    if     (                                                                      &
         &   wavelength < self%spectra%wavelengths(                            1) &
         & .or.                                                                   &
         &   wavelength > self%spectra%wavelengths(self%spectra%wavelengthsCount) &
         & ) then
       fileLuminosity=0.0d0
       return
    end if
    ! Get the interpolating factors.
    iAge       =Interpolate_Locate                 (                                     &
         &                                          self%spectra%           ages       , &
         &                                          self%spectra%acceleratorAge        , &
         &                                          age                                , &
         &                                          self%spectra%      resetAge          &
         &                                         )
    iWavelength=Interpolate_Locate                 (                                     &
         &                                          self%spectra%           wavelengths, &
         &                                          self%spectra%acceleratorWavelength , &
         &                                                                  wavelength , &
         &                                          self%spectra%      resetWavelength   &
         &                                         )
    hAge       =Interpolate_Linear_Generate_Factors(                                     &
         &                                          self%spectra%           ages       , &
         &                                                                 iAge        , &
         &                                                                  age          &
         &                                         )
    hWavelength=Interpolate_Linear_Generate_Factors(                                     &
         &                                          self%spectra%           wavelengths, &
         &                                                                 iWavelength , &
         &                                                                  wavelength   &
         &                                         )
    if      (                                                                         &
         &    metallicity == logMetallicityZero                                       &
         &   .or.                                                                     &
         &    metallicity <  self%spectra%metallicities(                           1) &
         &  ) then
       iMetallicity=1
       hMetallicity=[1.0d0,0.0d0]
    else if (                                                                         &
         &   metallicity >  self%spectra%metallicities(self%spectra%metallicityCount) &
         &  ) then
       iMetallicity=self%spectra%metallicityCount-1
       hMetallicity=[0.0d0,1.0d0]
    else
       iMetallicity=Interpolate_Locate                 (                                       &
            &                                           self%spectra%           metallicities, &
            &                                           self%spectra%acceleratorMetallicity  , &
            &                                                                   metallicity  , &
            &                                           self%spectra%      resetMetallicity    &
            &                                          )
       hMetallicity=Interpolate_Linear_Generate_Factors(                                       &
            &                                           self%spectra%           metallicities, &
            &                                                                  iMetallicity  , &
            &                                                                   metallicity    &
            &                                          )
    end if
    ! Do the interpolation.
    fileLuminosity=0.0d0
    do jAge=0,1
       do jWavelength=0,1
          do jMetallicity=0,1
             fileLuminosity=+fileLuminosity                                                &
                  &         +self                                                          &
                  &              %spectra                                  &
                  &                      %table(                           &
                  &                             iWavelength +jWavelength , &
                  &                             iAge        +jAge        , &
                  &                             iMetallicity+jMetallicity  &
                  &                            )                           &
                  &         *hAge              (             jAge        ) &
                  &         *hWavelength       (             jWavelength ) &
                  &         *hMetallicity      (             jMetallicity)
           end do
        end do
     end do
     ! Prevent interpolation from returning negative fluxes.
     fileLuminosity=max(fileLuminosity,0.0d0)
    return
  end function fileLuminosity

  subroutine fileReadFile(self)
    !% Read a file of simple stellar population spectra.
    use Galacticus_Error
    use Memory_Management
    use IO_HDF5
    use File_Utilities
    implicit none
    class  (stellarPopulationSpectraFile), intent(inout) :: self
    integer                                              :: fileFormatVersion
    type   (hdf5Object                  )                :: spectraFile

    ! Read file if necessary.
    if (.not.self%fileRead) then
       !$ call hdf5Access%set()
       ! Open the HDF5 file.
       call spectraFile%openFile(char(File_Name_Expand(char(self%fileName))),readOnly=.true.)
       ! Check that this file has the correct format.
       call spectraFile%readAttribute('fileFormat',fileFormatVersion)
       if (fileFormatVersion /= fileFormatVersionCurrent) call Galacticus_Error_Report('format of stellar tracks file is out of date'//{introspection:location})
       ! Read the wavelengths array.
       call spectraFile%readDataset('wavelengths'                 ,self%spectra%wavelengths  )
       self%spectra%wavelengthsCount=size(self%spectra%wavelengths  )
       ! Read the ages array.
       call spectraFile%readDataset('ages'                        ,self%spectra%ages         )
       self%spectra%agesCount       =size(self%spectra%ages         )
       ! Read the metallicity array.
       call spectraFile%readDataset('metallicities'               ,self%spectra%metallicities)
       self%spectra%metallicityCount=size(self%spectra%metallicities)
       ! Read the spectra.
       call spectraFile%readDataset('spectra'                     ,self%spectra%Table        )
       ! Close the HDF5 file.
       call spectraFile%close()
       !$ call hdf5Access%unset()
       ! Force interpolation accelerators to be reset.
       self%spectra%resetAge        =.true.
       self%spectra%resetWavelength =.true.
       self%spectra%resetMetallicity=.true.
       self        %fileRead        =.true.
    end if
    return
  end subroutine fileReadFile

  subroutine fileTabulation(self,agesCount,metallicitiesCount,ages,metallicity)
    !% Return a tabulation of ages and metallicities at which stellar spectra should be tabulated.
    use Memory_Management
    use Numerical_Constants_Astronomical
    implicit none
    class           (stellarPopulationSpectraFile)                           , intent(inout) :: self
    integer                                                                  , intent(  out) :: agesCount, metallicitiesCount
    double precision                              , allocatable, dimension(:), intent(  out) :: ages     , metallicity

    ! Ensure that the file has been read.
    call self%readFile()
    ! Return the relevant data.
    agesCount         =self%spectra%agesCount
    metallicitiesCount=self%spectra%metallicityCount
    call allocateArray(ages       ,[agesCount         ])
    call allocateArray(metallicity,[metallicitiesCount])
    ages              =         self%spectra%ages
    metallicity       =(10.0d0**self%spectra%metallicities)*metallicitySolar
    return
  end subroutine fileTabulation

  subroutine fileWavelengths(self,wavelengthsCount,wavelengths)
    !% Return a tabulation of wavelengths at which stellar spectra should be tabulated.
    use Memory_Management
    use Numerical_Constants_Astronomical
    implicit none
    class           (stellarPopulationSpectraFile)                           , intent(inout) :: self
    integer                                                                  , intent(  out) :: wavelengthsCount
    double precision                              , allocatable, dimension(:), intent(  out) :: wavelengths

    ! Ensure that the file has been read.
    call self%readFile()
    ! Return the relevant data.
    wavelengthsCount=self%spectra  %wavelengthsCount
    call allocateArray(wavelengths,[wavelengthsCount])
    wavelengths     =self%spectra  %wavelengths
    return
  end subroutine fileWavelengths

  double precision function fileWavelengthInterval(self,wavelength)
    !% Return a tabulation of wavelengths at which stellar spectra should be tabulated.
    use Memory_Management
    use Numerical_Constants_Astronomical
    implicit none
    class           (stellarPopulationSpectraFile)                           , intent(inout) :: self
    double precision                                                         , intent(in   ) :: wavelength
    integer                                                                                  :: wavelengthsCount
    double precision                              , allocatable, dimension(:)                :: wavelengths     , wavelengthDifference

    ! Ensure that the file is read.
    call self%readFile()
    ! Return the relevant data.
    wavelengthsCount=self%spectra  %wavelengthsCount
    call allocateArray(wavelengths,[wavelengthsCount])
    wavelengths     =self%spectra  %wavelengths
    ! Check if wavelength inside range
    if     (                                            &
         &   wavelength < wavelengths(               1) &
         &  .or.                                        &
         &   wavelength > wavelengths(wavelengthsCount) &
         & ) then
       fileWavelengthInterval=-999.9d0
    else
       ! Compute difference in wavelength at position of interest
       call allocateArray(wavelengthDifference,[wavelengthsCount])
       wavelengthDifference  =+       wavelengths&
            &                 -       wavelength
       fileWavelengthInterval=+minval(wavelengths,dim=1,mask=wavelengthDifference >  0.0d0) &
            &                 -maxval(wavelengths,dim=1,mask=wavelengthDifference <= 0.0d0)
    end if
    call deallocateArray(wavelengths)
    if(allocated(wavelengthDifference)) call deallocateArray(wavelengthDifference)
    return
  end function fileWavelengthInterval
