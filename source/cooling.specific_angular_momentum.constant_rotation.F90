!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which calculates the specific angular momentum of cooling gas assuming a constant rotation velocity as a
!% function of radius.

module Cooling_Specific_Angular_Momenta_Constant_Rotation
  !% Calculates the specific angular momentum of cooling gas assuming a constant rotation velocity as a function of radius.
  use Kind_Numbers
  implicit none
  private
  public :: Cooling_Specific_AM_Constant_Rotation_Initialize, Cooling_Specific_AM_Constant_Rotation_Reset

  ! Record of unique ID of node which we last computed results for.
  integer         (kind=kind_int8)            :: lastUniqueID                          =-1
  !$omp threadprivate(lastUniqueID)
  ! Record of whether or not specific angular momentum of cooling has has already been computed for this node.
  logical                                     :: coolingSpecificAngularMomentumComputed=.false.
  !$omp threadprivate(coolingSpecificAngularMomentumComputed)
  ! Stored values of specific angular momentum of cooling gas.
  double precision                            :: coolingSpecificAngularMomentumStored
  !$omp threadprivate(coolingSpecificAngularMomentumStored)
  ! Parameters controlling the calculation.
  integer                         , parameter :: profileDarkMatter                     =0
  integer                         , parameter :: profileHotGas                         =1
  integer                                     :: meanSpecificAngularMomentumFrom               , rotationNormalizationFrom
  logical                                     :: coolingAngularMomentumUseInteriorMean

contains

  !# <coolingSpecificAngularMomentumMethod>
  !#  <unitName>Cooling_Specific_AM_Constant_Rotation_Initialize</unitName>
  !# </coolingSpecificAngularMomentumMethod>
  subroutine Cooling_Specific_AM_Constant_Rotation_Initialize(coolingSpecificAngularMomentumMethod,Cooling_Specific_Angular_Momentum_Get)
    !% Initializes the ``constant rotation'' specific angular momentum of cooling gas module.
    use ISO_Varying_String
    use Input_Parameters
    use Galacticus_Error
    implicit none
    type     (varying_string                                     ), intent(in   )          :: coolingSpecificAngularMomentumMethod
    procedure(Cooling_Specific_Angular_Momentum_Constant_Rotation), intent(inout), pointer :: Cooling_Specific_Angular_Momentum_Get
    type     (varying_string                                     )                         :: inputOption

    if (coolingSpecificAngularMomentumMethod == 'constantRotation') then
       Cooling_Specific_Angular_Momentum_Get => Cooling_Specific_Angular_Momentum_Constant_Rotation

       !# <inputParameter>
       !#   <name>coolingMeanAngularMomentumFrom</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>var_str('hotGas')</defaultValue>
       !#   <description>The component (``{\normalfont \ttfamily hotGas}'' or ``{\normalfont \ttfamily darkMatter}'') from which the mean specific angular momentum should be computed for
       !#      calculations of cooling gas specific angular momentum.</description>
       !#   <source>globalParameters</source>
       !#   <type>string</type>
       !#   <variable>inputOption</variable>
       !# </inputParameter>
       select case (char(inputOption))
       case ("darkMatter")
          meanSpecificAngularMomentumFrom=profileDarkMatter
       case ("hotGas"    )
          meanSpecificAngularMomentumFrom=profileHotGas
       case default
          call Galacticus_Error_Report('Cooling_Specific_AM_Constant_Rotation_Initialize','[coolingMeanAngularMomentumFrom] must be either "darkMatter" or "hotGas"')
       end select

       !# <inputParameter>
       !#   <name>coolingRotationVelocityFrom</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>var_str('hotGas')</defaultValue>
       !#   <description>The component (``{\normalfont \ttfamily hotGas}'' or ``{\normalfont \ttfamily darkMatter}'') from which the constant rotation speed should be computed for
       !#      calculations of cooling gas specific angular momentum.</description>
       !#   <source>globalParameters</source>
       !#   <type>string</type>
       !#   <variable>inputOption</variable>
       !# </inputParameter>
       select case (char(inputOption))
       case ("darkMatter")
          rotationNormalizationFrom=profileDarkMatter
       case ("hotGas"    )
          rotationNormalizationFrom=profileHotGas
       case default
          call Galacticus_Error_Report('Cooling_Specific_AM_Constant_Rotation_Initialize','[coolingRotationVelocityFrom] must be either "darkMatter" or "hotGas"')
       end select
       
       !# <inputParameter>
       !#   <name>coolingAngularMomentumUseInteriorMean</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>.false.</defaultValue>
       !#   <description>Specifies whether to use the specific angular momentum at the cooling radius, or the mean specific angular momentum interior to that radius.</description>
       !#   <source>globalParameters</source>
       !#   <type>string</type>
       !# </inputParameter>

    end if
    return
  end subroutine Cooling_Specific_AM_Constant_Rotation_Initialize

  !# <calculationResetTask>
  !# <unitName>Cooling_Specific_AM_Constant_Rotation_Reset</unitName>
  !# </calculationResetTask>
  subroutine Cooling_Specific_AM_Constant_Rotation_Reset(node)
    !% Reset the specific angular momentum of cooling gas calculation.
    use Galacticus_Nodes
    implicit none
    type(treeNode), intent(inout) :: node

    coolingSpecificAngularMomentumComputed=.false.
    lastUniqueID                          =node%uniqueID()
    return
  end subroutine Cooling_Specific_AM_Constant_Rotation_Reset

  double precision function Cooling_Specific_Angular_Momentum_Constant_Rotation(node,radius)
    !% Return the specific angular momentum of cooling gas in the constant rotation model.
    use Galacticus_Error
    use Galacticus_Nodes
    use Dark_Matter_Profiles
    use Hot_Halo_Mass_Distributions
    use Numerical_Constants_Physical
    implicit none
    type            (treeNode                    ), intent(inout) :: node
    double precision                              , intent(in   ) :: radius
    class           (nodeComponentBasic          ), pointer       :: basic
    class           (nodeComponentSpin           ), pointer       :: spin
    class           (nodeComponentHotHalo        ), pointer       :: hotHalo
    class           (hotHaloMassDistributionClass), pointer       :: hotHaloMassDistribution_
    class           (darkMatterProfileClass      ), pointer       :: darkMatterProfile_
    double precision                                              :: meanSpecificAngularMomentum, rotationNormalization

    ! Check if node differs from previous one for which we performed calculations.
    if (node%uniqueID() /= lastUniqueID) call Cooling_Specific_AM_Constant_Rotation_Reset(node)
    ! Get required objects.
    darkMatterProfile_       => darkMatterProfile      ()
    ! Get the hot halo mass distribution.
    hotHaloMassDistribution_ => hotHaloMassDistribution()
    ! Check if specific angular momentum of cooling gas is already computed.
    if (.not.coolingSpecificAngularMomentumComputed) then
       ! Flag that cooling radius is now computed.
       coolingSpecificAngularMomentumComputed=.true.
       ! Compute the mean specific angular momentum.
       select case (meanSpecificAngularMomentumFrom)
       case (profileDarkMatter)
          ! Compute mean specific angular momentum of the dark matter halo from the spin parameter, mass and energy of the halo.
          basic => node%basic()
          spin  => node%spin ()
          meanSpecificAngularMomentum= gravitationalConstantGalacticus            &
               &                      *spin %spin()                               &
               &                      *basic%mass()**1.5d0                        &
               &                      /sqrt(abs(darkMatterProfile_%energy(node)))
       case (profileHotGas    )
          ! Compute mean specific angular momentum from the hot halo component.
          hotHalo => node%hotHalo()
          meanSpecificAngularMomentum= hotHalo%angularMomentum() &
               &                      /hotHalo%mass           ()
       case default
          meanSpecificAngularMomentum=0.0d0
          call Galacticus_Error_Report('Cooling_Specific_Angular_Momentum_Constant_Rotation','unknown profile type')
       end select

       ! Compute the rotation normalization.
       select case (rotationNormalizationFrom      )
       case (profileDarkMatter)
          rotationNormalization=darkMatterProfile_      %rotationNormalization(node)
       case (profileHotGas    )
          rotationNormalization=hotHaloMassDistribution_%rotationNormalization(node)
       case default
          rotationNormalization=0.0d0
          call Galacticus_Error_Report('Cooling_Specific_Angular_Momentum_Constant_Rotation','unknown profile type')
       end select

       ! Compute the specific angular momentum of the cooling gas.
       coolingSpecificAngularMomentumStored= rotationNormalization       &
            &                               *meanSpecificAngularMomentum
    end if

    ! Check that the radius is positive.
    if (radius > 0.0d0) then
       ! Return the computed value.
       if (coolingAngularMomentumUseInteriorMean) then
          ! Find the specific angular momentum interior to the specified radius.
          Cooling_Specific_Angular_Momentum_Constant_Rotation= coolingSpecificAngularMomentumStored                     &
               &                                              *hotHaloMassDistribution_%radialMoment(node,3.0d0,radius) &
               &                                              /hotHaloMassDistribution_%radialMoment(node,2.0d0,radius)
       else
          ! Find the specific angular momentum at the specified radius.
          Cooling_Specific_Angular_Momentum_Constant_Rotation= coolingSpecificAngularMomentumStored                     &
               &                                              *                                                 radius
       end if
    else
       ! Radius is non-positive - return zero.
       Cooling_Specific_Angular_Momentum_Constant_Rotation=0.0d0
    end if
    return
  end function Cooling_Specific_Angular_Momentum_Constant_Rotation

end module Cooling_Specific_Angular_Momenta_Constant_Rotation
