!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implementation of a specific angular momentum of cooling gas class assuming a constant rotation velocity as a function of
  !% radius.

  use Kind_Numbers
  use Dark_Matter_Profiles_DMO
  use Hot_Halo_Mass_Distributions

  !# <coolingSpecificAngularMomentum name="coolingSpecificAngularMomentumConstantRotation">
  !#  <description>
  !#   A specific angular momentum of cooling gas class which assumes a constant rotation velocity as a function of radius.
  !#  </description>
  !# </coolingSpecificAngularMomentum>
  type, extends(coolingSpecificAngularMomentumClass) :: coolingSpecificAngularMomentumConstantRotation
     !% Implementation of the specific angular momentum of cooling gas class which assumes a constant rotation velocity as a function of radius.
     private 
     class           (darkMatterProfileDMOClass   ), pointer :: darkMatterProfileDMO_             => null()
     class           (hotHaloMassDistributionClass), pointer :: hotHaloMassDistribution_          => null()
     integer         (kind=kind_int8              )          :: lastUniqueID
     logical                                                 :: angularMomentumSpecificComputed
     double precision                                        :: angularMomentumSpecificPrevious
     integer                                                 :: sourceAngularMomentumSpecificMean          , sourceNormalizationRotation
     logical                                                 :: useInteriorMean
   contains
     !@ <objectMethods>
     !@   <object>coolingSpecificAngularMomentumConstantRotation</object>
     !@   <objectMethod>
     !@     <method>calculationReset</method>
     !@     <type>\void</type>
     !@     <arguments>\textcolor{red}{\textless type(table)\textgreater} node\arginout</arguments>
     !@     <description>Reset memoized calculations.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     final     ::                            constantRotationDestructor
     procedure :: autoHook                => constantRotationAutoHook
     procedure :: calculationReset        => constantRotationCalculationReset
     procedure :: angularMomentumSpecific => constantRotationAngularMomentumSpecific
  end type coolingSpecificAngularMomentumConstantRotation

  interface coolingSpecificAngularMomentumConstantRotation
     !% Constructors for the constantRotation specific angular momentum of cooling gas class.
     module procedure constantRotationConstructorParameters
     module procedure constantRotationConstructorInternal
  end interface coolingSpecificAngularMomentumConstantRotation

  ! Enumeration for angular momentum source.
  !# <enumeration>
  !#  <name>angularMomentumSource</name>
  !#  <description>Enumeration specifying the origin of angular momentum of cooling gas in the constant rotation class.</description>
  !#  <encodeFunction>yes</encodeFunction>
  !#  <entry label="darkMatter"/>
  !#  <entry label="hotGas"    />
  !# </enumeration>

contains

  function constantRotationConstructorParameters(parameters) result(self)
    !% Constructor for the constantRotation freefall radius class which builds the object from a parameter set.
    use Input_Parameters
    implicit none
    type   (coolingSpecificAngularMomentumConstantRotation)                :: self
    type   (inputParameters                               ), intent(inout) :: parameters
    class  (darkMatterProfileDMOClass                     ), pointer       :: darkMatterProfileDMO_
    class  (hotHaloMassDistributionClass                  ), pointer       :: hotHaloMassDistribution_
    logical                                                                :: useInteriorMean
    type   (varying_string                                )                :: sourceAngularMomentumSpecificMean, sourceNormalizationRotation

    !# <inputParameter>
    !#   <name>sourceAngularMomentumSpecificMean</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>var_str('hotGas')</defaultValue>
    !#   <description>
    !#    The component (``{\normalfont \ttfamily hotGas}'' or ``{\normalfont \ttfamily darkMatter}'') from which the mean specific angular momentum should be computed for
    !#    calculations of cooling gas specific angular momentum.
    !#   </description>
    !#   <source>parameters</source>
    !#   <type>string</type>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>sourceNormalizationRotation</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>var_str('hotGas')</defaultValue>
    !#   <description>
    !#    The component (``{\normalfont \ttfamily hotGas}'' or ``{\normalfont \ttfamily darkMatter}'') from which the constant rotation speed should be computed for
    !#    calculations of cooling gas specific angular momentum.
    !#   </description>
    !#   <source>parameters</source>
    !#   <type>string</type>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>useInteriorMean</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>Specifies whether to use the specific angular momentum at the cooling radius, or the mean specific angular momentum interior to that radius.</description>
    !#   <source>parameters</source>
    !#   <type>boolean</type>
    !# </inputParameter>
    !# <objectBuilder class="darkMatterProfileDMO"       name="darkMatterProfileDMO_"       source="parameters"/>
    !# <objectBuilder class="hotHaloMassDistribution" name="hotHaloMassDistribution_" source="parameters"/>
    self=coolingSpecificAngularMomentumConstantRotation(                                                                                                        &
         &                                              darkMatterProfileDMO_                                                                                    , &
         &                                              hotHaloMassDistribution_                                                                              , &
         &                                              enumerationAngularMomentumSourceEncode(char(sourceAngularMomentumSpecificMean),includesPrefix=.false.), &
         &                                              enumerationAngularMomentumSourceEncode(char(sourceNormalizationRotation      ),includesPrefix=.false.), &
         &                                              useInteriorMean                                                                                         &
         &                                             )
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="darkMatterProfileDMO_"      />
    !# <objectDestructor name="hotHaloMassDistribution_"/>
    return
  end function constantRotationConstructorParameters

  function constantRotationConstructorInternal(darkMatterProfileDMO_,hotHaloMassDistribution_,sourceAngularMomentumSpecificMean,sourceNormalizationRotation,useInteriorMean) result(self)
    !% Internal constructor for the darkMatterHalo freefall radius class.
    implicit none
    type   (coolingSpecificAngularMomentumConstantRotation)                        :: self
    class  (darkMatterProfileDMOClass                        ), intent(in   ), target :: darkMatterProfileDMO_
    class  (hotHaloMassDistributionClass                  ), intent(in   ), target :: hotHaloMassDistribution_
    integer                                                , intent(in   )         :: sourceAngularMomentumSpecificMean, sourceNormalizationRotation
    logical                                                , intent(in   )         :: useInteriorMean
    !# <constructorAssign variables="*darkMatterProfileDMO_, *hotHaloMassDistribution_, sourceAngularMomentumSpecificMean, sourceNormalizationRotation, useInteriorMean"/>

    self%lastUniqueID                   =-1_kind_int8
    self%angularMomentumSpecificComputed=.false.
    return
  end function constantRotationConstructorInternal
  
  subroutine constantRotationAutoHook(self)
    !% Attach to the calculation reset event.
    use Events_Hooks, only : calculationResetEvent, openMPThreadBindingAllLevels
    implicit none
    class(coolingSpecificAngularMomentumConstantRotation), intent(inout) :: self

    call calculationResetEvent%attach(self,constantRotationCalculationReset,openMPThreadBindingAllLevels)
    return
  end subroutine constantRotationAutoHook
  
  subroutine constantRotationDestructor(self)
    !% Destructor for the constant rotation specific angular momentum of cooling gas class.
    use Events_Hooks, only : calculationResetEvent
    implicit none
    type(coolingSpecificAngularMomentumConstantRotation), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterProfileDMO_"      />
    !# <objectDestructor name="self%hotHaloMassDistribution_"/>
    call calculationResetEvent%detach(self,constantRotationCalculationReset)
    return
  end subroutine constantRotationDestructor
  
  double precision function constantRotationAngularMomentumSpecific(self,node,radius)
    !% Return the specific angular momentum of cooling gas in the constantRotation model.
    use Galacticus_Nodes            , only : nodeComponentBasic, nodeComponentSpin, nodeComponentHotHalo
    use Galacticus_Error
    use Numerical_Constants_Physical
    implicit none
    class           (coolingSpecificAngularMomentumConstantRotation), intent(inout) :: self
    type            (treeNode                                      ), intent(inout) :: node
    double precision                                                , intent(in   ) :: radius
    class           (nodeComponentBasic                            ), pointer       :: basic
    class           (nodeComponentSpin                             ), pointer       :: spin
    class           (nodeComponentHotHalo                          ), pointer       :: hotHalo
    double precision                                                                :: angularMomentumSpecificMean, normalizationRotation

    ! Check if node differs from previous one for which we performed calculations.
    if (node%uniqueID() /= self%lastUniqueID) call self%calculationReset(node)
    ! Check if specific angular momentum of cooling gas is already computed.
    if (.not.self%angularMomentumSpecificComputed) then
       ! Flag that cooling radius is now computed.
       self%angularMomentumSpecificComputed=.true.
       ! Compute the mean specific angular momentum.
       select case (self%sourceAngularMomentumSpecificMean)
       case (angularMomentumSourceDarkMatter)
          ! Compute mean specific angular momentum of the dark matter halo from the spin parameter, mass and energy of the halo.
          basic => node%basic()
          spin  => node%spin ()
          angularMomentumSpecificMean=+gravitationalConstantGalacticus                         &
               &                      *         spin                    %spin  (    )          &
               &                      *         basic                   %mass  (    )  **1.5d0 &
               &                      /sqrt(abs(self %darkMatterProfileDMO_%energy(node)))
       case (angularMomentumSourceHotGas    )
          ! Compute mean specific angular momentum from the hot halo component.
          hotHalo => node%hotHalo()
          angularMomentumSpecificMean=+hotHalo%angularMomentum() &
               &                      /hotHalo%mass           ()
       case default
          angularMomentumSpecificMean=0.0d0
          call Galacticus_Error_Report('unknown profile type'//{introspection:location})
       end select
       ! Compute the rotation normalization.
       select case (self%sourceNormalizationRotation      )
       case (angularMomentumSourceDarkMatter)
          normalizationRotation=self%darkMatterProfileDMO_      %rotationNormalization(node)
       case (angularMomentumSourceHotGas    )
          normalizationRotation=self%hotHaloMassDistribution_%rotationNormalization(node)
       case default
          normalizationRotation=0.0d0
          call Galacticus_Error_Report('unknown profile type'//{introspection:location})
       end select
       ! Compute the specific angular momentum of the cooling gas.
       self%angularMomentumSpecificPrevious=+normalizationRotation       &
            &                               *angularMomentumSpecificMean
    end if
    ! Check that the radius is positive.
    if (radius > 0.0d0) then
       ! Return the computed value.
       if (self%useInteriorMean) then
          ! Find the specific angular momentum interior to the specified radius.
          constantRotationAngularMomentumSpecific=+self                         %angularMomentumSpecificPrevious                    &
               &                                  *self%hotHaloMassDistribution_%radialMoment                   (node,3.0d0,radius) &
               &                                  /self%hotHaloMassDistribution_%radialMoment                   (node,2.0d0,radius)
       else
          ! Find the specific angular momentum at the specified radius.
          constantRotationAngularMomentumSpecific=+self                         %angularMomentumSpecificPrevious                    &
               &                                  *                                                                         radius
       end if
    else
       ! Radius is non-positive - return zero.
       constantRotationAngularMomentumSpecific=0.0d0
    end if
    return
  end function constantRotationAngularMomentumSpecific

  subroutine constantRotationCalculationReset(self,node)
    !% Reset the specific angular momentum of cooling gas calculation.
    implicit none
    class(coolingSpecificAngularMomentumConstantRotation), intent(inout) :: self
    type (treeNode                                      ), intent(inout) :: node

    self%angularMomentumSpecificComputed=.false.
    self%lastUniqueID                   =node%uniqueID()
    return
  end subroutine constantRotationCalculationReset
