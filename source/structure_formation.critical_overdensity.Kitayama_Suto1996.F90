!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Contains a module which implements a critical overdensity class based on the fitting functions of
  !% \cite{kitayama_semianalytic_1996}.

  use Linear_Growth
  use Cosmology_Functions
  use Dark_Matter_Particles

  !# <criticalOverdensity name="criticalOverdensityKitayamaSuto1996" defaultThreadPrivate="yes">
  !#  <description>Provides a critical overdensity class based on the fitting functions of \cite{kitayama_semianalytic_1996}, and is therefore valid only for flat cosmological models.</description>
  !# </criticalOverdensity>
  type, extends(criticalOverdensityClass) :: criticalOverdensityKitayamaSuto1996
     !% A critical overdensity class based on the fitting functions of \cite{kitayama_semianalytic_1996}.
     private
     double precision                                   :: timePrevious       , valuePrevious
     class           (linearGrowthClass      ), pointer :: linearGrowth_
     class           (darkMatterParticleClass), pointer :: darkMatterParticle_
    contains
     final     ::                    kitayamaSuto1996Destructor
     procedure :: value           => kitayamaSuto1996Value
     procedure :: gradientTime    => kitayamaSuto1996GradientTime
     procedure :: gradientMass    => kitayamaSuto1996GradientMass
     procedure :: isMassDependent => kitayamaSuto1996IsMassDependent
  end type criticalOverdensityKitayamaSuto1996

  interface criticalOverdensityKitayamaSuto1996
     !% Constructors for the {\normalfont \ttfamily kitayamaSuto1996} critical overdensity class.
     module procedure kitayamaSuto1996ConstructorParameters
     module procedure kitayamaSuto1996ConstructorInternal
  end interface criticalOverdensityKitayamaSuto1996

contains

  function kitayamaSuto1996ConstructorParameters(parameters) result(self)
    !% Constructor for the {\normalfont \ttfamily kitayamaSuto1996} critical overdensity class
    !% which takes a parameter set as input.
    use Input_Parameters
    use Galacticus_Error
    implicit none
    type (criticalOverdensityKitayamaSuto1996)                :: self
    type (inputParameters                    ), intent(inout) :: parameters
    
    !# <objectBuilder class="linearGrowth"             name="self%linearGrowth_"             source="parameters"/>
    !# <objectBuilder class="cosmologyFunctions"       name="self%cosmologyFunctions_"       source="parameters"/>
    !# <objectBuilder class="cosmologicalMassVariance" name="self%cosmologicalMassVariance_" source="parameters"/>
    !# <objectBuilder class="darkMatterParticle"       name="self%darkMatterParticle_"       source="parameters"/>
    self%timePrevious=-1.0d0
    select type (darkMatterParticle_ => self%darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Galacticus_Error_Report('critical overdensity expects a cold dark matter particle'//{introspection:location})
    end select
    !# <inputParametersValidate source="parameters"/>
    return
  end function kitayamaSuto1996ConstructorParameters

  function kitayamaSuto1996ConstructorInternal(linearGrowth_,cosmologyFunctions_,cosmologicalMassVariance_,darkMatterParticle_) result(self)
    !% Internal constructor for the {\normalfont \ttfamily kitayamaSuto1996} critical overdensity class.
    use Dark_Matter_Particles
    use Galacticus_Error
    implicit none
    type (criticalOverdensityKitayamaSuto1996)                        :: self
    class(cosmologyFunctionsClass            ), target, intent(in   ) :: cosmologyFunctions_
    class(linearGrowthClass                  ), target, intent(in   ) :: linearGrowth_
    class(cosmologicalMassVarianceClass      ), target, intent(in   ) :: cosmologicalMassVariance_
    class(darkMatterParticleClass            ), target, intent(in   ) :: darkMatterParticle_

    self%timePrevious              =  -1.0d0
    self%cosmologyFunctions_       => cosmologyFunctions_
    self%linearGrowth_             => linearGrowth_
    self%cosmologicalMassVariance_ => cosmologicalMassVariance_
    self%darkMatterParticle_       => darkMatterParticle_
    ! Require that the dark matter be cold dark matter.
    select type (darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Galacticus_Error_Report('critical overdensity expects a cold dark matter particle'//{introspection:location})
    end select
   return
  end function kitayamaSuto1996ConstructorInternal

  subroutine kitayamaSuto1996Destructor(self)
    !% Destructor for the {\normalfont \ttfamily kitayamaSuto1996} critical overdensity class.
    implicit none
    type(criticalOverdensityKitayamaSuto1996), intent(inout) :: self

    !# <objectDestructor name="self%cosmologyFunctions_"      />
    !# <objectDestructor name="self%linearGrowth_"            />
    !# <objectDestructor name="self%darkMatterParticle_"      />
    !# <objectDestructor name="self%cosmologicalMassVariance_"/>
    return
  end subroutine kitayamaSuto1996Destructor

  double precision function kitayamaSuto1996Value(self,time,expansionFactor,collapsing,mass,node)
    !% Return the critical overdensity at the given time and mass.
    use Numerical_Constants_Math
    implicit none
    class           (criticalOverdensityKitayamaSuto1996), intent(inout)           :: self
    double precision                                     , intent(in   ), optional :: time               , expansionFactor
    logical                                              , intent(in   ), optional :: collapsing
    double precision                                     , intent(in   ), optional :: mass
    type            (treeNode                           ), intent(inout), optional :: node
    double precision                                                               :: time_
    !GCC$ attributes unused :: mass, node
    
    call self%cosmologyFunctions_%epochValidate(time,expansionFactor,collapsing,timeOut=time_)
    if (time_ /= self%timePrevious)                                                                       &
         & self%valuePrevious=+(3.0d0*(12.0d0*Pi)**(2.0d0/3.0d0)/20.0d0)                                  &
         &                    *(1.0d0+0.0123d0*log10(self%cosmologyFunctions_%omegaMatterEpochal(time_))) &
         &                    /                      self%linearGrowth_      %value             (time_)
    kitayamaSuto1996Value=self%valuePrevious
    return
  end function kitayamaSuto1996Value

  double precision function kitayamaSuto1996GradientTime(self,time,expansionFactor,collapsing,mass,node)
    !% Return the gradient with respect to time of critical overdensity at the given time and mass.
    use Numerical_Constants_Math
    implicit none
    class           (criticalOverdensityKitayamaSuto1996), intent(inout)           :: self
    double precision                                     , intent(in   ), optional :: time      , expansionFactor
    logical                                              , intent(in   ), optional :: collapsing
    double precision                                     , intent(in   ), optional :: mass
    type            (treeNode                           ), intent(inout), optional :: node
    double precision                                                               :: time_     , expansionFactor_
    !GCC$ attributes unused :: mass, node
    
    call self%cosmologyFunctions_%epochValidate(time,expansionFactor,collapsing,timeOut=time_,expansionFactorOut=expansionFactor_)
    kitayamaSuto1996GradientTime=+(3.0d0*(12.0d0*Pi)**(2.0d0/3.0d0)/20.0d0)                                                                 &
         &                       *(                                                                                                         &
         &                                +0.0123d0*      self%cosmologyFunctions_%omegaMatterRateOfChange             (time_           )   &
         &                         /                      self%cosmologyFunctions_%omegaMatterEpochal                  (time_           )   &
         &                         /                log  (10.0d0                                                                         )  &
         &                         -(1.0d0+0.0123d0*log10(self%cosmologyFunctions_%omegaMatterEpochal                  (time_           ))) &
         &                         *                      self%linearGrowth_      %logarithmicDerivativeExpansionFactor(time_           )   &
         &                         *                      self%cosmologyFunctions_%expansionRate                       (expansionFactor_)   &
         &                        )                                                                                                         &
         &                       /                        self%linearGrowth_      %value                               (time_           )
    return
  end function kitayamaSuto1996GradientTime

  double precision function kitayamaSuto1996GradientMass(self,time,expansionFactor,collapsing,mass,node)
    !% Return the gradient with respect to mass of critical overdensity at the given time and mass.
    implicit none
    class           (criticalOverdensityKitayamaSuto1996), intent(inout)           :: self
    double precision                                     , intent(in   ), optional :: time      , expansionFactor
    logical                                              , intent(in   ), optional :: collapsing
    double precision                                     , intent(in   ), optional :: mass
    type            (treeNode                           ), intent(inout), optional :: node
    !GCC$ attributes unused :: self, time, expansionFactor, collapsing, mass, node

    kitayamaSuto1996GradientMass=0.0d0
    return
  end function kitayamaSuto1996GradientMass

  logical function kitayamaSuto1996IsMassDependent(self)
    !% Return whether the critical overdensity is mass dependent.
    implicit none
    class(criticalOverdensityKitayamaSuto1996), intent(inout) :: self
    !GCC$ attributes unused :: self

    kitayamaSuto1996IsMassDependent=.false.
    return
  end function kitayamaSuto1996IsMassDependent
