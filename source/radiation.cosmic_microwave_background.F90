!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implements a class for the cosmic microwave background radiation field.

  use Cosmology_Functions
  
  !# <radiationField name="radiationFieldCosmicMicrowaveBackground">
  !#  <description>A radiation field class for the cosmic microwave background.</description>
  !# </radiationField>
  type, extends(radiationFieldBlackBody) :: radiationFieldCosmicMicrowaveBackground
     !% A radiation field class for the cosmic microwave background.
     private
     class           (cosmologyFunctionsClass), pointer :: cosmologyFunctions_
     double precision                                   :: time
   contains
     !@ <objectMethods>
     !@  <object>radiationFieldCosmicMicrowaveBackground</object>
     !@  <objectMethod>
     !@   <method>timeSet</method>
     !@   <type>\void</type>
     !@   <arguments>\doublezero\ time\argin</arguments>
     !@   <description>Set the time for the radiation field.</description>
     !@  </objectMethod>
     !@ </objectMethods>
     final     ::            cosmicMicrowaveBackgroundDestructor
     procedure :: timeSet => cosmicMicrowaveBackgroundTimeSet
  end type radiationFieldCosmicMicrowaveBackground

  interface radiationFieldCosmicMicrowaveBackground
     !% Constructors for the {\normalfont \ttfamily cosmicMicrowaveBackground} radiation field class.
     module procedure cosmicMicrowaveBackgroundConstructorParameters
     module procedure cosmicMicrowaveBackgroundConstructorInternal
  end interface radiationFieldCosmicMicrowaveBackground

contains

  function cosmicMicrowaveBackgroundConstructorParameters(parameters) result(self)
    !% Constructor for the {\normalfont \ttfamily cosmicMicrowaveBackground} radiation field class which takes a parameter list as input.
    use Input_Parameters
    implicit none
    type (radiationFieldCosmicMicrowaveBackground)                :: self
    type (inputParameters                        ), intent(inout) :: parameters
    class(cosmologyFunctionsClass                ), pointer       :: cosmologyFunctions_

    !# <objectBuilder class="cosmologyFunctions" name="cosmologyFunctions_" source="parameters"/>
    self=radiationFieldCosmicMicrowaveBackground(cosmologyFunctions_)
    !# <inputParametersValidate source="parameters"/>
    return
  end function cosmicMicrowaveBackgroundConstructorParameters

  function cosmicMicrowaveBackgroundConstructorInternal(cosmologyFunctions_) result(self)
    !% Internal constructor for the {\normalfont \ttfamily cosmicMicrowaveBackground} radiation field class.
    implicit none
    type (radiationFieldCosmicMicrowaveBackground)                        :: self
    class(cosmologyFunctionsClass                ), intent(in   ), target :: cosmologyFunctions_
    !# <constructorAssign variables="*cosmologyFunctions_"/>

    self%temperature_=-1.0d0 ! Initialize to an unphysical value.
    return
  end function cosmicMicrowaveBackgroundConstructorInternal
  
  subroutine cosmicMicrowaveBackgroundDestructor(self)
    !% Destructor for the {\normalfont \ttfamily cosmicMicrowaveBackground} radiation field class.
    implicit none
    type(radiationFieldCosmicMicrowaveBackground), intent(inout) :: self

    !# <objectDestructor name="self%cosmologyFunctions_"/>
    return
  end subroutine cosmicMicrowaveBackgroundDestructor
  
  subroutine cosmicMicrowaveBackgroundTimeSet(self,time)
    !% Set the time (and temperature) of the cosmic microwave background radiation field.
    implicit none
    class           (radiationFieldCosmicMicrowaveBackground), intent(inout) :: self
    double precision                                         , intent(in   ) :: time

    self%time        =                                                    time
    self%temperature_=self%cosmologyFunctions_%temperatureCMBEpochal(time=time)
    return
  end subroutine cosmicMicrowaveBackgroundTimeSet
