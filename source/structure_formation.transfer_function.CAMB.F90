!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements a transfer function class using the CAMB code.

  use Tables
  use Cosmology_Parameters
  use Input_Parameters
  use File_Utilities
  use Dark_Matter_Particles

  !# <transferFunction name="transferFunctionCAMB" defaultThreadPrivate="yes">
  !#  <description>Provides a transfer function class in which the transfer function is generated by \href{http://camb.info/}{\normalfont \scshape CAMB} using the specified cosmological parameters. The transfer function is written out to a file in the {\normalfont \ttfamily data/} directory and will be re-read later if needed.</description>
  !# </transferFunction>
  type, extends(transferFunctionFile) :: transferFunctionCAMB
     !% A transfer function class which utilizes the CAMB code to compute transfer functions.
     private
     logical                                             :: initialized
     class           (darkMatterParticleClass ), pointer :: darkMatterParticle_
     type            (inputParameters         )          :: descriptor_
     double precision                                    :: wavenumberMaximum
     logical                                             :: wavenumberMaximumReached, lockFileGlobally
   contains
     !@ <objectMethods>
     !@   <object>transferFunctionCAMB</object>
     !@   <objectMethod>
     !@     <method>checkRange</method>
     !@     <type>void</type>
     !@     <arguments>\doublezero wavenumber\argin</arguments>
     !@     <description>Check that the provided wavenumber is within the tabulated range of the transfer function.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     final     ::                          cambDestructor
     procedure :: checkRange            => cambCheckRange
     procedure :: value                 => cambValue
     procedure :: logarithmicDerivative => cambLogarithmicDerivative
  end type transferFunctionCAMB
  
  interface transferFunctionCAMB
     !% Constructors for the file transfer function class.
     module procedure cambConstructorParameters
     module procedure cambConstructorInternal
  end interface transferFunctionCAMB

  ! Smallest maximum wavenumber to tabulate.
  double precision, parameter :: cambLogWavenumberMaximumDefault=log(10.0d0)
  double precision, parameter :: cambWavenumberMaximumLimit     =50000.0d0

  ! Generate a source digest.
  !# <sourceDigest name="cambSourceDigest"/>
  
  ! Global lock descriptor to be used in non-(recent Linux) cases
  type   (lockDescriptor) :: cambFileLockGlobal
  logical                 :: cambFileLockInitialized       =.false.
  logical                 :: cambFileLockPrivateInitialized=.false.
  !$omp threadprivate(cambFileLockPrivateInitialized)

  ! Current file format version for transfer function files.
  integer, parameter :: cambFormatVersionCurrent=1

contains

  function cambConstructorParameters(parameters) result(self)
    !% Constructor for the CAMB transfer function class which takes a parameter set as input.
    use Input_Parameters
    implicit none
    type   (transferFunctionCAMB    )                :: self
    type   (inputParameters         ), intent(inout) :: parameters
    class  (cosmologyParametersClass), pointer       :: cosmologyParameters_    
    class  (darkMatterParticleClass ), pointer       :: darkMatterParticle_
    logical                                          :: lockFileGlobally
    
    !# <inputParameter>
    !#   <name>lockFileGlobally</name>
    !#   <source>parameters</source>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>If true, attempt to lock the CAMB transfer function file before accessing. Otherwise, no locking is attempted.</description>
    !#   <type>boolean</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    !# <objectBuilder class="darkMatterParticle"  name="darkMatterParticle_"  source="parameters"/>
    self=transferFunctionCAMB(darkMatterParticle_,cosmologyParameters_,lockFileGlobally)
    !# <inputParametersValidate source="parameters"/>
    return
  end function cambConstructorParameters
  
  function cambConstructorInternal(darkMatterParticle_,cosmologyParameters_,lockFileGlobally) result(self)
    !% Internal constructor for the \href{http://camb.info}{\normalfont \scshape CAMB} transfer function class.
    use Input_Parameters
    use Galacticus_Error
    use Numerical_Constants_Astronomical
    use Galacticus_Paths
    use Hashes_Cryptographic
    use Dark_Matter_Particles
    use System_Command
    implicit none
    type     (transferFunctionCAMB    )                          :: self
    class    (darkMatterParticleClass ), intent(in   ), target   :: darkMatterParticle_
    class    (cosmologyParametersClass), intent(in   ), target   :: cosmologyParameters_    
    logical                            , intent(in   ), optional :: lockFileGlobally
    character(len=32                  )                          :: parameterLabel
    type     (varying_string          )                          :: uniqueLabel
    !# <optionalArgument name="lockFileGlobally" defaultsTo=".true." />
    
    ! Require that the dark matter be cold dark matter.
    self%darkMatterParticle_ => darkMatterParticle_
    select type (darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Galacticus_Error_Report('transfer function expects a cold dark matter particle'//{introspection:location})
    end select
    ! Set lock file option.
    self%lockFileGlobally=lockFileGlobally_
    ! Set cosmoogical parameters.
    self%cosmologyParameters_ => cosmologyParameters_
    ! Get a constructor descriptor for this object.
    self%descriptor_=inputParameters()
    call self%cosmologyParameters_%descriptor(self%descriptor_)
    ! Add primordial helium abundance to the descriptor.
    write (parameterLabel,'(f4.2)') heliumByMassPrimordial
    call self%descriptor_%addParameter("Y_He",parameterLabel)
    ! Add the unique label string to the descriptor.
    uniqueLabel=self%descriptor_%serializeToString()// &
         &      "_sourceDigest:"                    // &
         &      cambSourceDigest
    call self%descriptor_%addParameter("uniqueLabel",char(uniqueLabel))
    ! Generate the name of the data file.
    self%fileName=char(galacticusPath(pathTypeDataDynamic))                       // &
         &                           'largeScaleStructure/transfer_function_CAMB_'// &
         &                           Hash_MD5(uniqueLabel)                        // &
         &                           '.hdf5'
    ! Create the directory.
    call System_Command_Do("mkdir -p `dirname "//self%fileName//"`")
    self%initialized=.false.
    ! Set maximum wavenumber.
    self%wavenumberMaximum=+cambWavenumberMaximumLimit                                         &
         &                 *self%cosmologyParameters_%hubbleConstant(units=hubbleUnitsLittleH)
    self%wavenumberMaximumReached=.false.
    return
  end function cambConstructorInternal

  subroutine cambDestructor(self)
    !% Destructor for the CAMB transfer function class.
    implicit none
    type(transferFunctionCAMB), intent(inout) :: self
    
    !# <objectDestructor name="self%cosmologyParameters_"/>
    !# <objectDestructor name="self%darkMatterParticle_" />
    return
  end subroutine cambDestructor
  
  subroutine cambCheckRange(self,wavenumber)
    !% Check that the provided wavenumber is within the tabulated range and, if not, recompute
    !% the CAMB transfer function.
    !$ use OMP_Lib
    use, intrinsic :: ISO_C_Binding
    use               IO_HDF5
    use               File_Utilities
    use               System_Command
    use               Galacticus_Paths
    use               String_Handling
    use               Numerical_Comparison
    use               Galacticus_Error
    use               Galacticus_Display
    use               Numerical_Constants_Astronomical
    implicit none
    class           (transferFunctionCAMB), intent(inout)               :: self
    double precision                      , intent(in   )               :: wavenumber
    double precision                      , allocatable  , dimension(:) :: cambTransferWavenumber      , cambTransferTransfer
    type            (lockDescriptor      )                              :: fileLock
    character       (len=32              )                              :: wavenumberLabel
    character       (len=255             )                              :: hostName                    , cambTransferLine
    type            (varying_string      )                              :: command                     , parameterFile
    logical                                                             :: makeTransferFunction
    double precision                                                    :: wavenumberCAMB
    integer                                                             :: status                      , cambParameterFile   , &
         &                                                                 i                           , cambTransferFile
    integer         (c_size_t            )                              :: cambTransferCount
    type            (hdf5Object          )                              :: cambOutput                  , parametersGroup     , &
         &                                                                 extrapolationWavenumberGroup, extrapolationGroup
    
    ! If the file has been read and the wavenumber is within range, simply return.
    makeTransferFunction=.false.
    if (self%initialized) then
       if     (                                       &
            &   wavenumber > exp(self%transfer%x(-1)) &
            &  .and.                                  &
            &   .not.self%wavenumberMaximumReached    &
            & ) makeTransferFunction=.true.
    else
       makeTransferFunction=.true.
    end if    
    if (.not.makeTransferFunction) return
    ! If the file exists but has not yet been read, read it now.
    if (self%lockFileGlobally) then
       if (.not.cambFileLockInitialized) then
          !$omp critical (cambFileLockInitialize)
          if (.not.cambFileLockInitialized) then
             call File_Lock_Initialize(cambFileLockGlobal)
             cambFileLockInitialized=.true.
          end if
          !$omp end critical (cambFileLockInitialize)
       end if
    else
       call File_Lock_Initialize(fileLock)
    end if
    if (.not.self%initialized.and.File_Exists(self%fileName)) then
       if (self%lockFileGlobally) then
          call File_Lock(char(self%fileName),cambFileLockGlobal)
       else
          call File_Lock(char(self%fileName),fileLock          )
       end if
       call self%readFile(char(self%fileName))
       if (self%lockFileGlobally) then
          call File_Unlock(cambFileLockGlobal)
       else
          call File_Unlock(fileLock          )
       end if
    else if (.not.self%initialized .or. wavenumber > self%transfer%x(-1)) then
       ! If the wavenumber if out of range, recompute the CAMB transfer function.
       ! Get a lock on the relevant lock file.
       if (self%lockFileGlobally) then
          call File_Lock(char(self%fileName),cambFileLockGlobal)
       else
          call File_Lock(char(self%fileName),fileLock          )
       end if
       ! Remove the transfer function file so that a new one will be created.
       command='rm -f '//self%fileName
       call System_Command_Do(command)
       ! Download CAMB if necessary.
       if (.not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB.tar.gz")) then
          call Galacticus_Display_Message("downloading CAMB code....",verbosityWorking)
          call System_Command_Do("wget https://github.com/cmbant/CAMB/archive/0.1.7.tar.gz -O "//galacticusPath(pathTypeExec)//"aux/CAMB.tar.gz",status)
          if (status /= 0 .or. .not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB.tar.gz")) call Galacticus_Error_Report("unable to download CAMB"//{introspection:location})
       end if
       ! Unpack the code.
       if (.not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB")) then
          call Galacticus_Display_Message("unpacking CAMB code....",verbosityWorking)
          call System_Command_Do("tar -x -v -z -C "//galacticusPath(pathTypeExec)//"aux -f "//galacticusPath(pathTypeExec)//"aux/CAMB.tar.gz");
          if (status /= 0 .or. .not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB")) call Galacticus_Error_Report('failed to unpack CAMB code'//{introspection:location})
       end if
       ! Build the CAMB code.
       if (.not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB/camb")) then
          call Galacticus_Display_Message("compiling CAMB code",verbosityWorking)
          call System_Command_Do('cd '//galacticusPath(pathTypeExec)//'/aux/CAMB/; sed -r -i~ s/"ifortErr\s*=.*"/"ifortErr = 1"/ Makefile; sed -r -i~ s/"gfortErr\s*=.*"/"gfortErr = 0"/ Makefile; sed -r -i~ s/"^FFLAGS\s*\+=\s*\-march=native"/"FFLAGS+="/ Makefile; sed -r -i~ s/"^FFLAGS\s*=\s*.*"/"FFLAGS = -Ofast -fopenmp"/ Makefile; make -j1',status);
          if (status /= 0 .or. .not.File_Exists(galacticusPath(pathTypeExec)//"aux/CAMB/camb")) call Galacticus_Error_Report("failed to build CAMB code"//{introspection:location})
       end if
       ! Determine maximum wavenumber.
       wavenumberCAMB=exp(max(log(wavenumber)+1.0d0,cambLogWavenumberMaximumDefault))
       if (wavenumberCAMB > self%wavenumberMaximum) then
          wavenumberCAMB=self%wavenumberMaximum
          self%wavenumberMaximumReached=.true.
       end if
       write (wavenumberLabel,'(e12.6)') wavenumberCAMB
       ! Construct input file for CAMB.
       call Get_Environment_Variable('HOSTNAME',hostName)
       call System_Command_Do("mkdir -p "//galacticusPath(pathTypeDataDynamic)//'largeScaleStructure')
       parameterFile=galacticusPath(pathTypeDataDynamic)//'largeScaleStructure/transfer_function_parameters'//'_'//trim(hostName)//'_'//GetPID()
       !$ parameterFile=parameterFile//'_'//OMP_Get_Thread_Num()
       parameterFile=parameterFile//'.xml'
       open(newunit=cambParameterFile,file=char(parameterFile),status='unknown',form='formatted')
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'output_root                  ','camb'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'get_scalar_cls               ','F'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'get_vector_cls               ','F'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'get_tensor_cls               ','F'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'get_transfer                 ','T'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'do_lensing                   ','F'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'do_nonlinear                 ',0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'l_max_scalar                 ',2200.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'l_max_tensor                 ',1500.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'k_eta_max_tensor             ',3000.0d0
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'use_physical                 ','F'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'omega_baryon                 ',self%cosmologyParameters_%OmegaBaryon()
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'omega_cdm                    ',self%cosmologyParameters_%OmegaMatter()-self%cosmologyParameters_%OmegaBaryon()
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'omega_lambda                 ',self%cosmologyParameters_%OmegaDarkEnergy()
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'omega_neutrino               ',0.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'omk                          ',0.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'hubble                       ',self%cosmologyParameters_%HubbleConstant()
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'w                            ',-1.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'cs2_lam                      ',1.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'temp_cmb                     ',self%cosmologyParameters_%temperatureCMB()
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'helium_fraction              ',heliumByMassPrimordial
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'massless_neutrinos           ',2.046d0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'nu_mass_eigenstates          ',1
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'massive_neutrinos            ',1
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'share_delta_neff             ','T'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'nu_mass_fractions            ',1.0d0
       write (cambParameterFile,'(a,1x,"=",1x      )') 'nu_mass_degeneracies         '
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'initial_power_num            ',1
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'pivot_scalar                 ',0.05d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'pivot_tensor                 ',0.05d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'scalar_amp(1)                ',2.1d-9
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'scalar_spectral_index(1)     ',0.96d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'scalar_nrun(1)               ',0.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'tensor_spectral_index(1)     ',0.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'initial_ratio(1)             ',1.0d0
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'reionization                 ','T'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 're_use_optical_depth         ','T'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 're_optical_depth             ',0.09d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 're_redshift                  ',11.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 're_delta_redshift            ',1.5d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 're_ionization_frac           ',-1.0d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'RECFAST_fudge                ',1.14d0
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'RECFAST_fudge_He             ',0.86d0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'RECFAST_Heswitch             ',6
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'RECFAST_Hswitch              ','T'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'initial_condition            ',1
       write (cambParameterFile,'(a,1x,"=",1x,5(i2))') 'initial_vector               ',-1,0,0,0,0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'vector_mode                  ',0
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'COBE_normalize               ','F'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'CMB_outputscale              ',7.42835025d12 
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'transfer_high_precision      ','F'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'transfer_kmax                ',wavenumberCAMB/self%cosmologyParameters_%HubbleConstant(units=hubbleUnitsLittleH)
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'transfer_k_per_logint        ',0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'transfer_num_redshifts       ',1
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'transfer_interp_matterpower  ','T'
       write (cambParameterFile,'(a,1x,"=",1x,e12.6)') 'transfer_redshift(1)         ',0.0d0
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'transfer_filename(1)         ','transfer_out.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'transfer_matterpower(1)      ','matterpower.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'scalar_output_file           ','scalCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'vector_output_file           ','vecCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'tensor_output_file           ','tensCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'total_output_file            ','totCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'lensed_output_file           ','lensedCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'lensed_total_output_file     ','lensedtotCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'lens_potential_output_file   ','lenspotentialCls.dat'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'FITS_filename                ','scalCls.fits'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'do_lensing_bispectrum        ','F'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'do_primordial_bispectrum     ','F'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_nfields           ',1
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_slice_base_L      ',0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_ndelta            ',3
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_delta(1)          ',0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_delta(2)          ',2
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_delta(3)          ',4
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'bispectrum_do_fisher         ','F'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_fisher_noise      ',0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_fisher_noise_pol  ',0
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'bispectrum_fisher_fwhm_arcmin',7
       write (cambParameterFile,'(a,1x,"=",1x      )') 'bispectrum_full_output_file  '
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'bispectrum_full_output_sparse','F'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'bispectrum_export_alpha_beta ','F'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'feedback_level               ',1
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'derived_parameters           ','T'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'lensing_method               ',1
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'accurate_BB                  ','F'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'massive_nu_approx            ',1
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'accurate_polarization        ','T'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'accurate_reionization        ','T'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'do_tensor_neutrinos          ','T'
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'do_late_rad_truncation       ','T'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'number_of_threads            ',0
       write (cambParameterFile,'(a,1x,"=",1x,a    )') 'high_accuracy_default        ','T'
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'accuracy_boost               ',1
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'l_accuracy_boost             ',1
       write (cambParameterFile,'(a,1x,"=",1x,i1   )') 'l_sample_boost               ',1
       close(cambParameterFile)
       ! Run CAMB.
       call System_Command_Do(galacticusPath(pathTypeExec)//"aux/CAMB/camb "//parameterFile)
        ! Read the CAMB transfer function file.
       cambTransferCount=Count_Lines_In_File("camb_transfer_out.dat","#")
       allocate(cambTransferWavenumber(cambTransferCount))
       allocate(cambTransferTransfer  (cambTransferCount))
       open(newunit=cambTransferFile,file="camb_transfer_out.dat",status='old',form='formatted')
       i=0
       do while (i < cambTransferCount)
          read (cambTransferFile,'(a)',iostat=status) cambTransferLine
          if (status == 0) then
             if (cambTransferLine(1:1) /= "#") then
                i=i+1
                read (cambTransferLine,*) cambTransferWavenumber(i),cambTransferTransfer(i)
             end if
          else
             call Galacticus_Error_Report('unable to read CAMB transfer function file'//{introspection:location})
          end if
       end do
       close(cambTransferFile)
       ! Remove temporary files.
       command="rm -f "                    // &
            &   parameterFile         //" "// &
            &  "camb_params.ini"      //" "// &
            &  "camb_transfer_out.dat"//" "// &
            &  "camb_matterpower.dat"
       call System_Command_Do(command)
       ! Convert from CAMB units to Galacticus units.
       cambTransferWavenumber=+cambTransferWavenumber                                             &
            &                 *self%cosmologyParameters_%HubbleConstant(units=hubbleUnitsLittleH)       
       ! Construct the output HDF5 file.
       call cambOutput%openFile(char(self%fileName))
       call cambOutput%writeDataset(cambTransferWavenumber,'wavenumber'      )
       call cambOutput%writeDataset(cambTransferTransfer  ,'transferFunction')
       call cambOutput%writeAttribute('Cold dark matter power spectrum created by CAMB.','description')
       call cambOutput%writeAttribute(cambFormatVersionCurrent,'fileFormat')
       parametersGroup=cambOutput%openGroup('parameters')
       call parametersGroup%writeAttribute(self%cosmologyParameters_%HubbleConstant (),'HubbleConstant' )
       call parametersGroup%writeAttribute(self%cosmologyParameters_%OmegaBaryon    (),'OmegaBaryon'    )
       call parametersGroup%writeAttribute(self%cosmologyParameters_%OmegaDarkEnergy(),'OmegaDarkEnergy')
       call parametersGroup%writeAttribute(self%cosmologyParameters_%OmegaMatter    (),'OmegaMatter'    )
       call parametersGroup%writeAttribute(self%cosmologyParameters_%temperatureCMB (),'temperatureCMB' )
       call parametersGroup%close()
       extrapolationGroup          =cambOutput        %openGroup('extrapolation')
       extrapolationWavenumberGroup=extrapolationGroup%openGroup('wavenumber'   )
       call extrapolationWavenumberGroup%writeAttribute('extrapolate','low' )
       call extrapolationWavenumberGroup%writeAttribute('extrapolate','high')
       call extrapolationWavenumberGroup%close()
       call extrapolationGroup          %close()
       call cambOutput                  %close()
       ! Read the newly created file.
       call self%readFile(char(self%fileName))
       ! Unlock the lock file.
       if (self%lockFileGlobally) then
          call File_Unlock(cambFileLockGlobal)
       else
          call File_Unlock(fileLock          )
       end if      
    end if
    ! Check the maximum wavenumber.
    if (self%transfer%x(-1) > log(self%wavenumberMaximum)-0.01d0) self%wavenumberMaximumReached=.true.
    ! Record that the transfer function has now been initialized.
    self%initialized=.true.
    return
  end subroutine cambCheckRange

  double precision function cambValue(self,wavenumber)
    !% Return the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambValue=self%transferFunctionFile%value(wavenumber)
    return
  end function cambValue

  double precision function cambLogarithmicDerivative(self,wavenumber)
    !% Return the logarithmic derivative of the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambLogarithmicDerivative=self%transferFunctionFile%logarithmicDerivative(wavenumber)
    return
  end function cambLogarithmicDerivative
  
