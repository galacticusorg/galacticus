!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements a transfer function class using the CAMB code.

  use Tables
  use Cosmology_Parameters
  use File_Utilities
  use Dark_Matter_Particles

  !# <transferFunction name="transferFunctionCAMB">
  !#  <description>Provides a transfer function class in which the transfer function is generated by \href{http://camb.info/}{\normalfont \scshape CAMB} using the specified cosmological parameters. The transfer function is written out to a file in the {\normalfont \ttfamily data/} directory and will be re-read later if needed.</description>
  !# </transferFunction>
  type, extends(transferFunctionFile) :: transferFunctionCAMB
     !% A transfer function class which utilizes the CAMB code to compute transfer functions.
     private
     logical                                            :: initialized
     class           (darkMatterParticleClass), pointer :: darkMatterParticle_      => null()
     double precision                                   :: wavenumberMaximum
     logical                                            :: wavenumberMaximumReached          , lockFileGlobally
   contains
     !@ <objectMethods>
     !@   <object>transferFunctionCAMB</object>
     !@   <objectMethod>
     !@     <method>checkRange</method>
     !@     <type>void</type>
     !@     <arguments>\doublezero wavenumber\argin</arguments>
     !@     <description>Check that the provided wavenumber is within the tabulated range of the transfer function.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     final     ::                          cambDestructor
     procedure :: checkRange            => cambCheckRange
     procedure :: value                 => cambValue
     procedure :: logarithmicDerivative => cambLogarithmicDerivative
  end type transferFunctionCAMB
  
  interface transferFunctionCAMB
     !% Constructors for the file transfer function class.
     module procedure cambConstructorParameters
     module procedure cambConstructorInternal
  end interface transferFunctionCAMB

  ! Global lock descriptor to be used in non-(recent Linux) cases
  type            (lockDescriptor)            :: cambFileLockGlobal
  logical                                     :: cambFileLockInitialized   =.false.

  ! Smallest maximum wavenumber to tabulate.
  double precision                , parameter :: cambWavenumberMaximumLimit=50000.0d0

contains

  function cambConstructorParameters(parameters) result(self)
    !% Constructor for the CAMB transfer function class which takes a parameter set as input.
    use Input_Parameters
    implicit none
    type            (transferFunctionCAMB    )                :: self
    type            (inputParameters         ), intent(inout) :: parameters
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_    
    class           (darkMatterParticleClass ), pointer       :: darkMatterParticle_
    logical                                                   :: lockFileGlobally
    double precision                                          :: redshift
    
    !# <inputParameter>
    !#   <name>redshift</name>
    !#   <source>parameters</source>
    !#   <defaultValue>0.0d0</defaultValue>
    !#   <description>The redshift at which the transfer function should be evaluated.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>lockFileGlobally</name>
    !#   <source>parameters</source>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>If true, attempt to lock the CAMB transfer function file before accessing. Otherwise, no locking is attempted.</description>
    !#   <type>boolean</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    !# <objectBuilder class="darkMatterParticle"  name="darkMatterParticle_"  source="parameters"/>
    self=transferFunctionCAMB(darkMatterParticle_,cosmologyParameters_,redshift,lockFileGlobally)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="cosmologyParameters_"/>
    !# <objectDestructor name="darkMatterParticle_" />
    return
  end function cambConstructorParameters
  
  function cambConstructorInternal(darkMatterParticle_,cosmologyParameters_,redshift,lockFileGlobally) result(self)
    !% Internal constructor for the \href{http://camb.info}{\normalfont \scshape CAMB} transfer function class.
    use Input_Parameters
    use Galacticus_Error
    use Numerical_Constants_Astronomical
    use Dark_Matter_Particles
    implicit none
    type            (transferFunctionCAMB    )                          :: self
    class           (darkMatterParticleClass ), intent(in   ), target   :: darkMatterParticle_
    class           (cosmologyParametersClass), intent(in   ), target   :: cosmologyParameters_
    double precision                          , intent(in   )           :: redshift
    logical                            ,        intent(in   ), optional :: lockFileGlobally
    !# <optionalArgument name="lockFileGlobally" defaultsTo=".true." />
    !# <constructorAssign variables="redshift, *darkMatterParticle_, *cosmologyParameters_"/>
    
    ! Require that the dark matter be cold dark matter.
    select type (darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Galacticus_Error_Report('transfer function expects a cold dark matter particle'//{introspection:location})
    end select
    ! Set lock file option.
    self%lockFileGlobally=lockFileGlobally_
    ! Set initialization state.
    self%initialized=.false.
    ! Set maximum wavenumber.
    self%wavenumberMaximum=+cambWavenumberMaximumLimit                                         &
         &                 *self%cosmologyParameters_%hubbleConstant(units=hubbleUnitsLittleH)
    self%wavenumberMaximumReached=.false.
    return
  end function cambConstructorInternal

  subroutine cambDestructor(self)
    !% Destructor for the CAMB transfer function class.
    implicit none
    type(transferFunctionCAMB), intent(inout) :: self
    
    !# <objectDestructor name="self%cosmologyParameters_"/>
    !# <objectDestructor name="self%darkMatterParticle_" />
    return
  end subroutine cambDestructor
  
  subroutine cambCheckRange(self,wavenumber)
    !% Check that the provided wavenumber is within the tabulated range and, if not, recompute
    !% the CAMB transfer function.
    use Interfaces_CAMB, only : Interface_CAMB_Transfer_Function
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber
    logical                                               :: makeTransferFunction
    type            (lockDescriptor      )                :: fileLock

    ! If the file has been read and the wavenumber is within range, simply return.
    makeTransferFunction=.false.
    if (self%initialized) then
       if     (                                       &
            &   wavenumber > exp(self%transfer%x(-1)) &
            &  .and.                                  &
            &   .not.self%wavenumberMaximumReached    &
            & ) makeTransferFunction=.true.
    else
       makeTransferFunction=.true.
    end if
    if (.not.makeTransferFunction) return
    ! Retrieve the transfer function.
    call Interface_CAMB_Transfer_Function(self%cosmologyParameters_,[self%redshift],wavenumber,self%wavenumberMaximum,self%lockFileGlobally,self%fileName,self%wavenumberMaximumReached)
    ! Initialize the file lock.
    if (self%lockFileGlobally) then
       if (.not.cambFileLockInitialized) then
          !$omp critical (cambFileLockInitialize)
          if (.not.cambFileLockInitialized) then
             call File_Lock_Initialize(cambFileLockGlobal)
             cambFileLockInitialized=.true.
          end if
          !$omp end critical (cambFileLockInitialize)
       end if
    else
       call File_Lock_Initialize(fileLock)
    end if
    ! Get a lock on the relevant lock file.
    if (self%lockFileGlobally) then
       call File_Lock(char(self%fileName),cambFileLockGlobal)
    else
       call File_Lock(char(self%fileName),fileLock          )
    end if
    ! Read the newly created file.
    call self%readFile(char(self%fileName))
    ! Unlock the lock file.
    if (self%lockFileGlobally) then
       call File_Unlock(cambFileLockGlobal)
    else
       call File_Unlock(fileLock          )
    end if
    ! Check the maximum wavenumber.
    if (self%transfer%x(-1) > log(self%wavenumberMaximum)-0.01d0) self%wavenumberMaximumReached=.true.
    ! Record that the transfer function has now been initialized.
    self%initialized=.true.
    return
  end subroutine cambCheckRange

  double precision function cambValue(self,wavenumber)
    !% Return the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambValue=self%transferFunctionFile%value(wavenumber)
    return
  end function cambValue

  double precision function cambLogarithmicDerivative(self,wavenumber)
    !% Return the logarithmic derivative of the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambLogarithmicDerivative=self%transferFunctionFile%logarithmicDerivative(wavenumber)
    return
  end function cambLogarithmicDerivative
  
