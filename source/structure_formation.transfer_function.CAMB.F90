!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements a transfer function class using the CAMB code.

  use Tables
  use Cosmology_Parameters
  use Input_Parameters2

  !# <transferFunction name="transferFunctionCAMB" defaultThreadPrivate="yes">
  !#  <description>Provides a transfer function class in which the transfer function is generated by \href{http://camb.info/}{\normalfont \scshape CAMB} using the specified cosmological parameters. The transfer function is written out to a file in the {\normalfont \ttfamily data/} directory and will be re-read later if needed.</description>
  !# </transferFunction>
  type, extends(transferFunctionFile) :: transferFunctionCAMB
     !% A transfer function class which utilizes the CAMB code to compute transfer functions.
     private
     type            (varying_string          )          :: fileName
     logical                                             :: initialized
     class           (cosmologyParametersClass), pointer :: cosmologyParameters_
     type            (inputParameters         )          :: descriptor_
     double precision                                    :: wavenumberMaximum
     logical                                             :: wavenumberMaximumReached, lockFile
   contains
     !@ <objectMethods>
     !@   <object>transferFunctionCAMB</object>
     !@   <objectMethod>
     !@     <method>checkRange</method>
     !@     <type>void</type>
     !@     <arguments>\doublezero wavenumber\argin</arguments>
     !@     <description>Check that the provided wavenumber is within the tabulated range of the transfer function.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     final     ::                          cambDestructor
     procedure :: checkRange            => cambCheckRange
     procedure :: value                 => cambValue
     procedure :: logarithmicDerivative => cambLogarithmicDerivative
     procedure :: descriptor            => cambDescriptor
  end type transferFunctionCAMB
  
  interface transferFunctionCAMB
     !% Constructors for the file transfer function class.
     module procedure cambConstructorParameters
     module procedure cambConstructorInternal
  end interface transferFunctionCAMB

  ! Smallest maximum wavenumber to tabulate.
  double precision, parameter :: cambLogWavenumberMaximumDefault=log(10.0d0)
  double precision, parameter :: cambWavenumberMaximumLimit     =50000.0d0

  ! Generate a source digest.
  !# <sourceDigest name="cambSourceDigest"/>

contains

  function cambConstructorParameters(parameters)
    !% Constructor for the CAMB transfer function class which takes a parameter set as input.
    use Input_Parameters2
    use Dark_Matter_Particles
    implicit none
    type  (transferFunctionCAMB     )                :: cambConstructorParameters
    type  (inputParameters          ), intent(inout) :: parameters
    class  (cosmologyParametersClass), pointer       :: cosmologyParameters_    
    class  (darkMatterParticleClass ), pointer       :: darkMatterParticle_
    logical                                          :: lockFile
    
    !# <inputParameter>
    !#   <name>lockFile</name>
    !#   <source>parameters</source>
    !#   <defaultValue>.true.</defaultValue>
    !#   <description>If true, attempt to lock the CAMB transfer function file before accessing. Otherwise, no locking is attempted.</description>
    !#   <type>boolean</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    !# <objectBuilder class="darkMatterParticle"  name="darkMatterParticle_"  source="parameters"/>
    cambConstructorParameters=cambConstructorInternal(darkMatterParticle_,cosmologyParameters_,lockFile)
    return
  end function cambConstructorParameters
  
  function cambConstructorInternal(darkMatterParticle_,cosmologyParameters_,lockFile)
    !% Internal constructor for the \href{http://camb.info}{\normalfont \scshape CAMB} transfer function class.
    use Input_Parameters2
    use Galacticus_Error
    use Numerical_Constants_Astronomical
    use Galacticus_Input_Paths
    use Hashes_Cryptographic
    use Dark_Matter_Particles
    implicit none
    type     (transferFunctionCAMB    )                                  :: cambConstructorInternal
    class    (darkMatterParticleClass ), intent(in   )                   :: darkMatterParticle_
    class    (cosmologyParametersClass), intent(in   ), target, optional :: cosmologyParameters_    
    logical                            , intent(in   )        , optional :: lockFile
    character(len=32                  )                                  :: parameterLabel
    type     (varying_string          )                                  :: uniqueLabel
    !# <optionalArgument name="lockFile" defaultsTo=".true." />
    
    ! Require that the dark matter be cold dark matter.
    select type (darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Galacticus_Error_Report('cambConstructorInternal','transfer function expects a cold dark matter particle')
    end select
    ! Set lock file option.
    cambConstructorInternal%lockFile=lockFile_
    ! Determine the cosmological parameters to use.
    if (present(cosmologyParameters_)) then
       cambConstructorInternal%cosmologyParameters_ => cosmologyParameters_
    else
       cambConstructorInternal%cosmologyParameters_ => cosmologyParameters()
    end if
    ! Get a constructor descriptor for this object.
    cambConstructorInternal%descriptor_=inputParameters()
    call cambConstructorInternal%cosmologyParameters_%descriptor(cambConstructorInternal%descriptor_)
    ! Add primordial helium abundance to the descriptor.
    write (parameterLabel,'(f4.2)') heliumByMassPrimordial
    call cambConstructorInternal%descriptor_%addParameter("Y_He",parameterLabel)
    ! Add the unique label string to the descriptor.
    uniqueLabel=cambConstructorInternal%descriptor_%serializeToString()// &
         &      "_sourceDigest:"                                       // &
         &      cambSourceDigest
    call cambConstructorInternal%descriptor_%addParameter("uniqueLabel",char(uniqueLabel))
    ! Generate the name of the data file.
    cambConstructorInternal%fileName=char(Galacticus_Input_Path())                     // &
         &                           'data/largeScaleStructure/transfer_function_CAMB_'// &
         &                           Hash_MD5(uniqueLabel)                             // &
         &                           '.xml'
    cambConstructorInternal%initialized=.false.
    ! Set maximum wavenumber.
    cambConstructorInternal%wavenumberMaximum=+cambWavenumberMaximumLimit                                                            &
         &                                    *cambConstructorInternal%cosmologyParameters_%hubbleConstant(units=hubbleUnitsLittleH)
    cambConstructorInternal%wavenumberMaximumReached=.false.
    return
  end function cambConstructorInternal

  subroutine cambDestructor(self)
    !% Destructor for the CAMB transfer function class.
    implicit none
    type(transferFunctionCAMB), intent(inout) :: self
    
    !# <objectDestructor name="self%cosmologyParameters_"/>
    return
  end subroutine cambDestructor
  
  subroutine cambCheckRange(self,wavenumber)
    !% Check that the provided wavenumber is within the tabulated range and, if not, recompute
    !% the CAMB transfer function.
    use, intrinsic :: ISO_C_Binding
    use FoX_wxml
    use FoX_DOM
    use System_Command
    use Galacticus_Input_Paths
    use String_Handling
    use File_Utilities
    use Numerical_Comparison
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber
    type            (lockDescriptor      )                :: fileLock
    character       (len=32              )                :: wavenumberLabel
    character       (len=255             )                :: hostName
    type            (varying_string      )                :: command             , parameterFile
    logical                                               :: makeTransferFunction
    double precision                                      :: wavenumberCAMB

    ! If the file has been read and the wavenumber is within range, simply return.
    makeTransferFunction=.false.
    if (self%initialized) then
       if     (                                       &
            &   wavenumber > exp(self%transfer%x(-1)) &
            &  .and.                                  &
            &   .not.self%wavenumberMaximumReached    &
            & ) makeTransferFunction=.true.
    else
       makeTransferFunction=.true.
    end if    
    if (.not.makeTransferFunction) return
    ! If the file exists but has not yet been read, read it now.
    if (.not.self%initialized.and.File_Exists(self%fileName)) then
       if (self%lockFile) call File_Lock(char(self%fileName//".lock"),fileLock)
       call self%readFile(char(self%fileName))
       if (self%lockFile) call File_Unlock(fileLock)
    else if (.not.self%initialized .or. wavenumber > self%transfer%x(-1)) then
       ! If the wavenumber if out of range, recompute the CAMB transfer function.
       ! Get a lock on the relevant lock file.
       if (self%lockFile) call File_Lock(char(self%fileName//".lock"),fileLock)
       ! Remove the transfer function file so that a new one will be created.
       command='rm -f '//self%fileName
       call System_Command_Do(command)
       ! Generate a parameter file for the CAMB wrapper script.
       call Get_Environment_Variable('HOSTNAME',hostName)
       parameterFile=Galacticus_Input_Path()//'data/transfer_function_parameters'//'_'//trim(hostName)//'_'//GetPID()//'.xml'
       call self%descriptor_%serializeToXML(parameterFile)
       ! Run CAMB wrapper script.
       wavenumberCAMB=exp(max(log(wavenumber)+1.0d0,cambLogWavenumberMaximumDefault))
       if (wavenumberCAMB > self%wavenumberMaximum) then
          wavenumberCAMB=self%wavenumberMaximum
          self%wavenumberMaximumReached=.true.
       end if
       write (wavenumberLabel,'(e12.6)') wavenumberCAMB
       command=char(Galacticus_Input_Path())                     // &
            &  'scripts/aux/CAMB_Driver.pl'                 //' '// &
            &  parameterFile                                //' '// &
            &  self%fileName                                //' '// &
            &  trim(wavenumberLabel)                        //' '// &
            &  fileFormatVersionCurrent
       call System_Command_Do(command)
       ! Remove the parameter file.
       command='rm -f '//parameterFile
       call System_Command_Do(command)
       ! Read the newly created file.
       call self%readFile(char(self%fileName))
       ! Unlock the lock file.
       if (self%lockFile) call File_Unlock(fileLock)
    end if
    ! Check the maximum wavenumber.
    if (self%transfer%x(-1) > log(self%wavenumberMaximum)-0.01d0) self%wavenumberMaximumReached=.true.
    ! Record that the transfer function has now been initialized.
    self%initialized=.true.
    return
  end subroutine cambCheckRange

  double precision function cambValue(self,wavenumber)
    !% Return the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambValue=self%transferFunctionFile%value(wavenumber)
    return
  end function cambValue

  double precision function cambLogarithmicDerivative(self,wavenumber)
    !% Return the logarithmic derivative of the transfer function at the given wavenumber.
    implicit none
    class           (transferFunctionCAMB), intent(inout) :: self
    double precision                      , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    cambLogarithmicDerivative=self%transferFunctionFile%logarithmicDerivative(wavenumber)
    return
  end function cambLogarithmicDerivative
  
  subroutine cambDescriptor(self,descriptor)
    !% Add parameters to an input parameter list descriptor which could be used to recreate this object.
    use Input_Parameters2
    use FoX_DOM
    implicit none
    class(transferFunctionCAMB), intent(inout) :: self
    type (inputParameters     ), intent(inout) :: descriptor
    type (inputParameters     )                :: subParameters

    call descriptor%addParameter("transferFunctionMethod","CAMB")
    subParameters=descriptor%subparameters("transferFunctionMethod")
    call self%cosmologyParameters_%descriptor(subParameters)
    return
  end subroutine cambDescriptor
