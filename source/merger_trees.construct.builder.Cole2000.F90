!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% An implementation of a merger tree builder using the algorithm of \cite{cole_hierarchical_2000}.

  use Merger_Trees_Build_Mass_Resolution
  use Cosmology_Functions
  use Statistics_Distributions
  use Cosmological_Density_Field
  use Merger_Tree_Branching

  !# <mergerTreeBuilder name="mergerTreeBuilderCole2000">
  !#  <description>Merger trees are built using the algorithm of \cite{cole_hierarchical_2000}.</description>
  !# </mergerTreeBuilder>

  type, extends(mergerTreeBuilderClass) :: mergerTreeBuilderCole2000
     !% A merger tree builder class using the algorithm of \cite{cole_hierarchical_2000}.
     private
     class           (cosmologyFunctionsClass                  ), pointer :: cosmologyFunctions_
     class           (mergerTreeMassResolutionClass            ), pointer :: mergerTreeMassResolution_
     class           (criticalOverdensityClass                 ), pointer :: criticalOverdensity_
     class           (mergerTreeBranchingProbabilityClass      ), pointer :: mergerTreeBranchingProbability_
     ! Variables controlling merger tree accuracy.
     double precision                                                     :: accretionLimit                          , timeEarliest             , &
          &                                                                  mergeProbability
     ! Random number sequence variables
     logical                                                              :: branchIntervalStep
     ! Interval distribution.
     logical                                                              :: branchingIntervalDistributionInitialized
     type            (distributionFunction1DNegativeExponential)          :: branchingIntervalDistribution
     ! Tolerances for behavior close to the resolution limit.
     double precision                                                     :: toleranceResolutionSelf                 , toleranceResolutionParent
   contains
     !@ <objectMethods>
     !@   <object>mergerTreeBuilderCole2000</object>
     !@   <objectMethod>
     !@     <method>shouldAbort</method>
     !@     <type>\logicalzero</type>
     !@     <arguments>\textless type(mergerTree)\textgreater\ tree\argin</arguments>
     !@     <description>Return true if construction of the merger tree should be aborted.</description>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>shouldFollowBranch</method>
     !@     <type>\logicalzero</type>
     !@     <arguments>\textless type(mergerTree)\textgreater\ tree\argin, \textless type(treeNode)\textgreater\ node\argin</arguments>
     !@     <description>Return true if the branch should be followed.</description>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>criticalOverdensitySet</method>
     !@     <type>\void</type>
     !@     <arguments>\textless class(criticalOverdensityClass)\textgreater criticalOverdensity_</arguments>
     !@     <description>Set the critical overdensity object.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     final     ::                           cole2000Destructor
     procedure :: build                  => cole2000Build
     procedure :: shouldAbort            => cole2000ShouldAbort
     procedure :: shouldFollowBranch     => cole2000ShouldFollowBranch
     procedure :: timeEarliestSet        => cole2000TimeEarliestSet
     procedure :: criticalOverdensitySet => cole2000CriticalOverdensitySet
  end type mergerTreeBuilderCole2000

  interface mergerTreeBuilderCole2000
     !% Constructors for the {\normalfont \ttfamily cole2000} merger tree builder class.
     module procedure cole2000ConstructorParameters
     module procedure cole2000ConstructorInternal
  end interface mergerTreeBuilderCole2000
  
contains

  function cole2000ConstructorParameters(parameters) result(self)
    !% Constructor for the \cite{cole_hierarchical_2000} merger tree building class which reads parameters from a provided parameter list.
    implicit none
    type            (mergerTreeBuilderCole2000          )                :: self    
    type            (inputParameters                    ), intent(inout) :: parameters
    class           (cosmologyFunctionsClass            ), pointer       :: cosmologyFunctions_
    class           (mergerTreeMassResolutionClass      ), pointer       :: mergerTreeMassResolution_
    class           (criticalOverdensityClass           ), pointer       :: criticalOverdensity_
    class           (mergerTreeBranchingProbabilityClass), pointer       :: mergerTreeBranchingProbability_
    double precision                                                     :: mergeProbability               , accretionLimit         , &
         &                                                                  redshiftMaximum                , toleranceResolutionSelf, &
         &                                                                  toleranceResolutionParent
    logical                                                              :: branchIntervalStep

    ! Check and read parameters.
    !# <inputParameter>
    !#   <name>mergeProbability</name>
    !#   <source>parameters</source>
    !#   <defaultValue>0.1d0</defaultValue>
    !#   <description>The largest probability of branching allowed in a timestep in merger trees built by the \cite{cole_hierarchical_2000} method.</description>
    !#   <type>real</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>accretionLimit</name>
    !#   <source>parameters</source>
    !#   <defaultValue>0.1d0</defaultValue>
    !#   <description>The largest fractional mass change due to subresolution accretion allowed in a timestep in merger trees built by the \cite{cole_hierarchical_2000} method.</description>
    !#   <type>real</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>redshiftMaximum</name>
    !#   <source>parameters</source>
    !#   <defaultValue>1.0d5</defaultValue>
    !#   <description>The highest redshift to which merger trees will be built in the \cite{cole_hierarchical_2000} method.</description>
    !#   <type>real</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>branchIntervalStep</name>
    !#   <source>parameters</source>
    !#   <defaultValue>.true.</defaultValue>
    !#   <description>If {\normalfont \ttfamily false} use the original \cite{cole_hierarchical_2000} method to determine whether branching occurs in a timestep. If {\normalfont \ttfamily true} draw branching intervals from a negative exponential distribution.</description>
    !#   <type>boolean</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>toleranceResolutionSelf</name>
    !#   <source>parameters</source>
    !#   <defaultValue>1.0d-6</defaultValue>
    !#   <description>The fractional tolerance in node mass at the resolution limit below which branch mis-orderings will be ignored.</description>
    !#   <type>real</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>toleranceResolutionParent</name>
    !#   <source>parameters</source>
    !#   <defaultValue>1.0d-3</defaultValue>
    !#   <description>The fractional tolerance in parent node mass at the resolution limit below which branch mis-orderings will be ignored.</description>
    !#   <type>real</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="mergerTreeBranchingProbability" name="mergerTreeBranchingProbability_" source="parameters"/>
    !# <objectBuilder class="mergerTreeMassResolution"       name="mergerTreeMassResolution_"       source="parameters"/>
    !# <objectBuilder class="cosmologyFunctions"             name="cosmologyFunctions_"             source="parameters"/>
    !# <objectBuilder class="criticalOverdensity"            name="criticalOverdensity_"            source="parameters"/>
    self   =mergerTreeBuilderCole2000(                                                                                                                  &
         &                                                                                                           mergeProbability                 , &
         &                                                                                                           accretionLimit                   , &
         &                            cosmologyFunctions_%cosmicTime(cosmologyFunctions_%expansionFactorFromRedshift(redshiftMaximum                )), &
         &                                                                                                           branchIntervalStep               , &
         &                                                                                                           toleranceResolutionSelf          , &
         &                                                                                                           toleranceResolutionParent        , &
         &                                                                                                           mergerTreeBranchingProbability_  , &
         &                                                                                                           mergerTreeMassResolution_        , &
         &                                                                                                           cosmologyFunctions_             , &
         &                                                                                                           criticalOverdensity_                &
         &                           )
    !# <inputParametersValidate source="parameters"/>
    return
  end function cole2000ConstructorParameters

  function cole2000ConstructorInternal(mergeProbability,accretionLimit,timeEarliest,branchIntervalStep,toleranceResolutionSelf,toleranceResolutionParent,mergerTreeBranchingProbability_,mergerTreeMassResolution_,cosmologyFunctions_,criticalOverdensity_) result(self)
    !% Internal constructor for the \cite{cole_hierarchical_2000} merger tree building class.
    use Galacticus_Error
    implicit none
    type            (mergerTreeBuilderCole2000          )                        :: self
    double precision                                     , intent(in   )         :: mergeProbability           , accretionLimit         , &
         &                                                                          timeEarliest               , toleranceResolutionSelf, &
         &                                                                          toleranceResolutionParent
    logical                                              , intent(in   )         :: branchIntervalStep
    class           (mergerTreeBranchingProbabilityClass), intent(in   ), target :: mergerTreeBranchingProbability_
    class           (mergerTreeMassResolutionClass      ), intent(in   ), target :: mergerTreeMassResolution_
    class           (cosmologyFunctionsClass            ), intent(in   ), target :: cosmologyFunctions_
    class           (criticalOverdensityClass           ), intent(in   ), target :: criticalOverdensity_
    !# <constructorAssign variables="mergeProbability, accretionLimit, timeEarliest, branchIntervalStep, toleranceResolutionSelf, toleranceResolutionParent, *mergerTreeBranchingProbability_, *mergerTreeMassResolution_, *cosmologyFunctions_, *criticalOverdensity_"/>
    ! Initialize state.
    self%branchingIntervalDistributionInitialized=.false.
    ! Validate parameters.
    if (self%accretionLimit >= 1.0d0) call Galacticus_Error_Report('accretionLimit < 1 required'//{introspection:location})
    return
  end function cole2000ConstructorInternal

  subroutine cole2000Destructor(self)
    !% Destructor for the {\normalfont \ttfamily cole2000} merger tree builder class.
    implicit none
    type(mergerTreeBuilderCole2000), intent(inout) :: self

    !# <objectDestructor name="self%mergerTreeBranchingProbability_"/>
    !# <objectDestructor name="self%mergerTreeMassResolution_"      />
    !# <objectDestructor name="self%cosmologyFunctions_"            />
    !# <objectDestructor name="self%criticalOverdensity_"           />
    return
  end subroutine cole2000Destructor

  subroutine cole2000Build(self,tree)
    !% Build a merger tree.
    use Galacticus_Nodes
    use Galacticus_Error
    use Merger_Tree_Walkers
    use Pseudo_Random
    use Kind_Numbers
    use ISO_Varying_String
    use Numerical_Comparison
    implicit none
    class           (mergerTreeBuilderCole2000       ), intent(inout)         :: self
    type            (mergerTree                      ), intent(inout), target :: tree
    type            (treeNode                        ), pointer               :: nodeNew1                  , nodeNew2                   , node
    class           (nodeComponentBasic              ), pointer               :: basicNew1                 , basicNew2                  , basic                     , &
         &                                                                       basicParent
    double precision                                  , parameter             :: toleranceDeltaCritical   =1.0d-6
    type            (mergerTreeWalkerTreeConstruction)                        :: treeWalkerConstruction
    type            (mergerTreeWalkerIsolatedNodes   )                        :: treeWalkerIsolated
    integer         (kind=kind_int8                  )                        :: nodeIndex
    double precision                                                          :: accretionFraction         , baseNodeTime               , branchingProbability      , &
         &                                                                       collapseTime              , deltaCritical              , deltaCritical1            , &
         &                                                                       deltaCritical2            , deltaW                     , nodeMass1                 , &
         &                                                                       nodeMass2                 , time                       , uniformRandom             , &
         &                                                                       massResolution            , accretionFractionCumulative, branchMassCurrent         , &
         &                                                                       branchDeltaCriticalCurrent, branchingInterval          , branchingIntervalScaleFree, &
         &                                                                       branchingProbabilityRate  , deltaWAccretionLimit       , deltaWEarliestTime        , &
         &                                                                       deltaCriticalEarliest
    logical                                                                   :: doBranch                  , branchIsDone               , snapAccretionFraction     , &
         &                                                                       snapEarliestTime
    type            (varying_string                  )                        :: message
    character       (len=20                          )                        :: label
    
    ! Begin construction.
    nodeIndex =  1           ! Initialize the node index counter to unity.
    node  => tree%baseNode   ! Point to the base node.
    basic => node%basic   () ! Get the basic component of the node.
    if (.not.self%branchingIntervalDistributionInitialized.and.self%branchIntervalStep) then
       ! Note that we use a unit rate - we will scale the results to the actual rate required.
       self%branchingIntervalDistribution           =distributionFunction1DNegativeExponential(1.0d0)
       self%branchingIntervalDistributionInitialized=.true.
    end if
    ! Get the mass resolution for this tree.
    massResolution=self%mergerTreeMassResolution_%resolution(tree)
    ! Find the critical overdensity at the earliest time to which we will build this tree. We evaluate this here to ensure that
    ! δc(t) is already evaluated over the full range of epochs that we will require - this prevents any possible retabulation
    ! during tree construction which can potentially lead to misordering of branches. This is not the ideal solution - ideally,
    ! critical overdensity classes which rely on tabulation and which have to retabulate themselves should ensure that they simply
    ! expand their range without changing any of the previous computed values (as we do for expansion factor vs. time in the
    ! cosmology function class).
    deltaCriticalEarliest=self%criticalOverdensity_%value(time=self%timeEarliest,mass=basic%mass(),node=node)
    ! Convert time for base node to critical overdensity (which we use as a time coordinate in this module).
    baseNodeTime =basic%time()
    deltaCritical=self%criticalOverdensity_%value(time=basic%time(),mass=basic%mass(),node=node)
    call basic%timeSet(deltaCritical)
    ! Begin tree build loop.
    treeWalkerConstruction=mergerTreeWalkerTreeConstruction(tree)
    do while (treeWalkerConstruction%next(node).and..not.self%shouldAbort(tree))
       ! Get the basic component of the node.
       basic => node%basic()
       ! Initialize the state for this branch.
       accretionFractionCumulative=0.0d0
       branchMassCurrent          =basic%mass()
       branchDeltaCriticalCurrent =basic%time()
       ! Evolve the branch until mass falls below the resolution limit, the earliest time is reached, or the branch ends.       
       branchIsDone=.false.
       do while (.not.branchIsDone)
          if     (                                                                                                                                                                    &
               &   branchMassCurrent                       <= massResolution                                                                                                          &
               &  .or.                                                                                                                                                                &
               &   branchDeltaCriticalCurrent              >= self%criticalOverdensity_%value(time=self%timeEarliest,mass=branchMassCurrent,node=node)*(1.0d0-toleranceDeltaCritical) &
               &  .or.                                                                                                                                                                &
               &   .not.self%shouldFollowBranch(tree,node)                                                                                                                            &
               & ) then
             ! Branch should be terminated. If we have any accumulated accretion, terminate the branch with a final node.
             if (accretionFractionCumulative > 0.0d0) then
                nodeIndex      =  nodeIndex+1
                nodeNew1       => treeNode(nodeIndex,tree)
                basicNew1      => nodeNew1%basic(autoCreate=.true.)
                ! Compute new mass accounting for sub-resolution accretion.
                nodeMass1      =  basic%mass()*(1.0d0-accretionFractionCumulative)
                ! Compute the time corresponding to this new node.
                time           =  self%criticalOverdensity_%timeOfCollapse(criticalOverdensity=deltaCritical,mass=branchMassCurrent,node=nodeNew1)
                ! Set properties of the new node.
                deltaCritical1 =  self%criticalOverdensity_%value         (time               =time         ,mass=nodeMass1        ,node=nodeNew1)
                call basicNew1%massSet(nodeMass1     )
                call basicNew1%timeSet(deltaCritical1)
                ! Create links from old to new node and vice-versa.
                node%firstChild => nodeNew1
                nodeNew1%parent => node
                ! Move to the terminating node (necessary otherwise we would move to this terminating node next and continue to
                ! grow a branch from it).
                node            => nodeNew1
             end if
             ! Flag that the branch is done.
             branchIsDone=.true.
          else
             ! Find branching probability rate per unit deltaW.
             branchingProbabilityRate=self%mergerTreeBranchingProbability_%probabilityBound     (branchMassCurrent,branchDeltaCriticalCurrent,massResolution,mergerTreeBranchingBoundUpper,node)
             ! Find accretion rate.
             accretionFraction       =self%mergerTreeBranchingProbability_%fractionSubresolution(branchMassCurrent,branchDeltaCriticalCurrent,massResolution                              ,node)
             ! A negative accretion fraction indicates that the node is so close to the resolution limit that
             ! an accretion rate cannot be determined (given available numerical accuracy). In such cases we
             ! consider the node to have reached the end of its resolved evolution and so walk to the next node.
             if (accretionFraction < 0.0d0) then
                ! Terminate the branch with a final node.
                nodeIndex          =  nodeIndex+1
                nodeNew1           => treeNode      (nodeIndex,tree)
                basicNew1          => nodeNew1%basic(autoCreate=.true. )
                ! Compute new mass accounting for sub-resolution accretion.
                nodeMass1          = massResolution
                ! Compute the time corresponding to this event.
                time               = self%criticalOverdensity_%timeOfCollapse(criticalOverdensity=branchDeltaCriticalCurrent,mass=branchMassCurrent,node=nodeNew1)
                ! Convert to a critical overdensity.
                deltaCritical1     = self%criticalOverdensity_%value         (time               =time                      ,mass=nodeMass1        ,node=nodeNew1)
                ! Ensure critical overdensity exceeds that of the current node.
                if      (                                                                             &
                     &    deltaCritical1 <  branchDeltaCriticalCurrent*(1.0d0-toleranceDeltaCritical) &
                     &  ) then
                   call Galacticus_Error_Report('truncating to resolution, but resolution node exists after parent'//{introspection:location})
                else if (                                                                             &
                     &    deltaCritical1 >= branchDeltaCriticalCurrent*(1.0d0-toleranceDeltaCritical) &
                     &   .and.                                                                        &
                     &    deltaCritical1 <= branchDeltaCriticalCurrent*(1.0d0+toleranceDeltaCritical) &
                     &  ) then
                   deltaCritical1=branchDeltaCriticalCurrent*(1.0d0+toleranceDeltaCritical)
                end if
                ! Set properties of the new node.
                call basicNew1%massSet(nodeMass1     )
                call basicNew1%timeSet(deltaCritical1)
                ! Create links from old to new node and vice-versa.
                node%firstChild => nodeNew1
                nodeNew1%parent     => node
                ! Move to the terminating node (necessary otherwise we would move to this terminating node next and continue to
                ! grow a branch from it), and flag that the branch is done.
                node            => nodeNew1
                branchIsDone        =  .true.
             else
                ! Finding maximum allowed step in w. Limit based on branching rate only if we are using the original Cole et
                ! al. (2000) algorithm.
                deltaW               =self%mergerTreeBranchingProbability_%stepMaximum(branchMassCurrent,branchDeltaCriticalCurrent,massResolution)
                snapAccretionFraction=.false.
                if (accretionFraction > 0.0d0) then
                   deltaWAccretionLimit=(self%accretionLimit-accretionFractionCumulative)/accretionFraction
                   if (deltaWAccretionLimit <= deltaW) then
                      deltaW               =deltaWAccretionLimit
                      snapAccretionFraction=.true.
                   end if
                end if
                if     (                                                                   &
                     &   branchingProbabilityRate > 0.0d0                                  &
                     &  .and.                                                              &
                     &   .not.self%branchIntervalStep                                      &
                     & ) then
                   if (self%mergeProbability/branchingProbabilityRate < deltaW) then
                      ! Timestep is limited by branching rate. Reduce the timestep to the allowed
                      ! size and unset the flag to snap the accretion fraction to its maximum
                      ! allowed value (since we won't reach that value with this new, reduced
                      ! timestep).
                      deltaW               =self%mergeProbability/branchingProbabilityRate
                      snapAccretionFraction=.false.
                   end if
                end if
                ! Limit the timestep so that the maximum allowed time is not exceeded.
                deltaWEarliestTime=+self%criticalOverdensity_%value(                        &
                     &                                              time=self%timeEarliest, &
                     &                                              mass=branchMassCurrent, &
                     &                                              node=node               &
                     &                                             )                        &
                     &             -branchDeltaCriticalCurrent
                if (deltaWEarliestTime < deltaW) then
                   deltaW               =deltaWEarliestTime
                   snapEarliestTime     =.true.
                   snapAccretionFraction=.false.
                else
                   snapEarliestTime     =.false.
                end if
                ! Scale values to the determined timestep.
                if (.not.self%branchIntervalStep)                           &
                     & branchingProbability=branchingProbabilityRate*deltaW
                accretionFraction          =accretionFraction       *deltaW
                ! Accretion fraction must be less than unity. Reduce timestep (and branching
                ! probability and accretion fraction) by factors of two until this condition is
                ! satisfied.
                do while (accretionFraction+accretionFractionCumulative >= 1.0d0)
                   if (.not.self%branchIntervalStep)                      &
                        & branchingProbability=branchingProbability*0.5d0
                   accretionFraction          =accretionFraction   *0.5d0
                   deltaW                     =deltaW              *0.5d0
                   snapAccretionFraction      =.false.
                   snapEarliestTime           =.false.
                end do
                ! Decide if a branching occurs.
                if (self%branchIntervalStep) then
                   ! In this case we draw intervals between branching events from a negative
                   ! exponential distribution.
                   if (branchingProbabilityRate > 0.0d0) then
                      branchingIntervalScaleFree=0.0d0
                      do while (branchingIntervalScaleFree <= 0.0d0)
                         branchingIntervalScaleFree=self%branchingIntervalDistribution%sample(randomNumberGenerator=tree%randomNumberGenerator)
                      end do
                      branchingInterval=branchingIntervalScaleFree/branchingProbabilityRate
                      ! Based on the upper bound on the rate, check if branching occurs before the maximum allowed timestep.
                      if (branchingInterval < deltaW) then
                         ! It does, so recheck using the actual branching rate.
                         branchingProbabilityRate=self%mergerTreeBranchingProbability_%probability(branchMassCurrent,branchDeltaCriticalCurrent,massResolution,node)
                         branchingInterval       =branchingIntervalScaleFree/branchingProbabilityRate
                         doBranch                =(branchingInterval <= deltaW)
                         if (doBranch) then
                            ! Branching occured, adjust the accretion fraction, and timestep to their values at the branching event.
                            accretionFraction    =accretionFraction*branchingInterval/deltaW
                            deltaW               =branchingInterval
                            snapAccretionFraction=.false.
                            snapEarliestTime     =.false.
                            ! Draw a random deviate and scale by the branching rate - this will be used to choose the branch mass.
                            uniformRandom       =tree%randomNumberGenerator%uniformSample()
                            branchingProbability=uniformRandom*branchingProbabilityRate
                         end if
                      else
                         doBranch=.false.
                      end if
                   else
                      doBranch=.false.
                   end if
                else
                   ! In this case we're using the original Cole et al. (2000) algorithm.
                   if (branchingProbability > 0.0d0) then
                      uniformRandom=tree%randomNumberGenerator%uniformSample()
                      doBranch=(uniformRandom <= branchingProbability)
                      if (doBranch) then
                         branchingProbability=self%mergerTreeBranchingProbability_%probabilityBound(branchMassCurrent,branchDeltaCriticalCurrent,massResolution,mergerTreeBranchingBoundLower,node)*deltaW
                         if (uniformRandom <= branchingProbability) then
                            doBranch=.true.
                         else
                            branchingProbability=self%mergerTreeBranchingProbability_%probability(branchMassCurrent,branchDeltaCriticalCurrent,massResolution,node)*deltaW
                            doBranch=(uniformRandom <= branchingProbability)
                         end if
                         ! First convert the realized probability back to a rate.               
                         if (doBranch) branchingProbability=uniformRandom/deltaW
                      end if
                   else
                      doBranch=.false.
                   end if
                end if
                ! Determine the critical overdensity for collapse for the new halo(s).
                if (snapEarliestTime) then
                   deltaCritical=+self%criticalOverdensity_%value(                        &
                        &                                         time=self%timeEarliest, &
                        &                                         mass=branchMassCurrent, &
                        &                                         node=node               &
                        &                                        )
                else
                   deltaCritical=+branchDeltaCriticalCurrent &
                        &        +deltaW
                end if
                if (snapAccretionFraction) then
                   accretionFractionCumulative=self%accretionLimit
                else
                   accretionFractionCumulative=accretionFractionCumulative+accretionFraction
                end if
                ! Create new nodes.
                select case (doBranch)
                case (.true.)
                   ! Branching occurs - create two progenitors.
                   nodeIndex     =  nodeIndex+1
                   nodeNew1      => treeNode(nodeIndex,tree)
                   basicNew1     => nodeNew1%basic(autoCreate=.true.)
                   ! Compute mass of one of the new nodes.
                   nodeMass1     =  self%mergerTreeBranchingProbability_%massBranch(branchMassCurrent,branchDeltaCriticalCurrent,massResolution,branchingProbability,tree%randomNumberGenerator,node)
                   nodeMass2     =  basic%mass()-nodeMass1
                   nodeMass1=nodeMass1*(1.0d0-accretionFractionCumulative)
                   nodeMass2=nodeMass2*(1.0d0-accretionFractionCumulative)
                   ! Compute the time corresponding to this branching event.
                   time          =  self%criticalOverdensity_%timeOfCollapse(criticalOverdensity=deltaCritical,mass=branchMassCurrent,node=nodeNew1)
                   ! Set properties of first new node.
                   deltaCritical1=  self%criticalOverdensity_%value         (time               =time         ,mass=nodeMass1        ,node=nodeNew1)
                   ! If we are to snap halos to the earliest time, and the computed deltaCritical is sufficiently close to that time, snap it.
                   if (snapEarliestTime.and.Values_Agree(deltaCritical1,deltaCritical,relTol=toleranceDeltaCritical)) deltaCritical1=deltaCritical
                   call basicNew1%massSet(nodeMass1     )
                   call basicNew1%timeSet(deltaCritical1)
                   ! Create second progenitor.
                   nodeIndex=nodeIndex+1
                   nodeNew2  => treeNode(nodeIndex,tree)
                   basicNew2 => nodeNew2%basic(autoCreate=.true.)
                   ! Set properties of second new node.
                   deltaCritical2=self%criticalOverdensity_%value(time=time,mass=nodeMass2,node=nodeNew2)
                   ! If we are to snap halos to the earliest time, and the computed deltaCritical is sufficiently close to that time, snap it.
                   if (snapEarliestTime.and.Values_Agree(deltaCritical2,deltaCritical,relTol=toleranceDeltaCritical)) deltaCritical2=deltaCritical
                   call basicNew2%massSet(nodeMass2     )
                   call basicNew2%timeSet(deltaCritical2)
                   ! Create links from old to new nodes and vice-versa. (Ensure that child node is the more massive progenitor.)
                   if (nodeMass2 > nodeMass1) then
                      node%firstChild => nodeNew2
                      nodeNew2%sibling    => nodeNew1
                   else
                      node%firstChild => nodeNew1
                      nodeNew1%sibling    => nodeNew2
                   end if
                   nodeNew1%parent        => node
                   nodeNew2%parent        => node
                   branchIsDone           =  .true.
                case (.false.)
                   ! No branching occurs - create one progenitor.
                   if (accretionFractionCumulative >= self%accretionLimit) then
                      nodeIndex      =  nodeIndex+1
                      nodeNew1       => treeNode(nodeIndex,tree)
                      basicNew1      => nodeNew1%basic(autoCreate=.true.)
                      ! Compute new mass accounting for sub-resolution accretion.
                      nodeMass1      =  basic%mass()*(1.0d0-accretionFractionCumulative)
                      ! Compute the time corresponding to this new node.
                      time           =  self%criticalOverdensity_%timeOfCollapse(criticalOverdensity=deltaCritical,mass=branchMassCurrent,node=nodeNew1)
                      ! Set properties of the new node.
                      deltaCritical1 =  self%criticalOverdensity_%value         (time               =time         ,mass=nodeMass1        ,node=nodeNew1)
                      call basicNew1%massSet(nodeMass1     )
                      call basicNew1%timeSet(deltaCritical1)
                      ! Create links from old to new node and vice-versa.
                      node%firstChild => nodeNew1
                      nodeNew1%parent     => node
                      branchIsDone=.true.
                   else
                      branchMassCurrent         =basic%mass()*(1.0d0-accretionFractionCumulative)
                      branchDeltaCriticalCurrent=deltaCritical
                   end if
                end select
             end if
          end if
       end do
    end do
    ! Walk the tree and convert w to time.
    treeWalkerIsolated=mergerTreeWalkerIsolatedNodes(tree)
    do while (treeWalkerIsolated%next(node))
       ! Get the basic component of the node.
       basic    => node%basic()
       ! Compute the collapse time.
       collapseTime =  self%criticalOverdensity_%timeOfCollapse(criticalOverdensity=basic%time(),mass=basic%mass(),node=node)       
       call basic%timeSet(collapseTime)
    end do
    basic => tree%baseNode%basic()
    call basic%timeSet(baseNodeTime)
    ! Check for well-ordering in time.
    treeWalkerIsolated=mergerTreeWalkerIsolatedNodes(tree)
    do while (treeWalkerIsolated%next(node))       
       if (associated(node%parent)) then
          basic       => node       %basic()
          basicParent => node%parent%basic()
          if (basicParent%time() <= basic%time()) then
             if     (                                                                            &
                  &   basicParent%mass() < massResolution*(1.0d0+self%toleranceResolutionParent) &
                  &  .and.                                                                       &
                  &   basic      %mass() < massResolution*(1.0d0+self%toleranceResolutionSelf  ) &
                  & ) then
                ! Parent halo is very close to the resolution limit. Simply prune away the remainder of this branch.
                call node%destroyBranch()
                deallocate(node)
                call treeWalkerIsolated%previous(node)
             else
                ! Parent halo is not close to the resolution limit - this is an error.
                message="branch is not well-ordered in time:"           //char(10)
                write (label,'(i20)'   ) node       %index()
                message=message//" ->      node index = "//label        //char(10)
                write (label,'(i20)'   ) node%parent%index()
                message=message//" ->    parent index = "//label        //char(10)
                write (label,'(e20.14)')                                   basic%time()
                message=message//" ->       node time = "//label//" Gyr"//char(10)
                write (label,'(e20.14)')                                 basicParent%time()
                message=message//" ->     parent time = "//label//" Gyr"//char(10)
                write (label,'(e20.14)')                                                              basic      %mass()
                message=message//" ->       node mass = "//label//" M☉" //char(10)
                write (label,'(e20.14)')                                                              basicParent%mass()
                message=message//" ->     parent mass = "//label//" M☉" //char(10)
                write (label,'(e20.14)') self%criticalOverdensity_%value(time=basic      %time(),mass=basic      %mass(),node=node       )
                message=message//" ->         node δc = "//label        //char(10)
                write (label,'(e20.14)') self%criticalOverdensity_%value(time=basicParent%time(),mass=basicParent%mass(),node=node%parent)
                message=message//" ->       parent δc = "//label        //char(10)
                basic => tree%baseNode%basic()
                write (label,'(e20.14)')                                   basic%time()
                message=message//" ->       tree time = "//label//" Gyr"//char(10)
                write (label,'(e20.14)')                                                         basic      %mass()
                message=message//" ->       tree mass = "//label//" M☉" //char(10)
                write (label,'(e20.14)') massResolution
                message=message//" -> mass resolution = "//label//" M☉"
                call Galacticus_Error_Report(message//{introspection:location})
             end if
          end if
       end if
    end do
    return
  end subroutine cole2000Build

  logical function cole2000ShouldAbort(self,tree)
    !% Return {\normalfont \ttfamily true} if tree construction should be aborted. In the {\normalfont \ttfamily cole2000} tree
    !% builder we never abort.
    implicit none
    class(mergerTreeBuilderCole2000), intent(inout) :: self
    type (mergerTree               ), intent(in   ) :: tree
    !GCC$ attributes unused :: self, tree
    
    cole2000ShouldAbort=.false.
    return
  end function cole2000ShouldAbort
  
  logical function cole2000ShouldFollowBranch(self,tree,node)
    !% Return {\normalfont \ttfamily true} if tree construction should continue to follow the current branch. In the {\normalfont
    !% \ttfamily cole2000} tree builder we always continue.
    implicit none
    class(mergerTreeBuilderCole2000), intent(inout)          :: self
    type (mergerTree               ), intent(in   )          :: tree
    type (treeNode                 ), intent(inout), pointer :: node
    !GCC$ attributes unused :: self, tree, node

    cole2000ShouldFollowBranch=.true.
    return
  end function cole2000ShouldFollowBranch
  
  subroutine cole2000TimeEarliestSet(self,timeEarliest)
    !% Set the earliest time for the tree builder.
    implicit none
    class           (mergerTreeBuilderCole2000), intent(inout) :: self
    double precision                           , intent(in   ) :: timeEarliest

    self%timeEarliest=timeEarliest
    return
  end subroutine cole2000TimeEarliestSet

  subroutine cole2000CriticalOverdensitySet(self,criticalOverdensity_)
    !% Set the critical overdensity object for this tree builder.
    class(mergerTreeBuilderCole2000), intent(inout)         :: self
    class(criticalOverdensityClass ), intent(in   ), target :: criticalOverdensity_

    !# <objectDestructor name="self%criticalOverdensity_"/>
    self%criticalOverdensity_ => criticalOverdensity_
    return
  end subroutine cole2000CriticalOverdensitySet
