!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module that implements a very simple disk component.

module Node_Component_Disk_Very_Simple
  !% Implements a very simple disk component.
  use ISO_Varying_String
  use Galacticus_Nodes, only : treeNode
  use Math_Exponentiation
  use Cosmology_Functions
  use Stellar_Population_Properties
  use Dark_Matter_Halo_Scales
  use Star_Formation_Feedback_Disks
  use Star_Formation_Timescales_Disks
  use Dark_Matter_Profiles_DMO
  implicit none
  private
  public :: Node_Component_Disk_Very_Simple_Post_Evolve        , Node_Component_Disk_Very_Simple_Rate_Compute     , &
       &    Node_Component_Disk_Very_Simple_Scale_Set          , Node_Component_Disk_Very_Simple_Satellite_Merging, &
       &    Node_Component_Disk_Very_Simple_Initialize         , Node_Component_Disk_Very_Simple_Pre_Evolve       , &
       &    Node_Component_Disk_Very_Simple_Rates              , Node_Component_Disk_Very_Simple_Analytic_Solver  , &
       &    Node_Component_Disk_Very_Simple_Post_Step          , Node_Component_Disk_Very_Simple_Thread_Initialize, &
       &    Node_Component_Disk_Very_Simple_Thread_Uninitialize

  !# <component>
  !#  <class>disk</class>
  !#  <name>verySimple</name>
  !#  <isDefault>false</isDefault>
  !#  <properties>
  !#   <property>
  !#     <name>isInitialized</name>
  !#     <type>logical</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="false" />
  !#   </property>
  !#   <property>
  !#     <name>massStellar</name>
  !#     <type>double</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" />
  !#     <output unitsInSI="massSolar" comment="Mass of stars in the very simple disk."/>
  !#   </property>
  !#   <property>
  !#     <name>abundancesStellar</name>
  !#     <type>abundances</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" />
  !#     <output unitsInSI="massSolar" comment="Mass of metals in the stellar phase of the standard disk."/>
  !#   </property>
  !#   <property>
  !#     <name>massGas</name>
  !#     <type>double</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" createIfNeeded="true" makeGeneric="true" />
  !#     <output unitsInSI="massSolar" comment="Mass of gas in the very simple disk."/>
  !#   </property>
  !#   <property>
  !#     <name>abundancesGas</name>
  !#     <type>abundances</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" createIfNeeded="true" makeGeneric="true" />
  !#     <output unitsInSI="massSolar" comment="Mass of metals in the gas phase of the standard disk."/>
  !#   </property>
  !#   <property>
  !#     <name>starFormationRate</name>
  !#     <attributes isSettable="false" isGettable="true" isEvolvable="false" isDeferred="get" isVirtual="true" />
  !#     <type>double</type>
  !#     <rank>0</rank>
  !#     <output condition="[[diskOutputStarFormationRate]]" unitsInSI="massSolar/gigaYear" comment="Disk star formation rate."/>
  !#   </property>
  !#   <property>
  !#     <name>stellarPropertiesHistory</name>
  !#     <type>history</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" />
  !#   </property>
  !#   <property>
  !#     <name>luminositiesStellar</name>
  !#     <type>stellarLuminosities</type>
  !#     <rank>0</rank>
  !#     <attributes isSettable="true" isGettable="true" isEvolvable="true" />
  !#     <output unitsInSI="luminosityZeroPointAB" comment="Luminosity of disk stars."/>
  !#   </property>
  !#  </properties>
  !#  <bindings>
  !#   <binding method="attachPipe"   function="Node_Component_Disk_Very_Simple_Attach_Pipe" description="Attach pipes to the very simple disk component." bindsTo="component" returnType="\void" arguments="" />
  !#   <binding method="enclosedMass" function="Node_Component_Disk_Very_Simple_Enclosed_Mass" bindsTo="component" />
  !#  </bindings>
  !#  <functions>objects.nodes.components.disk.very_simple.bound_functions.inc</functions>
  !# </component>

  ! Objects used by this component.
  class(cosmologyFunctionsClass         ), pointer :: cosmologyFunctions_
  class(stellarPopulationPropertiesClass), pointer :: stellarPopulationProperties_
  class(darkMatterHaloScaleClass        ), pointer :: darkMatterHaloScale_
  class(starFormationFeedbackDisksClass ), pointer :: starFormationFeedbackDisks_
  class(starFormationTimescaleDisksClass), pointer :: starFormationTimescaleDisks_
  class(darkMatterProfileDMOClass          ), pointer :: darkMatterProfileDMO_
  !$omp threadprivate(cosmologyFunctions_,stellarPopulationProperties_,darkMatterHaloScale_,starFormationFeedbackDisks_,starFormationTimescaleDisks_,darkMatterProfileDMO_)

  ! Record of whether to use the simple disk analytic solver.
  logical                             :: diskVerySimpleUseAnalyticSolver
  double precision                    :: diskVerySimpleAnalyticSolverPruneMassStars         , diskVerySimpleAnalyticSolverPruneMassGas, &
       &                                 timePresentDay                              =-1.0d0
 
  ! Parameters controlling the physical implementation.
  double precision                    :: diskOutflowTimescaleMinimum                        , diskStarFormationTimescaleMinimum       , &
       &                                 diskVerySimpleMassScaleAbsolute                    , diskVerySimpleSurfaceDensityThreshold   , &
       &                                 diskVerySimpleSurfaceDensityVelocityExponent       , surfaceDensityNormalization
  logical                             :: diskVerySimpleTrackAbundances                      , diskVerySimpleTrackLuminosities

  ! Fast exponentiation tables for rapid computation of the surface density threshold.
  type            (fastExponentiator) :: velocityExponentiator

contains

  !# <nodeComponentInitializationTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Initialize</unitName>
  !# </nodeComponentInitializationTask>
  subroutine Node_Component_Disk_Very_Simple_Initialize(globalParameters_)
    !% Initializes the tree node very simple disk component module.
    use Input_Parameters
    use Galacticus_Nodes, only : nodeComponentDiskVerySimple, defaultDiskComponent
    implicit none
    type            (inputParameters            ), intent(inout) :: globalParameters_
    type            (nodeComponentDiskVerySimple)                :: diskVerySimpleComponent
    double precision                             , parameter     :: velocityNormalization  =200.0d0

    ! Initialize the module if necessary.
    if (defaultDiskComponent%verySimpleIsActive()) then
       ! Read parameters controlling the physical implementation.
       !# <inputParameter>
       !#   <name>diskVerySimpleMassScaleAbsolute</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>100.0d0</defaultValue>
       !#   <description>The absolute mass scale below which calculations in the very simple disk component are allowed to become inaccurate.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskOutflowTimescaleMinimum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>1.0d-3</defaultValue>
       !#   <description>The minimum timescale (in units of the halo dynamical time) on which outflows may deplete gas in the disk.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskStarFormationTimescaleMinimum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>1.0d-3</defaultValue>
       !#   <description>The minimum timescale (in units of the halo dynamical time) on which star formation may occur in the disk.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleTrackAbundances</name>
       !#   <cardinality>0..1</cardinality>
       !#   <defaultValue>.false.</defaultValue>
       !#   <description>Specifies whether or not to track abundances in the very simple disk component.</description>
       !#   <source>globalParameters_</source>
       !#   <type>boolean</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleTrackLuminosities</name>
       !#   <cardinality>0..1</cardinality>
       !#   <defaultValue>.false.</defaultValue>
       !#   <description>Specifies whether or not to track stellar luminosities in the very simple disk component.</description>
       !#   <source>globalParameters_</source>
       !#   <type>boolean</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleSurfaceDensityThreshold</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.0d0</defaultValue>
       !#   <description>The threshold gas surface denisty above this star formation occurs [$M_\odot$/Mpc$^2$].</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleSurfaceDensityVelocityExponent</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.0d0</defaultValue>
       !#   <description>The exponent of velocity in the threshold gas surface denisty above this star formation occurs.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleUseAnalyticSolver</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>.false.</defaultValue>
       !#   <description>If true, employ an analytic ODE solver when evolving satellites.</description>
       !#   <source>globalParameters_</source>
       !#   <type>boolean</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleAnalyticSolverPruneMassGas</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.0d0</defaultValue>
       !#   <description>Gas mass below which the analytic solver will prune a galaxy.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>diskVerySimpleAnalyticSolverPruneMassStars</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.0d0</defaultValue>
       !#   <description>Stellar mass below which the analytic solver will prune a galaxy.</description>
       !#   <source>globalParameters_</source>
       !#   <type>double</type>
       !# </inputParameter>
       ! Initialize exponentiators.
       velocityExponentiator=fastExponentiator(1.0d+0,1.0d+3,diskVerySimpleSurfaceDensityVelocityExponent,1.0d+1,abortOutsideRange=.false.)
       ! Compute normalization factor for surface density.
       surfaceDensityNormalization=diskVerySimpleSurfaceDensityThreshold/velocityNormalization**diskVerySimpleSurfaceDensityVelocityExponent
       ! Attach the cooling mass pipe from the hot halo component.
       call diskVerySimpleComponent%attachPipe()
       ! Bind the star formation rate function.
       call diskVerySimpleComponent%starFormationRateFunction(Node_Component_Disk_Very_Simple_SFR)
    end if
    return
  end subroutine Node_Component_Disk_Very_Simple_Initialize

  !# <nodeComponentThreadInitializationTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Thread_Initialize</unitName>
  !# </nodeComponentThreadInitializationTask>
  subroutine Node_Component_Disk_Very_Simple_Thread_Initialize(globalParameters_)
    !% Initializes the tree node very simple disk profile module.
    use Input_Parameters
    use Galacticus_Nodes, only : defaultDiskComponent
    implicit none
    type(inputParameters), intent(inout) :: globalParameters_

    if (defaultDiskComponent%verySimpleIsActive()) then
       !# <objectBuilder class="cosmologyFunctions"          name="cosmologyFunctions_"          source="globalParameters_"/>
       !# <objectBuilder class="stellarPopulationProperties" name="stellarPopulationProperties_" source="globalParameters_"/>
       !# <objectBuilder class="darkMatterHaloScale"         name="darkMatterHaloScale_"         source="globalParameters_"/>
       !# <objectBuilder class="darkMatterProfileDMO"        name="darkMatterProfileDMO_"        source="globalParameters_"/>
       !# <objectBuilder class="starFormationFeedbackDisks"  name="starFormationFeedbackDisks_"  source="globalParameters_"/>
       !# <objectBuilder class="starFormationTimescaleDisks" name="starFormationTimescaleDisks_" source="globalParameters_"/>       
       ! If using the analytic solver, find the time at the present day.
       !$omp critical (Node_Component_Disk_Very_Simple_Thread_Initialize)
       if (diskVerySimpleUseAnalyticSolver.and.timePresentDay < 0.0d0) timePresentDay=cosmologyFunctions_%cosmicTime(1.0d0)
       !$omp end critical (Node_Component_Disk_Very_Simple_Thread_Initialize)
    end if
    return
  end subroutine Node_Component_Disk_Very_Simple_Thread_Initialize

  !# <nodeComponentThreadUninitializationTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Thread_Uninitialize</unitName>
  !# </nodeComponentThreadUninitializationTask>
  subroutine Node_Component_Disk_Very_Simple_Thread_Uninitialize()
    !% Uninitializes the tree node very simple disk profile module.
    use Galacticus_Nodes, only : defaultDiskComponent
    implicit none

    if (defaultDiskComponent%verySimpleIsActive()) then
       !# <objectDestructor name="cosmologyFunctions_"         />
       !# <objectDestructor name="stellarPopulationProperties_"/>
       !# <objectDestructor name="darkMatterHaloScale_"        />
       !# <objectDestructor name="darkMatterProfileDMO_"       />
       !# <objectDestructor name="starFormationFeedbackDisks_" />
       !# <objectDestructor name="starFormationTimescaleDisks_"/>       
    end if
    return
  end subroutine Node_Component_Disk_Very_Simple_Thread_Uninitialize

  !# <preEvolveTask>
  !# <unitName>Node_Component_Disk_Very_Simple_Pre_Evolve</unitName>
  !# </preEvolveTask>
  subroutine Node_Component_Disk_Very_Simple_Pre_Evolve(node)
    !% Ensure the disk has been initialized.
    use Galacticus_Nodes, only : treeNode, nodeComponentDisk, nodeComponentDiskVerySimple
    implicit none
    type (treeNode         ), intent(inout), pointer :: node
    class(nodeComponentDisk)               , pointer :: disk

    ! Get the disk component.
    disk => node%disk()
    ! Check if an exponential disk component exists.
    select type (disk)
       class is (nodeComponentDiskVerySimple)
       ! Initialize the disk
       call Node_Component_Disk_Very_Simple_Create(node)
    end select
    return
  end subroutine Node_Component_Disk_Very_Simple_Pre_Evolve

  !# <postEvolveTask>
  !# <unitName>Node_Component_Disk_Very_Simple_Post_Evolve</unitName>
  !# </postEvolveTask>
  subroutine Node_Component_Disk_Very_Simple_Post_Evolve(node)
    !% Catch rounding errors in the very simple disk gas evolution.
    use Galacticus_Display
    use String_Handling
    use Histories
    use Stellar_Luminosities_Structure
    use Abundances_Structure
    use Galacticus_Nodes              , only : treeNode, nodeComponentDisk, nodeComponentDiskVerySimple, nodeComponentBasic
    implicit none
    type (treeNode          ), intent(inout), pointer :: node
    class(nodeComponentDisk )               , pointer :: disk
    class(nodeComponentBasic)               , pointer :: basic
    type (history           )                         :: stellarPropertiesHistory

    ! Get the disk component.
    disk => node%disk()
    ! Check if a very simple disk component exists.
    select type (disk)
    class is (nodeComponentDiskVerySimple)
       ! Trim the stellar populations properties future history.
       basic => node%basic()
       stellarPropertiesHistory=disk%stellarPropertiesHistory()
       call stellarPropertiesHistory%trim(basic%time())
       call disk%stellarPropertiesHistorySet(stellarPropertiesHistory)
    end select
    return
  end subroutine Node_Component_Disk_Very_Simple_Post_Evolve
  
  !# <postStepTask>
  !# <unitName>Node_Component_Disk_Very_Simple_Post_Step</unitName>
  !# </postStepTask>
  subroutine Node_Component_Disk_Very_Simple_Post_Step(node,status)
    !% Catch rounding errors in the very simple disk gas evolution.
    use FGSL                          , only : FGSL_Failure
    use Galacticus_Nodes              , only : treeNode    , nodeComponentDisk, nodeComponentDiskVerySimple
    use Galacticus_Display
    use String_Handling
    use Abundances_Structure
    use Stellar_Luminosities_Structure
    implicit none
    type            (treeNode          ), intent(inout), pointer :: node
    integer                             , intent(inout)          :: status
    class           (nodeComponentDisk )               , pointer :: disk
    double precision                    , save                   :: fractionalErrorMaximum=0.0d0
    double precision                                             :: diskMass                    , fractionalError
    character       (len=20            )                         :: valueString
    type            (varying_string    )                         :: message

    ! Get the disk component.
    disk => node%disk()
    ! Check if a very simple disk component exists.
    select type (disk)
    class is (nodeComponentDiskVerySimple)
       ! Trap negative gas masses.
       if (disk%massGas() < 0.0d0) then
          ! Check if this exceeds the maximum previously recorded error.
          fractionalError=   abs(disk%massGas    ()) &
               &          /(                         &
               &                 disk%massStellar()  &
               &            +abs(disk%massGas    ()) &
               &           )
          !$omp critical (Very_Simple_Disk_Post_Evolve_Check)
          if (fractionalError > fractionalErrorMaximum) then
             ! Report a warning.
             message='Warning: disk has negative gas mass (fractional error exceeds any previously reported):'//char(10)
             message=message//'  Node index        = '//node%index() //char(10)
             write (valueString,'(e12.6)') disk%massGas()
             message=message//'  Disk gas mass     = '//trim(valueString)//char(10)
             write (valueString,'(e12.6)') disk%massStellar()
             message=message//'  Disk stellar mass = '//trim(valueString)//char(10)
             write (valueString,'(e12.6)') fractionalError
             message=message//'  Error measure     = '//trim(valueString)//char(10)
             if (fractionalErrorMaximum == 0.0d0) then
                ! This is the first time this warning has been issued, so give some extra information.
                message=message//'  Gas mass will be reset to zero (in future cases also).'//char(10)
                message=message//'  Future cases will be reported only when they exceed the previous maximum error measure.'//char(10)
                message=message//'  Negative masses are due to numerical inaccuracy in the ODE solutions.'//char(10)
                message=message//'  If significant, consider using a higher tolerance in the ODE solver.'
             end if
             call Galacticus_Display_Message(message,verbosityWarn)
             ! Store the new maximum fractional error.
             fractionalErrorMaximum=fractionalError
          end if
          !$omp end critical (Very_Simple_Disk_Post_Evolve_Check)
          ! Get the total mass of the disk material
          diskMass= disk%massGas    () &
               &   +disk%massStellar()
          if (diskMass == 0.0d0) then
             call disk%        massStellarSet(                  0.0d0)
             call disk%  abundancesStellarSet(         zeroAbundances)
             call disk%luminositiesStellarSet(zeroStellarLuminosities)
          end if
          ! Reset the gas mass of the disk.
          call disk%      massGasSet(         0.0d0)
          call disk%abundancesGasSet(zeroAbundances)
          ! Record that state was changed.
          status=FGSL_Failure
       end if
    end select
    return
  end subroutine Node_Component_Disk_Very_Simple_Post_Step

  subroutine Node_Component_Disk_Very_Simple_Create(node)
    !% Create properties in a very simple disk component.
    use Histories
    use Galacticus_Nodes, only : treeNode, nodeComponentDisk
    implicit none
    type   (treeNode         ), intent(inout), pointer :: node
    class  (nodeComponentDisk)               , pointer :: disk
    type   (history          )                         :: stellarPropertiesHistory
    logical                                            :: createStellarPropertiesHistory

    ! Get the disk component.
    disk => node%disk()
    ! Exit if already initialized.
    if (disk%isInitialized()) return
    ! Determine which histories must be created.
    stellarPropertiesHistory      =     disk                    %stellarPropertiesHistory()
    createStellarPropertiesHistory=.not.stellarPropertiesHistory%exists                  ()
    call                                stellarPropertiesHistory%destroy                 ()
    ! Create the stellar properties history.
    if (createStellarPropertiesHistory) then
       ! Create the stellar properties history.
       call stellarPopulationProperties_%historyCreate(node,stellarPropertiesHistory)
       call disk%stellarPropertiesHistorySet          (     stellarPropertiesHistory)
    end if
    ! Record that the disk has been initialized.
    call disk%isInitializedSet(.true.)
    return
  end subroutine Node_Component_Disk_Very_Simple_Create

  !# <rateComputeTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Rate_Compute</unitName>
  !# </rateComputeTask>
  subroutine Node_Component_Disk_Very_Simple_Rate_Compute(node,odeConverged,interrupt,interruptProcedureReturn,propertyType)
    !% Compute the very simple disk node mass rate of change.
    use Star_Formation_Feedback_Disks
    use Stellar_Feedback
    use Dark_Matter_Halo_Scales
    use Abundances_Structure
    use Galactic_Structure_Options
    use Histories
    use Stellar_Luminosities_Structure
    use Galacticus_Nodes              , only : treeNode            , nodeComponentDisk          , nodeComponentHotHalo, interruptTask, &
         &                                     propertyTypeInactive, nodeComponentDiskVerySimple
    implicit none
    type            (treeNode            ), intent(inout), pointer :: node
    logical                               , intent(in   )          :: odeConverged
    class           (nodeComponentDisk   )               , pointer :: disk
    class           (nodeComponentHotHalo)               , pointer :: hotHalo
    logical                               , intent(inout)          :: interrupt
    procedure       (interruptTask       ), intent(inout), pointer :: interruptProcedureReturn
    procedure       (interruptTask       )               , pointer :: interruptProcedure
    integer                               , intent(in   )          :: propertyType
    double precision                                               :: stellarMassRate           , fuelMassRate         , &
         &                                                            massOutflowRate
    type            (history             )                         :: stellarHistoryRate
    type            (abundances          ), save                   :: fuelAbundancesRate        , stellarAbundancesRate, &
         &                                                            abundancesOutflowRate
    type            (stellarLuminosities ), save                   :: luminositiesStellarRates
    !$omp threadprivate(fuelAbundancesRate,stellarAbundancesRate,abundancesOutflowRate,luminositiesStellarRates)
    !GCC$ attributes unused :: odeConverged
        
    ! Return immediately if inactive variables are requested.
    if (propertyType == propertyTypeInactive) return
    ! Get a local copy of the interrupt procedure.
    interruptProcedure => interruptProcedureReturn
    ! Get the disk and check that it is of our class.
    disk => node%disk()
    select type (disk)
    class is (nodeComponentDiskVerySimple)
       ! Check for a realistic disk, return immediately if disk is unphysical.
       if (disk%massGas() < 0.0d0) return
       ! Interrupt if the disk is not initialized.
       if (.not.disk%isInitialized()) then
          interrupt=.true.
          interruptProcedureReturn => Node_Component_Disk_Very_Simple_Create
          return
       end if
       ! Get rates.
       call Node_Component_Disk_Very_Simple_Rates(node,fuelMassRate,fuelAbundancesRate,stellarMassRate,stellarAbundancesRate,massOutflowRate,stellarHistoryRate,luminositiesStellarRates)
       ! Adjust rates.
       call                                      disk%             massStellarRate(         stellarMassRate)
       call                                      disk%                 massGasRate(            fuelMassRate)
       if (diskVerySimpleTrackAbundances) then
          call                                   disk%       abundancesStellarRate(   stellarAbundancesRate)
          call                                   disk%           abundancesGasRate(      fuelAbundancesRate)
       end if
       if (diskVerySimpleTrackLuminosities) call disk%     luminositiesStellarRate(luminositiesStellarRates)
       if (stellarHistoryRate%exists()    ) call disk%stellarPropertiesHistoryRate(      stellarHistoryRate)
       if (massOutflowRate > 0.0d0) then
          ! Push to the hot halo.
          hotHalo => node%hotHalo      ()
          call hotHalo%outflowingMassRate(+massOutflowRate)
          call disk   %       massGasRate(-massOutflowRate)
          if (diskVerySimpleTrackAbundances) then
             abundancesOutflowRate=disk%abundancesGas()
             call abundancesOutflowRate%massToMassFraction(disk%massGas())
             abundancesOutflowRate=abundancesOutflowRate*massOutflowRate
             call hotHalo%outflowingAbundancesRate(+abundancesOutflowRate)
             call disk   %       abundancesGasRate(-abundancesOutflowRate)
          end if
       end if
    end select
    ! Return the procedure pointer.
    interruptProcedureReturn => interruptProcedure
    return
  end subroutine Node_Component_Disk_Very_Simple_Rate_Compute

  !# <analyticSolverTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Analytic_Solver</unitName>
  !# </analyticSolverTask>
  subroutine Node_Component_Disk_Very_Simple_Analytic_Solver(node,timeStart,timeEnd,solved)
    use Histories
    use Abundances_Structure
    use Stellar_Luminosities_Structure
    use Galacticus_Error
    use Galacticus_Nodes              , only : treeNode, nodeComponentBasic, nodeComponentDisk, nodeComponentHotHalo, nodeComponentSatellite
    implicit none
    type            (treeNode              ), intent(inout), pointer   :: node
    double precision                        , intent(in   )            :: timeStart              , timeEnd
    logical                                 , intent(inout)            :: solved
    type            (treeNode              ),                pointer   :: nodeHost
    class           (nodeComponentBasic    )               , pointer   :: basic                  , basicHost               , &
         &                                                                basicParentHost
    class           (nodeComponentDisk     )               , pointer   :: disk
    class           (nodeComponentHotHalo  )               , pointer   :: hotHalo                , hotHaloHost
    class           (nodeComponentSatellite)               , pointer   :: satellite
    double precision                                       , parameter :: massTolerance=1.0d-6
    double precision                                                   :: massGasInitial         , massStellarInitial      , &
         &                                                                timescaleFuel          , timescaleOutflow        , &
         &                                                                timescaleStellar       , massGasFinal            , &
         &                                                                massStellarFinal       , massOutflowed           , &
         &                                                                rateFuel               , rateStars               , &
         &                                                                rateOutflow            , timeStep                , &
         &                                                                exponentialFactor      , massStellarAsymptotic
    type            (history               )                           :: stellarHistoryRate
    type            (abundances            ), save                     :: rateAbundanceFuel      , rateAbundanceStars      , &
         &                                                                abundancesGasInitial   , abundancesStellarInitial, &
         &                                                                yieldMassEffective     , abundancesStellarFinal  , &
         &                                                                abundancesGasFinal     , abundancesOutflowed
    type            (stellarLuminosities   ), save                     :: luminositiesStellarRates
    !$omp threadprivate(rateAbundanceFuel,rateAbundanceStars,abundancesGasInitial,abundancesStellarInitial,yieldMassEffective,abundancesStellarFinal,abundancesGasFinal,abundancesOutflowed,luminositiesStellarRates)

    if (diskVerySimpleUseAnalyticSolver) then
       disk => node%disk()
       if (node%isSatellite().and.disk%isInitialized()) then
          ! Luminosities can not be computed analytically.
          if (diskVerySimpleTrackLuminosities) call Galacticus_Error_Report('analytic solver does not support stellar luminosity calculation'//{introspection:location})
          ! Calculate analytic solution.
          timeStep          =timeEnd-timeStart
          massGasInitial          =disk%massGas          ()
          massStellarInitial      =disk%massStellar      ()
          abundancesGasInitial    =disk%abundancesGas    ()
          abundancesStellarInitial=disk%abundancesStellar()
          if (massGasInitial > massTolerance) then
             hotHalo   => node%hotHalo  ()
             satellite => node%satellite()
             call Node_Component_Disk_Very_Simple_Rates(node,rateFuel,rateAbundanceFuel,rateStars,rateAbundanceStars,rateOutflow,stellarHistoryRate,luminositiesStellarRates)
             ! If any rates are zero, return without an analytic solution
             if (rateFuel == 0.0d0 .and. rateStars == 0.0d0 .and. rateOutflow == 0.0d0) return
             ! Compute timescales.             
             timescaleFuel     =massGasInitial/(+rateOutflow-rateFuel                            )
             timescaleStellar  =massGasInitial/(                              +rateStars         )
             timescaleOutflow  =massGasInitial/(+rateOutflow                                     )
             yieldMassEffective=timescaleFuel *(            +rateAbundanceFuel+rateAbundanceStars)
             ! Estimate asymptotic final stellar mass
             massStellarAsymptotic=massStellarInitial+massGasInitial*(timescaleFuel/timescaleStellar)            
             ! Check if this galaxy can be removed because it will never be of sufficient mass to be interesting.
             if     (                                                                        &
                  &   massStellarAsymptotic     < diskVerySimpleAnalyticSolverPruneMassStars &
                  &  .and.                                                                   &
                  &   massGasInitial            < diskVerySimpleAnalyticSolverPruneMassGas   &
                  &  .and.                                                                   &
                  &   satellite%timeOfMerging() > timePresentDay                             &
                  & ) then
                ! Galaxy is too small to care about. Add gas and abundances which will outflow from this satellite to its future host halos.
                nodeHost => node%parent
                do while (associated(nodeHost%parent))
                   basicHost       => nodeHost%basic       (                 )
                   hotHaloHost     => nodeHost%hotHalo     (autoCreate=.true.)
                   basicParentHost => nodeHost%parent%basic(                 )
                   massOutflowed   =  +massGasInitial                                                    &
                        &             *(                                                                 &
                        &               +timescaleFuel                                                   &
                        &               /timescaleOutflow                                                &
                        &              )                                                                 &
                        &             *(                                                                 &
                        &               +exp(-max(0.0d0,basicHost      %time()-timeStart)/timescaleFuel) &
                        &               -exp(-max(0.0d0,basicParentHost%time()-timeStart)/timescaleFuel) &
                        &              )
                   call hotHaloHost%outflowedMassSet(                             &
                        &                            +hotHaloHost%outflowedMass() &
                        &                            +            massOutflowed   &
                        &                           )
                   if (diskVerySimpleTrackAbundances) then
                      abundancesOutflowed=+(                                &
                           &                -(                              &
                           &                  +abundancesGasInitial         &
                           &                  +yieldMassEffective           &
                           &                  *(                            &
                           &                    +1.0d0                      &
                           &                    +(                          &
                           &                      +basicHost        %time() &
                           &                      -timeStart                &
                           &                     )                          &
                           &                    /timescaleFuel              &
                           &                   )                            &
                           &                 )                              &
                           &                *exp(                           &
                           &                     -(                         &
                           &                       +basicHost      %time()  &
                           &                       -timeStart               &
                           &                      )                         &
                           &                     /timescaleFuel             &
                           &                    )                           &
                           &                +(                              &
                           &                  +abundancesGasInitial         &
                           &                  +yieldMassEffective           &
                           &                  *(                            &
                           &                    +1.0d0                      &
                           &                    +(                          &
                           &                      +basicParentHost%time()   &
                           &                      -timeStart                &
                           &                     )                          &
                           &                    /timescaleFuel              &
                           &                   )                            &
                           &                 )                              &
                           &                *exp(                           &
                           &                     -(                         &
                           &                       +basicParentHost%time()  &
                           &                       -timeStart               &
                           &                      )                         &
                           &                     /timescaleFuel             &
                           &                    )                           &
                           &               )                                &
                           &              *timescaleFuel                    &
                           &              /timescaleOutflow                   
                      call hotHaloHost%outflowedAbundancesSet(                                   &
                           &                                  +hotHaloHost%outflowedAbundances() &
                           &                                  +            abundancesOutflowed   &
                           &                                 )
                   end if
                   nodeHost => nodeHost%parent
                end do
                ! Remove the node from the host and destroy it.
                call node%removeFromHost()
                call node%destroy       ()
                deallocate(node)
                nullify   (node)                
             else
                ! Galaxy is sufficiently large (or will merge), so simply process it to the end time.
                exponentialFactor=exp(-timeStep/timescaleFuel)
                massGasFinal     =                   +massGasInitial                                 *       exponentialFactor
                massStellarFinal =+massStellarInitial+massGasInitial*(timescaleFuel/timescaleStellar)*(1.0d0-exponentialFactor)
                massOutflowed    =                   +massGasInitial*(timescaleFuel/timescaleOutflow)*(1.0d0-exponentialFactor)
                call hotHalo%outflowedMassSet(massOutflowed   )
                call disk   %massGasSet      (massGasFinal    )
                call disk   %massStellarSet  (massStellarFinal)
                if (diskVerySimpleTrackAbundances) then                   
                   abundancesGasFinal    =+(                        &
                        &                   +abundancesGasInitial   &
                        &                   +yieldMassEffective     &
                        &                   *timeStep               &
                        &                   /timescaleFuel          &
                        &                  )                        &
                        &                 *exponentialFactor
                   abundancesStellarFinal=+abundancesStellarFinal   &
                        &                 +(                        &
                        &                   +(                      &
                        &                     +abundancesGasInitial &
                        &                     +yieldMassEffective   &
                        &                    )                      &
                        &                   +(                      &
                        &                     +abundancesGasInitial &
                        &                     +yieldMassEffective   &
                        &                     *(                    &
                        &                       +1.0d0              &
                        &                       +timeStep           &
                        &                       /timescaleFuel      &
                        &                      )                    &
                        &                    )                      &
                        &                   *exponentialFactor      &
                        &                  )                        &
                        &                 *timescaleFuel            &
                        &                 /timescaleStellar                   
                   abundancesOutflowed   =+(                        &
                        &                   +(                      &
                        &                     +abundancesGasInitial &
                        &                     +yieldMassEffective   &
                        &                    )                      &
                        &                   +(                      &
                        &                     +abundancesGasInitial &
                        &                     +yieldMassEffective   &
                        &                     *(                    &
                        &                       +1.0d0              &
                        &                       +timeStep           &
                        &                       /timescaleFuel      &
                        &                      )                    &
                        &                    )                      &
                        &                   *exponentialFactor      &
                        &                  )                        &
                        &                 *timescaleFuel            &
                        &                 /timescaleOutflow                   
                   call hotHalo%outflowedAbundancesSet(abundancesOutflowed   )
                   call disk   %abundancesGasSet      (abundancesGasFinal    )
                   call disk   %abundancesStellarSet  (abundancesStellarFinal)
                end if
             end if
          end if
          ! Update time and merging times.
          if (associated(node)) then
             basic     => node%basic    ()
             satellite => node%satellite()
             call basic    %     timeSet(                      timeEnd )
             call satellite%mergeTimeSet(satellite%mergeTime()-timeStep)
          end if
          ! Record that we solved this system analytically.
          solved=.true.
       end if
    end if
    return
  end subroutine Node_Component_Disk_Very_Simple_Analytic_Solver
  
  subroutine Node_Component_Disk_Very_Simple_Rates(node,fuelMassRate,fuelAbundancesRate,stellarMassRate,stellarAbundancesRate,massOutflowRate,stellarHistoryRate,luminositiesStellarRates)
    !% Compute rates.
    use Stellar_Feedback
    use Abundances_Structure
    use Galactic_Structure_Options
    use Histories
    use Stellar_Luminosities_Structure
    use Galacticus_Nodes              , only : treeNode, nodeComponentDisk, nodeComponentDiskVerySimple
    implicit none
    type            (treeNode           ), intent(inout), pointer :: node
    type            (history            ), intent(inout)          :: stellarHistoryRate
    double precision                     , intent(  out)          :: fuelMassRate            , stellarMassRate       , &
         &                                                           massOutflowRate
    type            (abundances         ), intent(inout)          :: fuelAbundancesRate      , stellarAbundancesRate
    type            (stellarLuminosities), intent(inout)          :: luminositiesStellarRates
    class           (nodeComponentDisk  )               , pointer :: disk
    double precision                                              :: diskDynamicalTime       , fuelMass              , &
         &                                                           energyInputRate         , starFormationRate
    type            (abundances         )               , save    :: fuelAbundances
    !$omp threadprivate (fuelAbundances)

    ! Get the disk.
    disk => node%disk()
    ! Initialize to zero rates.
    fuelMassRate         =0.0d0
    stellarMassRate      =0.0d0
    massOutflowRate      =0.0d0
    fuelAbundancesRate   =zeroAbundances
    stellarAbundancesRate=zeroAbundances
    ! Check for a realistic disk, return immediately if disk is unphysical.
    if (disk%massGas() <= 0.0d0) return
    ! Compute fuel abundances.
    fuelAbundances=disk%abundancesGas()
    call fuelAbundances%massToMassFraction(disk%massGas())    
    ! Compute the star formation rate.
    select type (disk)
    class is (nodeComponentDiskVerySimple)
       starFormationRate=Node_Component_Disk_Very_Simple_SFR(disk)
    end select
    ! Find rates of change of stellar mass, and gas mass.
    stellarHistoryRate=disk%stellarPropertiesHistory()
    call stellarPopulationProperties_%rates(starFormationRate,fuelAbundances,disk,node,stellarHistoryRate&
            &,stellarMassRate,fuelMassRate,energyInputRate,fuelAbundancesRate,stellarAbundancesRate,luminositiesStellarRates,computeRateLuminosityStellar=.true.)
    ! Find rate of outflow of material from the disk and pipe it to the outflowed reservoir.
    massOutflowRate=starFormationFeedbackDisks_%outflowRate(node,energyInputRate,starFormationRate)
    if (massOutflowRate > 0.0d0) then
       ! Limit the outflow rate timescale to a multiple of the dynamical time.
       fuelMass         =disk                %massGas           (    )
       diskDynamicalTime=darkMatterHaloScale_%dynamicalTimescale(node)
       massOutflowRate  =min(massOutflowRate,fuelMass/diskOutflowTimescaleMinimum/diskDynamicalTime)
    end if
    return
  end subroutine Node_Component_Disk_Very_Simple_Rates
  
  !# <scaleSetTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Scale_Set</unitName>
  !# </scaleSetTask>
  subroutine Node_Component_Disk_Very_Simple_Scale_Set(node)
    !% Set scales for properties of {\normalfont \ttfamily node}.
    use Abundances_Structure
    use Stellar_Luminosities_Structure
    use Histories
    use Galacticus_Nodes              , only : treeNode, nodeComponentDisk, nodeComponentDiskVerySimple
    implicit none
    type            (treeNode           ), intent(inout), pointer :: node
    class           (nodeComponentDisk  )               , pointer :: disk
    double precision                     , parameter              :: luminosityMinimum             =1.0d0
    double precision                                              :: mass
    type            (history            )                         :: stellarPopulationHistoryScales
    type            (abundances         )                         :: abundancesTotal
    type            (stellarLuminosities)                         :: stellarLuminositiesScale

    ! Get the disk component.
    disk => node%disk()
    ! Check if a very simple disk component exists.
    select type (disk)
    class is (nodeComponentDiskVerySimple)
       ! Set scale for gas and stellar mass.
       mass=disk%massGas()+disk%massStellar()
       call disk%massGasScale    (max(mass,diskVerySimpleMassScaleAbsolute))
       call disk%massStellarScale(max(mass,diskVerySimpleMassScaleAbsolute))
       ! Set scale for gas and stellar abundances.
       abundancesTotal=disk%abundancesGas()+disk%abundancesStellar()
       call disk%abundancesGasScale    (max(abundancesTotal,unitAbundances*diskVerySimpleMassScaleAbsolute))
       call disk%abundancesStellarScale(max(abundancesTotal,unitAbundances*diskVerySimpleMassScaleAbsolute))
       ! Set scales for stellar population properties and star formation histories.
       stellarPopulationHistoryScales=disk%stellarPropertiesHistory()
       call stellarPopulationProperties_%scales(disk%massStellar(),zeroAbundances,stellarPopulationHistoryScales)
       call disk%stellarPropertiesHistoryScale (                                  stellarPopulationHistoryScales)
       call stellarPopulationHistoryScales%destroy()
       ! Set scale for stellar luminosities.
       stellarLuminositiesScale=max(                                  &
            &                       +abs(disk%luminositiesStellar()), &
            &                       +unitStellarLuminosities          &
            &                       *luminosityMinimum                &
            &                      )
       call stellarLuminositiesScale%truncate                (disk                    %luminositiesStellar())
       call disk                    %luminositiesStellarScale(stellarLuminositiesScale                      )
    end select
    return
  end subroutine Node_Component_Disk_Very_Simple_Scale_Set

  !# <satelliteMergerTask>
  !#  <unitName>Node_Component_Disk_Very_Simple_Satellite_Merging</unitName>
  !#  <after>Satellite_Merging_Remnant_Compute</after>
  !# </satelliteMergerTask>
  subroutine Node_Component_Disk_Very_Simple_Satellite_Merging(node)
    !% Transfer any very simple disk associated with {\normalfont \ttfamily node} to its host halo.
    use Satellite_Merging_Remnant_Properties
    use Satellite_Merging_Mass_Movements
    use Galacticus_Error
    use Abundances_Structure
    use Stellar_Luminosities_Structure
    use Galacticus_Nodes              , only : treeNode, nodeComponentDisk, nodeComponentDiskVerySimple, nodeComponentSpheroid
    implicit none
    type (treeNode             ), intent(inout), pointer :: node
    type (treeNode             )               , pointer :: nodeHost
    class(nodeComponentDisk    )               , pointer :: diskHost    , disk
    class(nodeComponentSpheroid)               , pointer :: spheroidHost
 
    ! Check that the disk is of the verySimple class.
    disk => node%disk()
    select type (disk)
    class is (nodeComponentDiskVerySimple)       
       ! Find the node to merge with and its disk component (and spheroid if necessary).
       nodeHost => node    %mergesWith(                 )
       diskHost => nodeHost%disk      (autoCreate=.true.)
       if     (                                                        &
            &   destinationGasSatellite   == destinationMergerSpheroid &
            &  .or.                                                    &
            &   destinationStarsSatellite == destinationMergerSpheroid &
            & )                                                        &
            & spheroidHost => nodeHost%spheroid(autoCreate=.true.)
       ! Move the gas component of the very simple disk to the host.
       select case (destinationGasSatellite)
       case (destinationMergerDisk)
          call diskHost    %massGasSet          (                              &
               &                                  diskHost    %      massGas() &
               &                                 +disk        %      massGas() &
               &                                )
          call diskHost    %abundancesGasSet    (                              &
               &                                  diskHost    %abundancesGas() &
               &                                 +disk        %abundancesGas() &
               &                                )
       case (destinationMergerSpheroid)
          call spheroidHost%massGasSet          (                              &
               &                                  spheroidHost%massGas      () &
               &                                 +disk        %massGas      () &
               &                                )
          call spheroidHost%abundancesGasSet    (                              &
               &                                  spheroidHost%abundancesGas() &
               &                                 +disk        %abundancesGas() &
               &                                )
       case default
          call Galacticus_Error_Report(                                    &
               &                       'unrecognized movesTo descriptor'// &
               &                       {introspection:location}            &
               &                      )
       end select
       call    disk%massGasSet                  (                                        &
            &                                                             0.0d0          &
            &                                   )
       call    disk%abundancesGasSet            (                                        &
            &                                                    zeroAbundances          &
            &                                   )
       ! Move the stellar component of the very simple disk to the host.
       select case (destinationStarsSatellite)
       case (destinationMergerDisk)
          call diskHost    %massStellarSet        (                                    &
               &                                    diskHost    %        massStellar() &
               &                                   +disk        %        massStellar() &
               &                                  )
          call diskHost    %abundancesStellarSet  (                                    &
               &                                    diskHost    %  abundancesStellar() &
               &                                   +disk        %  abundancesStellar() &
               &                                  )
          call diskHost    %luminositiesStellarSet(                                    &
               &                                    diskHost    %luminositiesStellar() &
               &                                   +disk        %luminositiesStellar() &
               &                                  )
       case (destinationMergerSpheroid)
          call spheroidHost%massStellarSet        (                                    &
               &                                    spheroidHost%  massStellar      () &
               &                                   +disk        %  massStellar      () &
               &                                  )
          call spheroidHost%abundancesStellarSet  (                                    &
               &                                    spheroidHost%  abundancesStellar() &
               &                                   +disk        %  abundancesStellar() &
               &                                  )
          call spheroidHost%luminositiesStellarSet(                                    &
               &                                    spheroidHost%luminositiesStellar() &
               &                                   +disk        %luminositiesStellar() &
               &                                  )
       case default
          call Galacticus_Error_Report(                                    &
               &                       'unrecognized movesTo descriptor'// &
               &                       {introspection:location}            &
               &                      )
       end select
       call    disk%         massStellarSet(                       &
            &                                                0.0d0 &
            &                              )
       call    disk%  abundancesStellarSet(                        &
            &                                       zeroAbundances &
            &                             )
       call    disk%luminositiesStellarSet(                        &
            &                              zeroStellarLuminosities &
            &                             )
    end select
    return
  end subroutine Node_Component_Disk_Very_Simple_Satellite_Merging

  double precision function Node_Component_Disk_Very_Simple_SFR(self)
    !% Return the star formation rate of the very simple disk.
    use Numerical_Constants_Math
    use Galacticus_Nodes        , only : nodeComponentDiskVerySimple
    implicit none
    class           (nodeComponentDiskVerySimple     ), intent(inout) :: self
    double precision                                                  :: diskDynamicalTime           , gasMass              , &
         &                                                               starFormationTimescale      , surfaceDensityCentral, &
         &                                                               radiusThreshold             , massStarForming      , &
         &                                                               surfaceDensityThreshold

    ! Get the gas mass.
    gasMass=self%massGas()    
    ! Get the star formation timescale.
    starFormationTimescale=starFormationTimescaleDisks_%timescale(self%hostNode)
    ! Limit the star formation timescale to a multiple of the dynamical time.
    diskDynamicalTime     =darkMatterHaloScale_%dynamicalTimescale(self%hostNode)
    starFormationTimescale=max(starFormationTimescale,diskStarFormationTimescaleMinimum*diskDynamicalTime)
    ! If timescale is finite and gas mass is positive, then compute star formation rate.
    if (starFormationTimescale > 0.0d0 .and. gasMass > 0.0d0 .and. self%radius() > 0.0d0) then
       ! Find mass of gas actively involved in star formation.
       if (diskVerySimpleSurfaceDensityThreshold > 0.0d0) then
          surfaceDensityCentral  =+gasMass          &
               &                  /self%radius()**2 &
               &                  /2.0d0            &
               &                  /Pi
          surfaceDensityThreshold=+surfaceDensityNormalization                                                                    &
               &                  *velocityExponentiator%exponentiate(darkMatterProfileDMO_ %circularVelocityMaximum(self%hostNode))
          if (surfaceDensityCentral > surfaceDensityThreshold) then
             radiusThreshold=-log(surfaceDensityThreshold/surfaceDensityCentral)
             massStarForming=gasMass*(1.0d0-(1.0d0+radiusThreshold)*exp(-radiusThreshold))
          else
             massStarForming=0.0d0
          end if
       else
          massStarForming=gasMass
       end if
       Node_Component_Disk_Very_Simple_SFR=massStarForming/starFormationTimescale
    else
       Node_Component_Disk_Very_Simple_SFR=0.0d0
    end if
    return
  end function Node_Component_Disk_Very_Simple_SFR

end module Node_Component_Disk_Very_Simple
