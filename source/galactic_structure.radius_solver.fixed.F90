!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implementation of a ``fixed'' solver for galactic structure (no self-gravity of baryons, and size simply scales in
  !% proportion to specific angular momentum).
  
  use Dark_Matter_Halo_Scales , only : darkMatterHaloScale , darkMatterHaloScaleClass
  use Dark_Matter_Profiles_DMO, only : darkMatterProfileDMO, darkMatterProfileDMOClass

  !# <galacticStructureSolver name="galacticStructureSolverFixed" autoHook="yes">
  !#  <description>A ``fixed'' solver for galactic structure (no self-gravity of baryons, and size simply scales in proportion to specific angular momentum).</description>
  !# </galacticStructureSolver>
  type, extends(galacticStructureSolverClass) :: galacticStructureSolverFixed
     !% Implementation of a ``fixed'' solver for galactic structure (no self-gravity of baryons, and size simply scales in
     !% proportion to specific angular momentum).
     private
     double precision                                     :: factor
     integer                                              :: radiusFixed
     class           (darkMatterHaloScaleClass ), pointer :: darkMatterHaloScale_
     class           (darkMatterProfileDMOClass), pointer :: darkMatterProfileDMO_
   contains
     final     ::             fixedDestructor
     procedure :: solve    => fixedSolve
     procedure :: revert   => fixedRevert
     procedure :: autoHook => fixedAutoHook
  end type galacticStructureSolverFixed

  interface galacticStructureSolverFixed
     !% Constructors for the {\normalfont \ttfamily fixed} galactic structure solver class.
     module procedure fixedConstructorParameters
     module procedure fixedConstructorInternal
  end interface galacticStructureSolverFixed

  !# <enumeration>
  !#  <name>radiusFixed</name>
  !#  <description>Enumerates the possible definitions of radius used by the ``fixed'' galactic structure solver.</description>
  !#  <encodeFunction>yes</encodeFunction>
  !#  <validator>yes</validator>
  !#  <visibility>public</visibility>
  !#  <entry label="virial"    />
  !#  <entry label="turnaround"/>
  !# </enumeration>

contains
  
  function fixedConstructorParameters(parameters) result(self)
    !% Constructor for the {\normalfont \ttfamily fixed} galactic structure solver class which takes a
    !% parameter set as input.
    use Input_Parameters, only : inputParameter, inputParameters
    implicit none
    type            (galacticStructureSolverFixed)                :: self
    type            (inputParameters             ), intent(inout) :: parameters
    class           (darkMatterHaloScaleClass    ), pointer       :: darkMatterHaloScale_
    class           (darkMatterProfileDMOClass   ), pointer       :: darkMatterProfileDMO_
    double precision                                              :: factor
    type            (varying_string              )                :: radiusFixed
    
    !# <inputParameter>
    !#   <name>factor</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultSource>\citep{mo_formation_1998}</defaultSource>
    !#   <defaultValue>sqrt(0.5d0)</defaultValue>
    !#   <description>The ratio of galaxy radius to $\lambda r_\mathrm{vir}$ in the ``fixed'' galactic structure radius solver algorithm.</description>
    !#   <source>parameters</source>
    !#   <type>real</type>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>radiusFixed</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>var_str('virial')</defaultValue>
    !#   <description>The radius to use in the ``fixed'' galactic structure radius solver algorithm. Allowed options are ``virial'' and ``turnaround''.</description>
    !#   <source>parameters</source>
    !#   <type>string</type>
    !# </inputParameter>
    !# <objectBuilder class="darkMatterHaloScale"  name="darkMatterHaloScale_"  source="parameters"/>
    !# <objectBuilder class="darkMatterProfileDMO" name="darkMatterProfileDMO_" source="parameters"/>
    self=galacticStructureSolverFixed(factor,enumerationRadiusFixedEncode(char(radiusFixed),includesPrefix=.false.),darkMatterHaloScale_,darkMatterProfileDMO_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="darkMatterHaloScale_" />
    !# <objectDestructor name="darkMatterProfileDMO_"/>
    return
  end function fixedConstructorParameters

  function fixedConstructorInternal(factor,radiusFixed,darkMatterHaloScale_,darkMatterProfileDMO_) result(self)
    !% Internal constructor for the {\normalfont \ttfamily fixed} galactic structure solver class.
    use Galacticus_Error, only : Galacticus_Error_Report, Galacticus_Component_List
    use Galacticus_Nodes, only : defaultBasicComponent
    implicit none
    type            (galacticStructureSolverFixed)                        :: self
    double precision                              , intent(in   )         :: factor
    integer                                       , intent(in   )         :: radiusFixed
    class           (darkMatterHaloScaleClass    ), intent(in   ), target :: darkMatterHaloScale_
    class           (darkMatterProfileDMOClass   ), intent(in   ), target :: darkMatterProfileDMO_
    !# <constructorAssign variables="factor, radiusFixed, *darkMatterHaloScale_, *darkMatterProfileDMO_"/>

    if (.not.enumerationRadiusFixedIsValid(radiusFixed)) call Galacticus_Error_Report('invalid radiusFixed'//{introspection:location})
    if (radiusFixed == radiusFixedTurnaround) then
       if (.not.defaultBasicComponent%radiusTurnaroundIsGettable())                                                         &
            & call Galacticus_Error_Report                                                                                  &
            &   (                                                                                                           &
            &    'the "radiusTurnaround" property of the basic component must be gettable.'                              // &
            &    Galacticus_Component_List(                                                                                 &
            &                              'basic'                                                                        , &
            &                                defaultBasicComponent%radiusTurnaroundAttributeMatch(requireGettable=.true.)   &
            &                             )                                                                              // &
            &    {introspection:location}                                                                                   &
            &   )
    end if
    return
  end function fixedConstructorInternal

  subroutine fixedAutoHook(self)
    !% Attach to various event hooks.
    use Events_Hooks, only : preDerivativeEvent, postEvolveEvent, satelliteMergerEvent, nodePromotionEvent
    implicit none
    class(galacticStructureSolverFixed), intent(inout) :: self

    call   preDerivativeEvent%attach(self,fixedSolvePreDeriativeHook,bindToOpenMPThread=.true.)
    call      postEvolveEvent%attach(self,fixedSolveHook            ,bindToOpenMPThread=.true.)
    call satelliteMergerEvent%attach(self,fixedSolveHook            ,bindToOpenMPThread=.true.)
    call   nodePromotionEvent%attach(self,fixedSolveHook            ,bindToOpenMPThread=.true.)
    return
  end subroutine fixedAutoHook

  subroutine fixedDestructor(self)
    !% Destructor for the {\normalfont \ttfamily fixed} galactic structure solver class.
    use Events_Hooks, only : preDerivativeEvent, postEvolveEvent, satelliteMergerEvent, nodePromotionEvent
    implicit none
    type(galacticStructureSolverFixed), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterHaloScale_" />
    !# <objectDestructor name="self%darkMatterProfileDMO_"/>
    call   preDerivativeEvent%detach(self,fixedSolvePreDeriativeHook)
    call      postEvolveEvent%detach(self,fixedSolveHook            )
    call satelliteMergerEvent%detach(self,fixedSolveHook            )
    call   nodePromotionEvent%detach(self,fixedSolveHook            )
    return
  end subroutine fixedDestructor

  subroutine fixedSolveHook(self,node)
    !% Hookable wrapper around the solver.
    use Galacticus_Error, only : Galacticus_Error_Report
    implicit none
    class(*       ), intent(inout)         :: self
    type (treeNode), intent(inout), target :: node

    select type (self)
    type is (galacticStructureSolverFixed)
       call self%solve(node)
    class default
       call Galacticus_Error_Report('incorrect class'//{introspection:location})
    end select
    return
  end subroutine fixedSolveHook
  
  subroutine fixedSolvePreDeriativeHook(self,node,propertyType)
    !% Hookable wrapper around the solver.
    use Galacticus_Error, only : Galacticus_Error_Report
    implicit none
    class  (*       ), intent(inout)         :: self
    type   (treeNode), intent(inout), target :: node
    integer          , intent(in   )         :: propertyType
    !GCC$ attributes unused :: propertyType

    select type (self)
    type is (galacticStructureSolverFixed)
       call self%solve(node)
    class default
       call Galacticus_Error_Report('incorrect class'//{introspection:location})
    end select
    return
  end subroutine fixedSolvePreDeriativeHook
  
  subroutine fixedSolve(self,node)
    !% Solve for the structure of galactic components assuming no self-gravity of baryons, and that size simply scales in
    !% proportion to specific angular momentum.
    include 'galactic_structure.radius_solver.tasks.modules.inc'
    include 'galactic_structure.radius_solver.plausible.modules.inc'
    implicit none
    class           (galacticStructureSolverFixed), intent(inout)         :: self
    type            (treeNode                    ), intent(inout), target :: node
    logical                                       , parameter             :: specificAngularMomentumRequired=.false.
    procedure       (solverGet                   ), pointer               :: radiusGet                              , velocityGet
    procedure       (solverSet                   ), pointer               :: radiusSet                              , velocitySet
    logical                                                               :: componentActive
    double precision                                                      :: specificAngularMomentum

    ! Check that the galaxy is physical plausible. In this fixed solver, we don't act on this.
    node%isPhysicallyPlausible=.true.
    node%isSolvable           =.true.
    include 'galactic_structure.radius_solver.plausible.inc'
    if (.not.node%isPhysicallyPlausible) return
    include 'galactic_structure.radius_solver.tasks.inc'
    return

  contains
    
    subroutine radiusSolve(node,specificAngularMomentum,radiusGet,radiusSet,velocityGet,velocitySet)
      !% Solve for the equilibrium radius of the given component.
      use Galacticus_Nodes, only : treeNode, nodeComponentSpin, nodeComponentBasic
      implicit none
      type            (treeNode          ), intent(inout)          :: node
      double precision                    , intent(in   )          :: specificAngularMomentum
      procedure       (solverGet         ), intent(in   ), pointer :: radiusGet              , velocityGet
      procedure       (solverSet         ), intent(in   ), pointer :: radiusSet              , velocitySet
      class           (nodeComponentSpin )               , pointer :: spin
      class           (nodeComponentBasic)               , pointer :: basic
      double precision                                             :: radius                 , velocity
      !GCC$ attributes unused :: radiusGet, velocityGet, specificAngularMomentum

      ! Find the radius of the component, assuming radius is a fixed fraction of radius times spin parameter.
      spin => node%spin()
      select case (self%radiusFixed)
      case (radiusFixedVirial    )
         velocity             =  self %darkMatterHaloScale_ %virialVelocity         (node)
         radius               =  self %darkMatterHaloScale_ %virialRadius           (node)*spin%spin()*self%factor
      case (radiusFixedTurnaround)
         basic                => node                       %basic                  (    )
         velocity             =  self %darkMatterProfileDMO_%circularVelocityMaximum(node)
         radius               =  basic                      %radiusTurnaround       (    )*spin%spin()*self%factor
      end select
      ! Set the component size to new radius and velocity.
      call radiusSet  (node,radius  )
      call velocitySet(node,velocity)    
      return
    end subroutine radiusSolve

  end subroutine fixedSolve

  subroutine fixedRevert(self,node)
    !% Revert radii for the fixed galactic structure solve. Not necessary for this algorithm.
    implicit none
    class(galacticStructureSolverFixed), intent(inout) :: self
    type (treeNode                     ), intent(inout) :: node
    !GCC$ attributes unused :: self, node

    return
  end subroutine fixedRevert
