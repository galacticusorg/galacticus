!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019, 2020, 2021, 2022, 2023, 2024, 2025
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!!{
Implements a transfer function class using the CLASS code.
!!}

  use :: Dark_Matter_Particles, only : darkMatterParticleClass
  use :: File_Utilities       , only : lockDescriptor

  !![
  <transferFunction name="transferFunctionCLASSCDM">
   <description>
    A transfer function class in which the \gls{cdm} transfer function is generated by \href{http://class-code.net}{\normalfont
    \scshape CLASS} using the specified cosmological parameters. The transfer function is written out to a file in the
    {\normalfont \ttfamily data/dynamic/largeScaleStructure} directory and will be re-read later if needed. {\normalfont
    \scshape CLASS} will be downloaded and run if the transfer function needs to be computed.
   </description>
  </transferFunction>
  !!]
  type, extends(transferFunctionFile) :: transferFunctionCLASSCDM
     !!{
     A transfer function class which utilizes the CLASS code to compute transfer functions.
     !!}
     private
     logical                                            :: initialized
     class           (darkMatterParticleClass), pointer :: darkMatterParticle_      => null()
     double precision                                   :: wavenumberMaximum
     logical                                            :: wavenumberMaximumReached
     integer                                            :: countPerDecade
   contains
     !![
     <methods>
       <method description="Check that the provided wavenumber is within the tabulated range of the transfer function." method="checkRange" />
     </methods>
     !!]
     final     ::                           classCDMDestructor
     procedure :: checkRange             => classCDMCheckRange
     procedure :: value                  => classCDMValue
     procedure :: logarithmicDerivative  => classCDMLogarithmicDerivative
     procedure :: wavenumbersLocalMinima => classCDMWavenumbersLocalMinima
  end type transferFunctionCLASSCDM

  interface transferFunctionCLASSCDM
     !!{
     Constructors for the file transfer function class.
     !!}
     module procedure classCDMConstructorParameters
     module procedure classCDMConstructorInternal
  end interface transferFunctionCLASSCDM

  ! Smallest maximum wavenumber to tabulate.
  double precision, parameter :: classWavenumberMaximumLimit=25000.0d0

contains

  function classCDMConstructorParameters(parameters) result(self)
    !!{
    Constructor for the CLASS transfer function class which takes a parameter set as input.
    !!}
    use :: Input_Parameters, only : inputParameter, inputParameters
    implicit none
    type            (transferFunctionCLASSCDM)                :: self
    type            (inputParameters         ), intent(inout) :: parameters
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (cosmologyFunctionsClass ), pointer       :: cosmologyFunctions_
    class           (darkMatterParticleClass ), pointer       :: darkMatterParticle_
    double precision                                          :: redshift
    integer                                                   :: countPerDecade

    !![
    <inputParameter>
      <name>redshift</name>
      <source>parameters</source>
      <defaultValue>0.0d0</defaultValue>
      <description>The redshift at which the transfer function should be evaluated.</description>
    </inputParameter>
    <inputParameter>
      <name>countPerDecade</name>
      <source>parameters</source>
      <defaultValue>0</defaultValue>
      <description>The number of points per decade of wavenumber to compute in the CLASS transfer function. A value of 0 allows CLASS to choose what it considers to be optimal spacing of wavenumbers.</description>
    </inputParameter>
    <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    <objectBuilder class="cosmologyFunctions"  name="cosmologyFunctions_"  source="parameters"/>
    <objectBuilder class="darkMatterParticle"  name="darkMatterParticle_"  source="parameters"/>
    !!]
    self=transferFunctionCLASSCDM(darkMatterParticle_,cosmologyParameters_,cosmologyFunctions_,redshift,countPerDecade)
    !![
    <inputParametersValidate source="parameters"/>
    <objectDestructor name="cosmologyParameters_"/>
    <objectDestructor name="cosmologyFunctions_" />
    <objectDestructor name="darkMatterParticle_" />
    !!]
    return
  end function classCDMConstructorParameters

  function classCDMConstructorInternal(darkMatterParticle_,cosmologyParameters_,cosmologyFunctions_,redshift,countPerDecade) result(self)
    !!{
    Internal constructor for the \href{http://class-code.net}{\normalfont \scshape CLASSCDM} transfer function class.
    !!}
    use :: Cosmology_Parameters , only : hubbleUnitsLittleH
    use :: Dark_Matter_Particles, only : darkMatterParticleCDM
    use :: Error                , only : Error_Report
    implicit none
    type            (transferFunctionCLASSCDM)                          :: self
    class           (darkMatterParticleClass ), intent(in   ), target   :: darkMatterParticle_
    class           (cosmologyParametersClass), intent(in   ), target   :: cosmologyParameters_
    class           (cosmologyFunctionsClass ), intent(in   ), target   :: cosmologyFunctions_
    double precision                          , intent(in   )           :: redshift
    integer                                   , intent(in   )           :: countPerDecade
    !![
    <constructorAssign variables="countPerDecade, redshift, *darkMatterParticle_, *cosmologyParameters_, *cosmologyFunctions_"/>
    !!]

    ! Require that the dark matter be cold dark matter.
    select type (darkMatterParticle_)
    class is (darkMatterParticleCDM)
       ! Cold dark matter particle - this is as expected.
    class default
       call Error_Report('transfer function expects a cold dark matter particle'//{introspection:location})
    end select
    ! Set initialization state.
    self%initialized                        =  .false.
    self%massHalfModeAvailable              =  .false.
    self%massQuarterModeAvailable           =  .false.
    ! No reference transfer function is used.
    self%massHalfModeAvailable              =  .false.
    self%massQuarterModeAvailable           =  .false.
    self%transferFunctionReferenceAvailable =  .false.
    self%transferFunctionReference          => null()
    ! Set the epoch time for this transfer function.
    self%time=self%cosmologyFunctions_%cosmicTime(self%cosmologyFunctions_%expansionFactorFromRedshift(redshift))
    ! Set maximum wavenumber.
    self%wavenumberMaximum=+classWavenumberMaximumLimit                                        &
         &                 *self%cosmologyParameters_%hubbleConstant(units=hubbleUnitsLittleH)
    self%wavenumberMaximumReached=.false.
    ! Set smooth extrapolation beyond the maximum wavenumber.
    self%factorWavenumberSmoothExtrapolation=2.0d0
    return
  end function classCDMConstructorInternal

  subroutine classCDMDestructor(self)
    !!{
    Destructor for the CLASS transfer function class.
    !!}
    implicit none
    type(transferFunctionCLASSCDM), intent(inout) :: self

    !![
    <objectDestructor name="self%cosmologyParameters_"/>
    <objectDestructor name="self%cosmologyFunctions_" />
    <objectDestructor name="self%darkMatterParticle_" />
    !!]
    return
  end subroutine classCDMDestructor

  subroutine classCDMCheckRange(self,wavenumber)
    !!{
    Check that the provided wavenumber is within the tabulated range and, if not, recompute
    the CLASS transfer function.
    !!}
    use :: File_Utilities  , only : File_Lock                        , File_Unlock
    use :: Interfaces_CLASS, only : Interface_CLASS_Transfer_Function
    implicit none
    class           (transferFunctionCLASSCDM), intent(inout) :: self
    double precision                          , intent(in   ) :: wavenumber
    logical                                                   :: makeTransferFunction
    type            (lockDescriptor          )                :: fileLock

    ! If the file has been read and the wavenumber is within range, simply return.
    makeTransferFunction=.false.
    if (self%initialized) then
       if     (                                       &
            &   wavenumber > exp(self%transfer%x(-1)) &
            &  .and.                                  &
            &   .not.self%wavenumberMaximumReached    &
            & ) makeTransferFunction=.true.
    else
       makeTransferFunction=.true.
    end if
    if (.not.makeTransferFunction) return
    ! Retrieve the transfer function.
    call Interface_CLASS_Transfer_Function(self%cosmologyParameters_,[self%redshift],wavenumber,self%wavenumberMaximum,self%countPerDecade,self%fileName,self%wavenumberMaximumReached)
    ! Get a lock on the relevant lock file.
    call File_Lock(char(self%fileName),fileLock)
    ! Read the newly created file.
    call self%readFile(char(self%fileName))
    ! Unlock the lock file.
    call File_Unlock(fileLock)
    ! Check the maximum wavenumber.
    if (self%transfer%x(-1) > log(self%wavenumberMaximum)-0.01d0) self%wavenumberMaximumReached=.true.
    ! Record that the transfer function has now been initialized.
    self%initialized=.true.
    return
  end subroutine classCDMCheckRange

  double precision function classCDMValue(self,wavenumber)
    !!{
    Return the transfer function at the given wavenumber.
    !!}
    implicit none
    class           (transferFunctionCLASSCDM), intent(inout) :: self
    double precision                          , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    classCDMValue=self%transferFunctionFile%value(wavenumber)
    return
  end function classCDMValue

  double precision function classCDMLogarithmicDerivative(self,wavenumber)
    !!{
    Return the logarithmic derivative of the transfer function at the given wavenumber.
    !!}
    implicit none
    class           (transferFunctionCLASSCDM), intent(inout) :: self
    double precision                          , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    classCDMLogarithmicDerivative=self%transferFunctionFile%logarithmicDerivative(wavenumber)
    return
  end function classCDMLogarithmicDerivative

  subroutine classCDMWavenumbersLocalMinima(self,wavenumbers)
    !!{
    Return a list of wavenumbers corresponding to local minima in the transfer function.
    !!}
    implicit none
    class           (transferFunctionCLASSCDM), intent(inout)                            :: self
    double precision                          , intent(  out), allocatable, dimension(:) :: wavenumbers

    call self%checkRange(wavenumber=1.0d0)
    wavenumbers=self%wavenumbersLocalMinima_
    return
  end subroutine classCDMWavenumbersLocalMinima
