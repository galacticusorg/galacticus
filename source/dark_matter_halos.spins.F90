!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements calculations of dark matter halo angular momentum.

module Dark_Matter_Halo_Spins
  !% Implements calculations of dark matter halo angular momentum.
  implicit none
  private
  public :: Dark_Matter_Halo_Angular_Momentum, Dark_Matter_Halo_Spin, Dark_Matter_Halo_Angular_Momentum_Growth_Rate

  ! Record of whether the module has been initialized.
  logical :: moduleInitialized=.false.

contains

  subroutine Dark_Matter_Halo_Spins_Initialize()
    !% Initialize the halo spins module.
    use Galacticus_Nodes
    use Galacticus_Error
    use ISO_Varying_String
    implicit none

    if (.not.moduleInitialized) then
       !$omp critical(Dark_Matter_Halo_Spins_Initialize)
       if (.not.moduleInitialized) then
          ! Ensure that the spin property is available.
          if (.not.defaultSpinComponent%spinIsGettable())                                                           &
               & call Galacticus_Error_Report                                                                       &
               &      (                                                                                             &
               &       'spin property of spin component must be gettable.'//                                        &
               &       Galacticus_Component_List(                                                                   &
               &                                 'spin'                                                          ,  &
               &                                 defaultSpinComponent %spinAttributeMatch(requireGettable=.true.)   &
               &                                )                                                                // &
               &       {introspection:location}                                                                     &
               &      )
          if (.not.defaultBasicComponent%massIsGettable())                                                          &
               & call Galacticus_Error_Report                                                                       &
               &      (                                                                                             &
               &       'mass property of basic component must be gettable.'//                                       &
               &       Galacticus_Component_List(                                                                   &
               &                                 'basic'                                                         ,  &
               &                                 defaultBasicComponent%massAttributeMatch(requireGettable=.true.)   &
               &                                )                                                                // &
               &       {introspection:location}                                                                     &
               &      )
          ! Record that the module is now initialized.
          moduleInitialized=.true.
       end if
       !$omp end critical(Dark_Matter_Halo_Spins_Initialize)
    end if
    return
  end subroutine Dark_Matter_Halo_Spins_Initialize

  double precision function Dark_Matter_Halo_Angular_Momentum(node)
    !% Returns the total anuglar momentum of {\normalfont \ttfamily node} based on its mass, energy and spin parameter.
    use Galacticus_Nodes
    use Numerical_Constants_Physical
    use Dark_Matter_Profiles
    implicit none
    type (treeNode              ), intent(inout) :: node
    class(nodeComponentBasic    ), pointer       :: basic
    class(nodeComponentSpin     ), pointer       :: spin
    class(darkMatterProfileClass), pointer       :: darkMatterProfile_

    ! Ensure that the module is initialized.
    call Dark_Matter_Halo_Spins_Initialize

    basic              => node%basic       (                 )
    spin               => node%spin        (autoCreate=.true.)
    darkMatterProfile_ => darkMatterProfile(                 )
    Dark_Matter_Halo_Angular_Momentum=spin%spin()*gravitationalConstantGalacticus*basic%mass()**2.5d0 &
         &/sqrt(abs(darkMatterProfile_%energy(node)))
    return
  end function Dark_Matter_Halo_Angular_Momentum

  double precision function Dark_Matter_Halo_Angular_Momentum_Growth_Rate(node)
    !% Returns the rate of change of the total anuglar momentum of {\normalfont \ttfamily node} based on its mass, energy and spin parameter.
    use Galacticus_Nodes
    use Dark_Matter_Profiles
    implicit none
    type (treeNode              ), intent(inout), pointer :: node
    class(nodeComponentBasic    )               , pointer :: basic
    class(nodeComponentSpin     )               , pointer :: spin
    class(darkMatterProfileClass)               , pointer :: darkMatterProfile_

    ! Ensure that the module is initialized.
    call Dark_Matter_Halo_Spins_Initialize

    basic                                         =>  node%basic       (                 )
    spin                                          =>  node%spin        (autoCreate=.true.)
    darkMatterProfile_                            =>  darkMatterProfile(                 )
    Dark_Matter_Halo_Angular_Momentum_Growth_Rate =  +Dark_Matter_Halo_Angular_Momentum    (node) &
         &                                           *(                                           &
         &                                             +spin%spinGrowthRate                (    ) &
         &                                             /spin%spin                          (    ) &
         &                                             +2.5d0                                     &
         &                                             *basic%accretionRate                (    ) &
         &                                             /basic%mass                         (    ) &
         &                                             -0.5d0                                     &
         &                                             *darkMatterProfile_%energyGrowthRate(node) &
         &                                             /darkMatterProfile_%energy          (node) &
         &                                            )
    return
  end function Dark_Matter_Halo_Angular_Momentum_Growth_Rate

  double precision function Dark_Matter_Halo_Spin(node,angularMomentum)
    !% Returns the spin of {\normalfont \ttfamily node} given its angular momentum.
    use Galacticus_Nodes
    use Numerical_Constants_Physical
    use Dark_Matter_Profiles
    implicit none
    type            (treeNode              ), intent(inout), pointer :: node
    double precision                        , intent(in   )          :: angularMomentum
    class           (nodeComponentBasic    )               , pointer :: basic
    class           (darkMatterProfileClass)               , pointer :: darkMatterProfile_

    basic                 =>  node             %basic()
    darkMatterProfile_    =>  darkMatterProfile      ()
    Dark_Matter_Halo_Spin =  +angularMomentum                             &
         &                   /gravitationalConstantGalacticus             &
         &                   *abs(darkMatterProfile_%energy(node))**0.5d0 &
         &                   /    basic             %mass  (    ) **2.5d0
    return
  end function Dark_Matter_Halo_Spin

end module Dark_Matter_Halo_Spins
