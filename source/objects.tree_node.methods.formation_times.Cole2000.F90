!! Copyright 2009, 2010, 2011, 2012 Andrew Benson <abenson@obs.carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module of halo formation time methods.

module Tree_Node_Methods_Formation_Times_Cole2000
  !% Implement tracking of halo formation times.
  use Tree_Nodes
  use Components
  implicit none
  private
  public :: Tree_Node_Methods_Formation_Times_Cole2000_Initialize, Tree_Node_Formation_Time_Cole2000_Initialize,&
       & Tree_Node_Methods_Formation_Times_Cole2000_Dump, Tree_Node_Formation_Time_Promote_Cole2000
  
  ! The index used as a reference for this component.
  integer :: componentIndex=-1

  ! Property indices.
  integer, parameter :: propertyCount=0, dataCount=0, historyCount=0

  ! Define procedure pointers.
  !# <treeNodeMethodsPointer>
  !#  <methodName>Tree_Node_Formation_Time</methodName>
  !# </treeNodeMethodsPointer>

  ! Flag to indicate if this method is selected.
  logical          :: methodSelected=.false.

  ! Factor by which mass must increase to trigger a new formation event.
  double precision :: haloReformationMassFactor

  ! Switch indicating whether or not halo reformation should only be checked for at node promotion events.
  logical          :: haloReformationOnPromotionOnly

contains

  !# <treeNodeCreateInitialize>
  !#  <unitName>Tree_Node_Methods_Formation_Times_Cole2000_Initialize</unitName>
  !#  <optionName>treeNodeMethodFormationTimes</optionName>
  !# </treeNodeCreateInitialize>
  subroutine Tree_Node_Methods_Formation_Times_Cole2000_Initialize(componentOption,componentTypeCount)
    !% Initializes the tree node formation time tracking module.
    use ISO_Varying_String
    use Input_Parameters
    use String_Handling
    use Galacticus_Display
    implicit none
    type(varying_string), intent(in)    :: componentOption
    integer,              intent(inout) :: componentTypeCount
    type(varying_string)                :: message

    ! Check if this implementation is selected.
    if (componentOption == 'Cole2000') then
       ! Record that method is selected.
       methodSelected=.true.

       ! Increment the component count and store the value for later reference.
       componentTypeCount=componentTypeCount+1
       componentIndex    =componentTypeCount

       ! Display message.
       message='Cole2000 halo formation time method selected [component index '
       message=message//componentIndex//']'
       call Galacticus_Display_Message(message,verbosityInfo)

       ! Set up procedure pointers.
       Tree_Node_Formation_Time              => Tree_Node_Formation_Time_Formation_Times_Cole2000
       Tree_Node_Formation_Time_Set          => null()
       Tree_Node_Formation_Time_Rate_Adjust  => null()
       Tree_Node_Formation_Time_Rate_Compute => Tree_Node_Formation_Time_Rate_Compute_Cole2000

       !@ <inputParameter>
       !@   <name>haloReformationMassFactor</name>
       !@   <defaultValue>2.0</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     Factor by which halo mass must have increased to trigger a new formation event.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@ </inputParameter>
       call Get_Input_Parameter('haloReformationMassFactor',haloReformationMassFactor,defaultValue=2.0d0)
       !@ <inputParameter>
       !@   <name>haloReformationOnPromotionOnly</name>
       !@   <defaultValue>false</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     Specifies whether halo reformation should occur only at node promotion events, or at the precise time that
       !@     the halo mass has increased sufficiently in mass.
       !@   </description>
       !@   <type>boolean</type>
       !@   <cardinality>1</cardinality>
       !@ </inputParameter>
       call Get_Input_Parameter('haloReformationOnPromotionOnly',haloReformationOnPromotionOnly,defaultValue=.false.)
    end if
    return
  end subroutine Tree_Node_Methods_Formation_Times_Cole2000_Initialize

  double precision function Tree_Node_Formation_Time_Formation_Times_Cole2000(thisNode,instance)
    !% Return the time of the last major merger.
    implicit none
    integer, intent(in), optional :: instance
    type(treeNode), pointer, intent(inout) :: thisNode
    
    if (.not.thisNode%componentExists(componentIndex)) call Tree_Node_Formation_Time_Create_Component(thisNode)
    Tree_Node_Formation_Time_Formation_Times_Cole2000=Tree_Node_Time(thisNode%formationNode)
    return
  end function Tree_Node_Formation_Time_Formation_Times_Cole2000

  subroutine Tree_Node_Formation_Time_Rate_Compute_Cole2000(thisNode,interrupt,interruptProcedure)
    !% Compute the exponential disk node mass rate of change.
    use Cosmological_Parameters
    use Cooling_Rates
    implicit none
    type(treeNode), pointer, intent(inout) :: thisNode
    logical,                 intent(inout) :: interrupt
    procedure(),    pointer, intent(inout) :: interruptProcedure
   
    ! If no formation time component exists, stop and make one.
    if (.not.thisNode%componentExists(componentIndex)) then
       interrupt=.true.
       interruptProcedure => Tree_Node_Formation_Time_Create_Component
       return
    end if

    ! Check if the halo has grown sufficiently in mass to trigger a new formation event.
    if (.not.haloReformationOnPromotionOnly) then
       if (Tree_Node_Mass(thisNode) > haloReformationMassFactor*Tree_Node_Mass(thisNode%formationNode)) then
          interrupt=.true.
          interruptProcedure => Tree_Node_Formation_Time_Create_Component
          return
       end if
    end if

    return
  end subroutine Tree_Node_Formation_Time_Rate_Compute_Cole2000

  !# <nodePromotionTask>
  !#  <unitName>Tree_Node_Formation_Time_Promote_Cole2000</unitName>
  !#  <after>Tree_Node_Hot_Halo_Promote</after>
  !# </nodePromotionTask>
  subroutine Tree_Node_Formation_Time_Promote_Cole2000(thisNode)
    implicit none
    type(treeNode), pointer, intent(inout) :: thisNode

    if (methodSelected.and.haloReformationOnPromotionOnly) then
       if (Tree_Node_Mass(thisNode%parentNode) > haloReformationMassFactor*Tree_Node_Mass(thisNode%formationNode)) call&
            & Tree_Node_Formation_Time_Create_Component(thisNode)
    end if
    return
  end subroutine Tree_Node_Formation_Time_Promote_Cole2000

  subroutine Tree_Node_Formation_Time_Create_Component(thisNode)
    !% Creates a halo formation time component for {\tt thisNode}. This function is also used to ``reform'' the halo, since it
    !% simply resets the formation time and mass to the current values.
    use ISO_Varying_String
    use Galacticus_Display
    use String_Handling
    use Events_Halo_Formation
    implicit none
    type(treeNode),      pointer, intent(inout) :: thisNode
    type(varying_string)                        :: message

    ! Display a message.
    if (.not.thisNode%componentExists(componentIndex)) then
       if (Galacticus_Verbosity_Level() >= verbosityInfo) then
          message='Creating halo formation component for node '
          message=message//thisNode%index()
          call Galacticus_Display_Message(message,verbosityInfo)
       end if
    end if
    ! Trigger a halo formation event.
    call Event_Halo_Formation(thisNode)
    ! Get the index of the component (which will also ensure that the component is created).
    call thisNode%createComponent(componentIndex,propertyCount,dataCount,historyCount)
    ! Make a copy of the formation node, and decouple it from the tree, using the parentNode pointer to point to the node of which
    ! it is the formation node.    
    if (associated(thisNode%formationNode)) call thisNode%formationNode%destroy()
    allocate(thisNode%formationNode)
    call thisNode%formationNode%copy(thisNode)
    thisNode%formationNode%parentNode    => thisNode
    thisNode%formationNode%childNode     => null()
    thisNode%formationNode%siblingNode   => null()
    thisNode%formationNode%satelliteNode => null()
    thisNode%formationNode%mergeNode     => null()
    thisNode%formationNode%mergeeNode    => null()
    thisNode%formationNode%nextMergee    => null()
    thisNode%formationNode%formationNode => null()
    return
  end subroutine Tree_Node_Formation_Time_Create_Component

  !# <mergerTreeInitializeTask>
  !#  <unitName>Tree_Node_Formation_Time_Cole2000_Initialize</unitName>
  !# </mergerTreeInitializeTask>
  subroutine Tree_Node_Formation_Time_Cole2000_Initialize(thisNode)
    !% Initialize the formation node pointer for any childless node.
    use Tree_Nodes
    implicit none
    type(treeNode),                     pointer, intent(inout)     :: thisNode

    ! If this method is selected and the node has no child then initialize it.
    if (methodSelected.and..not.associated(thisNode%childNode)) call Tree_Node_Formation_Time_Create_Component(thisNode)
    
    return
  end subroutine Tree_Node_Formation_Time_Cole2000_Initialize

  !# <nodeDumpTask>
  !#  <unitName>Tree_Node_Methods_Formation_Times_Cole2000_Dump</unitName>
  !# </nodeDumpTask>
  subroutine Tree_Node_Methods_Formation_Times_Cole2000_Dump(thisNode)
    !% Dump all properties of {\tt thisNode} to screen.
    implicit none
    type(treeNode),   intent(inout), pointer :: thisNode
 
    if (methodSelected) then
       if (thisNode%componentExists(componentIndex)) then
          write (0,'(1x,a)'           ) 'halo formation time component -> properties:'
          write (0,'(2x,a50,1x,e12.6)') 'formation time:',Tree_Node_Formation_Time(thisNode)
       else
          write (0,'(1x,a)'           ) 'halo formation time component -> nonexistant'
       end if
    end if
    return
  end subroutine Tree_Node_Methods_Formation_Times_Cole2000_Dump

end module Tree_Node_Methods_Formation_Times_Cole2000
