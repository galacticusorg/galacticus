!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which defines an orbit structure for use in \glc.

module Kepler_Orbits
  !% Defines an orbit structure for use in \glc.
  implicit none
  private
  public :: keplerOrbit

  ! Effective infinite radius used for apocenters in unbound orbits.
  double precision, parameter :: radiusEffectiveInfinity=huge(1.0d0)

  type keplerOrbit
     !% The structure used for describing orbits in \glc. This object will automatically convert from one set of orbital
     !% parameters to another where possible. The orbitting bodies (a satellite orbitting around its host) are treated as point
     !% masses, and the usual ``reduced mass'' framework is used, such that radii and velocities are measured relative to a
     !% stationary host. Energy and angular momentum are defined per unit satellite mass (not per unit reduced mass). Note that
     !% not all interconversions between elements are implemented. The object works by attempting to get the radial and tangential
     !% velocities and the radius. If it can obtain these, any other parameter can be computed. Getting these three parameters
     !% relies on having known conversions from other possible combinations of parameters.
     private
     double precision :: hostMassValue        , specificReducedMassValue
     double precision :: radiusApocenterValue , radiusPericenterValue   , &
          &              radiusValue
     double precision :: velocityRadialValue  , velocityTangentialValue
     double precision :: angularMomentumValue
     double precision :: energyValue
     double precision :: eccentricityValue
     double precision :: semimajorAxisValue
     logical          :: massesIsSet
     logical          :: radiusApocenterIsSet , radiusIsSet             , &
          &              radiusPericenterIsSet
     logical          :: velocityRadialIsSet  , velocityTangentialIsSet
     logical          :: angularMomentumIsSet
     logical          :: energyIsSet
     logical          :: eccentricityIsSet
     logical          :: semimajorAxisIsSet
   contains
     ! Orbit methods.
     !@ <objectMethods>
     !@   <object>keplerOrbit</object>
     !@   <objectMethod>
     !@     <method>builder</method>
     !@     <description>Build a Kepler orbit from an XML definition.</description>
     !@     <type>\void</type>
     !@     <arguments>\textcolor{red}{\textless *type(node)\textgreater} keplerOrbitDefinition\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>dump</method>
     !@     <description>Dump an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>dumpRaw</method>
     !@     <description>Dump an orbit in binary.</description>
     !@     <type>\void</type>
     !@     <arguments>\intzero\ fileHandle\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>readRaw</method>
     !@     <description>Read an orbit in binary.</description>
     !@     <type>\void</type>
     !@     <arguments>\intzero\ fileHandle\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>reset</method>
     !@     <description>Resets an orbit to a null state.</description>
     !@     <type>\void</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>destroy</method>
     !@     <description>Destroys an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>output</method>
     !@     <description>Store a {\normalfont \ttfamily keplerOrbit} object in the output buffers.</description>
     !@     <type>\void</type>
     !@     <arguments>\intzero\ integerProperty\arginout, \intzero\ integerBufferCount\arginout, \inttwo\ integerBuffer\arginout, \intzero doubleProperty\arginout, \intzero\ doubleBufferCount\arginout, \doubletwo\ doubleBuffer\arginout, \doublezero\ time\argin, \intzero\ instance\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>outputCount</method>
     !@     <description>Specify the count of a {\normalfont \ttfamily keplerOrbit} object for output.</description>
     !@     <type>\void</type>
     !@     <arguments>\intzero\ integerPropertyCount\arginout, \intzero\ doublePropertyCount\arginout, \doublezero\ time\argin, \intzero\ instance\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>outputNames</method>
     !@     <description>Specify the names of a {\normalfont \ttfamily keplerOrbit} object properties for output.</description>
     !@     <type>\void</type>
     !@     <arguments>\intzero\ integerProperty\arginout, \textcolor{red}{\textless char[*](:)\textgreater} integerPropertyNames\arginout, \textcolor{red}{\textless char[*](:)\textgreater} integerPropertyComments\arginout, \doubleone\ integerPropertyUnitsSI\arginout, \intzero\ doubleProperty\arginout, \textcolor{red}{\textless char[*](:)\textgreater} doublePropertyNames\arginout, \textcolor{red}{\textless char[*](:)\textgreater} doublePropertyComments\arginout, \doubleone\ doublePropertyUnitsSI\arginout, \doublezero\ time\argin, \intzero\ instance\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>isDefined</method>
     !@     <description>Returns true if an orbit is fully defined.</description>
     !@     <type>\logicalzero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>assertIsDefined</method>
     !@     <description>Asserts that an orbit is fully defined.</description>
     !@     <type>\void</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>isBound</method>
     !@     <description>Returns true if the orbit is bound.</description>
     !@     <type>\logicalzero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>propagate</method>
     !@     <description>Propagates an orbit to a new position.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ velocityRadial\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityRadialSet</method>
     !@     <description>Sets the radial velocity of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ newRadius\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>massesSet</method>
     !@     <description>Sets the masses of satellite and host objects.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ satelliteMass\argin, \doublezero\ hostMass\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radiusSet</method>
     !@     <description>Sets the radius of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ radius\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radiusPericenterSet</method>
     !@     <description>Sets the pericenter radius of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ radius\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radiusApocenterSet</method>
     !@     <description>Sets the apocenter radius of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ radius\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityTangentialSet</method>
     !@     <description>Sets the tangential velocity of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ velocityTangential\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>energySet</method>
     !@     <description>Sets the energy of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ energy\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>eccentricitySet</method>
     !@     <description>Sets the eccentricity of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ eccentricity\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>angularMomentumSet</method>
     !@     <description>Sets the angular momentum of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ angularMomentum\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>semiMajorAxisSet</method>
     !@     <description>Sets the semi-major axis of an orbit.</description>
     !@     <type>\void</type>
     !@     <arguments>\doublezero\ semiMajorAxis\argin</arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>hostMass</method>
     !@     <description>Returns the host mass of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityScale</method>
     !@     <description>Returns the velocity scale of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>specificReducedMass</method>
     !@     <description>Returns the specific reduced mass (i.e. the reduced mass per unit satellite mass, $\mu_{\mathrm s} = M_{\mathrm host}/(M_{\mathrm satellite}+M_{\mathrm host})$) of the orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radius</method>
     !@     <description>Returns the radius of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radiusPericenter</method>
     !@     <description>Returns the pericenter radius of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>radiusApocenter</method>
     !@     <description>Returns the apocenter radius of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityRadial</method>
     !@     <description>Returns the radial velocity of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityTangential</method>
     !@     <description>Returns the tangential velocity of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>energy</method>
     !@     <description>Returns the energy of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>eccentricity</method>
     !@     <description>Returns the eccentricity of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>angularMomentum</method>
     !@     <description>Returns the angular momentum of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>semiMajorAxis</method>
     !@     <description>Returns the semi-major axis of an orbit.</description>
     !@     <type>\doublezero</type>
     !@     <arguments></arguments>
     !@   </objectMethod>
     !@ </objectMethods>
     procedure :: builder              =>Kepler_Orbits_Builder
     procedure :: dump                 =>Kepler_Orbits_Dump
     procedure :: dumpRaw              =>Kepler_Orbits_Dump_Raw
     procedure :: readRaw              =>Kepler_Orbits_Read_Raw
     procedure :: reset                =>Kepler_Orbits_Reset
     procedure :: destroy              =>Kepler_Orbits_Destroy
     procedure :: isDefined            =>Kepler_Orbits_Is_Defined
     procedure :: output               =>Kepler_Orbits_Output
     procedure :: outputCount          =>Kepler_Orbits_Output_Count
     procedure :: outputNames          =>Kepler_Orbits_Output_Names
     procedure :: assertIsDefined      =>Kepler_Orbits_Assert_Is_Defined
     procedure :: isBound              =>Kepler_Orbits_Is_Bound
     procedure :: propagate            =>Kepler_Orbits_Propagate
     procedure :: massesSet            =>Kepler_Orbits_Masses_Set
     procedure :: radiusSet            =>Kepler_Orbits_Radius_Set
     procedure :: radiusPericenterSet  =>Kepler_Orbits_Pericenter_Radius_Set
     procedure :: radiusApocenterSet   =>Kepler_Orbits_Apocenter_Radius_Set
     procedure :: velocityRadialSet    =>Kepler_Orbits_Velocity_Radial_Set
     procedure :: velocityTangentialSet=>Kepler_Orbits_Velocity_Tangential_Set
     procedure :: energySet            =>Kepler_Orbits_Energy_Set
     procedure :: angularMomentumSet   =>Kepler_Orbits_Angular_Momentum_Set
     procedure :: eccentricitySet      =>Kepler_Orbits_Eccentricity_Set
     procedure :: semiMajorAxisSet     =>Kepler_Orbits_Semi_Major_Axis_Set
     procedure :: specificReducedMass  =>Kepler_Orbits_Specific_Reduced_Mass
     procedure :: hostMass             =>Kepler_Orbits_Host_Mass
     procedure :: velocityScale        =>Kepler_Orbits_Velocity_Scale
     procedure :: radius               =>Kepler_Orbits_Radius
     procedure :: radiusPericenter     =>Kepler_Orbits_Pericenter_Radius
     procedure :: radiusApocenter      =>Kepler_Orbits_Apocenter_Radius
     procedure :: velocityRadial       =>Kepler_Orbits_Velocity_Radial
     procedure :: velocityTangential   =>Kepler_Orbits_Velocity_Tangential
     procedure :: energy               =>Kepler_Orbits_Energy
     procedure :: angularMomentum      =>Kepler_Orbits_Angular_Momentum
     procedure :: eccentricity         =>Kepler_Orbits_Eccentricity
     procedure :: semiMajorAxis        =>Kepler_Orbits_Semi_Major_Axis
  end type keplerOrbit

  ! A null orbit.
  type(keplerOrbit), public :: zeroKeplerOrbit=keplerOrbit(0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,0.0d0,.false.,.false.,.false.,.false.,.false.,.false.,.false.,.false.,.false.,.false.)
  
contains

  subroutine Kepler_Orbits_Destroy(thisOrbit)
    !% Destroy an orbit.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit
    !GCC$ attributes unused :: thisOrbit
    
    ! Nothing to do.
    return
  end subroutine Kepler_Orbits_Destroy

  subroutine Kepler_Orbits_Output(self,integerProperty,integerBufferCount,integerBuffer,doubleProperty,doubleBufferCount&
       &,doubleBuffer,time,outputInstance)
    !% Store a {\normalfont \ttfamily keplerOrbit} object in the output buffers.
    use Kind_Numbers
    use Multi_Counters
    implicit none
    class           (keplerOrbit   )                , intent(inout) :: self
    double precision                                , intent(in   ) :: time
    integer                                         , intent(inout) :: doubleBufferCount, doubleProperty, integerBufferCount, &
         &                                                             integerProperty
    integer         (kind=kind_int8), dimension(:,:), intent(inout) :: integerBuffer
    double precision                , dimension(:,:), intent(inout) :: doubleBuffer
    type            (multiCounter  )                , intent(in   ) :: outputInstance
    !GCC$ attributes unused :: integerBufferCount, integerProperty, integerBuffer, time, outputInstance
    
    if (self%isDefined()) then
       doubleBuffer(doubleBufferCount,doubleProperty+1)=self%energy         ()
       doubleBuffer(doubleBufferCount,doubleProperty+2)=self%angularMomentum()
       doubleBuffer(doubleBufferCount,doubleProperty+3)=self%velocityScale  ()
       doubleBuffer(doubleBufferCount,doubleProperty+4)=self%hostMass       ()
    else
       doubleBuffer(doubleBufferCount,doubleProperty+1:doubleProperty+4)=0.0d0
    end if
    doubleProperty=doubleProperty+4
    return
  end subroutine Kepler_Orbits_Output

  subroutine Kepler_Orbits_Output_Count(self,integerPropertyCount,doublePropertyCount,time)
    !% Increment the output count to account for a {\normalfont \ttfamily keplerOrbit} object.
    implicit none
    class           (keplerOrbit), intent(in   ) :: self
    integer                      , intent(inout) :: doublePropertyCount, integerPropertyCount
    double precision             , intent(in   ) :: time
    !GCC$ attributes unused :: self, integerPropertyCount, time
    
    doublePropertyCount=doublePropertyCount+4
    return
  end subroutine Kepler_Orbits_Output_Count

  subroutine Kepler_Orbits_Output_Names(self,integerProperty,integerPropertyNames,integerPropertyComments,integerPropertyUnitsSI&
       &,doubleProperty,doublePropertyNames,doublePropertyComments,doublePropertyUnitsSI,time,prefix,comment,unitsInSI)
    !% Assign names to output buffers for a {\normalfont \ttfamily keplerOrbit} object.
    use Numerical_Constants_Astronomical
    implicit none
    class           (keplerOrbit)              , intent(in   ) :: self
    double precision                           , intent(in   ) :: time
    integer                                    , intent(inout) :: doubleProperty         , integerProperty
    character       (len=*      ), dimension(:), intent(inout) :: doublePropertyComments , doublePropertyNames   , &
         &                                                        integerPropertyComments, integerPropertyNames
    double precision             , dimension(:), intent(inout) :: doublePropertyUnitsSI  , integerPropertyUnitsSI
    character       (len=*      )              , intent(in   ) :: comment                , prefix
    double precision                           , intent(in   ) :: unitsInSI
    !GCC$ attributes unused :: self, time, integerProperty, integerPropertyComments, integerPropertyNames, integerPropertyUnitsSI, unitsInSI
    
    doubleProperty=doubleProperty+1
    doublePropertyNames   (doubleProperty)=trim(prefix)//'SpecificEnergy'
    doublePropertyComments(doubleProperty)=trim(comment)//' [specific energy]'
    doublePropertyUnitsSI (doubleProperty)=kilo**2
    doubleProperty=doubleProperty+1
    doublePropertyNames   (doubleProperty)=trim(prefix)//'SpecificAngularMomentum'
    doublePropertyComments(doubleProperty)=trim(comment)//' [specific angular momentum]'
    doublePropertyUnitsSI (doubleProperty)=kilo*megaParsec
    doubleProperty=doubleProperty+1
    doublePropertyNames   (doubleProperty)=trim(prefix)//'VelocityScale'
    doublePropertyComments(doubleProperty)=trim(comment)//' [velocity scale]'
    doublePropertyUnitsSI (doubleProperty)=kilo
    doubleProperty=doubleProperty+1
    doublePropertyNames   (doubleProperty)=trim(prefix)//'HostMass'
    doublePropertyComments(doubleProperty)=trim(comment)//' [host mass]'
    doublePropertyUnitsSI (doubleProperty)=massSolar
    return
  end subroutine Kepler_Orbits_Output_Names

  subroutine Kepler_Orbits_Builder(self,keplerOrbitDefinition)
    !% Build a {\normalfont \ttfamily keplerOrbit} object from the given XML {\normalfont \ttfamily keplerOrbitDefinition}.
    use FoX_DOM
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: self
    type (node       ), pointer       :: keplerOrbitDefinition
    type (node       ), pointer       :: property
    type (nodeList   ), pointer       :: propertyList
    character(len=18     ), parameter    , dimension(5) :: propertyNames=                       &
         &                                                               [                      &
         &                                                                'massHost          ', &
         &                                                                'massSatellite     ', &
         &                                                                'velocityRadial    ', &
         &                                                                'velocityTangential', &
         &                                                                'radius            '  &
         &                                                               ]
    integer          :: i
    double precision :: massHost   , massSatellite   , propertyValue
    logical          :: massHostSet, massSatelliteSet

    ! Get the radius.
    do i=1,size(propertyNames)
       propertyList => getElementsByTagName(keplerOrbitDefinition,trim(propertyNames(i)))
       if (getLength(propertyList) >  1) call Galacticus_Error_Report('Kepler_Orbits_Builder','multiple '//trim(propertyNames(i))//' values specified')
       if (getLength(propertyList) == 1) then
          property => item(propertyList,0)
          call extractDataContent(property,propertyValue)
          select case (getNodeName(property))
          case ( 'radius'             )
             call self%            radiusSet(propertyValue)
          case ( 'velocityRadial'     )
             call self%    velocityRadialSet(propertyValue)
          case ( 'velocityTangential' )
             call self%velocityTangentialSet(propertyValue)
          case ( 'massHost'           )
             massHost        =propertyValue
             massHostSet     =.true.
          case ( 'massSatellite'      )
             massSatellite   =propertyValue
             massSatelliteSet=.true.
          case default
             call Galacticus_Error_Report('Kepler_Orbits_Builder','unrecognized property name')
          end select
       end if
    end do
    if (.not.(massHostSet.and.massSatelliteSet)) call Galacticus_Error_Report('Kepler_Orbits_Builder','satellite and host masses must be specified')
    call self%massesSet(massSatellite,massHost)
    call self%assertIsDefined()
    return
  end subroutine Kepler_Orbits_Builder

  subroutine Kepler_Orbits_Dump(self)
    !% Reset an orbit to a null state.
    use Galacticus_Display
    use ISO_Varying_String
    implicit none
    class    (keplerOrbit   ), intent(in   ) :: self
    character(len=12        )                :: label
    type     (varying_string)                :: message

    if (self%massesIsSet             ) then
       write (label,'(e12.6)') self%hostMassValue
       message='host mass:             '//label
       call Galacticus_Display_Message(message)
       write (label,'(e12.6)') self%specificReducedMassValue
       message='specific reduced mass: '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%radiusIsSet             ) then
       write (label,'(e12.6)') self%radiusValue
       message='radius            :     '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%radiusPericenterIsSet   ) then
       write (label,'(e12.6)') self%radiusPericenterValue
       message='radius pericenter:      '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%radiusApocenterIsSet   ) then
       write (label,'(e12.6)') self%radiusApocenterValue
       message='radius apocenter:       '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%velocityRadialIsSet    ) then
       write (label,'(e12.6)') self%velocityRadialValue
       message='velocity radial:        '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%velocityTangentialIsSet) then
       write (label,'(e12.6)') self%velocityTangentialValue
       message='velocity tangential:    '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%angularMomentumIsSet   ) then
       write (label,'(e12.6)') self%angularMomentumValue
       message='angular momentum:       '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%energyIsSet            ) then
       write (label,'(e12.6)') self%energyValue
       message='energy:                 '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%eccentricityIsSet      ) then
       write (label,'(e12.6)') self%eccentricityValue
       message='eccentricity:           '//label
       call Galacticus_Display_Message(message)
    end if
    if (self%semimajorAxisIsSet     ) then
       write (label,'(e12.6)') self%semimajorAxisValue
       message='semi-major axis:        '//label
       call Galacticus_Display_Message(message)
    end if
    return
  end subroutine Kepler_Orbits_Dump

  subroutine Kepler_Orbits_Dump_Raw(self,fileHandle)
    !% Dump a {\normalfont \ttfamily keplerOrbit} object in binary.
    implicit none
    class  (keplerOrbit), intent(in   ) :: self
    integer             , intent(in   ) :: fileHandle

    write (fileHandle) self%massesIsSet,self%massesIsSet,self%radiusIsSet,self%radiusPericenterIsSet,self%radiusApocenterIsSet&
         &,self%velocityRadialIsSet,self%velocityTangentialIsSet,self%angularMomentumIsSet,self%energyIsSet&
         &,self%eccentricityIsSet,self%semimajorAxisIsSet
    if (self%massesIsSet            ) write (fileHandle) self%hostMassValue,self%specificReducedMassValue
    if (self%radiusIsSet            ) write (fileHandle) self%radiusValue
    if (self%radiusPericenterIsSet  ) write (fileHandle) self%radiusPericenterValue
    if (self%radiusApocenterIsSet   ) write (fileHandle) self%radiusApocenterValue
    if (self%velocityRadialIsSet    ) write (fileHandle) self%velocityRadialValue
    if (self%velocityTangentialIsSet) write (fileHandle) self%velocityTangentialValue
    if (self%angularMomentumIsSet   ) write (fileHandle) self%angularMomentumValue
    if (self%energyIsSet            ) write (fileHandle) self%energyValue
    if (self%eccentricityIsSet      ) write (fileHandle) self%eccentricityValue
    if (self%semimajorAxisIsSet     ) write (fileHandle) self%semimajorAxisValue
    return
  end subroutine Kepler_Orbits_Dump_Raw

  subroutine Kepler_Orbits_Read_Raw(self,fileHandle)
    !% Read a {\normalfont \ttfamily keplerOrbit} object in binary.
    implicit none
    class  (keplerOrbit), intent(inout) :: self
    integer             , intent(in   ) :: fileHandle

    read (fileHandle) self%massesIsSet,self%massesIsSet,self%radiusIsSet,self%radiusPericenterIsSet,self%radiusApocenterIsSet&
         &,self%velocityRadialIsSet,self%velocityTangentialIsSet,self%angularMomentumIsSet,self%energyIsSet&
         &,self%eccentricityIsSet,self%semimajorAxisIsSet
    if (self%massesIsSet            ) read (fileHandle) self%hostMassValue,self%specificReducedMassValue
    if (self%radiusIsSet            ) read (fileHandle) self%radiusValue
    if (self%radiusPericenterIsSet  ) read (fileHandle) self%radiusPericenterValue
    if (self%radiusApocenterIsSet   ) read (fileHandle) self%radiusApocenterValue
    if (self%velocityRadialIsSet    ) read (fileHandle) self%velocityRadialValue
    if (self%velocityTangentialIsSet) read (fileHandle) self%velocityTangentialValue
    if (self%angularMomentumIsSet   ) read (fileHandle) self%angularMomentumValue
    if (self%energyIsSet            ) read (fileHandle) self%energyValue
    if (self%eccentricityIsSet      ) read (fileHandle) self%eccentricityValue
    if (self%semimajorAxisIsSet     ) read (fileHandle) self%semimajorAxisValue
    return
  end subroutine Kepler_Orbits_Read_Raw

  subroutine Kepler_Orbits_Reset(thisOrbit)
    !% Reset an orbit to a null state.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    ! Simply specify that no properties have been set as yet.
    thisOrbit%massesIsSet            =.false.
    thisOrbit%radiusIsSet            =.false.
    thisOrbit%radiusPericenterIsSet  =.false.
    thisOrbit%radiusApocenterIsSet   =.false.
    thisOrbit%velocityRadialIsSet    =.false.
    thisOrbit%velocityTangentialIsSet=.false.
    thisOrbit%angularMomentumIsSet   =.false.
    thisOrbit%energyIsSet            =.false.
    thisOrbit%eccentricityIsSet      =.false.
    thisOrbit%semimajorAxisIsSet     =.false.
    return
  end subroutine Kepler_Orbits_Reset

  subroutine Kepler_Orbits_Masses_Set(thisOrbit,satelliteMass,hostMass)
    !% Sets the masses of the two orbitting objects in a {\normalfont \ttfamily keplerOrbit} object.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: hostMass , satelliteMass

    ! Set the mass factor and flag that is set.
    thisOrbit%specificReducedMassValue=1.0d0/(1.0d0+satelliteMass/hostMass)
    thisOrbit%hostMassValue           =hostMass
    thisOrbit%massesIsSet             =.true.
    return
  end subroutine Kepler_Orbits_Masses_Set

  subroutine Kepler_Orbits_Radius_Set(thisOrbit,radius)
    !% Sets the radius to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: radius

    ! Set the radius and flag that is set.
    thisOrbit%radiusValue=radius
    thisOrbit%radiusIsSet=.true.
    return
  end subroutine Kepler_Orbits_Radius_Set

  subroutine Kepler_Orbits_Pericenter_Radius_Set(thisOrbit,radius)
    !% Sets the pericenter radius to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: radius

    ! Set the pericenter radius and flag that is set.
    thisOrbit%radiusPericenterValue=radius
    thisOrbit%radiusPericenterIsSet=.true.
    return
  end subroutine Kepler_Orbits_Pericenter_Radius_Set

  subroutine Kepler_Orbits_Apocenter_Radius_Set(thisOrbit,radius)
    !% Sets the apocenter radius to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: radius

    ! Set the apocenter radius and flag that is set.
    thisOrbit%radiusApocenterValue=radius
    thisOrbit%radiusApocenterIsSet=.true.
    return
  end subroutine Kepler_Orbits_Apocenter_Radius_Set

  subroutine Kepler_Orbits_Velocity_Radial_Set(thisOrbit,velocityRadial)
    !% Sets the radial velocity to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: velocityRadial

    ! Set the radial velocity and flag that is set.
    thisOrbit%velocityRadialValue=velocityRadial
    thisOrbit%velocityRadialIsSet=.true.
    return
  end subroutine Kepler_Orbits_Velocity_Radial_Set

  subroutine Kepler_Orbits_Velocity_Tangential_Set(thisOrbit,velocityTangential)
    !% Sets the tangential velocity to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: velocityTangential

    ! Set the tangential velocity and flag that is set.
    thisOrbit%velocityTangentialValue=velocityTangential
    thisOrbit%velocityTangentialIsSet=.true.
    return
  end subroutine Kepler_Orbits_Velocity_Tangential_Set

  subroutine Kepler_Orbits_Energy_Set(thisOrbit,energy)
    !% Sets the tangential velocity to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: energy

    ! Set the tangential velocity and flag that is set.
    thisOrbit%energyValue=energy
    thisOrbit%energyIsSet=.true.
    return
  end subroutine Kepler_Orbits_Energy_Set

  subroutine Kepler_Orbits_Eccentricity_Set(thisOrbit,eccentricity)
    !% Sets the tangential velocity to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: eccentricity

    ! Set the tangential velocity and flag that is set.
    thisOrbit%eccentricityValue=eccentricity
    thisOrbit%eccentricityIsSet=.true.
    return
  end subroutine Kepler_Orbits_Eccentricity_Set

  subroutine Kepler_Orbits_Angular_Momentum_Set(thisOrbit,angularMomentum)
    !% Sets the tangential velocity to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: angularMomentum

    ! Set the tangential velocity and flag that is set.
    thisOrbit%angularMomentumValue=angularMomentum
    thisOrbit%angularMomentumIsSet=.true.
    return
  end subroutine Kepler_Orbits_Angular_Momentum_Set

  subroutine Kepler_Orbits_Semi_Major_Axis_Set(thisOrbit,semiMajorAxis)
    !% Sets the semi-major axis to the specified value.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision             , intent(in   ) :: semiMajorAxis

    ! Set the tangential velocity and flag that is set.
    thisOrbit%semiMajorAxisValue=semiMajorAxis
    thisOrbit%semiMajorAxisIsSet=.true.
    return
  end subroutine Kepler_Orbits_Semi_Major_Axis_Set

  double precision function Kepler_Orbits_Specific_Reduced_Mass(thisOrbit)
    !% Return the specific reduced mass for this orbit.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%massesIsSet) call Galacticus_Error_Report('Kepler_Orbits_Specific_Reduced_Mass', &
         & 'mass factor has not been set for this orbit')
    Kepler_Orbits_Specific_Reduced_Mass=thisOrbit%specificReducedMassValue
    return
  end function Kepler_Orbits_Specific_Reduced_Mass

  double precision function Kepler_Orbits_Host_Mass(thisOrbit)
    !% Return the host mass for this orbit.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%massesIsSet) call Galacticus_Error_Report('Kepler_Orbits_Host_Mass', &
         & 'host mass has not been set for this orbit')
    Kepler_Orbits_Host_Mass=thisOrbit%hostMassValue
    return
  end function Kepler_Orbits_Host_Mass

  double precision function Kepler_Orbits_Radius(thisOrbit)
    !% Return the radius for this orbit.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%radiusIsSet) call Galacticus_Error_Report('Kepler_Orbits_Radius', &
         & 'radius has not been set for this orbit')
    Kepler_Orbits_Radius=thisOrbit%radiusValue
    return
  end function Kepler_Orbits_Radius

  double precision function Kepler_Orbits_Pericenter_Radius(thisOrbit)
    !% Return the pericenter radius for this orbit.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%radiusPericenterIsSet) then
       ! Assert that the orbit is defined.
       call thisOrbit%assertIsDefined()
       ! Compute the pericenter radius.
       thisOrbit%radiusPericenterValue=thisOrbit%semiMajorAxis()*(1.0d0-thisOrbit%eccentricity())
       thisOrbit%radiusPericenterIsSet=.true.
    end if
    Kepler_Orbits_Pericenter_Radius=thisOrbit%radiusPericenterValue
    return
  end function Kepler_Orbits_Pericenter_Radius

  double precision function Kepler_Orbits_Apocenter_Radius(thisOrbit)
    !% Return the apocenter radius for this orbit.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%radiusApocenterIsSet) then
       ! Assert that the orbit is defined.
       call thisOrbit%assertIsDefined()
       ! Compute the pericenter radius.
       if (thisOrbit%isBound()) then
          thisOrbit%radiusApocenterValue=thisOrbit%semiMajorAxis()*(1.0d0+thisOrbit%eccentricity())
       else
          thisOrbit%radiusApocenterValue=radiusEffectiveInfinity
       end if
       thisOrbit%radiusApocenterIsSet=.true.
    end if
    Kepler_Orbits_Apocenter_Radius=thisOrbit%radiusApocenterValue
    return
  end function Kepler_Orbits_Apocenter_Radius

  double precision function Kepler_Orbits_Velocity_Radial(thisOrbit)
    !% Return the radial velocity for this orbit.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%velocityRadialIsSet) then
       ! Assert that the orbit is defined.
       call thisOrbit%assertIsDefined()
       ! Compute from eccentricity, radius and periapsis if possible.
       if (thisOrbit%eccentricityIsSet.and.thisOrbit%radiusIsSet.and.thisOrbit%radiusPericenterIsSet) then
          thisOrbit%velocityRadialValue=thisOrbit%velocityScale()*sqrt((2.0d0*(1.0d0-thisOrbit%radius()&
               &/thisOrbit%radiusPericenter())+(1.0d0+thisOrbit%eccentricity())*(thisOrbit%radius()/thisOrbit%radiusPericenter()&
               &-thisOrbit%radiusPericenter()/thisOrbit%radius()))/thisOrbit%specificReducedMass())
          thisOrbit%velocityRadialIsSet=.true.
       end if
       ! If we were not able to compute the radial velocity, exit.
       if (.not.thisOrbit%velocityRadialIsSet) call Galacticus_Error_Report('Kepler_Orbits_Velocity_Radial','radial velocity&
            & has not been set for this orbit and can not be computed')
    end if
    Kepler_Orbits_Velocity_Radial=thisOrbit%velocityRadialValue
    return
  end function Kepler_Orbits_Velocity_Radial

  double precision function Kepler_Orbits_Velocity_Tangential(thisOrbit)
    !% Return the tangential velocity for this orbit.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%velocityTangentialIsSet) then
       ! Assert that the orbit is defined.
       call thisOrbit%assertIsDefined()
       ! Compute from angular momentum and radius if possible.
       if (thisOrbit%angularMomentumIsSet.and.thisOrbit%radiusIsSet) then
          thisOrbit%velocityTangentialValue=thisOrbit%angularMomentum()/thisOrbit%radius()/thisOrbit%specificReducedMass()
          thisOrbit%velocityTangentialIsSet=.true.
       end if
       ! Compute from eccentricity, radius and periapsis if possible.
       if (thisOrbit%eccentricityIsSet.and.thisOrbit%radiusIsSet.and.thisOrbit%radiusPericenterIsSet) then
          thisOrbit%velocityTangentialValue=thisOrbit%velocityScale()*sqrt((1.0d0+thisOrbit%eccentricity())&
               &*thisOrbit%radiusPericenter() /thisOrbit%radius() /thisOrbit%specificReducedMass())
          thisOrbit%velocityTangentialIsSet=.true.
       end if
       ! If we were not able to compute the tangential velocity, exit.
       if (.not.thisOrbit%velocityTangentialIsSet) call Galacticus_Error_Report('Kepler_Orbits_Velocity_Tangential','tangential velocity&
            & has not been set for this orbit and can not be computed')
    end if
    Kepler_Orbits_Velocity_Tangential=thisOrbit%velocityTangentialValue
    return
  end function Kepler_Orbits_Velocity_Tangential

  double precision function Kepler_Orbits_Energy(thisOrbit)
    !% Return the energy for this orbit.
    use Numerical_Constants_Physical
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    ! Check if energy is set.
    if (.not.thisOrbit%energyIsSet) then
       ! Assert that the orbit is defined.
       call thisOrbit%assertIsDefined()
       ! Compute the energy.
       thisOrbit%energyValue=-gravitationalConstantGalacticus*thisOrbit%hostMass()/thisOrbit%radius()+0.5d0&
            &*(thisOrbit%velocityRadial()**2+thisOrbit%velocityTangential()**2)*thisOrbit%specificReducedMass()
       thisOrbit%energyIsSet=.true.
    end if
    Kepler_Orbits_Energy=thisOrbit%energyValue
    return
  end function Kepler_Orbits_Energy

  double precision function Kepler_Orbits_Angular_Momentum(thisOrbit)
    !% Return the angular momentum for this orbit.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    if (.not.thisOrbit%angularMomentumIsSet) then
       thisOrbit%angularMomentumValue=thisOrbit%radius()*thisOrbit%velocityTangential()*thisOrbit%specificReducedMass()
       thisOrbit%angularMomentumIsSet=.true.
    end if   
    Kepler_Orbits_Angular_Momentum=thisOrbit%angularMomentumValue
    return
  end function Kepler_Orbits_Angular_Momentum

  double precision function Kepler_Orbits_Eccentricity(thisOrbit)
    !% Return the eccentricity for this orbit.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision                             :: velocityRadial, velocityTangential

    if (.not.thisOrbit%eccentricityIsSet) then
       velocityTangential         =thisOrbit%velocityTangential()/thisOrbit%velocityScale()
       velocityRadial             =thisOrbit%velocityRadial    ()/thisOrbit%velocityScale()
       thisOrbit%eccentricityValue=sqrt(1.0d0+2.0d0*thisOrbit%energy()*thisOrbit%angularMomentum()**2/thisOrbit%radius()**2&
            &/thisOrbit%velocityScale()**4/thisOrbit%specificReducedMass())
       thisOrbit%eccentricityIsSet=.true.
    end if    
    Kepler_Orbits_Eccentricity=thisOrbit%eccentricityValue
    return
  end function Kepler_Orbits_Eccentricity

  double precision function Kepler_Orbits_Semi_Major_Axis(thisOrbit)
    !% Return the semi-major axis for this orbit.
    implicit none
    class           (keplerOrbit), intent(inout) :: thisOrbit
    double precision                             :: velocityRadial, velocityTangential

    if (.not.thisOrbit%semiMajorAxisIsSet) then
       velocityTangential          =thisOrbit%velocityTangential()/thisOrbit%velocityScale()
       velocityRadial              =thisOrbit%velocityRadial    ()/thisOrbit%velocityScale()
       thisOrbit%semiMajorAxisValue=thisOrbit%radius()/thisOrbit%specificReducedMass()/(2.0d0/thisOrbit%specificReducedMass()&
            &-velocityRadial**2-velocityTangential**2)
       thisOrbit%semiMajorAxisIsSet=.true.
    end if
    Kepler_Orbits_Semi_Major_Axis=thisOrbit%semiMajorAxisValue
    return
  end function Kepler_Orbits_Semi_Major_Axis

  logical function Kepler_Orbits_Is_Defined(thisOrbit)
    !% Returns true if the orbit is fully defined. For the orbits consider here, in which we don't care about the orientation of
    !% the orbital plane or the argument of pericenter, this requires that three orbital parameter be set (in addition to the
    !% masses of the orbitting bodies).
    implicit none
    class  (keplerOrbit), intent(in   ) :: thisOrbit
    integer                             :: orbitalParameterCount

    ! Assume orbit is not defined by default.
    Kepler_Orbits_Is_Defined=.false.
    ! Ensure that masses are set.
    if (thisOrbit%massesIsSet) then
       ! Count how many parameters are set.
       orbitalParameterCount=0
       if (thisOrbit%radiusIsSet            ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%radiusPericenterIsSet  ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%radiusApocenterIsSet   ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%energyIsSet            ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%angularMomentumIsSet   ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%velocityRadialIsSet    ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%velocityTangentialIsSet) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%eccentricityIsSet      ) orbitalParameterCount=orbitalParameterCount+1
       if (thisOrbit%semiMajorAxisIsSet     ) orbitalParameterCount=orbitalParameterCount+1
       ! Orbit is defined if at least 3 parameters are set.
       Kepler_Orbits_Is_Defined=(orbitalParameterCount >= 3)
    end if
    return
  end function Kepler_Orbits_Is_Defined

  subroutine Kepler_Orbits_Assert_Is_Defined(thisOrbit)
    !% Assert that an orbit is defined - quit with an error if it is not.
    use Galacticus_Error
    implicit none
    class(keplerOrbit), intent(in   ) :: thisOrbit

    if (.not.thisOrbit%isDefined()) call Galacticus_Error_Report('Kepler_Orbits_Assert_Is_Defined','orbit is not defined')
    return
  end subroutine Kepler_Orbits_Assert_Is_Defined

  logical function Kepler_Orbits_Is_Bound(thisOrbit)
    !% Returns true if the orbit is bound.
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    ! Assert that the orbit is defined.
    call thisOrbit%assertIsDefined()
    ! Test if the energy is negative (indicating a bound orbit).
    Kepler_Orbits_Is_Bound=(thisOrbit%energy() < 0.0d0)
    return
  end function Kepler_Orbits_Is_Bound

  double precision function Kepler_Orbits_Velocity_Scale(thisOrbit)
    !% Return the velocity scale for the orbit.
    use Galacticus_Error
    use Numerical_Constants_Physical
    implicit none
    class(keplerOrbit), intent(inout) :: thisOrbit

    ! Check that masses and radius have been specified.
    if (.not.(thisOrbit%radiusIsSet.and.thisOrbit%massesIsSet)) call Galacticus_Error_Report('Kepler_Orbits_Velocity_Scale','orbit&
         & masses and radius must be specified')
    ! Compute the velocity scale.
    Kepler_Orbits_Velocity_Scale=sqrt(gravitationalConstantGalacticus*thisOrbit%hostMass()/thisOrbit%radius())
    return
  end function Kepler_Orbits_Velocity_Scale

  subroutine Kepler_Orbits_Propagate(thisOrbit,newRadius,infalling)
    !% Propagate an orbit along its path.
    use Galacticus_Error
    use Numerical_Constants_Physical
    implicit none
    class           (keplerOrbit), intent(inout)           :: thisOrbit
    double precision             , intent(in   )           :: newRadius
    logical                      , intent(in   ), optional :: infalling
    double precision                                       :: angularMomentum      , energy, newVelocityRadial, &
         &                                                    newVelocityTangential

    ! Assert that the orbit is defined.
    call thisOrbit%assertIsDefined()
    ! Check that the radius is within the allowed range.
    if     (                                                                                                               &
         &    newRadius < thisOrbit%radiusPericenter()                                                                     &
         &  .or.                                                                                                           &
         &   (newRadius > thisOrbit%radiusApocenter () .and. thisOrbit%radiusApocenter() > 0.0d0)                          &
         & ) call Galacticus_Error_Report('Kepler_Orbits_Propagate','radius lies outside of allowed range for this orbit')
    ! Get the energy and angular momentum
    energy         =thisOrbit%energy         ()
    angularMomentum=thisOrbit%angularMomentum()
    ! Compute velocity components.
    newVelocityTangential=angularMomentum/newRadius
    newVelocityRadial    =sqrt(2.0d0*(energy+gravitationalConstantGalacticus*thisOrbit%hostMass()/newRadius)/thisOrbit%specificReducedMass()-newVelocityTangential**2)
    ! Move to the infalling phase of the orbit if requested.
    if (present(infalling)) then
       if (infalling) newVelocityRadial=-newVelocityRadial
    end if
    ! Set new values for the state vector.
    call thisOrbit%radiusSet            (newRadius            )
    call thisOrbit%velocityTangentialSet(newVelocityTangential)
    call thisOrbit%velocityRadialSet    (newVelocityRadial    )
    return
  end subroutine Kepler_Orbits_Propagate
  
end module Kepler_Orbits
