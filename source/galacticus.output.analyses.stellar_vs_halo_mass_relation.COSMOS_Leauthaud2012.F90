!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Contains a module which implements a stellar vs halo mass relation analysis class.

  use, intrinsic :: ISO_C_Binding, only : c_size_t
  
  !# <outputAnalysis name="outputAnalysisStellarVsHaloMassRelationLeauthaud2012">
  !#  <description>A stellar vs halo mass relation output analysis class.</description>
  !# </outputAnalysis>
  type, extends(outputAnalysisClass) :: outputAnalysisStellarVsHaloMassRelationLeauthaud2012
     !% A stellar vs halo mass relation output analysis class.
     private
     class  (outputAnalysisClass), pointer :: outputAnalysis_ => null()
     integer(c_size_t           )          :: likelihoodBin
   contains
     final     ::                  stellarVsHaloMassRelationLeauthaud2012Destructor
     procedure :: analyze       => stellarVsHaloMassRelationLeauthaud2012Analyze
     procedure :: finalize      => stellarVsHaloMassRelationLeauthaud2012Finalize
     procedure :: reduce        => stellarVsHaloMassRelationLeauthaud2012Reduce
     procedure :: logLikelihood => stellarVsHaloMassRelationLeauthaud2012LogLikelihood
  end type outputAnalysisStellarVsHaloMassRelationLeauthaud2012

  interface outputAnalysisStellarVsHaloMassRelationLeauthaud2012
     !% Constructors for the ``stellarVsHaloMassRelationLeauthaud2012'' output analysis class.
     module procedure stellarVsHaloMassRelationLeauthaud2012ConstructorParameters
     module procedure stellarVsHaloMassRelationLeauthaud2012ConstructorInternal
  end interface outputAnalysisStellarVsHaloMassRelationLeauthaud2012

contains

  function stellarVsHaloMassRelationLeauthaud2012ConstructorParameters(parameters) result (self)
    !% Constructor for the ``stellarVsHaloMassRelationLeauthaud2012'' output analysis class which takes a parameter set as input.
    use Cosmology_Functions
    use Input_Parameters
    implicit none
    type            (outputAnalysisStellarVsHaloMassRelationLeauthaud2012)                              :: self
    type            (inputParameters                                     ), intent(inout)               :: parameters
    double precision                                                      , allocatable  , dimension(:) :: systematicErrorPolynomialCoefficient
    class           (cosmologyFunctionsClass                             ), pointer                     :: cosmologyFunctions_
    class           (outputTimesClass                                    ), pointer                     :: outputTimes_
    integer                                                                                             :: redshiftInterval
    logical                                                                                             :: computeScatter
    integer         (c_size_t                                            )                              :: likelihoodBin

    ! Check and read parameters.
    if (parameters%isPresent('systematicErrorPolynomialCoefficient')) then
       allocate(systematicErrorPolynomialCoefficient(parameters%count('systematicErrorPolynomialCoefficient')))
    else
       allocate(systematicErrorPolynomialCoefficient(1                                                       ))
    end if
    !# <inputParameter>
    !#   <name>redshiftInterval</name>
    !#   <source>parameters</source>
    !#   <variable>redshiftInterval</variable>
    !#   <description>The redshift interval (1, 2, or 3) to use.</description>
    !#   <type>integer</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>computeScatter</name>
    !#   <source>parameters</source>
    !#   <variable>computeScatter</variable>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>If true, the scatter in log10(stellar mass) is computed. Otherwise, the mean is computed.</description>
    !#   <type>logical</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>systematicErrorPolynomialCoefficient</name>
    !#   <source>parameters</source>
    !#   <variable>systematicErrorPolynomialCoefficient</variable>
    !#   <defaultValue>[0.0d0]</defaultValue>
    !#   <description>The coefficients of the systematic error polynomial for stellar vs halo mass relation.</description>
    !#   <type>float</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>likelihoodBin</name>
    !#   <source>parameters</source>
    !#   <defaultValue>0_c_size_t</defaultValue>
    !#   <description>If $>0$ then use only the mass bin given by this value in the likelihood calculation.</description>
    !#   <type>integer</type>
    !#   <cardinality>0..1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyFunctions" name="cosmologyFunctions_" source="parameters"/>
    !# <objectBuilder class="outputTimes"        name="outputTimes_"        source="parameters"/>
    ! Build the object.
    self=outputAnalysisStellarVsHaloMassRelationLeauthaud2012(redshiftInterval,likelihoodBin,computeScatter,systematicErrorPolynomialCoefficient,cosmologyFunctions_,outputTimes_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="cosmologyFunctions_"/>
    !# <objectDestructor name="outputTimes_"       />
    return
  end function stellarVsHaloMassRelationLeauthaud2012ConstructorParameters

  function stellarVsHaloMassRelationLeauthaud2012ConstructorInternal(redshiftInterval,likelihoodBin,computeScatter,systematicErrorPolynomialCoefficient,cosmologyFunctions_,outputTimes_) result (self)
    !% Constructor for the ``stellarVsHaloMassRelationLeauthaud2012'' output analysis class for internal use.
    use ISO_Varying_String
    use Numerical_Constants_Astronomical
    use Numerical_Ranges
    use Numerical_Comparison
    use Output_Analysis_Property_Operators
    use Output_Analysis_Property_Extractions
    use Output_Analysis_Distribution_Operators
    use Output_Analysis_Weight_Operators
    use Output_Analysis_Utilities
    use Memory_Management
    use Cosmology_Parameters
    use Cosmology_Functions
    use String_Handling
    use Galacticus_Error
    use Virial_Density_Contrast
    use IO_HDF5
    use Galacticus_Paths
    use Tables                               , only : table1DGeneric
    use FGSL                                 , only : fgsl_interp_cspline
    implicit none
    type            (outputAnalysisStellarVsHaloMassRelationLeauthaud2012)                                :: self
    integer                                                               , intent(in   )                 :: redshiftInterval
    logical                                                               , intent(in   )                 :: computeScatter
    integer         (c_size_t                                            ), intent(in   )                 :: likelihoodBin
    double precision                                                      , intent(in   ), dimension(:  ) :: systematicErrorPolynomialCoefficient
    class           (cosmologyFunctionsClass                             ), intent(inout), target         :: cosmologyFunctions_
    class           (outputTimesClass                                    ), intent(inout), target         :: outputTimes_
    integer         (c_size_t                                            ), parameter                     :: massHaloCount                                         =26
    double precision                                                      , parameter                     :: massHaloMinimum                                       = 1.0d10, massHaloMaximum                              =1.0d15
    integer                                                               , parameter                     :: covarianceBinomialBinsPerDecade                       =10
    double precision                                                      , parameter                     :: covarianceBinomialMassHaloMinimum                     = 1.0d08, covarianceBinomialMassHaloMaximum            =1.0d16
    double precision                                                      , allocatable  , dimension(:  ) :: massHalo                                                      , massStellarDataLogarithmic                          , &
         &                                                                                                   massHaloMeanDataLogarithmic                                   , massHaloLowDataLogarithmic                          , &
         &                                                                                                   massHaloHighDataLogarithmic                                   , massHaloErrorDataLogarithmic                        , &
         &                                                                                                   massStellarLogarithmicTarget                                  , massHaloHighData                                    , &
         &                                                                                                   massHaloMeanData                                              , massHaloLowData                                     , &
         &                                                                                                   massStellarData
    double precision                                                      , allocatable  , dimension(:,:) :: outputWeight                                                  , massStellarLogarithmicCovarianceTarget
    type            (galacticFilterStellarMass                           ), pointer                       :: galacticFilterStellarMass_
    type            (galacticFilterHaloIsolated                          ), pointer                       :: galacticFilterHaloIsolated_
    type            (galacticFilterAll                                   ), pointer                       :: galacticFilterAll_
    type            (filterList                                          ), pointer                       :: filters_
    type            (outputAnalysisDistributionOperatorIdentity          ), pointer                       :: outputAnalysisDistributionOperator_
    type            (outputAnalysisWeightOperatorIdentity                ), pointer                       :: outputAnalysisWeightOperator_
    type            (outputAnalysisPropertyOperatorLog10                 ), pointer                       :: outputAnalysisPropertyOperator_                              , outputAnalysisWeightPropertyOperatorLog10_          , &
         &                                                                                                   outputAnalysisWeightPropertyOperatorLog10Second_
    type            (outputAnalysisPropertyOperatorAntiLog10             ), pointer                       :: outputAnalysisPropertyUnoperator_                            , outputAnalysisWeightPropertyOperatorAntiLog10_
    type            (outputAnalysisPropertyOperatorSequence              ), pointer                       :: outputAnalysisWeightPropertyOperator_
    type            (outputAnalysisPropertyOperatorCsmlgyLmnstyDstnc     ), pointer                       :: outputAnalysisWeightPropertyOperatorCsmlgyLmnstyDstnc_
    type            (outputAnalysisPropertyOperatorSystmtcPolynomial     ), pointer                       :: outputAnalysisWeightPropertyOperatorSystmtcPolynomial_
    type            (outputAnalysisPropertyOperatorFilterHighPass        ), pointer                       :: outputAnalysisWeightPropertyOperatorFilterHighPass_
    type            (outputAnalysisPropertyExtractorMassHalo             ), pointer                       :: outputAnalysisPropertyExtractor_
    type            (outputAnalysisPropertyExtractorMassStellar          ), pointer                       :: outputAnalysisWeightPropertyExtractor_
    type            (propertyOperatorList                                ), pointer                       :: propertyOperators_
    type            (cosmologyParametersSimple                           ), pointer                       :: cosmologyParametersData
    type            (cosmologyFunctionsMatterLambda                      ), pointer                       :: cosmologyFunctionsData
    type            (virialDensityContrastFixed                          ), pointer                       :: virialDensityContrast_
    type            (surveyGeometryFullSky                               ), pointer                       :: surveyGeometry_    
    double precision                                                      , parameter                     :: errorPolynomialZeroPoint                              =11.3d00
    logical                                                               , parameter                     :: likelihoodNormalize                                   =.false.
    integer         (c_size_t                                            )                                :: iBin
    double precision                                                                                      :: massStellarLimit                                              , redshiftMinimum                                  , &
         &                                                                                                   redshiftMaximum
    type            (varying_string                                      )                                :: analysisLabel                                                 , weightPropertyLabel                              , &
         &                                                                                                   weightPropertyDescription                                     , groupRedshiftName
    type            (hdf5Object                                          )                                :: fileData                                                      , groupRedshift
    type            (table1DGeneric                                      )                                :: interpolator
    character       (len=4                                               )                                :: redshiftMinimumLabel                                          , redshiftMaximumLabel
    
    ! Construct survey geometry.
    select case (redshiftInterval)
    case (1)
       redshiftMinimum = 0.22d0
       redshiftMaximum = 0.48d0
       massStellarLimit=10.00d0**8.7d0
    case (2)
       redshiftMinimum = 0.48d0
       redshiftMaximum = 0.74d0
       massStellarLimit=10.00d0**9.3d0
    case (3)
       redshiftMinimum = 0.74d0
       redshiftMaximum = 1.00d0
       massStellarLimit=10.00d0**9.8d0
    case default
       call Galacticus_Error_Report('redshiftInterval ∈ {1,2,3}'//{introspection:location})
    end select
    write (redshiftMinimumLabel,'(f4.2)') redshiftMinimum
    write (redshiftMaximumLabel,'(f4.2)') redshiftMaximum
    allocate(surveyGeometry_)
    !# <referenceConstruct object="surveyGeometry_" constructor="surveyGeometryFullSky(redshiftMinimum,redshiftMaximum,cosmologyFunctions_)"/>
    ! Create output time weights.
    call allocateArray(outputWeight,[massHaloCount,outputTimes_%count()])
    outputWeight(1,:)=Output_Analysis_Output_Weight_Survey_Volume(surveyGeometry_,cosmologyFunctions_,outputTimes_,massStellarLimit,allowSingleEpoch=.true.)
    forall(iBin=2:massHaloCount)
       outputWeight(iBin,:)=outputWeight(1,:)
    end forall
    ! Create cosmological model in which data were analyzed.
    allocate(cosmologyParametersData)
    allocate(cosmologyFunctionsData )
    !# <referenceConstruct object="cosmologyParametersData">
    !#  <constructor>
    !#   cosmologyParametersSimple     (                            &amp;
    !#      &amp;                       OmegaMatter    = 0.25800d0, &amp;
    !#      &amp;                       OmegaDarkEnergy= 0.74200d0, &amp;
    !#      &amp;                       HubbleConstant =72.00000d0, &amp;
    !#      &amp;                       temperatureCMB = 2.72548d0, &amp;
    !#      &amp;                       OmegaBaryon    = 0.04385d0  &amp;
    !#      &amp;                      )
    !#  </constructor>
    !# </referenceConstruct>
    !# <referenceConstruct object="cosmologyFunctionsData">
    !#  <constructor>
    !#   cosmologyFunctionsMatterLambda(                            &amp;
    !#      &amp;                       cosmologyParametersData     &amp;
    !#      &amp;                      )
    !#  </constructor>
    !# </referenceConstruct>
    ! Create bins in halo mass.
    massHalo=Make_Range(log10(massHaloMinimum),log10(massHaloMaximum),int(massHaloCount),rangeType=rangeTypeLinear)
    ! Read observational data and convert masses to logarithmic.
    call fileData%openFile(char(galacticusPath(pathTypeDataStatic))//"observations/stellarHaloMassRelation/stellarHaloMassRelation_COSMOS_Leauthaud2012.hdf5")
    groupRedshiftName=var_str('redshiftInterval')//redshiftInterval
    groupRedshift=fileData%openGroup(char(groupRedshiftName))
    call groupRedshift%readDataset('massStellar' ,massStellarData )
    call groupRedshift%readDataset('massHaloMean',massHaloMeanData)
    call groupRedshift%readDataset('massHaloLow' ,massHaloLowData )
    call groupRedshift%readDataset('massHaloHigh',massHaloHighData)
    ! Find a spline fit to the observed data, and compute the uncertainty in logarithm of halo mass.
    allocate(massHaloErrorDataLogarithmic(size(massStellarData)))
    massStellarDataLogarithmic  =log(massStellarData )
    massHaloMeanDataLogarithmic =log(massHaloMeanData)
    massHaloLowDataLogarithmic  =log(massHaloLowData )
    massHaloHighDataLogarithmic =log(massHaloHighData)
    massHaloErrorDataLogarithmic=+0.5d0                         &
         &                       *(                             &
         &                         +massHaloHighDataLogarithmic &
         &                         -massHaloLowDataLogarithmic  &
         &                        )
    call interpolator%create  (massHaloMeanDataLogarithmic ,tableCount=2,interpolationType=fgsl_interp_cspline)
    call interpolator%populate(massStellarDataLogarithmic  ,table     =1                                      )
    call interpolator%populate(massHaloErrorDataLogarithmic,table     =2                                      )
    ! Interpolate observational data to model points.
    allocate(massStellarLogarithmicTarget          (massHaloCount              ))
    allocate(massStellarLogarithmicCovarianceTarget(massHaloCount,massHaloCount))
    massStellarLogarithmicCovarianceTarget=0.0d0
    do iBin=1,massHaloCount
       massStellarLogarithmicTarget          (iBin     )=+  interpolator%interpolate        (massHalo(iBin)*log(10.0d0),table=1)
       massStellarLogarithmicCovarianceTarget(iBin,iBin)=+(                                                                      &
            &                                              +interpolator%interpolateGradient(massHalo(iBin)*log(10.0d0),table=1) &
            &                                              *interpolator%interpolate        (massHalo(iBin)*log(10.0d0),table=2) &
            &                                             )**2
    end do
    call interpolator%destroy()
    massStellarLogarithmicTarget          =massStellarLogarithmicTarget          /log(10.0d0)
    massStellarLogarithmicCovarianceTarget=massStellarLogarithmicCovarianceTarget/log(10.0d0)**2
    self%likelihoodBin=likelihoodBin
    if (self%likelihoodBin > 0_c_size_t) then
       ! Assume that only a single bin of the relation is to be populated. Set the target dataset in all other bins to zero so
       ! that they do not contribute to the likelihood.
       if (self%likelihoodBin > massHaloCount) call Galacticus_Error_Report('lieklihoodBin is out of range'//{introspection:location})
       do iBin=1,massHaloCount
          if (iBin /= self%likelihoodBin) massStellarLogarithmicTarget(iBin)=0.0d0
       end do       
    end if
    ! Build a filter which select central galaxies with stellar mass above some coarse lower limit suitable for this sample.
    allocate(galacticFilterStellarMass_      )
    allocate(galacticFilterHaloIsolated_     )
    allocate(galacticFilterAll_              )
    allocate(filters_                        )
    allocate(filters_                   %next)
    filters_                        %filter_ => galacticFilterHaloIsolated_
    filters_                   %next%filter_ => galacticFilterStellarMass_
    !# <referenceConstruct object="galacticFilterStellarMass_"  constructor="galacticFilterStellarMass  (massThreshold=1.0d7   )"/>
    !# <referenceConstruct object="galacticFilterHaloIsolated_" constructor="galacticFilterHaloIsolated (                      )"/>
    !# <referenceConstruct object="galacticFilterAll_"          constructor="galacticFilterAll          (              filters_)"/>
    ! Build identity distribution operator.
    allocate   (outputAnalysisDistributionOperator_                   )
    !# <referenceConstruct object="outputAnalysisDistributionOperator_"                    constructor="outputAnalysisDistributionOperatorIdentity            (                                                                                 )"/>
    ! Build identity weight operator.
    allocate   (outputAnalysisWeightOperator_                         )
    !# <referenceConstruct object="outputAnalysisWeightOperator_"                          constructor="outputAnalysisWeightOperatorIdentity                  (                                                                                 )"/>
    ! Build log10() property operator.
    allocate   (outputAnalysisPropertyOperator_                       )
    !# <referenceConstruct object="outputAnalysisPropertyOperator_"                        constructor="outputAnalysisPropertyOperatorLog10                   (                                                                                 )"/>
    ! Build a sequence (log10, polynomial systematic, anti-log10, cosmological luminosity distance, high-pass filter) of weight property operators.
    allocate   (outputAnalysisWeightPropertyOperatorFilterHighPass_)
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorFilterHighPass_"    constructor="outputAnalysisPropertyOperatorFilterHighPass          (log10(massStellarLimit)                                                          )"/>
    allocate   (outputAnalysisWeightPropertyOperatorCsmlgyLmnstyDstnc_)
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorCsmlgyLmnstyDstnc_" constructor="outputAnalysisPropertyOperatorCsmlgyLmnstyDstnc       (cosmologyFunctions_     ,cosmologyFunctionsData              ,outputTimes_       )"/>
    allocate   (outputAnalysisWeightPropertyOperatorSystmtcPolynomial_)
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorSystmtcPolynomial_" constructor="outputAnalysisPropertyOperatorSystmtcPolynomial       (errorPolynomialZeroPoint,systematicErrorPolynomialCoefficient                    )"/>
    allocate   (outputAnalysisWeightPropertyOperatorLog10_            )
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorLog10_"             constructor="outputAnalysisPropertyOperatorLog10                   (                                                                                 )"/>
    allocate   (outputAnalysisWeightPropertyOperatorLog10Second_      )
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorLog10Second_"       constructor="outputAnalysisPropertyOperatorLog10                   (                                                                                 )"/>
    allocate   (outputAnalysisWeightPropertyOperatorAntiLog10_        )
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperatorAntiLog10_"         constructor="outputAnalysisPropertyOperatorAntiLog10               (                                                                                 )"/>
    allocate       (propertyOperators_                              )
    allocate       (propertyOperators_%next                         )
    allocate       (propertyOperators_%next%next                    )
    allocate       (propertyOperators_%next%next%next               )
    allocate       (propertyOperators_%next%next%next%next          )
    allocate       (propertyOperators_%next%next%next%next%next     )
    propertyOperators_                         %operator_ => outputAnalysisWeightPropertyOperatorLog10_
    propertyOperators_%next                    %operator_ => outputAnalysisWeightPropertyOperatorSystmtcPolynomial_
    propertyOperators_%next%next               %operator_ => outputAnalysisWeightPropertyOperatorAntiLog10_
    propertyOperators_%next%next%next          %operator_ => outputAnalysisWeightPropertyOperatorCsmlgyLmnstyDstnc_
    propertyOperators_%next%next%next%next     %operator_ => outputAnalysisWeightPropertyOperatorLog10Second_
    propertyOperators_%next%next%next%next%next%operator_ => outputAnalysisWeightPropertyOperatorFilterHighPass_
    allocate(outputAnalysisWeightPropertyOperator_                 )
    !# <referenceConstruct object="outputAnalysisWeightPropertyOperator_"  constructor="outputAnalysisPropertyOperatorSequence                (propertyOperators_                                                              )"/>
    ! Build anti-log10() property operator.
    allocate(outputAnalysisPropertyUnoperator_                     )
    !# <referenceConstruct object="outputAnalysisPropertyUnoperator_"      constructor="outputAnalysisPropertyOperatorAntiLog10               (                                                                                )"/>
    ! Create a stellar mass weight property extractor.
    allocate(outputAnalysisWeightPropertyExtractor_                )
    !# <referenceConstruct object="outputAnalysisWeightPropertyExtractor_" constructor="outputAnalysisPropertyExtractorMassStellar            (                                                                                )"/>
    ! Create a halo mass weight property extractor.
    allocate(virialDensityContrast_                                )
    !# <referenceConstruct object="virialDensityContrast_"                 constructor="virialDensityContrastFixed                            (200.0d0                 ,fixedDensityTypeMean               ,cosmologyFunctions_)"/>
    allocate(outputAnalysisPropertyExtractor_                      )
    !# <referenceConstruct object="outputAnalysisPropertyExtractor_"       constructor="outputAnalysisPropertyExtractorMassHalo               (virialDensityContrast_                                                          )"/>
    ! Build the object.
    if (computeScatter) then
       analysisLabel            =var_str('stellarHaloMassRelationScatterLeauthaud2012z')//redshiftInterval
       weightPropertyLabel      =var_str('massStellarLog10Scatter'                     )
       weightPropertyDescription=var_str('σ_{log₁₀(Stellar mass/M☉)}'                  )
       allocate(outputAnalysisScatterFunction1D :: self%outputAnalysis_)
       ! For the scatter we need to set an appropriate target and covariance for likelihood calculation. This measurement is based
       ! on the constraint on σ_{log₁₀L}=0.16±0.04 from More et al. (2009; MNRAS; 392; 801) for SDSS galaxies.
       do iBin=1,massHaloCount
          if (self%likelihoodBin <= 0_c_size_t .or. self%likelihoodBin == iBin) then        
             massStellarLogarithmicTarget          (iBin     )=0.16d0
             massStellarLogarithmicCovarianceTarget(iBin,iBin)=0.04d0
          end if
       end do
    else
       analysisLabel            =var_str('stellarHaloMassRelationLeauthaud2012z'       )//redshiftInterval
       weightPropertyLabel      =var_str('massStellarLog10'                            )
       weightPropertyDescription=var_str('⟨log₁₀(Stellar mass/M☉)⟩'                    )
       allocate(outputAnalysisMeanFunction1D    :: self%outputAnalysis_)
    end if
    select type (outputAnalysis_ => self%outputAnalysis_)
    type is (outputAnalysisScatterFunction1D)
       !# <referenceConstruct isResult="yes" object="outputAnalysis_">
       !#  <constructor>
       !#   outputAnalysisScatterFunction1D(                                                                                                                    &amp;
       !#    &amp;                          analysisLabel                                                                                                     , &amp;
       !#    &amp;                          var_str('Scatter in stellar-halo mass relation for $')//redshiftMinimumLabel//' \le z &lt; '//redshiftMaximumLabel, &amp;
       !#    &amp;                          var_str('massHalo'                                    )                                                           , &amp;
       !#    &amp;                          var_str('Halo mass'                                   )                                                           , &amp;
       !#    &amp;                          var_str('M☉'                                          )                                                           , &amp;
       !#    &amp;                          massSolar                                                                                                         , &amp;
       !#    &amp;                          weightPropertyLabel                                                                                               , &amp;
       !#    &amp;                          weightPropertyDescription                                                                                         , &amp;
       !#    &amp;                          var_str(' '                                           )                                                           , &amp;
       !#    &amp;                          0.0d0                                                                                                             , &amp;
       !#    &amp;                          massHalo                                                                                                          , &amp;
       !#    &amp;                          0_c_size_t                                                                                                        , &amp;
       !#    &amp;                          outputWeight                                                                                                      , &amp;
       !#    &amp;                          outputAnalysisPropertyExtractor_                                                                                  , &amp;
       !#    &amp;                          outputAnalysisWeightPropertyExtractor_                                                                            , &amp;
       !#    &amp;                          outputAnalysisPropertyOperator_                                                                                   , &amp;
       !#    &amp;                          outputAnalysisWeightPropertyOperator_                                                                             , &amp;
       !#    &amp;                          outputAnalysisPropertyUnoperator_                                                                                 , &amp;
       !#    &amp;                          outputAnalysisWeightOperator_                                                                                     , &amp;
       !#    &amp;                          outputAnalysisDistributionOperator_                                                                               , &amp;
       !#    &amp;                          galacticFilterAll_                                                                                                , &amp;
       !#    &amp;                          outputTimes_                                                                                                      , &amp;
       !#    &amp;                          outputAnalysisCovarianceModelBinomial                                                                             , &amp;
       !#    &amp;                          covarianceBinomialBinsPerDecade                                                                                   , &amp;
       !#    &amp;                          covarianceBinomialMassHaloMinimum                                                                                 , &amp;
       !#    &amp;                          covarianceBinomialMassHaloMaximum                                                                                 , &amp;
       !#    &amp;                          likelihoodNormalize                                                                                               , &amp;
       !#    &amp;                          var_str('M_\mathrm{halo}/\mathrm{M}_\odot'            )                                                           , &amp;
       !#    &amp;                          var_str('\sigma_{\log_{10}(M_\star/\mathrm{M}_\odot)}')                                                           , &amp;
       !#    &amp;                          .true.                                                                                                            , &amp;
       !#    &amp;                          .false.                                                                                                           , &amp;    
       !#    &amp;                          var_str('More et al. (2009)'                          )                                                           , &amp;
       !#    &amp;                          massStellarLogarithmicTarget                                                                                      , &amp;
       !#    &amp;                          massStellarLogarithmicCovarianceTarget                                                                              &amp;
       !#    &amp;                         )
       !#  </constructor>
       !# </referenceConstruct>
    type is (outputAnalysisMeanFunction1D   )
       !# <referenceConstruct isResult="yes" object="outputAnalysis_">
       !#  <constructor>
       !#   outputAnalysisMeanFunction1D   (                                                                                                         &amp;
       !#    &amp;                          analysisLabel                                                                                          , &amp;
       !#    &amp;                          var_str('Stellar-halo mass relation for $')//redshiftMinimumLabel//' \le z &lt; '//redshiftMaximumLabel, &amp;
       !#    &amp;                          var_str('massHalo'                            )                                                        , &amp;
       !#    &amp;                          var_str('Halo mass'                           )                                                        , &amp;
       !#    &amp;                          var_str('M☉'                                  )                                                        , &amp;
       !#    &amp;                          massSolar                                                                                              , &amp;
       !#    &amp;                          weightPropertyLabel                                                                                    , &amp;
       !#    &amp;                          weightPropertyDescription                                                                              , &amp;
       !#    &amp;                          var_str(' '                                   )                                                        , &amp;
       !#    &amp;                          0.0d0                                                                                                  , &amp;
       !#    &amp;                          massHalo                                                                                               , &amp;
       !#    &amp;                          0_c_size_t                                                                                             , &amp;
       !#    &amp;                          outputWeight                                                                                           , &amp;
       !#    &amp;                          outputAnalysisPropertyExtractor_                                                                       , &amp;
       !#    &amp;                          outputAnalysisWeightPropertyExtractor_                                                                 , &amp;
       !#    &amp;                          outputAnalysisPropertyOperator_                                                                        , &amp;
       !#    &amp;                          outputAnalysisWeightPropertyOperator_                                                                  , &amp;
       !#    &amp;                          outputAnalysisPropertyUnoperator_                                                                      , &amp;
       !#    &amp;                          outputAnalysisWeightOperator_                                                                          , &amp;
       !#    &amp;                          outputAnalysisDistributionOperator_                                                                    , &amp;
       !#    &amp;                          galacticFilterAll_                                                                                     , &amp;
       !#    &amp;                          outputTimes_                                                                                           , &amp;
       !#    &amp;                          outputAnalysisCovarianceModelBinomial                                                                  , &amp;
       !#    &amp;                          covarianceBinomialBinsPerDecade                                                                        , &amp;
       !#    &amp;                          covarianceBinomialMassHaloMinimum                                                                      , &amp;
       !#    &amp;                          covarianceBinomialMassHaloMaximum                                                                      , &amp;
       !#    &amp;                          likelihoodNormalize                                                                                    , &amp;
       !#    &amp;                          var_str('M_\mathrm{halo}/\mathrm{M}_\odot'    )                                                        , &amp;
       !#    &amp;                          var_str('\log_{10}(M_\star/\mathrm{M}_\odot)' )                                                        , &amp;
       !#    &amp;                          .true.                                                                                                 , &amp;
       !#    &amp;                          .false.                                                                                                , &amp;    
       !#    &amp;                          var_str('Leauthaud et al. (2012)'             )                                                        , &amp;
       !#    &amp;                          massStellarLogarithmicTarget                                                                           , &amp;
       !#    &amp;                          massStellarLogarithmicCovarianceTarget                                                                   &amp;
       !#    &amp;                         )
       !#  </constructor>
       !# </referenceConstruct>
    end select
    ! Clean up.
    !# <objectDestructor name="surveyGeometry_"                                        />
    !# <objectDestructor name="cosmologyParametersData"                                />
    !# <objectDestructor name="cosmologyFunctionsData"                                 />
    !# <objectDestructor name="galacticFilterStellarMass_"                             />
    !# <objectDestructor name="galacticFilterHaloIsolated_"                            />
    !# <objectDestructor name="galacticFilterAll_"                                     />
    !# <objectDestructor name="outputAnalysisDistributionOperator_"                    />               
    !# <objectDestructor name="outputAnalysisWeightOperator_"                          />
    !# <objectDestructor name="outputAnalysisPropertyOperator_"                        />            
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorFilterHighPass_"    />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorCsmlgyLmnstyDstnc_" />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorSystmtcPolynomial_" />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorLog10_"             />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorLog10Second_"       />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperatorAntiLog10_"         />
    !# <objectDestructor name="outputAnalysisWeightPropertyOperator_"                  />
    !# <objectDestructor name="outputAnalysisPropertyUnoperator_"                      />
    !# <objectDestructor name="outputAnalysisWeightPropertyExtractor_"                 />
    !# <objectDestructor name="virialDensityContrast_"                                 />
    !# <objectDestructor name="outputAnalysisPropertyExtractor_"                       />
    nullify(propertyOperators_)
    nullify(filters_          )
    return
  end function stellarVsHaloMassRelationLeauthaud2012ConstructorInternal

  subroutine stellarVsHaloMassRelationLeauthaud2012Analyze(self,node,iOutput)
    !% Implement a stellarVsHaloMassRelationLeauthaud2012 output analysis.
    implicit none
    class  (outputAnalysisStellarVsHaloMassRelationLeauthaud2012), intent(inout) :: self
    type   (treeNode                                            ), intent(inout) :: node
    integer(c_size_t                                            ), intent(in   ) :: iOutput

    call self%outputAnalysis_%analyze(node,iOutput)
    return
  end subroutine stellarVsHaloMassRelationLeauthaud2012Analyze

  subroutine stellarVsHaloMassRelationLeauthaud2012Destructor(self)
    !% Destructor for the {\normalfont \ttfamily stellarVsHaloMassRelationLeauthaud2012} output analysis class.
    implicit none
    type(outputAnalysisStellarVsHaloMassRelationLeauthaud2012), intent(inout) :: self

    !# <objectDestructor name="self%outputAnalysis_"/>
    return
  end subroutine stellarVsHaloMassRelationLeauthaud2012Destructor

  subroutine stellarVsHaloMassRelationLeauthaud2012Reduce(self,reduced)
    !% Implement reduction for the {\normalfont \ttfamily stellarVsHaloMassRelationLeauthaud2012} output analysis class.
    use Galacticus_Error
    implicit none
    class(outputAnalysisStellarVsHaloMassRelationLeauthaud2012), intent(inout) :: self
    class(outputAnalysisClass                                 ), intent(inout) :: reduced

    select type (reduced)
    class is (outputAnalysisStellarVsHaloMassRelationLeauthaud2012)
       call self%outputAnalysis_%reduce(reduced%outputAnalysis_)
    class default
       call Galacticus_Error_Report('incorrect class'//{introspection:location})
    end select
    return
  end subroutine stellarVsHaloMassRelationLeauthaud2012Reduce

  subroutine stellarVsHaloMassRelationLeauthaud2012Finalize(self)
    !% Implement a {\normalfont \ttfamily stellarVsHaloMassRelationLeauthaud2012} output analysis finalization.
    implicit none
    class(outputAnalysisStellarVsHaloMassRelationLeauthaud2012), intent(inout) :: self

    call self%outputAnalysis_%finalize()
    return
  end subroutine stellarVsHaloMassRelationLeauthaud2012Finalize

  double precision function stellarVsHaloMassRelationLeauthaud2012LogLikelihood(self)
    !% Return the log-likelihood of a {\normalfont \ttfamily stellarVsHaloMassRelationLeauthaud2012} output analysis.
    implicit none
    class(outputAnalysisStellarVsHaloMassRelationLeauthaud2012), intent(inout) :: self

    stellarVsHaloMassRelationLeauthaud2012LogLikelihood=self%outputAnalysis_%logLikelihood()
    return
  end function stellarVsHaloMassRelationLeauthaud2012LogLikelihood
