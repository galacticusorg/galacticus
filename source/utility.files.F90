!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015 Andrew Benson <abenson@obs.carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which stores file units and finds available file units.

! Specify an explicit dependence on the flock.o object file.
!: ./work/build/flock.o

module File_Utilities
  !% Contains a function which returns an available file unit. Also stores the name of the output directory and unit numbers for
  !% various files which remain open throughout.
  use, intrinsic :: ISO_C_Binding
  use ISO_Varying_String
  implicit none
  private
  public :: Count_Lines_in_File, File_Exists, File_Lock, File_Unlock

  interface Count_Lines_in_File
     !% Generic interface for {\normalfont \ttfamily Count\_Lines\_in\_File} function.
     module procedure Count_Lines_in_File_Char
     module procedure Count_Lines_in_File_VarStr
  end interface Count_Lines_in_File

  interface File_Exists
     !% Generic interface for functions that check for a files existance.
     module procedure File_Exists_Char
     module procedure File_Exists_VarStr
  end interface File_Exists

  interface
     function flock_C(name) bind(c,name='flock_C')
       !% Template for a C function that calls {\normalfont \ttfamily flock()} to lock a file..
       import
       integer  (c_int ) :: flock_C
       character(c_char) :: name
     end function flock_C
  end interface

  interface
     subroutine funlock_C(fd) bind(c,name='funlock_C')
       !% Template for a C function that calls {\normalfont \ttfamily flock()} to unlock a file..
       import
       integer(c_int ), value :: fd
     end subroutine funlock_C
  end interface

contains

  logical function File_Exists_VarStr(FileName)
    !% Checks for existance of file {\normalfont \ttfamily FileName} (version for varying string argument).
    implicit none
    type(varying_string), intent(in   ) :: FileName

    File_Exists_VarStr=File_Exists_Char(char(FileName))
    return
  end function File_Exists_VarStr

  logical function File_Exists_Char(FileName)
    !% Checks for existance of file {\normalfont \ttfamily FileName} (version for character argument).
    implicit none
    character(len=*), intent(in   ) :: FileName

    inquire(file=FileName,exist=File_Exists_Char)
    return
  end function File_Exists_Char

  integer function Count_Lines_in_File_VarStr(in_file,comment_char)
    !% Returns the number of lines in the file {\normalfont \ttfamily in\_file} (version for varying string argument).
    implicit none
    type     (varying_string), intent(in   )           :: in_file
    character(len=1         ), intent(in   ), optional :: comment_char

    if (present(comment_char)) then
       Count_Lines_in_File_VarStr=Count_Lines_in_File_Char(char(in_file),comment_char)
    else
       Count_Lines_in_File_VarStr=Count_Lines_in_File_Char(char(in_file))
    end if
    return
  end function Count_Lines_in_File_VarStr

  integer function Count_Lines_in_File_Char(in_file,comment_char)
    !% Returns the number of lines in the file {\normalfont \ttfamily in\_file} (version for character argument).
    use Galacticus_Error
    implicit none
    character(len=*), intent(in   )           :: in_file
    character(len=1), intent(in   ), optional :: comment_char
    character(len=1)                          :: first_char
    integer         , save                    :: i_unit      , io_status
    !$omp threadprivate(io_status,i_unit)
    open(newunit=i_unit,file=in_file,status='old',form='formatted',iostat=io_status)
    if (io_status /= 0) then
       write (0,*) 'Count_Lines_in_File(): FATAL - cannot open file ',trim(in_file)
       call Galacticus_Error_Report
    end if
    Count_Lines_in_File_Char=0
    do while (io_status == 0)
       read (i_unit,*,iostat=io_status) first_char
       if (io_status == 0) then
          if (present(comment_char)) then
             if (first_char /= comment_char) Count_Lines_in_File_Char=Count_Lines_in_File_Char+1
          else
             Count_Lines_in_File_Char=Count_Lines_in_File_Char+1
          end if
       end if
    end do
    close(i_unit)
    return
  end function Count_Lines_in_File_Char

  integer function File_Lock(fileName)
    !% Place a lock on a file.
    implicit none
    character(len=*), intent(in   ) :: fileName

    File_Lock=flock_C(trim(fileName)//char(0))
    return
  end function File_Lock

  subroutine File_Unlock(fileDescriptor)
    !% Remove a lock from a file.
    implicit none
    integer(c_int), intent(in   ) :: fileDescriptor

    call funlock_C(fileDescriptor)
    return
  end subroutine File_Unlock

end module File_Utilities

