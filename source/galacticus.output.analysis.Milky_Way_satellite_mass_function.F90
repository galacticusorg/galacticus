!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which performs analysis to compute the stellar mass function of Local Group satellite galaxies.

module Galacticus_Output_Analyses_LG_Satellite_Mass_Functions
  !% Performs analysis to compute the stellar mass function of Local Group satellite galaxies.
  use, intrinsic :: ISO_C_Binding
  use Kind_Numbers
  implicit none
  private
  public :: Galacticus_Output_Analysis_LG_Satellite_Mass_Functions, Galacticus_Output_Analysis_LG_Satellite_Mass_Functions_Output

  ! Record of module initialization.
  logical                                                                :: moduleInitialized =.false.

  ! Number of central galaxies supported, and enumeration.
  integer                   , parameter                                  :: centralGalaxyCount=2
  !@ <enumeration>
  !@  <name>localGroupCentralGalaxy</name>
  !@  <description>Used to specify central galaxy in the Local Group.</description>
  !@  <entry label="localGroupCentralMilkyWay"/>
  !@  <entry label="localGroupCentralM31"     />
  !@ </enumeration>
  integer                   , parameter                                  :: localGroupCentralMilkyWay=1
  integer                   , parameter                                  :: localGroupCentralM31     =2
  character       (len=8  )              , dimension(centralGalaxyCount) :: localGroupCentralLabel   =['MilkyWay','M31     ']

  ! Record of whether this analysis is active.
  logical                                , dimension(centralGalaxyCount) :: analysisActive=.false.

  ! Mass of Local Group halo.
  double precision                       , dimension(centralGalaxyCount) :: analysisLGSatelliteHaloMass
  double precision                       , dimension(centralGalaxyCount) :: analysisLGSatelliteHaloRadius

  ! Include constraints on presence of high-mass subhalos (to mimic presence of Magellanic Clouds).
  logical                                                                :: analysisLGSatelliteMFHighMassSubhalos
  double precision                                                       :: analysisLGSatelliteMFLogMassLMC      , analysisLGSatelliteMFLogMassSMC     , &
       &                                                                    analysisLGSatelliteMFSigmaLogMassLMC , analysisLGSatelliteMFSigmaLogMassSMC

  ! Output number corresponding to present day.
  integer         (c_size_t)                                             :: outputPresentDay

  ! Mass function bins.
  integer                                                                :: massBinsCount
  double precision          , allocatable, dimension(:                 ) :: massBins
  double precision          , allocatable, dimension(:,:               ) :: massFunctionCumulative               , massFunctionCumulativeHalo          , &
       &                                                                    massFunctionSquaredCumulative        , massFunctionCumulativeVariance
  integer                                , dimension(centralGalaxyCount) :: massFunctionTreeCount
  double precision                       , dimension(centralGalaxyCount) :: massFunctionHaloCount                , massFunctionLogLikelihood
  double precision                       , dimension(centralGalaxyCount) :: massFunctionHaloRadius               , treeFirstSubhaloMass                , &
       &                                                                    treeSecondSubhaloMass                , treeHaloRadius
  logical                                                                :: workArraysInitialized        =.false.
  logical                                , dimension(centralGalaxyCount) :: treeActive
  !$omp threadprivate(massFunctionCumulativeHalo,workArraysInitialized,treeActive,treeFirstSubhaloMass,treeSecondSubhaloMass,treeHaloRadius)

contains

  !# <mergerTreeAnalysisTask>
  !#  <unitName>Galacticus_Output_Analysis_LG_Satellite_Mass_Functions</unitName>
  !# </mergerTreeAnalysisTask>
  subroutine Galacticus_Output_Analysis_LG_Satellite_Mass_Functions(thisTree,thisNode,nodeStatus,iOutput,mergerTreeAnalyses)
    !% Construct the stellar mass function of Local Group satellites.
    use, intrinsic :: ISO_C_Binding
    use ISO_Varying_String
    use Input_Parameters
    use Galactic_Structure_Enclosed_Masses
    use Output_Times
    use Cosmology_Functions
    use Cosmology_Parameters
    use Numerical_Comparison
    use Numerical_Ranges
    use Galacticus_Error
    use Galacticus_Nodes
    use Galactic_Structure_Options
    use Galacticus_Output_Merger_Tree_Data
    use Memory_Management
    use Dark_Matter_Halo_Scales
    use Root_Finder
    implicit none
    type            (mergerTree              ), intent(in   )                 :: thisTree
    type            (treeNode                ), intent(inout), pointer        :: thisNode
    integer                                   , intent(in   )                 :: nodeStatus
    integer         (c_size_t                ), intent(in   )                 :: iOutput
    type            (varying_string          ), intent(in   ), dimension(:  ) :: mergerTreeAnalyses
    class           (nodeComponentBasic      )               , pointer        :: hostBasic                      , basic
    class           (cosmologyFunctionsClass )               , pointer        :: cosmologyFunctionsModel
    class           (cosmologyParametersClass)               , pointer        :: cosmologyParametersModel
    class           (darkMatterHaloScaleClass)               , pointer        :: darkMatterHaloScale_
    class           (outputTimesClass        )               , pointer        :: outputTimes_
    type            (treeNode                )               , pointer        :: host                           , node
    double precision                          , parameter                     :: redshiftPresentDay       =0.0d0
    double precision                          , parameter                     :: massMinimum              =1.0d0
    double precision                          , parameter                     :: massMaximum              =1.0d15
    integer                                   , parameter                     :: massFunctionBinsPerDecade=10
    double precision                                                          :: mass                            , timeNow     , &
         &                                                                       radiusVirial
    integer         (c_size_t                )                                :: k
    integer                                                                   :: j                               , i
    character       (len=64                  )                                :: parameterName
    type            (rootFinder              )                                :: finder
    !GCC$ attributes unused :: thisTree
    
    ! Initialize the module if necessary.
    if (.not.moduleInitialized) then
       !$omp critical(Galacticus_Output_Analysis_LG_Satellite_MF_Initialize)
       if (.not.moduleInitialized) then
          do i=1,centralGalaxyCount
             analysisActive(i)=any(trim(mergerTreeAnalyses) == "localGroupSatelliteMassFunction"//trim(localGroupCentralLabel(i)))
             if (analysisActive(i)) then
                parameterName="analysisLGSatelliteMFHaloMass"//trim(localGroupCentralLabel(i))
                !# <inputParameter>
                !#   <name>trim(parameterName)</name>
                !#   <variable>analysisLGSatelliteHaloMass(i)</variable>
                !#   <source>globalParameters</source>
                !#   <regEx>analysisLGSatelliteMFHaloMass(MilkyWay|M31)</regEx>
                !#   <defaultValue>1.0d12</defaultValue>
                !#   <description>The mass of the host halo to be used when constructing Local Group satellite galaxy stellar mass functions.</description>
                !#   <type>string</type>
                !#   <cardinality>0..1</cardinality>
                !#   <group>output</group>
                !# </inputParameter>
                ! Construct a temporary node, assign it the given mass, and find the virial radius.
                cosmologyFunctionsModel  => cosmologyFunctions ()
                cosmologyParametersModel => cosmologyParameters()
                darkMatterHaloScale_     => darkMatterHaloScale()
                timeNow                  =  cosmologyFunctionsModel%cosmicTime(1.0d0)              
                node                     => treeNode           ()
                basic                    => node%basic(autoCreate=.true.)
                call basic%timeSet(timeNow)
                call basic%massSet(analysisLGSatelliteHaloMass(i))
                radiusVirial=darkMatterHaloScale_%virialRadius(node)
                !# <inputParameter>
                !#   <name>'analysisLGSatelliteMFHaloRadius'//trim(localGroupCentralLabel(i))</name>
                !#   <regEx>analysisLGSatelliteMFHaloRadius(MilkyWay|M31)</regEx>
                !#   <variable>analysisLGSatelliteHaloRadius(i)</variable>
                !#   <source>globalParameters</source>
                !#   <defaultValue>radiusVirial</defaultValue>
                !#   <description>The radius within whcih the mass of the host halo is defined when constructing Local Group satellite galaxy stellar mass functions.</description>
                !#   <type>string</type>
                !#   <cardinality>0..1</cardinality>
                !#   <group>output</group>
                !# </inputParameter>
                !# <inputParameter>
                !#   <name>analysisLGSatelliteMFHighMassSubhalos</name>
                !#   <cardinality>0..1</cardinality>
                !#   <defaultValue>.false.</defaultValue>
                !#   <description>If true, include constraints on the presence of high mass subhalos (e.g. Magellanic Clouds).</description>
                !#   <group>output</group>
                !#   <source>globalParameters</source>
                !#   <type>boolean</type>
                !# </inputParameter>
                !# <inputParameter>
                !#   <name>analysisLGSatelliteMFLogMassLMC</name>
                !#   <cardinality>0..1</cardinality>
                !#   <defaultValue>11.0d0</defaultValue>
                !#   <description>Base-10 logarithm of the mass of the LMC subhalo (in units of $M_\odot$).</description>
                !#   <group>output</group>
                !#   <source>globalParameters</source>
                !#   <type>real</type>
                !# </inputParameter>
                !# <inputParameter>
                !#   <name>analysisLGSatelliteMFLogMassSMC</name>
                !#   <cardinality>0..1</cardinality>
                !#   <defaultValue>9.5d0</defaultValue>
                !#   <description>Base-10 logarithm of the mass of the SMC subhalo (in units of $M_\odot$).</description>
                !#   <group>output</group>
                !#   <source>globalParameters</source>
                !#   <type>real</type>
                !# </inputParameter>
                !# <inputParameter>
                !#   <name>analysisLGSatelliteMFSigmaLogMassLMC</name>
                !#   <cardinality>0..1</cardinality>
                !#   <defaultValue>0.5d0</defaultValue>
                !#   <description>Root-variance in the base-10 logarithm of the mass of the LMC subhalo (in units of $M_\odot$).</description>
                !#   <group>output</group>
                !#   <source>globalParameters</source>
                !#   <type>real</type>
                !# </inputParameter>
                !# <inputParameter>
                !#   <name>analysisLGSatelliteMFSigmaLogMassSMC</name>
                !#   <cardinality>0..1</cardinality>
                !#   <defaultValue>0.5d0</defaultValue>
                !#   <description>Root-variance in the base-10 logarithm of the mass of the SMC subhalo (in units of $M_\odot$).</description>
                !#   <group>output</group>
                !#   <source>globalParameters</source>
                !#   <type>real</type>
                !# </inputParameter>
                ! Solve for the virial mass that gives the required enclosed mass.
                call finder%tolerance(1.0d-6,1.0d-6)
                call finder%rangeExpand(rangeExpandUpward=2.0d0,rangeExpandDownward=0.5d0,rangeExpandType=rangeExpandMultiplicative)
                call finder%rootFunction(MW_Satellite_Mass_Function_Enclosed_Mass)
                analysisLGSatelliteHaloMass(i)=finder%find(rootGuess=analysisLGSatelliteHaloMass(i))
                call node%destroy()
             end if
          end do
          ! Continue only if an analysis is active.
          if (any(analysisActive)) then
             ! Find the output corresponding to the present day.
             cosmologyFunctionsModel => cosmologyFunctions()
             outputTimes_            => outputTimes       ()
             outputPresentDay=-1
             do k=1,outputTimes_%count()
                if     (                                                                                        &
                     &  Values_Agree(                                                                           &
                     &               redshiftPresentDay                                                       , &
                     &               cosmologyFunctionsModel%redshiftFromExpansionFactor(                       &
                     &               cosmologyFunctionsModel%expansionFactor             (                      &
                     &                                                                    outputTimes_%time(k)  &
                     &                                                                   )                      &
                     &                                                                  )                     , &
                     &               absTol=0.001d0                                                             &
                     &              )                                                                           &
                     & ) then
                   outputPresentDay=k
                   exit
                end if
             end do
             if (outputPresentDay < 0)                                                                                    &
                  & call Galacticus_Error_Report('unable to find present day [z=0] in outputs'//{introspection:location})
             ! Construct mass bins.
             massBinsCount=int(log10(massMaximum/massMinimum)*dble(massFunctionBinsPerDecade))+1
             call allocateArray(massBins                      ,[massBinsCount                   ])
             call allocateArray(massFunctionCumulative        ,[massBinsCount,centralGalaxyCount])
             call allocateArray(massFunctionSquaredCumulative ,[massBinsCount,centralGalaxyCount])
             call allocateArray(massFunctionCumulativeVariance,[massBinsCount,centralGalaxyCount])
             massBins=Make_Range(massMinimum,massMaximum,massBinsCount,rangeTypeLogarithmic)
             massFunctionHaloCount         =0.0d0
             massFunctionHaloRadius        =0.0d0
             massFunctionLogLikelihood     =0.0d0
             massFunctionCumulative        =0.0d0
             massFunctionSquaredCumulative =0.0d0
             massFunctionCumulativeVariance=0.0d0
             massFunctionTreeCount         =0
          end if
          ! Record that module is initialized.
          moduleInitialized=.true.
       end if
       !$omp end critical(Galacticus_Output_Analysis_LG_Satellite_MF_Initialize)
    end if
    ! Return if this analysis is not active.
    if (.not.any(analysisActive)   ) return
    ! Return if this output is not the present day.
    if (iOutput /= outputPresentDay) return
    ! Initialize mass function count array if necessary.
    if (.not.workArraysInitialized) then
       call allocateArray(massFunctionCumulativeHalo,[massBinsCount,centralGalaxyCount])
       massFunctionCumulativeHalo= 0.0d0
       treeActive                =.false.
       treeFirstSubhaloMass      = 0.0d0   
       treeSecondSubhaloMass     = 0.0d0   
       treeHaloRadius            = 0.0d0
       workArraysInitialized     =.true.
    end if
    ! Accumulate halo if necessary.
    if (nodeStatus == nodeStatusFinal) then
       do i=1,centralGalaxyCount
          if (treeActive(i)) call MW_Satellite_Mass_Function_Accumulate_Halo(i,massFunctionCumulativeHalo)
       end do
       return
    end if
    ! Find the host halo.
    host => thisNode
    do while (host%isSatellite())
       host => host%parent
    end do
    hostBasic => host    %basic()
    basic     => thisNode%basic()
    ! Iterate over central galaxies.
    do i=1,centralGalaxyCount
       ! Skip non-active analyses.
       if (analysisActive(i)) then
          ! Check if host halo mass agrees with Milky Way halo mass.
          if (Values_Differ(hostBasic%mass(),analysisLGSatelliteHaloMass(i),relTol=1.0d-3)) cycle
          ! Proceed for satellite galaxies.
          if (.not.treeActive(i)) then
             darkMatterHaloScale_ => darkMatterHaloScale()
             treeHaloRadius(i)=darkMatterHaloScale_%virialRadius(host)
             treeActive    (i)=.true.
          end if
          if (thisNode%isSatellite()) then
             ! Record masses of two most massive subhalos.
             if (basic%mass() > treeFirstSubhaloMass(i)) then
                treeSecondSubhaloMass(i)=treeFirstSubhaloMass(i)
                treeFirstSubhaloMass (i)=basic%mass()
             else if (basic%mass() > treeSecondSubhaloMass(i)) then
                treeSecondSubhaloMass(i)=basic%mass()
             end if
             ! Get the galactic mass.
             mass=                                                                                                                       &
                  &  Galactic_Structure_Enclosed_Mass(thisNode,radiusLarge,componentType=componentTypeDisk    ,massType=massTypeStellar) &
                  & +Galactic_Structure_Enclosed_Mass(thisNode,radiusLarge,componentType=componentTypeSpheroid,massType=massTypeStellar)
             ! Accumulate this galaxy.
             do j=1,massBinsCount
                if (mass > massBins(j)) massFunctionCumulativeHalo(j,i)=massFunctionCumulativeHalo(j,i)+1.0d0
             end do
          end if
          ! Accumulate halo if necessary.
          if (nodeStatus == nodeStatusLast) call MW_Satellite_Mass_Function_Accumulate_Halo(i,massFunctionCumulativeHalo)
       end if
    end do
    return

  contains

    double precision function MW_Satellite_Mass_Function_Enclosed_Mass(haloMass)
      !% Root finding function used to set the host halo mass for Local Group satellite mass function calculations.
      use Galacticus_Calculations_Resets
      implicit none
      double precision, intent(in   ) :: haloMass

      call basic%massSet(haloMass)
      call Galacticus_Calculations_Reset(node)
      MW_Satellite_Mass_Function_Enclosed_Mass=                                                             &
           & +Galactic_Structure_Enclosed_Mass(node,analysisLGSatelliteHaloRadius(i),massType=massTypeDark) &
           & *  cosmologyParametersModel%OmegaMatter()                                                      &
           & /(                                                                                             &
           &   +cosmologyParametersModel%OmegaMatter()                                                      &
           &   -cosmologyParametersModel%OmegaBaryon()                                                      &
           &  )                                                                                             &
           & -analysisLGSatelliteHaloMass(i)
      return
    end function MW_Satellite_Mass_Function_Enclosed_Mass

  end subroutine Galacticus_Output_Analysis_LG_Satellite_Mass_Functions

  subroutine MW_Satellite_Mass_Function_Accumulate_Halo(localGroupCentral,massFunctionCumulativeHalo)
    !% Accumulate the cumulative mass function for a single halo to the global arrays.
    implicit none
    integer         , intent(in   )                 :: localGroupCentral
    double precision, intent(inout), dimension(:,:) :: massFunctionCumulativeHalo
    double precision                                :: treeWeight

    ! Compute the weight for this tree.
    if (analysisLGSatelliteMFHighMassSubhalos .and. localGroupCentral == localGroupCentralMilkyWay) then
       if (treeFirstSubhaloMass(localGroupCentral) > 0.0d0 .and. treeSecondSubhaloMass(localGroupCentral) > 0.0d0) then
          treeWeight=+exp(                                                     &
               &          -0.5d0                                               &
               &          *(                                                   &
               &            +(                                                 &
               &              +log10(treeFirstSubhaloMass(localGroupCentral))  &
               &              -analysisLGSatelliteMFLogMassLMC                 &
               &             )                                                 &
               &            /analysisLGSatelliteMFSigmaLogMassLMC              &
               &           )**2                                                &
               &         )                                                     &
               &     *exp(                                                     &
               &          -0.5d0                                               &
               &          *(                                                   &
               &            +(                                                 &
               &              +log10(treeSecondSubhaloMass(localGroupCentral)) &
               &              -analysisLGSatelliteMFLogMassSMC                 &
               &             )                                                 &
               &            /analysisLGSatelliteMFSigmaLogMassSMC              &
               &           )**2                                                &
               &         )
       else
          treeWeight=0.0d0
       end if
    else
       treeWeight=1.0d0
    end if
    !$omp critical(MW_Satellite_Mass_Function_Accumulate)
    massFunctionCumulative        (:,localGroupCentral)=massFunctionCumulative        (:,localGroupCentral)+massFunctionCumulativeHalo(:,localGroupCentral)   *treeWeight
    massFunctionSquaredCumulative (:,localGroupCentral)=massFunctionSquaredCumulative (:,localGroupCentral)+massFunctionCumulativeHalo(:,localGroupCentral)**2*treeWeight
    massFunctionCumulativeVariance(:,localGroupCentral)=massFunctionCumulativeVariance(:,localGroupCentral)+massFunctionCumulativeHalo(:,localGroupCentral)   *treeWeight**2
    massFunctionHaloCount         (  localGroupCentral)=massFunctionHaloCount         (  localGroupCentral)+                                                   treeWeight
    massFunctionHaloRadius        (  localGroupCentral)=massFunctionHaloRadius        (  localGroupCentral)+treeHaloRadius            (  localGroupCentral)   *treeWeight
    massFunctionLogLikelihood     (  localGroupCentral)=massFunctionLogLikelihood     (  localGroupCentral)+                                                   treeWeight
    massFunctionTreeCount         (  localGroupCentral)=massFunctionTreeCount         (  localGroupCentral)+                                                   1
    !$omp end critical(MW_Satellite_Mass_Function_Accumulate)
    treeActive                (  localGroupCentral)=.false.
    treeFirstSubhaloMass      (  localGroupCentral)=0.0d0
    treeSecondSubhaloMass     (  localGroupCentral)=0.0d0
    treeHaloRadius            (  localGroupCentral)=0.0d0
    massFunctionCumulativeHalo(:,localGroupCentral)=0.0d0
    return
  end subroutine MW_Satellite_Mass_Function_Accumulate_Halo

  !# <hdfPreCloseTask>
  !#  <unitName>Galacticus_Output_Analysis_LG_Satellite_Mass_Functions_Output</unitName>
  !# </hdfPreCloseTask>
  subroutine Galacticus_Output_Analysis_LG_Satellite_Mass_Functions_Output()
    !% Outputs galaxy mass functions to file.
    use Galacticus_HDF5
    use Numerical_Constants_Astronomical
    use String_Handling
    implicit none
    double precision            , dimension(massBinsCount) :: massFunctionCumulativeMean                                  , massFunctionCumulativeMeanVariance, &
         &                                                    massFunctionCumulativeMeanPoissonVariance
    double precision            , parameter                :: logImpossible                            =-0.5d0*huge(1.0d0)
    type            (hdf5Object)                           :: analysisGroup                                               , massFunctionGroup                 , &
         &                                                    thisDataset
    integer                                                :: i

    ! Iterate over central galaxies.
    do i=1,centralGalaxyCount
       ! Skip if this analysis is not active.
       if (.not.analysisActive(i)) cycle   
       ! Compute the mean mass function and its variance.
       if (massFunctionHaloCount(i) > 0.0d0) then
          massFunctionCumulativeMean                  =+massFunctionCumulative        (:,i)    &
               &                                       /massFunctionHaloCount         (  i)
          massFunctionHaloRadius                   (i)=+massFunctionHaloRadius        (  i)    &
               &                                       /massFunctionHaloCount         (  i)  
          massFunctionCumulativeMeanVariance          =+massFunctionSquaredCumulative (:,i)    &
               &                                       /massFunctionHaloCount         (  i)    &
               &                                       -massFunctionCumulativeMean         **2
          massFunctionCumulativeMeanPoissonVariance=   +massFunctionCumulativeVariance(:,i)    &
               &                                       /massFunctionHaloCount         (  i)**2
       else
          massFunctionCumulativeMean                  =+0.0d0
          massFunctionHaloRadius                   (i)=+0.0d0
          massFunctionCumulativeMeanVariance          =+0.0d0
          massFunctionCumulativeMeanPoissonVariance   =+0.0d0
       end if
       ! Compute log-likelihood based on tree acceptibility.
       if (massFunctionLogLikelihood(i) > 0.0d0) then
          massFunctionLogLikelihood(i)=log(massFunctionLogLikelihood(i)/dble(massFunctionTreeCount(i)))
       else
          massFunctionLogLikelihood(i)=logImpossible
       end if
       ! Output the mass function.
       !$ call hdf5Access%set()
       analysisGroup    =galacticusOutputFile%openGroup('analysis'                                                              ,'Model analysis'                     )
       massFunctionGroup=analysisGroup       %openGroup(trim(String_Lower_Case_First(localGroupCentralLabel(i)))//'MassFunction','Stellar mass function of satellites')
       call massFunctionGroup%writeAttribute(massFunctionHaloRadius                   (i),'haloRadius'                                                                                                            )
       call massFunctionGroup%writeAttribute(massFunctionLogLikelihood                (i),'logLikelihood'                                                                                                         )
       call massFunctionGroup%writeAttribute(massFunctionHaloCount                    (i),'haloCount'                                                                                                             )
       call massFunctionGroup%writeDataset  (massFunctionCumulativeMean                  ,'massFunctionCumulative'               ,'Cumulative mass function'                                                      )
       call massFunctionGroup%writeDataset  (massFunctionCumulativeMeanVariance          ,'massFunctionCumulativeVariance'       ,'Cumulative mass function variance'                                             )
       call massFunctionGroup%writeDataset  (massFunctionCumulativeMeanPoissonVariance   ,'massFunctionCumulativePoissonVariance','Cumulative mass function variance assuming Poisson'                            )
       call massFunctionGroup%writeDataset  (massBins                                    ,'massStellar'                          ,'Stellar mass'                                      ,datasetReturned=thisDataset)
       call thisDataset      %writeAttribute(massSolar                                   ,'unitsInSI'                                                                                                             )
       call thisDataset      %close         (                                                                                                                                                                     )
       call massFunctionGroup%close         (                                                                                                                                                                     )
       call analysisGroup    %close         (                                                                                                                                                                     )
       !$ call hdf5Access%unset()
    end do
    return
  end subroutine Galacticus_Output_Analysis_LG_Satellite_Mass_Functions_Output

end module Galacticus_Output_Analyses_LG_Satellite_Mass_Functions
