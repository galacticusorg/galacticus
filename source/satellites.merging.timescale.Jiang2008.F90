!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implements calculations of satellite merging times using the \cite{jiang_fitting_2008} method.
  
  use Dark_Matter_Halo_Scales
  use Dark_Matter_Profiles_DMO
  
  !# <satelliteMergingTimescales name="satelliteMergingTimescalesJiang2008">
  !#  <description>Computes the merging timescale using the method of \cite{jiang_fitting_2008}.</description>
  !# </satelliteMergingTimescales>
  type, extends(satelliteMergingTimescalesClass) :: satelliteMergingTimescalesJiang2008
     !% A class implementing the \cite{jiang_fitting_2008} method for satellite merging timescales.
     private
     class          (darkMatterHaloScaleClass), pointer :: darkMatterHaloScale_ => null()
     class          (darkMatterProfileDMOClass  ), pointer :: darkMatterProfileDMO_ => null()
     double precision                                   :: timescaleMultiplier
     ! Scatter (in log(T_merge)) to add to the merger times.
     double precision                                   :: scatter
   contains
     final     ::                     jiang2008Destructor
     procedure :: timeUntilMerging => jiang2008TimeUntilMerging
  end type satelliteMergingTimescalesJiang2008

  interface satelliteMergingTimescalesJiang2008
     !% Constructors for the \cite{jiang_fitting_2008} merging timescale class.
     module procedure jiang2008ConstructorParameters
     module procedure jiang2008ConstructorInternal
  end interface satelliteMergingTimescalesJiang2008

contains

  function jiang2008ConstructorParameters(parameters) result(self)
    !% Constructor for the \cite{jiang_fitting_2008} merging timescale class which builds the object from a parameter set.
    use Galacticus_Nodes  , only : defaultBasicComponent
    use Galacticus_Display
    use Input_Parameters
    use Galacticus_Error
    implicit none
    type            (satelliteMergingTimescalesJiang2008)                :: self
    type            (inputParameters                    ), intent(inout) :: parameters
    class           (darkMatterHaloScaleClass           ), pointer       :: darkMatterHaloScale_
    class           (darkMatterProfileDMOClass             ), pointer       :: darkMatterProfileDMO_
    double precision                                                     :: scatter             , timescaleMultiplier

    if (.not.defaultBasicComponent%massIsGettable()) call Galacticus_Error_Report('this method requires that the "mass" property of the basic component be gettable'//{introspection:location})
    !# <inputParameter>
    !#   <name>timescaleMultiplier</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>0.75d0</defaultValue>
    !#   <description>A multiplier for the merging timescale in dynamical friction timescale calculations.</description>
    !#   <source>parameters</source>
    !#   <type>real</type>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>scatter</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>0.0d0</defaultValue>
    !#   <description>Specifies whether or not to add random scatter to the dynamical friction timescales in the {\normalfont \ttfamily Jiang2008} satellite merging time implementation.</description>
    !#   <group>starFormation</group>
    !#   <source>parameters</source>
    !#   <type>string</type>
    !# </inputParameter>       
    !# <objectBuilder class="darkMatterHaloScale" name="darkMatterHaloScale_" source="parameters"/>
    !# <objectBuilder class="darkMatterProfileDMO"   name="darkMatterProfileDMO_"   source="parameters"/>
    self=satelliteMergingTimescalesJiang2008(timescaleMultiplier,scatter,darkMatterHaloScale_,darkMatterProfileDMO_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="darkMatterHaloScale_"/>
    !# <objectDestructor name="darkMatterProfileDMO_"  />
    return
  end function jiang2008ConstructorParameters

  function jiang2008ConstructorInternal(timescaleMultiplier,scatter,darkMatterHaloScale_,darkMatterProfileDMO_) result(self)
    !% Constructor for the \cite{jiang_fitting_2008} merging timescale class.
    implicit none
    type            (satelliteMergingTimescalesJiang2008)                        :: self
    double precision                                     , intent(in   )         :: timescaleMultiplier , scatter
    class           (darkMatterHaloScaleClass           ), intent(in   ), target :: darkMatterHaloScale_
    class           (darkMatterProfileDMOClass             ), intent(in   ), target :: darkMatterProfileDMO_
    !# <constructorAssign variables="timescaleMultiplier, scatter, *darkMatterHaloScale_, *darkMatterProfileDMO_"/>

    return
  end function jiang2008ConstructorInternal

  subroutine jiang2008Destructor(self)
    !% Destructor for the {\normalfont \ttfamily jiang2008} satellite merging timescale class.
    implicit none
    type(satelliteMergingTimescalesJiang2008), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterHaloScale_"/>
    !# <objectDestructor name="self%darkMatterProfileDMO_"  />
    return
  end subroutine jiang2008Destructor

  double precision function jiang2008TimeUntilMerging(self,node,orbit)
    !% Return the timescale for merging satellites using the \cite{jiang_fitting_2008} method.
    use Galacticus_Nodes, only : nodeComponentBasic
    use Satellite_Orbits
    use Galacticus_Error
    implicit none
    class           (satelliteMergingTimescalesJiang2008), intent(inout) :: self
    type            (treeNode                           ), intent(inout) :: node
    type            (keplerOrbit                        ), intent(inout) :: orbit
    type            (treeNode                           ), pointer       :: nodeHost
    class           (nodeComponentBasic                 ), pointer       :: basicHost                            , basic
    logical                                              , parameter     :: acceptUnboundOrbits          =.false.

    double precision                                     , parameter     :: C                            =0.43d0 , a            =0.94d0, &  !   Fitting parameters from Jiang's paper.
         &                                                                  b                            =0.60d0 , d            =0.60d0
    integer                                                              :: errorCode
    double precision                                                     :: equivalentCircularOrbitRadius        , massRatio           , &
         &                                                                  orbitalCircularity                   , radialScale         , &
         &                                                                  velocityScale

    ! Find the host node.
    nodeHost => node%parent
    ! Get the equivalent circular orbit.
    equivalentCircularOrbitRadius=Satellite_Orbit_Equivalent_Circular_Orbit_Radius(nodeHost,orbit,self%darkMatterHaloScale_,self%darkMatterProfileDMO_,errorCode)
    ! Check error codes.
    select case (errorCode)
    case (errorCodeOrbitUnbound     )
       jiang2008TimeUntilMerging=satelliteMergeTimeInfinite
       return
    case (errorCodeNoEquivalentOrbit)
       ! Circularity is not defined. Assume instantaneous merging.
       jiang2008TimeUntilMerging=0.0d0
       return
    case (errorCodeSuccess          )
    case default
       call Galacticus_Error_Report('unrecognized error code'//{introspection:location})
    end select
    ! Get velocity scale.
    velocityScale=self%darkMatterHaloScale_%virialVelocity(nodeHost)
    radialScale  =self%darkMatterHaloScale_%virialRadius  (nodeHost)
    ! Compute orbital circularity.
    orbitalCircularity= orbit%angularMomentum()                                                          &
         &             /equivalentCircularOrbitRadius                                                    &
         &             /self%darkMatterProfileDMO_%circularVelocity(nodeHost,equivalentCircularOrbitRadius)
    ! Compute mass ratio (mass in host [not including satellite] divided by mass in satellite).
    basic => node%basic()
    basicHost => nodeHost%basic()
    massRatio=basicHost%mass()/basic%mass()-1.0d0
    ! Check for a non-zero mass ratio.
    if (massRatio <= 0.0d0) then
       ! Assume zero merging time as the satellite is as massive as the host.
       jiang2008TimeUntilMerging=0.0d0
    else
       ! Compute dynamical friction timescale.
       jiang2008TimeUntilMerging=+self%timescaleMultiplier                               &
            &                    *self%darkMatterHaloScale_%dynamicalTimescale(nodeHost) &
            &                    *sqrt(equivalentCircularOrbitRadius/radialScale)        &
            &                    *((a*(orbitalCircularity**b)+d)/2.0d0/C)                &
            &                    *          massRatio                                    &
            &                    /log(1.0d0+massRatio)
       ! Add scatter if necessary.
       if (self%scatter > 0.0d0)                                                                 &
            & jiang2008TimeUntilMerging=+jiang2008TimeUntilMerging                               &
            &                           *exp(                                                    &
            &                                +self%scatter                                       &
            &                                *node%hostTree%randomNumberGenerator%normalSample() &
            &                               )
    end if
    return
  end function jiang2008TimeUntilMerging
