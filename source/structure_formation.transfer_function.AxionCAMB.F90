!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019, 2020, 2021, 2022, 2023, 2024, 2025, 2026
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !!{
  Implements a transfer function class using the AxionCAMB code.
  !!}

  use :: Dark_Matter_Particles, only : darkMatterParticleClass
  use :: File_Utilities       , only : lockDescriptor

  !![
  <transferFunction name="transferFunctionAxionCAMB">
   <description>
    A transfer function class in which the \gls{fdm} transfer function is generated by
    \href{https://github.com/dgrin1/axionCAMB.git}{\normalfont \scshape AxionCAMB} using the specified
    cosmological parameters. The transfer function is written out to a file in the {\normalfont \ttfamily
    data/dynamic/largeScaleStructure} directory and will be re-read later if needed. {\normalfont
    \scshape AxionCAMB} will be downloaded and run if the transfer function needs to be computed.
   </description>
  </transferFunction>
  !!]
  type, extends(transferFunctionFileFuzzyDarkMatter) :: transferFunctionAxionCAMB
     !!{
     A transfer function class which utilizes the AxionCAMB code to compute transfer functions.
     !!}
     private
     logical          :: initialized
     double precision :: wavenumberMaximum
     logical          :: wavenumberMaximumReached
     integer          :: countPerDecade
   contains
     !![
     <methods>
       <method description="Check that the provided wavenumber is within the tabulated range of the transfer function." method="checkRange" />
     </methods>
     !!]
     final     ::                           axionCambDestructor
     procedure :: checkRange             => axionCambCheckRange
     procedure :: value                  => axionCambValue
     procedure :: logarithmicDerivative  => axionCambLogarithmicDerivative
     procedure :: wavenumbersLocalMinima => axionCambWavenumbersLocalMinima
  end type transferFunctionAxionCAMB

  interface transferFunctionAxionCAMB
     !!{
     Constructors for the file transfer function class.
     !!}
     module procedure axionCambConstructorParameters
     module procedure axionCambConstructorInternal
  end interface transferFunctionAxionCAMB

  ! Smallest maximum wavenumber to tabulate.
  double precision, parameter :: wavenumberMaximumLimit=15000.0d0

contains

  function axionCambConstructorParameters(parameters) result(self)
    !!{
    Constructor for the AxionCAMB transfer function class which takes a parameter set as input.
    !!}
    use :: Input_Parameters, only : inputParameter, inputParameters
    implicit none
    type            (transferFunctionAxionCAMB)                :: self
    type            (inputParameters          ), intent(inout) :: parameters
    class           (cosmologyParametersClass ), pointer       :: cosmologyParameters_
    class           (cosmologyFunctionsClass  ), pointer       :: cosmologyFunctions_
    class           (darkMatterParticleClass  ), pointer       :: darkMatterParticle_
    double precision                                           :: redshift
    integer                                                    :: countPerDecade

    !![
    <inputParameter>
      <name>redshift</name>
      <source>parameters</source>
      <defaultValue>0.0d0</defaultValue>
      <description>The redshift at which the transfer function should be evaluated.</description>
    </inputParameter>
    <inputParameter>
      <name>countPerDecade</name>
      <source>parameters</source>
      <defaultValue>0</defaultValue>
      <description>The number of points per decade of wavenumber to compute in the AxionCAMB transfer function. A value of 0 allows AxionCAMB to choose what it considers to be optimal spacing of wavenumbers.</description>
    </inputParameter>
    <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    <objectBuilder class="cosmologyFunctions"  name="cosmologyFunctions_"  source="parameters"/>
    <objectBuilder class="darkMatterParticle"  name="darkMatterParticle_"  source="parameters"/>
    !!]
    self=transferFunctionAxionCAMB(darkMatterParticle_,cosmologyParameters_,cosmologyFunctions_,redshift,countPerDecade)
    !![
    <inputParametersValidate source="parameters"/>
    <objectDestructor name="cosmologyParameters_"/>
    <objectDestructor name="cosmologyFunctions_" />
    <objectDestructor name="darkMatterParticle_" />
    !!]
    return
  end function axionCambConstructorParameters

  function axionCambConstructorInternal(darkMatterParticle_,cosmologyParameters_,cosmologyFunctions_,redshift,countPerDecade) result(self)
    !!{
    Internal constructor for the \refClass{transferFunctionAxionCAMB} transfer function class.
    !!}
    use :: Cosmology_Parameters , only : hubbleUnitsLittleH
    use :: Dark_Matter_Particles, only : darkMatterParticleFuzzyDarkMatter
    use :: Error                , only : Error_Report
    implicit none
    type            (transferFunctionAxionCAMB)                          :: self
    class           (darkMatterParticleClass  ), intent(in   ), target   :: darkMatterParticle_
    class           (cosmologyParametersClass ), intent(in   ), target   :: cosmologyParameters_
    class           (cosmologyFunctionsClass  ), intent(in   ), target   :: cosmologyFunctions_
    double precision                           , intent(in   )           :: redshift
    integer                                    , intent(in   )           :: countPerDecade
    !![
    <constructorAssign variables="countPerDecade, redshift, *darkMatterParticle_, *cosmologyParameters_, *cosmologyFunctions_"/>
    !!]

    ! Require that the dark matter be fuzzy dark matter.
    select type (darkMatterParticle_)
    class is (darkMatterParticleFuzzyDarkMatter)
       ! Fuzzy dark matter particle - this is as expected.
    class default
       call Error_Report('transfer function expects a fuzzy dark matter particle'//{introspection:location})
    end select
    ! Set initialization state.
    self%initialized=.false.
    ! No reference transfer function is used.
    self%transferFunctionReferenceAvailable =  .false.
    self%transferFunctionReference          => null()
    ! Set the epoch time for this transfer function.
    self%time=self%cosmologyFunctions_%cosmicTime(self%cosmologyFunctions_%expansionFactorFromRedshift(redshift))
    ! Set maximum wavenumber.
    self%wavenumberMaximum=+wavenumberMaximumLimit                                             &
         &                 *self%cosmologyParameters_%hubbleConstant(units=hubbleUnitsLittleH)
    self%wavenumberMaximumReached=.false.
    return
  end function axionCambConstructorInternal

  subroutine axionCambDestructor(self)
    !!{
    Destructor for the AxionCAMB transfer function class.
    !!}
    implicit none
    type(transferFunctionAxionCAMB), intent(inout) :: self

    !![
    <objectDestructor name="self%cosmologyParameters_"/>
    <objectDestructor name="self%cosmologyFunctions_" />
    <objectDestructor name="self%darkMatterParticle_" />
    !!]
    return
  end subroutine axionCambDestructor

  subroutine axionCambCheckRange(self,wavenumber)
    !!{
    Check that the provided wavenumber is within the tabulated range and, if not, recompute
    the AxionCAMB transfer function.
    !!}
    use :: File_Utilities      , only : File_Lock                            , File_Unlock
    use :: Interfaces_AxionCAMB, only : Interface_AxionCAMB_Transfer_Function
    implicit none
    class           (transferFunctionAxionCAMB), intent(inout) :: self
    double precision                           , intent(in   ) :: wavenumber
    logical                                                    :: makeTransferFunction
    type            (lockDescriptor           )                :: fileLock

    ! If the file has been read and the wavenumber is within range, simply return.
    makeTransferFunction=.false.
    if (self%initialized) then
       if     (                                       &
            &   wavenumber > exp(self%transfer%x(-1)) &
            &  .and.                                  &
            &   .not.self%wavenumberMaximumReached    &
            & ) makeTransferFunction=.true.
    else
       makeTransferFunction=.true.
    end if
    if (.not.makeTransferFunction) return
    ! Retrieve the transfer function.
    call Interface_AxionCAMB_Transfer_Function(self%cosmologyParameters_,self%darkMatterParticle_,[self%redshift],wavenumber,self%wavenumberMaximum,self%countPerDecade,self%fileName,self%wavenumberMaximumReached)
    ! Get a lock on the relevant lock file.
    call File_Lock(char(self%fileName),fileLock)
    ! Read the newly created file.
    call self%readFile(char(self%fileName))
    ! Unlock the lock file.
    call File_Unlock(fileLock)
    ! Check the maximum wavenumber.
    if (self%transfer%x(-1) > log(self%wavenumberMaximum)-0.01d0) self%wavenumberMaximumReached=.true.
    ! Record that the transfer function has now been initialized.
    self%initialized=.true.
    return
  end subroutine axionCambCheckRange

  double precision function axionCambValue(self,wavenumber)
    !!{
    Return the transfer function at the given wavenumber.
    !!}
    implicit none
    class           (transferFunctionAxionCAMB), intent(inout) :: self
    double precision                           , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    axionCambValue=self%transferFunctionFileFuzzyDarkMatter%value(wavenumber)
    return
  end function axionCambValue

  double precision function axionCambLogarithmicDerivative(self,wavenumber)
    !!{
    Return the logarithmic derivative of the transfer function at the given wavenumber.
    !!}
    implicit none
    class           (transferFunctionAxionCAMB), intent(inout) :: self
    double precision                           , intent(in   ) :: wavenumber

    call self%checkRange(wavenumber)
    axionCambLogarithmicDerivative=self%transferFunctionFileFuzzyDarkMatter%logarithmicDerivative(wavenumber)
    return
  end function axionCambLogarithmicDerivative

  subroutine axionCambWavenumbersLocalMinima(self,wavenumbers)
    !!{
    Return a list of wavenumbers corresponding to local minima in the transfer function.
    !!}
    implicit none
    class           (transferFunctionAxionCAMB), intent(inout)                            :: self
    double precision                           , intent(  out), allocatable, dimension(:) :: wavenumbers

    call self%checkRange(wavenumber=1.0d0)
    wavenumbers=self%wavenumbersLocalMinima_
    return
  end subroutine axionCambWavenumbersLocalMinima
