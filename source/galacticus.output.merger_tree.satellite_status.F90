!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which handles outputting of satellite status (i.e. whether orphaned or not) to the \glc\ output file.

module Galacticus_Output_Trees_Satellite_Status
  !% Handles outputting of satellite status (i.e. whether orphaned or not) to the \glc\ output file.
  implicit none
  private
  public :: Galacticus_Output_Tree_Satellite_Status, Galacticus_Output_Tree_Satellite_Status_Property_Count,&
       & Galacticus_Output_Tree_Satellite_Status_Names

  ! Number of host properties.
  integer :: satelliteStatusPropertyCount

  ! Flag indicating whether or not satellite host information is to be output.
  logical :: outputSatelliteStatus

  ! Flag indicating whether or not this module has been initialized.
  logical :: outputSatelliteStatusInitialized=.false.

  ! Discriminator to use to determine satellite status.
  integer :: statusOrphanDiscriminator
  !# <enumeration>
  !#  <name>statusOrphanDiscriminator</name>
  !#  <description>Enumeration of possible discriminators for satellite orphan status.</description>
  !#  <visibility>private</visibility>
  !#  <encodeFunction>yes</encodeFunction>
  !#  <entry label="boundMass"/>
  !#  <entry label="position" />
  !# </enumeration>
  
contains

  subroutine Galacticus_Output_Tree_Satellite_Status_Initialize
    !% Initializes the module by determining whether or not satellite host data should be output.
    use Input_Parameters
    use Galacticus_Error
    use Galacticus_Nodes
    use ISO_Varying_String
    implicit none
    type(varying_string) :: statusOrphanDiscriminatorText

    if (.not.outputSatelliteStatusInitialized) then
       !$omp critical(Galacticus_Output_Tree_Satellite_Status_Initialize)
       if (.not.outputSatelliteStatusInitialized) then
          !# <inputParameter>
          !#   <name>outputSatelliteStatus</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not satellite status (i.e. whether the satellite is orphaned or not) should be included in the output.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          !# <inputParameter>
          !#   <name>statusOrphanDiscriminator</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>var_str('boundMass')</defaultValue>
          !#   <description>Specifies whether bound mass or position history will be used to determine satellite orphan status.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>string</type>
          !#   <variable>statusOrphanDiscriminatorText</variable>
          !# </inputParameter>
          statusOrphanDiscriminator=enumerationStatusOrphanDiscriminatorEncode(char(statusOrphanDiscriminatorText),includesPrefix=.false.)             
          ! Count number of properties to output and check that required properties are gettable.
          satelliteStatusPropertyCount=0
          if (outputSatelliteStatus) then
             satelliteStatusPropertyCount=satelliteStatusPropertyCount+1
             select case (statusOrphanDiscriminator)
             case (statusOrphanDiscriminatorBoundMass)
                if     (                                                                                                                    &
                     &  .not.                                                                                                               &
                     &       (                                                                                                              &
                     &        defaultSatelliteComponent% boundMassHistoryIsGettable()                                                       &
                     &       )                                                                                                              &
                     & ) call Galacticus_Error_Report                                                                                       &
                     &        (                                                                                                             &
                     &         'this method requires that boundMassHistory property must be gettable for the satellite component.'       // &
                     &         Galacticus_Component_List(                                                                                   &
                     &                                   'satellite'                                                                     ,  &
                     &                                   defaultSatelliteComponent%boundMassHistoryAttributeMatch(requireGettable=.true.)   &
                     &                                  )                                                                                // &
                     &         {introspection:location}                                                                                     &
                     &        )
             case (statusOrphanDiscriminatorPosition )
                if     (                                                                                                                    &
                     &  .not.                                                                                                               &
                     &       (                                                                                                              &
                     &        defaultPositionComponent %  positionHistoryIsGettable()                                                       &
                     &       )                                                                                                              &
                     & ) call Galacticus_Error_Report                                                                                       &
                     &        (                                                                                                             &
                     &         'this method requires that positionHistory property must be gettable for the position component.'         // &
                     &         Galacticus_Component_List(                                                                                   &
                     &                                   'position'                                                                      ,  &
                     &                                   defaultPositionComponent%positionHistoryAttributeMatch  (requireGettable=.true.)   &
                     &                                  )                                                                                // &
                     &         {introspection:location}                                                                                     &
                     &        )
             end select
           end if
          ! Flag that module is now initialized.
          outputSatelliteStatusInitialized=.true.
       end if
       !$omp end critical(Galacticus_Output_Tree_Satellite_Status_Initialize)
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Status_Initialize

  !# <mergerTreeOutputNames>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Status_Names</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Status</sortName>
  !# </mergerTreeOutputNames>
  subroutine Galacticus_Output_Tree_Satellite_Status_Names(node,integerProperty,integerPropertyNames,integerPropertyComments,integerPropertyUnitsSI,doubleProperty&
       &,doublePropertyNames,doublePropertyComments,doublePropertyUnitsSI,time)
    !% Set the names of satellite orbital extremum properties to be written to the \glc\ output file.
    use Galacticus_Nodes
    use Numerical_Constants_Astronomical
    implicit none
    type            (treeNode)              , intent(inout) :: node
    double precision                        , intent(in   ) :: time
    integer                                 , intent(inout) :: doubleProperty         , integerProperty
    character       (len=*   ), dimension(:), intent(inout) :: doublePropertyComments , doublePropertyNames   , &
         &                                                     integerPropertyComments, integerPropertyNames
    double precision          , dimension(:), intent(inout) :: doublePropertyUnitsSI  , integerPropertyUnitsSI
    !GCC$ attributes unused :: node, time, doubleProperty, doublePropertyNames, doublePropertyComments, doublePropertyUnitsSI
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Status_Initialize

    ! Return property names if we are outputting satellite host data.
    if (outputSatelliteStatus) then
       integerProperty=integerProperty+1
       integerPropertyNames   (integerProperty)='satelliteStatus'
       integerPropertyComments(integerProperty)="Satellite status flag (0=not a satellite; 1=satellite with halo; 2=orphaned satellite)."
       integerPropertyUnitsSI (integerProperty)=0.0d0
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Status_Names

  !# <mergerTreeOutputPropertyCount>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Status_Property_Count</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Status</sortName>
  !# </mergerTreeOutputPropertyCount>
  subroutine Galacticus_Output_Tree_Satellite_Status_Property_Count(node,integerPropertyCount,doublePropertyCount,time)
    !% Account for the number of satellite host properties to be written to the \glc\ output file.
    use Galacticus_Nodes
    implicit none
    type            (treeNode), intent(inout) :: node
    double precision          , intent(in   ) :: time
    integer                   , intent(inout) :: doublePropertyCount, integerPropertyCount
    !GCC$ attributes unused :: node, time, doublePropertyCount
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Status_Initialize

    ! Increment property count.
    integerPropertyCount=integerPropertyCount+satelliteStatusPropertyCount
    return
  end subroutine Galacticus_Output_Tree_Satellite_Status_Property_Count

  !# <mergerTreeOutputTask>
  !#  <unitName>Galacticus_Output_Tree_Satellite_Status</unitName>
  !#  <sortName>Galacticus_Output_Tree_Satellite_Status</sortName>
  !# </mergerTreeOutputTask>
  subroutine Galacticus_Output_Tree_Satellite_Status(node,integerProperty,integerBufferCount,integerBuffer,doubleProperty&
       &,doubleBufferCount,doubleBuffer,time,instance)
    !% Store satellite host halo properties in the \glc\ output file buffers.
    use Galacticus_Nodes
    use Kind_Numbers
    use Histories
    use Multi_Counters
    implicit none
    double precision                        , intent(in   ) :: time
    type            (treeNode              ), intent(inout) :: node
    integer                                 , intent(inout) :: doubleBufferCount        , doubleProperty , integerBufferCount, &
         &                                                     integerProperty
    integer         (kind=kind_int8        ), intent(inout) :: integerBuffer       (:,:)
    double precision                        , intent(inout) :: doubleBuffer        (:,:)
    type            (multiCounter          ), intent(inout) :: instance
    class           (nodeComponentBasic    ), pointer       :: basic
    class           (nodeComponentSatellite), pointer       :: satellite
    class           (nodeComponentPosition ), pointer       :: position
    type            (history               )                :: discriminatorHistory
    integer         (kind=kind_int8        )                :: status
    !GCC$ attributes unused :: time, doubleBufferCount, doubleProperty, doubleBuffer, instance
    
    ! Initialize the module.
    call Galacticus_Output_Tree_Satellite_Status_Initialize

    ! Store property data if we are outputting satellite orbital pericenter data.
    if (outputSatelliteStatus) then
       ! Test for satellite.
       if (node%isSatellite()) then
          ! Is a satellite. Determine if we have halo information or not.
          basic => node%basic()
          select case (statusOrphanDiscriminator)
          case (statusOrphanDiscriminatorBoundMass)
             satellite            => node     %satellite       ()
             discriminatorHistory =  satellite%boundMassHistory()
          case (statusOrphanDiscriminatorPosition )
             position             => node     %position        ()
             discriminatorHistory =  position %positionHistory ()
          end select
          if (discriminatorHistory%exists()) then
             if (basic%time() > discriminatorHistory%time(size(discriminatorHistory%time))) then
                status=2
             else
                status=1
             end if
          else
             status=2
          end if
       else
          ! Not a satellite.
          status=0
       end if
       ! Store the orbital properties.
       integerProperty=integerProperty+1
       integerBuffer(integerBufferCount,integerProperty)=status
    end if
    return
  end subroutine Galacticus_Output_Tree_Satellite_Status

end module Galacticus_Output_Trees_Satellite_Status
