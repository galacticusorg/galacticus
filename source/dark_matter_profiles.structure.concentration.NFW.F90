!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% An implementation of dark matter halo profile concentrations using the \cite{navarro_structure_1996} algorithm.

  use Cosmology_Parameters
  use Cosmology_Functions
  use Cosmological_Density_Field
  use Virial_Density_Contrast   , only : virialDensityContrastClass, virialDensityContrast

  !# <darkMatterProfileConcentration name="darkMatterProfileConcentrationNFW1996">
  !#  <description>Dark matter halo concentrations are computed using the algorithm of \cite{navarro_structure_1996}.</description>
  !# </darkMatterProfileConcentration>
  type, extends(darkMatterProfileConcentrationClass) :: darkMatterProfileConcentrationNFW1996
     !% A dark matter halo profile concentration class implementing the algorithm of \cite{navarro_structure_1996}.
     private
     class           (cosmologyParametersClass     ), pointer :: cosmologyParameters_             => null()
     class           (cosmologyFunctionsClass      ), pointer :: cosmologyFunctions_              => null()
     class           (criticalOverdensityClass     ), pointer :: criticalOverdensity_             => null()
     class           (cosmologicalMassVarianceClass), pointer :: cosmologicalMassVariance_        => null()
     class           (virialDensityContrastClass   ), pointer :: virialDensityContrast_           => null()
     type            (virialDensityContrastFixed   ), pointer :: virialDensityContrastDefinition_ => null()
     type            (darkMatterProfileNFW         ), pointer :: darkMatterProfileDefinition_     => null()
     double precision                                         :: f                                         , C
   contains
     final     ::                                nfw1996Destructor
     procedure :: concentration               => nfw1996Concentration
     procedure :: densityContrastDefinition   => nfw1996DensityContrastDefinition
     procedure :: darkMatterProfileDefinition => nfw1996DarkMatterProfileDefinition
  end type darkMatterProfileConcentrationNFW1996
  
  interface darkMatterProfileConcentrationNFW1996
     !% Constructors for the {\normalfont \ttfamily nfw1996} dark matter halo profile concentration class.
     module procedure nfw1996ConstructorParameters
     module procedure nfw1996ConstructorInternal
  end interface darkMatterProfileConcentrationNFW1996

  ! Target value used in concentration root finder.
  double precision :: nfw1996RootTarget
  !$omp threadprivate(nfw1996RootTarget)
  
contains

  function nfw1996ConstructorParameters(parameters) result(self)
    !% Default constructor for the {\normalfont \ttfamily nfw1996} dark matter halo profile
    !% concentration class.
    use Input_Parameters
    implicit none
    type            (darkMatterProfileConcentrationNFW1996)                :: self
    type            (inputParameters                      ), intent(inout) :: parameters
    class           (cosmologyParametersClass             ), pointer       :: cosmologyParameters_
    class           (cosmologyFunctionsClass              ), pointer       :: cosmologyFunctions_
    class           (criticalOverdensityClass             ), pointer       :: criticalOverdensity_     
    class           (cosmologicalMassVarianceClass        ), pointer       :: cosmologicalMassVariance_
    class           (virialDensityContrastClass           ), pointer       :: virialDensityContrast_
    double precision                                                       :: f                        , C

    ! Check and read parameters.
    !# <inputParameter>
    !#   <name>f</name>
    !#   <source>parameters</source>
    !#   <variable>f</variable>
    !#   <defaultValue>0.01d0</defaultValue>
    !#   <defaultSource>\cite{navarro_structure_1996}</defaultSource>
    !#   <description>The parameter $f$ appearing in the halo concentration algorithm of \cite{navarro_structure_1996}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>C</name>
    !#   <source>parameters</source>
    !#   <variable>C</variable>
    !#   <defaultValue>2000.0d0</defaultValue>
    !#   <defaultSource>\cite{navarro_structure_1996}</defaultSource>
    !#   <description>The parameter $C$ appearing in the halo concentration algorithm of \cite{navarro_structure_1996}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyParameters"      name="cosmologyParameters_"      source="parameters"/>
    !# <objectBuilder class="cosmologyFunctions"       name="cosmologyFunctions_"       source="parameters"/>
    !# <objectBuilder class="criticalOverdensity"      name="criticalOverdensity_"      source="parameters"/>
    !# <objectBuilder class="cosmologicalMassVariance" name="cosmologicalMassVariance_" source="parameters"/>
    !# <objectBuilder class="virialDensityContrast"    name="virialDensityContrast_"    source="parameters"/>
    self=darkMatterProfileConcentrationNFW1996(f,C,cosmologyParameters_,cosmologyFunctions_,criticalOverdensity_,cosmologicalMassVariance_,virialDensityContrast_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="cosmologyParameters_"     />
    !# <objectDestructor name="cosmologyFunctions_"      />
    !# <objectDestructor name="criticalOverdensity_"     />
    !# <objectDestructor name="cosmologicalMassVariance_"/>
    !# <objectDestructor name="virialDensityContrast_"   />
    return
  end function nfw1996ConstructorParameters

  function nfw1996ConstructorInternal(f,C,cosmologyParameters_,cosmologyFunctions_,criticalOverdensity_,cosmologicalMassVariance_,virialDensityContrast_) result(self)
    !% Constructor for the {\normalfont \ttfamily nfw1996} dark matter halo profile
    !% concentration class.
    implicit none
    type            (darkMatterProfileConcentrationNFW1996)                        :: self
    double precision                                       , intent(in   )         :: f                         , C
    class           (cosmologyParametersClass             ), intent(in   ), target :: cosmologyParameters_
    class           (cosmologyFunctionsClass              ), intent(in   ), target :: cosmologyFunctions_
    class           (criticalOverdensityClass             ), intent(in   ), target :: criticalOverdensity_     
    class           (cosmologicalMassVarianceClass        ), intent(in   ), target :: cosmologicalMassVariance_
    class           (virialDensityContrastClass           ), intent(in   ), target :: virialDensityContrast_
    !# <constructorAssign variables="f, C, *cosmologyParameters_, *cosmologyFunctions_, *criticalOverdensity_, *cosmologicalMassVariance_, *virialDensityContrast_"/>

    return
  end function nfw1996ConstructorInternal

  subroutine nfw1996Destructor(self)
    !% Destructor for the {\normalfont \ttfamily nfw1996} dark matter halo profile concentration class.
    implicit none
    type(darkMatterProfileConcentrationNFW1996), intent(inout) :: self
    
    !# <objectDestructor name="self%cosmologyParameters_"            />
    !# <objectDestructor name="self%cosmologyFunctions_"             />
    !# <objectDestructor name="self%criticalOverdensity_"            />
    !# <objectDestructor name="self%cosmologicalMassVariance_"       />
    !# <objectDestructor name="self%virialDensityContrast_"          />
    !# <objectDestructor name="self%virialDensityContrastDefinition_"/>
    !# <objectDestructor name="self%darkMatterProfileDefinition_"    />
    return
  end subroutine nfw1996Destructor

  double precision function nfw1996Concentration(self,node)
    !% Return the concentration of the dark matter halo profile of {\normalfont \ttfamily node}
    !% using the \cite{navarro_structure_1996} algorithm.
    use Root_Finder
    use Virial_Density_Contrast
    use Galacticus_Nodes       , only : nodeComponentBasic
    implicit none
    class           (darkMatterProfileConcentrationNFW1996), intent(inout), target  :: self
    type            (treeNode                             ), intent(inout), target  :: node
    double precision                                       , parameter              :: fitParameterNuHalf         =0.47693628d0
    double precision                                       , parameter              :: toleranceAbsolute          =0.0d0       , toleranceRelative      =1.0d-6
    class           (nodeComponentBasic                   )               , pointer :: basic
    type            (rootFinder                           ), save                   :: finder
    !$omp threadprivate(finder)
    double precision                                                                :: collapseCriticalOverdensity             , collapseExpansionFactor       , &
         &                                                                             collapseMass                            , collapseOverdensity           , &
         &                                                                             collapseTime                            , expansionFactor               , &
         &                                                                             nodeMass                                , nodeTime

    ! Get the basic component.
    basic                      => node                     %basic          (        )
    ! Get the properties of the node.
    nodeMass                   =  basic                    %mass           (        )
    nodeTime                   =  basic                    %time           (        )
    expansionFactor            =  self %cosmologyFunctions_%expansionFactor(nodeTime)
    ! Compute the mass of a progenitor as defined by NFW.
    collapseMass               =self%F*nodeMass
    ! Find the time of collapse for this progenitor.
    collapseCriticalOverdensity=sqrt(2.0d0*fitParameterNuHalf**2*(self%cosmologicalMassVariance_%rootVariance(collapseMass)**2-self%cosmologicalMassVariance_%rootVariance(nodeMass)**2))+self%criticalOverdensity_%value(time=nodeTime,mass=nodeMass)
    collapseTime               =self%criticalOverdensity_%timeOfCollapse (collapseCriticalOverdensity,mass=nodeMass)
    collapseExpansionFactor    =self%cosmologyFunctions_ %expansionFactor(collapseTime                             )
    ! Compute the overdensity of the progenitor at collapse using the scaling given by NFW.
    collapseOverdensity        =self%C*(expansionFactor/collapseExpansionFactor)**3
    ! Find the ratio of this overdensity to that at for the present node.
    nfw1996RootTarget          =collapseOverdensity/self%virialDensityContrast_%densityContrast(nodeMass,nodeTime)
    ! Initialize our root finder.
    if (.not.finder%isInitialized()) then
       call finder%rangeExpand (                                               &
            &                   rangeExpandDownward=0.5d0                    , &
            &                   rangeExpandUpward  =2.0d0                    , &
            &                   rangeExpandType    =rangeExpandMultiplicative  &
            &                  )
       call finder%rootFunction(nfw1996ConcentrationRoot           )
       call finder%tolerance   (toleranceAbsolute,toleranceRelative)
    end if
    ! Find the concentration.
    nfw1996Concentration=finder%find(rootRange=[1.0d0,20.0d0])
    return
  end function nfw1996Concentration

  double precision function nfw1996ConcentrationRoot(concentration)
    !% Root function used in finding concentrations in the \cite{navarro_structure_1996} method.
    implicit none
    double precision, intent(in   ) :: concentration
    
    nfw1996ConcentrationRoot=concentration**3/(log(1.0d0+concentration)-concentration/(1.0d0+concentration))/3.0d0-nfw1996RootTarget
    return
  end function nfw1996ConcentrationRoot
    
  function nfw1996DensityContrastDefinition(self)
    !% Return a virial density contrast object defining that used in the definition of
    !% concentration in the \cite{navarro_structure_1996} algorithm.
    use Virial_Density_Contrast, only : virialDensityContrastFixed
    implicit none
    class(virialDensityContrastClass           ), pointer       :: nfw1996DensityContrastDefinition
    class(darkMatterProfileConcentrationNfw1996), intent(inout) :: self
    
    if (.not.associated(self%virialDensityContrastDefinition_)) then
       allocate(self%virialDensityContrastDefinition_)
       !# <referenceConstruct owner="self" object="virialDensityContrastDefinition_" constructor="virialDensityContrastFixed(200.0d0,fixedDensityTypeCritical,self%cosmologyFunctions_)"/>
    end if
    nfw1996DensityContrastDefinition => self%virialDensityContrastDefinition_
    return
  end function nfw1996DensityContrastDefinition
  
  function nfw1996DarkMatterProfileDefinition(self)
    !% Return a dark matter density profile object defining that used in the definition of concentration in the
    !% \cite{navarro_structure_1996} algorithm.
    use Dark_Matter_Halo_Scales
    implicit none
    class(darkMatterProfileClass                            ), pointer       :: nfw1996DarkMatterProfileDefinition
    class(darkMatterProfileConcentrationNFW1996             ), intent(inout) :: self
    type (darkMatterHaloScaleVirialDensityContrastDefinition), pointer       :: darkMatterHaloScaleDefinition_
    class(virialDensityContrastClass                        ), pointer       :: virialDensityContrastDefinition_

    if (.not.associated(self%darkMatterProfileDefinition_)) then
       allocate(self%darkMatterProfileDefinition_  )
       allocate(     darkMatterHaloScaleDefinition_)
       !# <referenceAcquire                target="virialDensityContrastDefinition_" source     ="self%densityContrastDefinition()"/>
       !# <referenceConstruct              object="darkMatterHaloScaleDefinition_"   constructor="darkMatterHaloScaleVirialDensityContrastDefinition(self%cosmologyParameters_,self%cosmologyFunctions_,virialDensityContrastDefinition_)"/>
       !# <referenceConstruct owner="self" object="darkMatterProfileDefinition_"     constructor="darkMatterProfileNFW                              (darkMatterHaloScaleDefinition_                                                     )"/>
       !# <objectDestructor name="darkMatterHaloScaleDefinition_"  />
       !# <objectDestructor name="virialDensityContrastDefinition_"/>
    end if
    nfw1996DarkMatterProfileDefinition => self%darkMatterProfileDefinition_
    return
  end function nfw1996DarkMatterProfileDefinition

