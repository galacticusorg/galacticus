!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which handles evolving the spectrum of background radiation.

module Radiation_Intergalactic_Background_Internal
  !% Handles evolving the spectrum of background radiation.
  use Cosmology_Functions
  use Cosmology_Parameters
  use Intergalactic_Medium_State
  use Abundances_Structure
  use FGSL
  private
  public :: Radiation_Intergalactic_Background_Internal_Initialize, Radiation_IGB_Internal_Initialize
  
  logical                                                                      :: backgroundRadiationCompute
  integer                                                                      :: backgroundRadiationWavelengthCountPerDecade, backgroundRadiationWavelengthCount
  double precision                                                             :: backgroundRadiationWavelengthMinimum       , backgroundRadiationWavelengthMaximum
  integer                                                                      :: backgroundRadiationTimeCountPerDecade      , backgroundRadiationTimeCount
  double precision                                                             :: backgroundRadiationRedshiftMinimum         , backgroundRadiationRedshiftMaximum
  double precision                                                             :: backgroundRadiationTimeMinimum             , backgroundRadiationTimeMaximum
  double precision                               , allocatable, dimension(:  ) :: backgroundRadiationWavelength              , backgroundRadiationSpectrum         , &
       &                                                                          backgroundRadiationTime                    , crossSectionNeutralHydrogen         , &
       &                                                                          crossSectionNeutralHelium                  , crossSectionSinglyIonizedHelium     , &
       &                                                                          backgroundRadiationRedshift
  double precision                               , allocatable, dimension(:,:) :: backgroundRadiationEmissivity              , emissivity                          , &
       &                                                                          backgroundRadiationFlux
  double precision                                            , dimension(0:1) :: emissivityTime
  double precision                                                             :: backgroundTimePrevious                     , backgroundTimeNext

  ! Classes used in ODE solution.
  class           (cosmologyParametersClass     ), pointer                     :: cosmologyParameters_
  class           (cosmologyFunctionsClass      ), pointer                     :: cosmologyFunctions_
  class           (intergalacticMediumStateClass), pointer                     :: intergalacticMediumState_

contains
  
  !# <universePreEvolveTask>
  !#  <unitName>Radiation_Intergalactic_Background_Internal_Initialize</unitName>
  !# </universePreEvolveTask>
  subroutine Radiation_Intergalactic_Background_Internal_Initialize(universe_)
    !% Attach an initial event to the universe to cause the background radiation update function to be called.
    use Galacticus_Nodes
    use Input_Parameters
    use Memory_Management
    use Numerical_Ranges
    use Cosmology_Functions
    use Atomic_Cross_Sections_Ionization_Photo
    implicit none
    type   (universe                              ), intent(inout) :: universe_
    type   (universeEvent                         ), pointer       :: event
    class  (cosmologyFunctionsClass               ), pointer       :: cosmologyFunctions_
    class  (atomicCrossSectionIonizationPhotoClass), pointer       :: atomicCrossSectionIonizationPhoto_
    integer                                                        :: iWavelength                       , iTime

    ! Get parameter controlling background radiation spectral and time resolution.
    !# <inputParameter>
    !#   <name>backgroundRadiationCompute</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>Specifies whether or not cosmic background radiation should be computed.</description>
    !#   <source>globalParameters</source>
    !#   <type>integer</type>
    !# </inputParameter>
    if (backgroundRadiationCompute) then
       !# <inputParameter>
       !#   <name>backgroundRadiationWavelengthCountPerDecade</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>10</defaultValue>
       !#   <description>The number of bins per decade of wavelength to use for calculations of the cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>integer</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>backgroundRadiationWavelengthMinimum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>100.0d0</defaultValue>
       !#   <description>The minimum wavelength (in units of \AA) to use in calculations of the cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>backgroundRadiationWavelengthMaximum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>100000.0d0</defaultValue>
       !#   <description>The maximum wavelength (in units of \AA) to use in calculations of the cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>backgroundRadiationTimeCountPerDecade</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>10</defaultValue>
       !#   <description>The number of bins per decade of time to use for calculations of tge cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>integer</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>backgroundRadiationRedshiftMinimum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.0d0</defaultValue>
       !#   <description>The minimum redshift to use in calculations of the cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>backgroundRadiationRedshiftMaximum</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>30.0d0</defaultValue>
       !#   <description>The maximum redshift to use in calculations of the cosmic background radiation.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       ! Build tables of wavelength and time for cosmic background radiation.
       cosmologyFunctions_                => cosmologyFunctions               ()
       atomicCrossSectionIonizationPhoto_ => atomicCrossSectionIonizationPhoto()
       backgroundRadiationTimeMaximum                                                              &
            & =cosmologyFunctions_%cosmicTime                 (                                    &
            &  cosmologyFunctions_%expansionFactorFromRedshift (                                   &
            &                                                   backgroundRadiationRedshiftMinimum &
            &                                                  )                                   &
            &                                                 )
       backgroundRadiationTimeMinimum                                                              &
            & =cosmologyFunctions_%cosmicTime                 (                                    &
            &  cosmologyFunctions_%expansionFactorFromRedshift (                                   &
            &                                                   backgroundRadiationRedshiftMaximum &
            &                                                  )                                   &
            &                                                 )
       backgroundRadiationWavelengthCount                              &
            & = int(                                                   &
            &        dble(backgroundRadiationWavelengthCountPerDecade) &
            &       *log10(                                            &
            &               backgroundRadiationWavelengthMaximum       &
            &              /backgroundRadiationWavelengthMinimum       &
            &             )                                            &
            &      )                                                   &
            &  +1
       backgroundRadiationTimeCount                                    &
            & = int(                                                   &
            &        dble(backgroundRadiationTimeCountPerDecade      ) &
            &       *log10(                                            &
            &               backgroundRadiationTimeMaximum             &
            &              /backgroundRadiationTimeMinimum             &
            &             )                                            &
            &      )                                                   &
            &  +1
       call allocateArray(backgroundRadiationWavelength,[backgroundRadiationWavelengthCount                             ]                  )
       call allocateArray(backgroundRadiationSpectrum  ,[backgroundRadiationWavelengthCount                             ]                  )
       call allocateArray(backgroundRadiationTime      ,[                                   backgroundRadiationTimeCount]                  )
       call allocateArray(backgroundRadiationRedshift  ,[                                   backgroundRadiationTimeCount]                  )
       call allocateArray(backgroundRadiationEmissivity,[backgroundRadiationWavelengthCount,backgroundRadiationTimeCount]                  )
       call allocateArray(backgroundRadiationFlux      ,[backgroundRadiationWavelengthCount,backgroundRadiationTimeCount]                  )
       call allocateArray(emissivity                   ,[backgroundRadiationWavelengthCount,2                           ],lowerBounds=[1,0])
       backgroundRadiationWavelength                            &
            & =Make_Range(                                      &
            &             backgroundRadiationWavelengthMinimum, &
            &             backgroundRadiationWavelengthMaximum, &
            &             backgroundRadiationWavelengthCount  , &
            &             rangeTypeLogarithmic                  &
            &            )
       backgroundRadiationTime                                  &
            & =Make_Range(                                      &
            &             backgroundRadiationTimeMinimum      , &
            &             backgroundRadiationTimeMaximum      , &
            &             backgroundRadiationTimeCount        , &
            &             rangeTypeLogarithmic                  &
            &            )
       backgroundRadiationFlux=0.0d0
       ! Convert times to redshifts.
       do iTime=1,backgroundRadiationTimeCount
          backgroundRadiationRedshift(iTime)                                                       &
               & =cosmologyFunctions_ %redshiftFromExpansionFactor(                                &
               &   cosmologyFunctions_%expansionFactor             (                               &
               &                                                    backgroundRadiationTime(iTime) &
               &                                                   )                               &
               &                                                  )
       end do
       ! Initialize the background radiation to zero.
       backgroundRadiationSpectrum  =0.0d0
       ! Initialize the emissivity to zero.
       backgroundRadiationEmissivity=0.0d0
       ! Construct tables of photoionization cross-sections.
       call allocateArray(crossSectionNeutralHydrogen    ,[backgroundRadiationWavelengthCount])
       call allocateArray(crossSectionNeutralHelium      ,[backgroundRadiationWavelengthCount])
       call allocateArray(crossSectionSinglyIonizedHelium,[backgroundRadiationWavelengthCount])
       do iWavelength=1,backgroundRadiationWavelengthCount
          crossSectionNeutralHydrogen    (iWavelength)=atomicCrossSectionIonizationPhoto_%crossSection(1,1,1,backgroundRadiationWavelength(iWavelength))
          crossSectionNeutralHelium      (iWavelength)=atomicCrossSectionIonizationPhoto_%crossSection(2,1,1,backgroundRadiationWavelength(iWavelength))
          crossSectionSinglyIonizedHelium(iWavelength)=atomicCrossSectionIonizationPhoto_%crossSection(2,2,1,backgroundRadiationWavelength(iWavelength))
       end do
       ! Create the first interrupt event in the universe object.
       backgroundTimePrevious=  0.0d0
       backgroundTimeNext    =  backgroundRadiationTime (1)
       event             => universe_%createEvent( )
       event%time        =  backgroundRadiationTime (1)
       event%task        => Radiation_Intergalactic_Background_Internal_Update
    end if
    return
  end subroutine Radiation_Intergalactic_Background_Internal_Initialize
  
  logical function Radiation_Intergalactic_Background_Internal_Update(event,universe_) result (success)
    !% Update the radiation background for a given universe.
    use               Kind_Numbers
    use               Galacticus_Output_Times
    use               Galacticus_Nodes
    use               Galacticus_Display
    use               Star_Formation_IMF
    use               Galactic_Structure_Options
    use               Galacticus_Error
    use               Stellar_Population_Spectra
    use               Accretion_Disk_Spectra
    use               Arrays_Search
    use               FODEIV2
    use               ODEIV2_Solver
    use, intrinsic :: ISO_C_Binding
    use               Numerical_Constants_Prefixes
    use               Numerical_Constants_Math
    use               Numerical_Constants_Physical
    use               Numerical_Constants_Units
    use               Numerical_Integration
    use               ISO_Varying_String
    use               Galacticus_HDF5
    use               IO_HDF5
    implicit none
    class           (universeEvent                ), intent(in   ) :: event
    type            (universe                     ), intent(inout) :: universe_
    type            (mergerTree                   ), pointer       :: tree       
    type            (mergerTreeList               ), pointer       :: forest       
    type            (treeNode                     ), pointer       :: node
    class           (nodeComponentBasic           ), pointer       :: basic
    class           (nodeComponentDisk            ), pointer       :: disk
    class           (nodeComponentSpheroid        ), pointer       :: spheroid
    type            (universeEvent                ), pointer       :: eventNew
    class           (accretionDiskSpectraClass    ), pointer       :: accretionDiskSpectra_
    class           (stellarPopulationSpectraClass), pointer       :: stellarPopulationSpectra_
    double precision                               , parameter     :: odeToleranceAbsolute        =1.0d-30, odeToleranceRelative        =1.0d-3
    double precision                               , parameter     :: integrationToleranceAbsolute=1.0d-30, integrationToleranceRelative=1.0d-3
    type            (abundances                   ), target        :: gasAbundancesDisk                   , gasAbundancesSpheroid
    type            (abundances                   ), pointer       :: gasAbundances
    type            (fodeiv2_system               ), save          :: ode2System
    type            (fodeiv2_driver               ), save          :: ode2Driver
    type            (fgsl_function                ), save          :: integrandFunction
    type            (fgsl_integration_workspace   ), save          :: integrationWorkspace
    logical                                        , save          :: odeReset                            , integrationReset            =.true.
    double precision                                               :: starFormationRateDisk               , starFormationRateSpheroid          , &
         &                                                            gasMassDisk                         , gasMassSpheroid                    , &
         &                                                            ageEnd                              , ageStart                           , &
         &                                                            stellarSpectrumDisk                 , stellarSpectrumSpheroid            , & 
         &                                                            timeStart                           , timeEnd                            , &
         &                                                            treeTimeLatest                      , wavelength
    integer                                                        :: imfIndexDisk                        , imfIndexSpheroid                   , &
         &                                                            iTime                               , iWavelength                        , &
         &                                                            imfIndex
    type            (varying_string               )                :: message
    character       (len=6                        )                :: label
    type            (hdf5Object                   )                :: backgroundRadiationGroup            , backgroundRadiationDataset
    integer         (c_size_t                     )                :: iNow
    logical                                                        :: firstTime

    ! Display message.
    write (label,'(f6.3)') event%time
    message="Evolving cosmic background radiation to time "//trim(label)//" Gyr"
    call Galacticus_Display_Indent(message)
    ! Find the current timestep.
    iNow=Search_Array_For_Closest(backgroundRadiationTime,event%time)
    ! Get required objects.
    accretionDiskSpectra_     => accretionDiskSpectra    ()
    stellarPopulationSpectra_ => stellarPopulationSpectra()
    ! Iterate over all nodes.
    call Galacticus_Display_Message('Accumulating emissivity')
    treeTimeLatest=0.0d0
    forest => universe_%trees
    do while (associated(forest))
       tree => forest%tree
       do while (associated(tree))
          node => tree%baseNode
          do while (associated(node))
             basic => node%basic()
             treeTimeLatest=max(treeTimeLatest,basic%time())
             if (basic%time() == event%time) then
                ! Get the star formation rates and metallicites for this node.
                disk                  => node    %disk             ()
                spheroid              => node    %spheroid         ()
                starFormationRateDisk     =  disk    %starFormationRate()
                starFormationRateSpheroid =  spheroid%starFormationRate()
                gasMassDisk               =  disk    %massGas          ()
                gasMassSpheroid           =  spheroid%massGas          ()
                gasAbundancesDisk         =  disk    %abundancesGas    ()
                gasAbundancesSpheroid     =  spheroid%abundancesGas    ()
                if (starFormationRateDisk     > 0.0d0) gasAbundancesDisk    =gasAbundancesDisk    /gasMassDisk
                if (starFormationRateSpheroid > 0.0d0) gasAbundancesSpheroid=gasAbundancesSpheroid/gasMassSpheroid
                if (starFormationRateDisk > 0.0d0 .or. starFormationRateSpheroid > 0.0d0) then
                   ! Find IMF indices for disk and spheroid.
                   imfIndexDisk    =IMF_Select(starFormationRateDisk    ,gasAbundancesDisk    ,componentTypeDisk    )
                   imfIndexSpheroid=IMF_Select(starFormationRateSpheroid,gasAbundancesSpheroid,componentTypeSpheroid)
                   ! Find the duration of the current timestep.
                   ! Accumulate emissivity to each timestep.
                   firstTime=.true.
                   do iTime=1,backgroundRadiationTimeCount
                      ! Skip times in the past.
                      if (backgroundRadiationTime(iTime) < event%time) cycle
                      ! Compute age of the currently forming population at this time.
                      ageEnd=backgroundRadiationTime(iTime)-event%time
                      if (iTime == 1) then
                         ageStart=0.0d0
                      else
                         ageStart=max(backgroundRadiationTime(iTime-1)-event%time,0.0d0)
                      end if
                      ! Iterate over wavelength
                      do iWavelength=1,backgroundRadiationWavelengthCount                         
                         wavelength              =  backgroundRadiationWavelength(iWavelength)
                         imfIndex                =  imfIndexDisk
                         gasAbundances           => gasAbundancesDisk
                         integrationReset=.true.
                         stellarSpectrumDisk     =  Integrate(                                                        &
                              &                               ageStart                                              , &
                              &                               ageEnd                                                , &
                              &                               stellarSpectraConvolution                             , &
                              &                               integrandFunction                                     , &
                              &                               integrationWorkspace                                  , &
                              &                               toleranceAbsolute        =integrationToleranceAbsolute, &
                              &                               toleranceRelative        =integrationToleranceRelative, &
                              &                               reset                    =integrationReset              &
                              &                              )
                         call Integrate_Done(integrandFunction,integrationWorkspace)
                         gasAbundances           => gasAbundancesSpheroid
                         integrationReset=.true.
                         stellarSpectrumSpheroid =  Integrate(                                                        &
                              &                               ageStart                                              , &
                              &                               ageEnd                                                , &
                              &                               stellarSpectraConvolution                             , &
                              &                               integrandFunction                                     , &
                              &                               integrationWorkspace                                  , &
                              &                               toleranceAbsolute        =integrationToleranceAbsolute, &
                              &                               toleranceRelative        =integrationToleranceRelative, &
                              &                               reset                    =integrationReset              &
                              &                              )
                         call Integrate_Done(integrandFunction,integrationWorkspace)
                         backgroundRadiationEmissivity        (iWavelength,iTime)          &
                              & =backgroundRadiationEmissivity(iWavelength,iTime)          &
                              & +(                                                         &
                              &   +stellarSpectrumDisk                                     &
                              &   *starFormationRateDisk                                   &
                              &   +stellarSpectrumSpheroid                                 &
                              &   *starFormationRateSpheroid                               &
                              &  )                                                         &
                              & *tree%volumeWeight
                         ! Add AGN emission. This accumulates only to the the current time.
                         if (firstTime)                                                       &
                              & backgroundRadiationEmissivity  (iWavelength,iTime)            &
                              &  =backgroundRadiationEmissivity(iWavelength,iTime)            &
                              &  +accretionDiskSpectra_        %spectrum(node,wavelength) &
                              &  *tree%volumeWeight
                      end do
                      firstTime=.false.
                   end do
                end if
             end if
             node => node%walkTreeWithSatellites()
          end do
          tree => tree%nextTree
       end do
       forest => forest%next
    end do
    ! Evolve the cosmic background radiation up to this timestep.
    if (iNow > 1) then
       call Galacticus_Display_Message('Solving cosmic background radiation evolution')
       emissivityTime(  0:1)=backgroundRadiationTime      (  iNow-1:iNow)
       emissivity    (:,0  )=backgroundRadiationEmissivity(:,iNow-1     )
       emissivity    (:,  1)=backgroundRadiationEmissivity(:,       iNow)
       cosmologyParameters_      => cosmologyParameters     ()
       cosmologyFunctions_       => cosmologyFunctions      ()
       intergalacticMediumState_ => intergalacticMediumState()
       odeReset=.true.
       timeStart=backgroundRadiationTime(iNow-1)
       timeEnd  =backgroundRadiationTime(iNow  )
       call ODEIV2_Solve(                                            &
            &            ode2Driver                                , &
            &            ode2System                                , &
            &            timeStart                                 , &
            &            timeEnd                                   , &
            &            backgroundRadiationWavelengthCount        , &
            &            backgroundRadiationSpectrum               , &
            &            backgroundRadiationODEs                   , &
            &            odeToleranceAbsolute                      , &
            &            odeToleranceRelative                      , &
            &            reset=odeReset                              &
            &           )
       call ODEIV2_Solver_Free(ode2Driver,ode2System)
       ! Convert
       backgroundRadiationFlux(:,iNow)                &
            & =max(                                   &
            &      +plancksConstant                   &
            &      *speedLight                   **2  &
            &      *angstromsPerMeter                 &
            &      /4.0d0                             &
            &      /Pi                                &
            &      /backgroundRadiationWavelength     &
            &      *backgroundRadiationSpectrum       &
            &      *centi                        **2  &
            &      /ergs                            , &
            &      +0.0d0                             &
            &     )
    end if
    ! Add the next event to the universe.
    backgroundTimeNext       =  backgroundRadiationTime(iNow+1)
    if     (                                                                                          &
         &                           iNow    <                        backgroundRadiationTimeCount    &
         &  .and.                                                                                     &
         &   backgroundRadiationTime(iNow+1) < treeTimeLatest                                         &
         &  .and.                                                                                     &
         &   backgroundRadiationTime(iNow+1) < Galacticus_Output_Time(Galacticus_Output_Time_Count()) &
         & ) then
       backgroundTimePrevious=  backgroundRadiationTime(iNow  )
       eventNew              => universe_%createEvent()
       eventNew%time         =  backgroundRadiationTime(iNow+1)
       eventNew%task         => Radiation_Intergalactic_Background_Internal_Update
    else
       ! Output the results to file.
       call hdf5Access%set()
       backgroundRadiationGroup=galacticusOutputFile%openGroup('backgroundRadiation','Cosmic background radiation data.')
       call backgroundRadiationGroup  %writeDataset  (backgroundRadiationWavelength,'wavelength','Wavelength at which the background radiation is tabulated [Å].',datasetReturned=backgroundRadiationDataset)
       call backgroundRadiationDataset%writeAttribute(1.0d0/angstromsPerMeter      ,'unitsInSI'                                                                                                             )
       call backgroundRadiationDataset%close()
       call backgroundRadiationGroup  %writeDataset  (backgroundRadiationRedshift  ,'redshift'  ,'Redshift at which the background radiation is tabulated [].'   ,datasetReturned=backgroundRadiationDataset)
       call backgroundRadiationDataset%writeAttribute(0.0d0                        ,'unitsInSI'                                                                                                             )
       call backgroundRadiationDataset%close()
       call backgroundRadiationGroup  %writeDataset  (backgroundRadiationFlux      ,'flux'  ,'Flux is the cosmic background radiation [erg cm⁻² s⁻¹ Hz⁻¹ sr⁻¹].' ,datasetReturned=backgroundRadiationDataset)
       call backgroundRadiationDataset%writeAttribute(ergs/centi**2                ,'unitsInSI'                                                                                                             )
       call backgroundRadiationDataset%close()
       call backgroundRadiationGroup  %close()
       call hdf5Access%unset()
    end if
    ! Display message.
    call Galacticus_Display_Unindent('done')
    ! Return true since we've performed our task.
    success=.true.
    return

  contains
    
    double precision function stellarSpectraConvolution(age)
      !% Integrand for convolution of stellar spectra.
      implicit none
      double precision, intent(in   ) :: age
      integer                         :: status

      stellarSpectraConvolution=stellarPopulationSpectra_%luminosity(               &
           &                                                         gasAbundances, &
           &                                                         age          , &
           &                                                         wavelength   , &
           &                                                         imfIndex     , &
           &                                                         status         &
           &                                                        )
      if     (                                                                              &
           &   status /= errorStatusSuccess                                                 &
           &  .and.                                                                         &
           &   status /= errorStatusInputDomain                                             &
           & ) call Galacticus_Error_Report(                                                &
           &                                'stellar population spectrum function failed'// &
           &                                {introspection:location}                        &
           &                               )
      return
    end function stellarSpectraConvolution

  end function Radiation_Intergalactic_Background_Internal_Update
    
  integer function backgroundRadiationODEs(time,spectrum,spectrumRateOfChange)
    !% Evaluates the ODEs controlling the evolution of cosmic background radiation.
    use ODE_Solver_Error_Codes
    use Numerical_Constants_Astronomical
    use Numerical_Constants_Physical
    use Numerical_Constants_Units
    use Numerical_Constants_Atomic
    implicit none
    double precision, intent(in   )               :: time
    double precision, intent(in   ), dimension(:) :: spectrum            
    double precision, intent(  out), dimension(:) :: spectrumRateOfChange
    double precision                              :: spectralGradient    (backgroundRadiationWavelengthCount)
    double precision                              :: expansionFactor

    ! Get the expansion factor.
    expansionFactor=cosmologyFunctions_%expansionFactor(time)
    ! Add source terms, linearly interpolating between timesteps. Convert from emissivity units [L☉ Hz⁻¹ Mpc⁻³] to
    ! background units [photons m⁻³ Hz⁻¹].
    spectrumRateOfChange(1:backgroundRadiationWavelengthCount) &
         & =(                                                  &
         &   +                     emissivity    (:,0)         &
         &   +(emissivity    (:,1)-emissivity    (:,0))        &
         &   *(time               -emissivityTime(  0))        &
         &   /(emissivityTime(  1)-emissivityTime(  0))        &
         &  )                                                  &
         & *backgroundRadiationWavelength                      &
         & *luminositySolar                                    &
         & *gigaYear                                           &
         & /angstromsPerMeter                                  &
         & /plancksConstant                                    &
         & /speedLight                                         &
         & /megaParsec**3
    ! Add expansion dilution: -3 H(t)      n_nu
    spectrumRateOfChange(1:backgroundRadiationWavelengthCount)=spectrumRateOfChange(1:backgroundRadiationWavelengthCount)-3.0d0*cosmologyFunctions_%expansionRate(expansionFactor)*spectrum(1:backgroundRadiationWavelengthCount)
    ! Add redshifting       : +  H(t) d(nu n_nu)/dnu
    spectralGradient(1                                   )=-spectrum(1)
    spectralGradient (2:backgroundRadiationWavelengthCount)                                                                                                                                                                                             &
         & =-                                               backgroundRadiationWavelength(2:backgroundRadiationWavelengthCount)**2                                                                                                                      &
         & *(spectrum(2:backgroundRadiationWavelengthCount)/backgroundRadiationWavelength(2:backgroundRadiationWavelengthCount)-spectrum(1:backgroundRadiationWavelengthCount-1)/backgroundRadiationWavelength(1:backgroundRadiationWavelengthCount-1)) &
         & /(                                               backgroundRadiationWavelength(2:backgroundRadiationWavelengthCount)-                                                 backgroundRadiationWavelength(1:backgroundRadiationWavelengthCount-1))
    spectrumRateOfChange(1:backgroundRadiationWavelengthCount)=spectrumRateOfChange(1:backgroundRadiationWavelengthCount)+spectralGradient*cosmologyFunctions_%expansionRate(expansionFactor)
    ! Absorption.
    where (spectrum(1:backgroundRadiationWavelengthCount) > 0.0d0)
       spectrumRateOfChange(1:backgroundRadiationWavelengthCount)                  &
            & =+spectrumRateOfChange(1:backgroundRadiationWavelengthCount)         &
            &  -gigaYear                                                           &
            &  *massSolar                                                          &
            &  /megaParsec                                                     **3 &
            &  *centi                                                          **2 &
            &  *speedLight                                                         &
            &  *(                                                                  &
            &    +crossSectionNeutralHydrogen                                      &
            &    *hydrogenByMassPrimordial                                         &
            &    *intergalacticMediumState_  %neutralHydrogenFraction    (time)    &
            &    /atomicMassHydrogen                                               &
            &    +crossSectionNeutralHelium                                        &
            &    *heliumByMassPrimordial                                           &
            &    *intergalacticMediumState_  %neutralHeliumFraction      (time)    &
            &    /atomicMassHelium                                                 &
            &    +crossSectionSinglyIonizedHelium                                  &
            &    *heliumByMassPrimordial                                           &
            &    *intergalacticMediumState_  %singlyIonizedHeliumFraction(time)    &
            &    /atomicMassHelium                                                 &
            &   )                                                                  &
            &  *cosmologyParameters_       %OmegaBaryon                  (    )    &
            &  *cosmologyParameters_       %densityCritical              (    )    &
            &  /cosmologyFunctions_        %expansionFactor              (time)**3 &
            &  /atomicMassUnit                                                     &
            &  *spectrum(1:backgroundRadiationWavelengthCount)
    end where
    ! Return success.
    backgroundRadiationODEs=FGSL_Success
    return
  end function backgroundRadiationODEs

  !# <radiationIntergalacticBackgroundMethod>
  !#  <unitName>Radiation_IGB_Internal_Initialize</unitName>
  !# </radiationIntergalacticBackgroundMethod>
  subroutine Radiation_IGB_Internal_Initialize(radiationIntergalacticBackgroundMethod,Radiation_Set_Intergalactic_Background_Do,Radiation_Flux_Intergalactic_Background_Do)
    !% Initialize the internally-computed intergalactic background radiation component module.
    implicit none
    type     (varying_string             ), intent(in   )          :: radiationIntergalacticBackgroundMethod
    procedure(Radiation_IGB_Internal_Set ), intent(inout), pointer :: Radiation_Set_Intergalactic_Background_Do
    procedure(Radiation_IGB_Internal_Flux), intent(inout), pointer :: Radiation_Flux_Intergalactic_Background_Do

    if (radiationIntergalacticBackgroundMethod == 'internal') then
       Radiation_Set_Intergalactic_Background_Do  => Radiation_IGB_Internal_Set
       Radiation_Flux_Intergalactic_Background_Do => Radiation_IGB_Internal_Flux
    end if
    return
  end subroutine Radiation_IGB_Internal_Initialize

  subroutine Radiation_IGB_Internal_Set(time,radiationProperties)
    !% Property setting routine for the internally-computed intergalactic background radiation component method.
    use Memory_Management
    implicit none
    double precision, intent(in   )                            :: time
    double precision, allocatable, dimension(:), intent(inout) :: radiationProperties
    
    ! Ensure that the properties array is allocated.
    if (.not.allocated(radiationProperties)) call allocateArray(radiationProperties,[1])
    ! Store the time for the radiation field.
    radiationProperties(1)=time
    return
  end subroutine Radiation_IGB_Internal_Set

  subroutine Radiation_IGB_Internal_Flux(radiationProperties,wavelength,radiationFlux)
    !% Flux method for the radiation component from file method.
    use, intrinsic :: ISO_C_Binding
    use FGSL
    use Numerical_Interpolation
    use Numerical_Constants_Astronomical
    use Numerical_Constants_Physical
    use Numerical_Constants_Units
    use Numerical_Constants_Atomic
    use Galacticus_Error
    implicit none
    double precision                                   , intent(in   ) :: wavelength
    double precision                   , dimension( : ), intent(in   ) :: radiationProperties
    double precision                                   , intent(inout) :: radiationFlux
    double precision                   , dimension(0:1)                :: hWavelength                     , hTime
    double precision                   , parameter                     :: timeTolerance=1.0d-3
    double precision                                                   :: time
    integer         (c_size_t         )                                :: iWavelength                     , jWavelength                        , &
         &                                                                iTime                           , jTime
    logical                            , save                          :: interpolationReset      =.true. , interpolationResetTime      =.true.
    type            (fgsl_interp_accel), save                          :: interpolationAccelerator        , interpolationAcceleratorTime
    !$omp threadprivate(interpolationReset,interpolationResetTime,interpolationAccelerator,interpolationAcceleratorTime)

    ! Check that the time is within the applicable range.
    time=radiationProperties(1)
    if (time > backgroundTimeNext*(1.0d0+timeTolerance)) call Galacticus_Error_Report('time is out of range'//{introspection:location})
    ! Find interpolation in the array of wavelengths.
    iWavelength=Interpolate_Locate                 (backgroundRadiationWavelength,interpolationAccelerator,wavelength,reset=interpolationReset)
    hWavelength=Interpolate_Linear_Generate_Factors(backgroundRadiationWavelength,iWavelength             ,wavelength)
    ! Find interpolation in array of times.
    iTime=Interpolate_Locate                 (backgroundRadiationTime,interpolationAcceleratorTime,time,reset=interpolationResetTime)
    hTime=Interpolate_Linear_Generate_Factors(backgroundRadiationTime,iTime                       ,time                             )
    if (time > backgroundTimePrevious) hTime=[1.0d0,0.0d0]
    ! Interpolate in wavelength and time.
    radiationFlux=0.0d0
    do jTime=0,1
       do jWavelength=0,1
          radiationFlux=radiationFlux+hTime(jTime)*hWavelength(jWavelength)*backgroundRadiationFlux(jWavelength+iWavelength,jTime+iTime)
       end do
    end do
    radiationFlux=max(radiationFlux,0.0d0)
    return
  end subroutine Radiation_IGB_Internal_Flux

end module Radiation_Intergalactic_Background_Internal
