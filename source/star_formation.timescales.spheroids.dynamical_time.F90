!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements a a dynamical time-based star formation timescale for galactic spheroids.

module Star_Formation_Timescale_Spheroids_Dynamical_Time
  !% Implements a a dynamical time-based star formation timescale for galactic spheroids.
  implicit none
  private
  public :: Star_Formation_Timescale_Spheroids_Dynamical_Time_Initialize

  ! Parameters of the timescale model.
  double precision :: starFormationSpheroidEfficiency      , starFormationSpheroidMinimumTimescale, &
       &              starFormationSpheroidVelocityExponent

contains

  !# <starFormationTimescaleSpheroidsMethod>
  !#  <unitName>Star_Formation_Timescale_Spheroids_Dynamical_Time_Initialize</unitName>
  !# </starFormationTimescaleSpheroidsMethod>
  subroutine Star_Formation_Timescale_Spheroids_Dynamical_Time_Initialize(starFormationTimescaleSpheroidsMethod&
       &,Star_Formation_Timescale_Spheroid_Get)
    !% Initializes the ``dynamical time'' spheroid star formation timescale module.
    use ISO_Varying_String
    use Input_Parameters
    implicit none
    type     (varying_string                                  ), intent(in   )          :: starFormationTimescaleSpheroidsMethod
    procedure(Star_Formation_Timescale_Spheroid_Dynamical_Time), intent(inout), pointer :: Star_Formation_Timescale_Spheroid_Get

    if (starFormationTimescaleSpheroidsMethod == 'dynamicalTime') then
       Star_Formation_Timescale_Spheroid_Get => Star_Formation_Timescale_Spheroid_Dynamical_Time
       ! Get parameters of for the timescale calculation.
       !@ <inputParameter>
       !@   <name>starFormationSpheroidEfficiency</name>
       !@   <defaultValue>0.04</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     The efficiency of star formation in spheroids for the dynamical time method.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('starFormationSpheroidEfficiency'      ,starFormationSpheroidEfficiency      ,defaultValue=0.04d0)
       !@ <inputParameter>
       !@   <name>starFormationSpheroidVelocityExponent</name>
       !@   <defaultValue>2.0</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     The velocity exponent for star formation in spheroids for the dynamical time method.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
       call Get_Input_Parameter('starFormationSpheroidVelocityExponent',starFormationSpheroidVelocityExponent,defaultValue=2.0d0)
       !@ <inputParameter>
       !@   <name>starFormationSpheroidMinimumTimescale</name>
       !@   <defaultValue>$10^{-3}$ Gyr</defaultValue>
       !@   <attachedTo>module</attachedTo>
       !@   <description>
       !@     The minimum timescale for star formation in disks.
       !@   </description>
       !@   <type>real</type>
       !@   <cardinality>1</cardinality>
       !@   <group>starFormation</group>
       !@ </inputParameter>
      call Get_Input_Parameter('starFormationSpheroidMinimumTimescale',starFormationSpheroidMinimumTimescale,defaultValue=1.0d-3)
    end if
    return
  end subroutine Star_Formation_Timescale_Spheroids_Dynamical_Time_Initialize

  double precision function Star_Formation_Timescale_Spheroid_Dynamical_Time(thisNode)
    !% Returns the timescale (in Gyr) for star formation in the galactic spheroid of {\normalfont \ttfamily thisNode}. The timescale is given by
    !% \begin{equation}
    !% \tau_\star = \epsilon_\star^{-1} \tau_{\mathrm dynamical, spheroid} \left( {V_{\mathrm spheroid} \over 200\hbox{km/s}} \right)^{\alpha_\star},
    !% \end{equation}
    !% where $\epsilon_\star$(={\normalfont \ttfamily starFormationSpheroidEfficiency}) is a star formation efficiency and $\alpha_\star$(={\tt
    !% starFormationSpheroidVelocityExponent}) controls the scaling with velocity. Note that $\tau_{\mathrm dynamical,spheroid}=R_{\rm
    !% spheroid}/V_{\mathrm spheroid}$ where the radius and velocity are whatever characteristic values returned by the spheroid method. This
    !% scaling is functionally similar to that adopted by \cite{cole_hierarchical_2000}, but that they specifically used the
    !% half-mass radius and circular velocity at that radius.
    use Galacticus_Nodes
    use Numerical_Constants_Astronomical
    implicit none
    type            (treeNode             ), intent(inout) :: thisNode
    class           (nodeComponentSpheroid), pointer       :: thisSpheroidComponent
    double precision                       , parameter     :: velocityZeroPoint    =200.0d0                   !   (km/s)
    double precision                                       :: dynamicalTime                , spheroidVelocity

    ! Get spheroid circular velocity.
    thisSpheroidComponent => thisNode%spheroid()
    spheroidVelocity=thisSpheroidComponent%velocity()

    ! Check for zero velocity spheroid.
    if (spheroidVelocity <= 0.0d0) then
       Star_Formation_Timescale_Spheroid_Dynamical_Time=0.0d0 ! No well defined answer in this case.
    else if (starFormationSpheroidEfficiency == 0.0d0) then
       ! No star formation occurs if the efficiency is zero.
       Star_Formation_Timescale_Spheroid_Dynamical_Time=0.0d0
    else
       ! Get the dynamical time in Gyr.
       dynamicalTime=Mpc_per_km_per_s_To_Gyr*thisSpheroidComponent%radius()/spheroidVelocity

       ! Compute the star formation timescale using a simple scaling factor.
       Star_Formation_Timescale_Spheroid_Dynamical_Time=max(dynamicalTime*(spheroidVelocity/velocityZeroPoint)&
            &**starFormationSpheroidVelocityExponent/starFormationSpheroidEfficiency,starFormationSpheroidMinimumTimescale)
    end if
    return
  end function Star_Formation_Timescale_Spheroid_Dynamical_Time

end module Star_Formation_Timescale_Spheroids_Dynamical_Time
