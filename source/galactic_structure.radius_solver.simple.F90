!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implementation of a simple solver for galactic structure (self-gravity of baryons is ignored).

  use Dark_Matter_Profiles_DMO, only : darkMatterProfileDMO, darkMatterProfileDMOClass

  !# <galacticStructureSolver name="galacticStructureSolverSimple">
  !#  <description>A simple solver for galactic structure (self-gravity of baryons is ignored).</description>
  !# </galacticStructureSolver>
  type, extends(galacticStructureSolverClass) :: galacticStructureSolverSimple
     !% Implementation of a simple solver for galactic structure (self-gravity of baryons is ignored).
     private
     class  (darkMatterProfileDMOClass), pointer :: darkMatterProfileDMO_
     logical                                     :: useFormationHalo
   contains
     final     ::             simpleDestructor
     procedure :: solve    => simpleSolve
     procedure :: revert   => simpleRevert
     procedure :: autoHook => simpleAutoHook
  end type galacticStructureSolverSimple

  interface galacticStructureSolverSimple
     !% Constructors for the {\normalfont \ttfamily simple} galactic structure solver class.
     module procedure simpleConstructorParameters
     module procedure simpleConstructorInternal
  end interface galacticStructureSolverSimple

contains

  function simpleConstructorParameters(parameters) result(self)
    !% Constructor for the {\normalfont \ttfamily simple} galactic structure solver class which takes a
    !% parameter set as input.
    use Input_Parameters, only : inputParameter, inputParameters
    implicit none
    type   (galacticStructureSolverSimple)                :: self
    type   (inputParameters              ), intent(inout) :: parameters
    class  (darkMatterProfileDMOClass    ), pointer       :: darkMatterProfileDMO_
    logical                                               :: useFormationHalo

    !# <inputParameter>
    !#   <name>useFormationHalo</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>.false.</defaultValue>
    !#   <description>Specifies whether or not the ``formation halo'' should be used when solving for the radii of galaxies.</description>
    !#   <source>parameters</source>
    !#   <type>boolean</type>
    !# </inputParameter>
    !# <objectBuilder class="darkMatterProfileDMO" name="darkMatterProfileDMO_" source="parameters"/>
    self=galacticStructureSolverSimple(useFormationHalo,darkMatterProfileDMO_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="darkMatterProfileDMO_"/>
    return
  end function simpleConstructorParameters

  function simpleConstructorInternal(useFormationHalo,darkMatterProfileDMO_) result(self)
    !% Internal constructor for the {\normalfont \ttfamily simple} galactic structure solver class.
    implicit none
    type   (galacticStructureSolverSimple)                        :: self
    logical                               , intent(in   )         :: useFormationHalo
    class  (darkMatterProfileDMOClass    ), intent(in   ), target :: darkMatterProfileDMO_
    !# <constructorAssign variables="useFormationHalo, *darkMatterProfileDMO_"/>
   
    return
  end function simpleConstructorInternal

  subroutine simpleAutoHook(self)
    !% Attach to various event hooks.
    use Events_Hooks, only : preDerivativeEvent, postEvolveEvent, satelliteMergerEvent, nodePromotionEvent
    implicit none
    class(galacticStructureSolverSimple), intent(inout) :: self

    call   preDerivativeEvent%attach(self,simpleSolvePreDeriativeHook,bindToOpenMPThread=.true.)
    call      postEvolveEvent%attach(self,simpleSolveHook            ,bindToOpenMPThread=.true.)
    call satelliteMergerEvent%attach(self,simpleSolveHook            ,bindToOpenMPThread=.true.)
    call   nodePromotionEvent%attach(self,simpleSolveHook            ,bindToOpenMPThread=.true.)
    return
  end subroutine simpleAutoHook

  subroutine simpleDestructor(self)
    !% Destructor for the {\normalfont \ttfamily simple} galactic structure solver class.
    use Events_Hooks, only : preDerivativeEvent, postEvolveEvent, satelliteMergerEvent, nodePromotionEvent
    implicit none
    type(galacticStructureSolverSimple), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterProfileDMO_"/>
    call   preDerivativeEvent%detach(self,simpleSolvePreDeriativeHook)
    call      postEvolveEvent%detach(self,simpleSolveHook            )
    call satelliteMergerEvent%detach(self,simpleSolveHook            )
    call   nodePromotionEvent%detach(self,simpleSolveHook            )
    return
  end subroutine simpleDestructor

  subroutine simpleSolveHook(self,node)
    !% Hookable wrapper around the solver.
    use Galacticus_Error, only : Galacticus_Error_Report
    implicit none
    class(*       ), intent(inout)         :: self
    type (treeNode), intent(inout), target :: node

    select type (self)
    type is (galacticStructureSolverSimple)
       call self%solve(node)
    class default
       call Galacticus_Error_Report('incorrect class'//{introspection:location})
    end select
    return
  end subroutine simpleSolveHook
  
  subroutine simpleSolvePreDeriativeHook(self,node,propertyType)
    !% Hookable wrapper around the solver.
    use Galacticus_Error, only : Galacticus_Error_Report
    implicit none
    class  (*       ), intent(inout)         :: self
    type   (treeNode), intent(inout), target :: node
    integer          , intent(in   )         :: propertyType
    !GCC$ attributes unused :: propertyType

    select type (self)
    type is (galacticStructureSolverSimple)
       call self%solve(node)
    class default
       call Galacticus_Error_Report('incorrect class'//{introspection:location})
    end select
    return
  end subroutine simpleSolvePreDeriativeHook
  
  subroutine simpleSolve(self,node)
    !% Solve for the structure of galactic components.
    use Galacticus_Error, only : Galacticus_Error_Report
    !# <include directive="radiusSolverTask" type="moduleUse">
    include 'galactic_structure.radius_solver.tasks.modules.inc'
    !# </include>
    !# <include directive="radiusSolverPlausibility" type="moduleUse">
    include 'galactic_structure.radius_solver.plausible.modules.inc'
    !# </include>
    implicit none
    class           (galacticStructureSolverSimple), intent(inout)         :: self
    type            (treeNode                     ), intent(inout), target :: node
    logical                                        , parameter             :: specificAngularMomentumRequired=.true.
    procedure       (solverGet                    ), pointer               :: radiusGet                             , velocityGet
    procedure       (solverSet                    ), pointer               :: radiusSet                             , velocitySet
    type            (treeNode                     ), pointer               :: haloNode
    logical                                                                :: componentActive
    double precision                                                       :: specificAngularMomentum

    ! Check that the galaxy is physical plausible. In this simple solver, we don't act on this.
    node%isPhysicallyPlausible=.true.
    node%isSolvable           =.true.
    !# <include directive="radiusSolverPlausibility" type="functionCall" functionType="void">
    !#  <functionArgs>node</functionArgs>
    include 'galactic_structure.radius_solver.plausible.inc'
    !# </include>
    ! Determine which node to use for halo properties.
    if (self%useFormationHalo) then
       if (.not.associated(node%formationNode)) call Galacticus_Error_Report('no formation node exists'//{introspection:location})
       haloNode => node%formationNode
    else
       haloNode => node
    end if
    ! Solve for each component.
    !# <include directive="radiusSolverTask" type="functionCall" functionType="void">
    !#  <functionArgs>node,componentActive,specificAngularMomentumRequired,specificAngularMomentum,radiusGet,radiusSet,velocityGet,velocitySet</functionArgs>
    !#  <onReturn>if (componentActive) call radiusSolve(node,specificAngularMomentum,radiusGet,radiusSet,velocityGet,velocitySet)</onReturn>
    include 'galactic_structure.radius_solver.tasks.inc'
    !# </include>
    return

  contains
    
    subroutine radiusSolve(node,specificAngularMomentum,radiusGet,radiusSet,velocityGet,velocitySet)
      !% Solve for the equilibrium radius of the given component.
      implicit none
      type            (treeNode ), intent(inout)          :: node
      double precision           , intent(in   )          :: specificAngularMomentum
      procedure       (solverGet), intent(in   ), pointer :: radiusGet              , velocityGet
      procedure       (solverSet), intent(in   ), pointer :: radiusSet              , velocitySet
      double precision                                    :: radius                 , velocity
      !GCC$ attributes unused :: radiusGet, velocityGet

      ! Return immediately if the specific angular momentum is zero.
      if (specificAngularMomentum <= 0.0d0) return
      ! Find the radius in the dark matter profile with the required specific angular momentum
      radius=self%darkMatterProfileDMO_%radiusFromSpecificAngularMomentum(haloNode,specificAngularMomentum)
      ! Find the velocity at this radius.
      velocity=self%darkMatterProfileDMO_%circularVelocity(haloNode,radius)
      ! Set the component size to new radius and velocity.
      call radiusSet  (node,radius  )
      call velocitySet(node,velocity)
      return
    end subroutine radiusSolve

  end subroutine simpleSolve

  subroutine simpleRevert(self,node)
    !% Revert radii for the simple galactic structure solve. Not necessary for this algorithm.
    implicit none
    class(galacticStructureSolverSimple), intent(inout) :: self
    type (treeNode                     ), intent(inout) :: node
    !GCC$ attributes unused :: self, node
    
    return
  end subroutine simpleRevert
