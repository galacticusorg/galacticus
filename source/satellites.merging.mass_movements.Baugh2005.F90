!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements the \cite{baugh_can_2005} model of mass movements during satellite mergers.

module Satellite_Merging_Mass_Movements_Baugh2005
  !% Implements the \cite{baugh_can_2005} model of mass movements during satellite mergers.
  use Satellite_Merging_Mass_Movements_Descriptors
  implicit none
  private
  public :: Satellite_Merging_Mass_Movements_Baugh2005_Initialize

  ! Mass ratio above which a merger is considered to be "major".
  double precision :: burstCriticalGasFraction  , burstMassRatio, &
       &              majorMergerMassRatio

  ! Location to which gas from satellite galaxy in minor merger is moved.
  integer          :: minorMergerGasMovesToValue

contains

  !# <satelliteMergingMassMovementsMethod>
  !#  <unitName>Satellite_Merging_Mass_Movements_Baugh2005_Initialize</unitName>
  !# </satelliteMergingMassMovementsMethod>
  subroutine Satellite_Merging_Mass_Movements_Baugh2005_Initialize(satelliteMergingMassMovementsMethod,Satellite_Merging_Mass_Movement_Get)
    !% Test if this method is to be used and set procedure pointer appropriately.
    use ISO_Varying_String
    use Input_Parameters
    use Galacticus_Error
    implicit none
    type     (varying_string                           ), intent(in   )          :: satelliteMergingMassMovementsMethod
    procedure(Satellite_Merging_Mass_Movement_Baugh2005), intent(inout), pointer :: Satellite_Merging_Mass_Movement_Get
    type     (varying_string                           )                         :: minorMergerGasMovesTo

    if (satelliteMergingMassMovementsMethod == 'Baugh2005') then
       Satellite_Merging_Mass_Movement_Get => Satellite_Merging_Mass_Movement_Baugh2005
       !# <inputParameter>
       !#   <name>majorMergerMassRatio</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.25d0</defaultValue>
       !#   <description>The mass ratio above which mergers are considered to be ``major'' in the \cite{baugh_can_2005} merger mass movements method.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>burstMassRatio</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.05d0</defaultValue>
       !#   <description>The mass ratio above which mergers are considered to trigger a burst in the \cite{baugh_can_2005} merger mass movements method.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>burstCriticalGasFraction</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>0.75d0</defaultValue>
       !#   <description>The host gas fraction above which mergers are considered to trigger a burst in the \cite{baugh_can_2005} merger mass movements method.</description>
       !#   <source>globalParameters</source>
       !#   <type>real</type>
       !# </inputParameter>
       !# <inputParameter>
       !#   <name>minorMergerGasMovesTo</name>
       !#   <cardinality>1</cardinality>
       !#   <defaultValue>var_str('spheroid')</defaultValue>
       !#   <description>The component to which satellite galaxy gas moves to as a result of a minor merger.</description>
       !#   <source>globalParameters</source>
       !#   <type>string</type>
       !# </inputParameter>
       select case (char(minorMergerGasMovesTo))
       case ("disk"    )
          minorMergerGasMovesToValue=movesToDisk
       case ("spheroid")
          minorMergerGasMovesToValue=movesToSpheroid
       case default
          call Galacticus_Error_Report('unrecognized location for minor merger satellite gas'//{introspection:location})
       end select
    end if
    return
  end subroutine Satellite_Merging_Mass_Movements_Baugh2005_Initialize

  subroutine Satellite_Merging_Mass_Movement_Baugh2005(thisNode,gasMovesTo,starsMoveTo,hostGasMovesTo,hostStarsMoveTo,mergerIsMajor)
    !% Determine how different mass components should be redistributed as the result of a merger according to the model of \cite{baugh_can_2005}.
    use Galacticus_Nodes
    use Galactic_Structure_Enclosed_Masses
    use Galactic_Structure_Options
    implicit none
    type            (treeNode), intent(inout) :: thisNode
    integer                   , intent(  out) :: gasMovesTo   , hostGasMovesTo, hostStarsMoveTo , starsMoveTo
    logical                   , intent(  out) :: mergerIsMajor
    type            (treeNode), pointer       :: hostNode
    logical                                   :: triggersBurst
    double precision                          :: hostGasMass  , hostMass      , hostSpheroidMass, satelliteMass

    ! Find the node to merge with.
   hostNode => thisNode%mergesWith()

    ! Find the baryonic masses of the two galaxies.
    satelliteMass=Galactic_Structure_Enclosed_Mass(thisNode,massType=massTypeGalactic)
    hostMass        =Galactic_Structure_Enclosed_Mass(hostNode,massType=massTypeGalactic                                    )
    hostGasMass     =Galactic_Structure_Enclosed_Mass(hostNode,massType=massTypeGaseous                                     )
    hostSpheroidMass=Galactic_Structure_Enclosed_Mass(hostNode,componentType=componentTypeSpheroid,massType=massTypeGalactic)
    ! Decide if the mass ratio is large enough to trigger a major merger.
    mergerIsMajor=satelliteMass >= majorMergerMassRatio*hostMass

    ! Determine if the merger will trigger a burst, in which case gas will be moved to the spheroid.
    triggersBurst=mergerIsMajor                                          &
         &         .or.                                                  &
         &        (                                                      &
         &         hostSpheroidMass <  burstMassRatio          *hostMass &
         &          .and.                                                &
         &         hostGasMass      >= burstCriticalGasFraction*hostMass &
         &        )

    if (mergerIsMajor) then
       gasMovesTo     =movesToSpheroid
       starsMoveTo    =movesToSpheroid
       hostGasMovesTo =movesToSpheroid
       hostStarsMoveTo=movesToSpheroid
    else
       if (triggersBurst) then
          gasMovesTo     =movesToSpheroid
          starsMoveTo    =movesToSpheroid
          hostGasMovesTo =movesToSpheroid
          hostStarsMoveTo=doesNotMove
       else
          gasMovesTo     =minorMergerGasMovesToValue
          starsMoveTo    =movesToSpheroid
          hostGasMovesTo =doesNotMove
          hostStarsMoveTo=doesNotMove
       end if
    end if

    return
  end subroutine Satellite_Merging_Mass_Movement_Baugh2005

end module Satellite_Merging_Mass_Movements_Baugh2005
