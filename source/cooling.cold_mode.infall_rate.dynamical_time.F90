!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implementation of a calculation of cold mode infall rates assuming infall on a dynamical timescale.

  use Dark_Matter_Halo_Scales
  
  !# <coldModeInfallRate name="coldModeInfallRateDynamicalTime" defaultThreadPrivate="yes">
  !#  <description>A dynamicalTime cooling time calculation (based on the ratio of the thermal energy density to the volume cooling rate).</description>
  !# </coldModeInfallRate>
  type, extends(coldModeInfallRateClass) :: coldModeInfallRateDynamicalTime
     !% Implementation of a calculation of cold mode infall rates assuming infall on a dynamical timescale.
     private
     class           (darkMatterHaloScaleClass), pointer :: darkMatterHaloScale_
     double precision                                    :: dynamicalRateFraction
   contains
     final     ::               dynamicalTimeDestructor
     procedure :: infallRate => dynamicalTimeInfallRate
  end type coldModeInfallRateDynamicalTime

  interface coldModeInfallRateDynamicalTime
     !% Constructors for the dynamicalTime cooling time class.
     module procedure dynamicalTimeConstructorParameters
     module procedure dynamicalTimeConstructorInternal
  end interface coldModeInfallRateDynamicalTime

contains

  function dynamicalTimeConstructorParameters(parameters) result(self)
    !% Constructor for the dynamical time cooling time class which builds the object from a parameter set.
    use Input_Parameters2
    implicit none
    type            (coldModeInfallRateDynamicalTime)                :: self
    type            (inputParameters                ), intent(inout) :: parameters
    class           (darkMatterHaloScaleClass       ), pointer       :: darkMatterHaloScale_
    double precision                                                 :: dynamicalRateFraction

    !# <inputParameter>
    !#   <name>dynamicalRateFraction</name>
    !#   <defaultValue>2.0d0</defaultValue>
    !#   <source>parameters</source>
    !#   <description>The fraction of the inverse dynamical time to use as the rate for infall of the cold mode component.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="darkMatterHaloScale" name="darkMatterHaloScale_" source="parameters"/>
    self=coldModeInfallRateDynamicalTime(dynamicalRateFraction,darkMatterHaloScale_)
    !# <inputParametersValidate source="parameters"/>
    return
  end function dynamicalTimeConstructorParameters

  function dynamicalTimeConstructorInternal(dynamicalRateFraction,darkMatterHaloScale_) result(self)
    !% Internal constructor for the dynamical time cooling time class.
    use Galacticus_Error
    implicit none
    type            (coldModeInfallRateDynamicalTime)                        :: self
    class           (darkMatterHaloScaleClass       ), intent(in   ), target :: darkMatterHaloScale_
    double precision                                 , intent(in   )         :: dynamicalRateFraction
    !# <constructorAssign variables="dynamicalRateFraction, *darkMatterHaloScale_"/>
    
    ! Check that the properties we need are gettable.
    if (.not.defaultHotHaloComponent%massColdIsGettable())                                 &
         & call Galacticus_Error_Report(                                                   &
         &                              'dynamicalTimeConstructorInternal'               , &
         &                              'hot halo component must have gettable cold mass'  &
         &                             )
    return
  end function dynamicalTimeConstructorInternal

  subroutine dynamicalTimeDestructor(self)
    !% Destructor for the dynamical time cold mode infall rate class.
    implicit none
    type(coldModeInfallRateDynamicalTime), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterHaloScale_"/>
    return
  end subroutine dynamicalTimeDestructor

  double precision function dynamicalTimeInfallRate(self,node)
    !% Computes the cold mode infall rate as a fraction of the halo dynamical time.
    class(coldModeInfallRateDynamicalTime), intent(inout) :: self
    type (treeNode                       ), intent(inout) :: node
    class(nodeComponentHotHalo           ), pointer       :: hotHalo
    
    hotHalo                 =>  node                        %hotHalo              (    )
    dynamicalTimeInfallRate =  +self                        %dynamicalRateFraction       &
         &                     /self   %darkMatterHaloScale_%dynamicalTimescale   (node) &
         &                     *hotHalo                     %massCold             (    )
    return
  end function dynamicalTimeInfallRate
