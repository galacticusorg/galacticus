!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which implements a time-stepping criterion for merger tree evolution which permits evolution of the main
!% branch galaxy to be stored.

module Merger_Tree_Timesteps_Record_Evolution
  !% Implements a time-stepping criterion for merger tree evolution which permits evolution of the main
  !% branch galaxy to be stored.
  use FGSL
  implicit none
  private
  public :: Merger_Tree_Timestep_Record_Evolution, Merger_Tree_Record_Evolution_Output

  ! Variable inidicating if module is initialized and active.
  logical                                                        :: timestepRecordEvolutionInitialized=.false.

  ! Variable indicating if one-time datasets have been written yet.
  logical                                                        :: oneTimeDatasetsWritten            =.false.

  ! Variable indicating if evolution should be recorded.
  logical                                                        :: timestepRecordEvolution

  ! Variables which control the distribution of timesteps.
  integer                                                        :: timestepRecordEvolutionSteps
  double precision                                               :: timestepRecordEvolutionBegin              , timestepRecordEvolutionEnd

  ! Storage arrays.
  double precision                   , allocatable, dimension(:) :: evolutionExpansion                        , evolutionStellarMass      , &
       &                                                            evolutionTime                             , evolutionTotalMass

  ! Interpolation variables.
  type            (fgsl_interp_accel)                            :: interpolationAccelerator
  !$omp threadprivate(interpolationAccelerator)
contains

  !# <timeStepsTask>
  !#  <unitName>Merger_Tree_Timestep_Record_Evolution</unitName>
  !# </timeStepsTask>
  subroutine Merger_Tree_Timestep_Record_Evolution(node,timeStep,End_Of_Timestep_Task,report,nodeLock,lockType)
    !% Determines the timestep to go to the next tabulation point for galaxy evolution storage.
    use, intrinsic :: ISO_C_Binding
    use Input_Parameters
    use Cosmology_Functions
    use Memory_Management
    use Numerical_Ranges
    use Numerical_Interpolation
    use Merger_Trees_Evolve_Timesteps_Template
    use Evolve_To_Time_Reports
    use ISO_Varying_String
    implicit none
    type            (treeNode                     ), intent(inout)          , pointer :: node
    procedure       (End_Of_Timestep_Task_Template), intent(inout)          , pointer :: End_Of_Timestep_Task
    double precision                               , intent(inout)                    :: timeStep
    logical                                        , intent(in   )                    :: report
    type            (treeNode                     ), intent(inout), optional, pointer :: nodeLock
    type            (varying_string               ), intent(inout), optional          :: lockType
    class           (nodeComponentBasic           )                         , pointer :: basic
    class           (cosmologyFunctionsClass      )                         , pointer :: cosmologyFunctions_
    integer         (c_size_t                     )                                   :: timeIndex
    double precision                                                                  :: ourTimeStep         , time, &
         &                                                                               universeAge

    if (.not.timestepRecordEvolutionInitialized) then
       !$omp critical (timestepRecordEvolutionInitialize)
       if (.not.timestepRecordEvolutionInitialized) then
          ! Get module parameters.
          !# <inputParameter>
          !#   <name>timestepRecordEvolution</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not the evolution of the main branch galaxy should be recorded.</description>
          !#   <group>timeStepping</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          if (timestepRecordEvolution) then
             ! Get the default cosmology functions object.
             cosmologyFunctions_ => cosmologyFunctions()
             ! Get time at present day.
             universeAge=cosmologyFunctions_%cosmicTime(expansionFactor=0.999d0)
             ! Get module parameters.
             !# <inputParameter>
             !#   <name>timestepRecordEvolutionBegin</name>
             !#   <cardinality>1</cardinality>
             !#   <defaultValue>0.05d0*universeAge</defaultValue>
             !#   <description>The earliest time at which to tabulate the evolution of main branch progenitor galaxies (in Gyr).</description>
             !#   <group>timeStepping</group>
             !#   <source>globalParameters</source>
             !#   <type>real</type>
             !# </inputParameter>
             !# <inputParameter>
             !#   <name>timestepRecordEvolutionEnd</name>
             !#   <cardinality>1</cardinality>
             !#   <defaultValue>universeAge</defaultValue>
             !#   <description>The latest time at which to tabulate the evolution of main branch progenitor galaxies (in Gyr).</description>
             !#   <group>timeStepping</group>
             !#   <source>globalParameters</source>
             !#   <type>real</type>
             !# </inputParameter>
             !# <inputParameter>
             !#   <name>timestepRecordEvolutionSteps</name>
             !#   <cardinality>1</cardinality>
             !#   <defaultValue>100</defaultValue>
             !#   <description>The number of steps (spaced logarithmically in cosmic time) at which to tabulate the evolution of main branch progenitor galaxies.</description>
             !#   <group>timeStepping</group>
             !#   <source>globalParameters</source>
             !#   <type>integer</type>
             !# </inputParameter>
             ! Allocate storage arrays.
             call allocateArray(evolutionTime       ,[timestepRecordEvolutionSteps])
             call allocateArray(evolutionExpansion  ,[timestepRecordEvolutionSteps])
             call allocateArray(evolutionStellarMass,[timestepRecordEvolutionSteps])
             call allocateArray(evolutionTotalMass  ,[timestepRecordEvolutionSteps])
             ! Initialize arrays.
             evolutionTime=Make_Range(timestepRecordEvolutionBegin,timestepRecordEvolutionEnd,timestepRecordEvolutionSteps,rangeTypeLogarithmic)
             do timeIndex=1,timestepRecordEvolutionSteps
                evolutionExpansion(timeIndex)=cosmologyFunctions_%expansionFactor(evolutionTime(timeIndex))
             end do
             call Reset_Records()
          end if
          timestepRecordEvolutionInitialized=.true.
       end if
       !$omp end critical (timestepRecordEvolutionInitialize)
    end if

    ! Adjust timestep if applicable.
    if (timestepRecordEvolution.and.node%isOnMainBranch()) then
       ! Get current cosmic time.
       basic => node%basic()
       time=basic%time()

       ! Determine how long until next available timestep.
       timeIndex=Interpolate_Locate(evolutionTime,interpolationAccelerator,time)
       if (time < evolutionTime(timeIndex+1)) then
          ! Find next time for storage.
          ourTimeStep=evolutionTime(timeIndex+1)-time

          ! Set return value if our timestep is smaller than current one.
          if (ourTimeStep <= timeStep) then
             if (present(nodeLock)) nodeLock => node
             if (present(lockType)) lockType =  "record evolution"
             timeStep=ourTimeStep
             End_Of_Timestep_Task => Merger_Tree_Record_Evolution_Store
          end if
       end if
    end if
    if (report) call Evolve_To_Time_Report("record evolution: ",timeStep)
    return
  end subroutine Merger_Tree_Timestep_Record_Evolution

  subroutine Merger_Tree_Record_Evolution_Store(thisTree,node,deadlockStatus)
    !% Store properties of the main progenitor galaxy.
    use, intrinsic :: ISO_C_Binding
    use Galacticus_Nodes
    use Numerical_Interpolation
    use Galactic_Structure_Options
    use Galactic_Structure_Enclosed_Masses
    implicit none
    type            (mergerTree        ), intent(in   )          :: thisTree
    type            (treeNode          ), intent(inout), pointer :: node
    integer                             , intent(inout)          :: deadlockStatus
    class           (nodeComponentBasic)               , pointer :: basic
    integer         (c_size_t          )                         :: timeIndex
    double precision                                             :: time
    !GCC$ attributes unused :: deadlockStatus, thisTree
    
    ! Get current cosmic time.
    basic => node%basic()
    time=basic%time()

    ! Determine how long until next available timestep.
    if (time == evolutionTime(timestepRecordEvolutionSteps)) then
       timeIndex=timestepRecordEvolutionSteps
    else
       timeIndex=Interpolate_Locate(evolutionTime,interpolationAccelerator,time)
    end if

    ! Accumulate the properties.
    evolutionStellarMass(timeIndex)=Galactic_Structure_Enclosed_Mass(node,massType=massTypeStellar )
    evolutionTotalMass  (timeIndex)=Galactic_Structure_Enclosed_Mass(node,massType=massTypeGalactic)

    return
  end subroutine Merger_Tree_Record_Evolution_Store

  !# <mergerTreeExtraOutputTask>
  !#  <unitName>Merger_Tree_Record_Evolution_Output</unitName>
  !# </mergerTreeExtraOutputTask>
  subroutine Merger_Tree_Record_Evolution_Output(node,iOutput,treeIndex,nodePassesFilter)
    !% Store Fourier-space halo profiles to the output file.
    use, intrinsic :: ISO_C_Binding
    use Galacticus_Nodes
    use Galacticus_HDF5
    use Galacticus_Output_Times
    use ISO_Varying_String
    use String_Handling
    use Numerical_Constants_Astronomical
    implicit none
    type   (treeNode      ), intent(inout), pointer :: node
    integer(c_size_t      ), intent(in   )          :: iOutput
    integer(kind=kind_int8), intent(in   )          :: treeIndex
    logical                , intent(in   )          :: nodePassesFilter
    type   (varying_string)                         :: datasetName
    type   (hdf5Object    )                         :: outputGroup     , thisDataset

    ! If halo model output was requested, output the Fourier-space halo profiles.
    if (nodePassesFilter.and.timestepRecordEvolution.and.iOutput == Galacticus_Output_Time_Count().and.node%isOnMainBranch())&
         & then
       ! Create a group for the profile datasets.
       outputGroup=galacticusOutputFile%openGroup("mainProgenitorEvolution","Evolution data of main progenitors.")

       ! Write one time datasets if necessary.
       if (.not.oneTimeDatasetsWritten) then
          ! Write the datasets.
          call outputGroup%writeDataset(evolutionTime     ,"time"           ,"The time of the main progenitor."            ,datasetReturned=thisDataset)
          call thisDataset%writeAttribute(gigaYear,"unitsInSI")
          call thisDataset%close()
          call outputGroup%writeDataset(evolutionExpansion,"expansionFactor","The expansion factor of the main progenitor."                            )
          ! Record that these datasets have been written.
          oneTimeDatasetsWritten=.true.
       end if

       ! Write datasets to the group.
       datasetName="stellarMass"
       datasetName=datasetName//treeIndex
       call outputGroup%writeDataset(evolutionStellarMass,char(datasetName),"The stellar mass of the main progenitor."       ,datasetReturned=thisDataset)
       call thisDataset%writeAttribute(massSolar,"unitsInSI")
       call thisDataset%close()
       datasetName="totalMass"
       datasetName=datasetName//treeIndex
       call outputGroup%writeDataset(evolutionTotalMass  ,char(datasetName),"The total baryonic mass of the main progenitor.",datasetReturned=thisDataset)
       call thisDataset%writeAttribute(massSolar,"unitsInSI")
       call thisDataset%close()
       ! Close the output group.
       call outputGroup%close()
       ! Reset the recorded masses to zero.
       call Reset_Records()
    end if
    return
  end subroutine Merger_Tree_Record_Evolution_Output

  subroutine Reset_Records()
    !% Resets recorded datasets to zero.
    implicit none

    ! Reset all recorded values to zero.
    evolutionStellarMass=0.0d0
    evolutionTotalMass  =0.0d0
    return
  end subroutine Reset_Records

end module Merger_Tree_Timesteps_Record_Evolution
