!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% An implementation of dark matter halo profile concentrations using the
  !% \cite{diemer_universal_2014} algorithm.

  use Cosmology_Functions
  use Cosmological_Density_Field
  use Cosmology_Parameters
  use Power_Spectra
  
  !# <darkMatterProfileConcentration name="darkMatterProfileConcentrationDiemerKravtsov2014">
  !#  <description>Dark matter halo concentrations are computed using the algorithm of \cite{diemer_universal_2014}.</description>
  !# </darkMatterProfileConcentration>
  type, extends(darkMatterProfileConcentrationClass) :: darkMatterProfileConcentrationDiemerKravtsov2014
     !% A dark matter halo profile concentration class implementing the algorithm of
     !% \cite{diemer_universal_2014}.
     private
     class           (cosmologyFunctionsClass      ), pointer :: cosmologyFunctions_       => null()
     class           (cosmologyParametersClass     ), pointer :: cosmologyParameters_      => null()
     class           (criticalOverdensityClass     ), pointer :: criticalOverdensity_      => null()
     class           (cosmologicalMassVarianceClass), pointer :: cosmologicalMassVariance_ => null()
     class           (powerSpectrumClass           ), pointer :: powerSpectrum_            => null()
     double precision                                         :: kappa                     , scatter              , &
          &                                                      phi0                      , phi1                 , &
          &                                                      eta0                      , eta1                 , &
          &                                                      alpha                     , beta                 , &
          &                                                      timePrevious              , massPrevious         , &
          &                                                      concentrationMeanPrevious
   contains
     final     ::                                diemerKravtsov2014Destructor
     procedure :: concentration               => diemerKravtsov2014Concentration
     procedure :: concentrationMean           => diemerKravtsov2014ConcentrationMean
     procedure :: densityContrastDefinition   => diemerKravtsov2014DensityContrastDefinition
     procedure :: darkMatterProfileDefinition => diemerKravtsov2014DarkMatterProfileDefinition
  end type darkMatterProfileConcentrationDiemerKravtsov2014

  interface darkMatterProfileConcentrationDiemerKravtsov2014
     !% Constructors for the {\normalfont \ttfamily diemerKravtsov2014} dark matter halo profile concentration class.
     module procedure diemerKravtsov2014ConstructorParameters
     module procedure diemerKravtsov2014ConstructorInternal
  end interface darkMatterProfileConcentrationDiemerKravtsov2014

contains

  function diemerKravtsov2014ConstructorParameters(parameters) result(self)
    !% Default constructor for the {\normalfont \ttfamily diemerKravtsov2014} dark matter halo
    !% profile concentration class.
    implicit none
    type            (darkMatterProfileConcentrationDiemerKravtsov2014)                :: self
    type            (inputParameters                                 ), intent(inout) :: parameters
    class           (cosmologyFunctionsClass                         ), pointer       :: cosmologyFunctions_
    class           (cosmologyParametersClass                        ), pointer       :: cosmologyParameters_     
    class           (criticalOverdensityClass                        ), pointer       :: criticalOverdensity_     
    class           (cosmologicalMassVarianceClass                   ), pointer       :: cosmologicalMassVariance_
    class           (powerSpectrumClass                              ), pointer       :: powerSpectrum_
    double precision                                                                  :: kappa, phi0, phi1, eta0, eta1, alpha, beta, scatter

    ! Check and read parameters.
    !# <inputParameter>
    !#   <name>kappa</name>
    !#   <source>parameters</source>
    !#   <variable>kappa</variable>
    !#   <defaultValue>0.69d0</defaultValue>
    !#   <description>The parameter $\kappa$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>phi0</name>
    !#   <source>parameters</source>
    !#   <variable>phi0</variable>
    !#   <defaultValue>6.58d0</defaultValue>
    !#   <description>The parameter $\phi_0$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>phi1</name>
    !#   <source>parameters</source>
    !#   <variable>phi1</variable>
    !#   <defaultValue>1.37d0</defaultValue>
    !#   <description>The parameter $\phi_1$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>eta0</name>
    !#   <source>parameters</source>
    !#   <variable>eta0</variable>
    !#   <defaultValue>6.82d0</defaultValue>
    !#   <description>The parameter $\eta_0$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>eta1</name>
    !#   <source>parameters</source>
    !#   <variable>eta1</variable>
    !#   <defaultValue>1.42d0</defaultValue>
    !#   <description>The parameter $\eta_1$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>alpha</name>
    !#   <source>parameters</source>
    !#   <variable>alpha</variable>
    !#   <defaultValue>1.12d0</defaultValue>
    !#   <attachedTo>module</attachedTo>
    !#   <description>The parameter $\alpha$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>beta</name>
    !#   <source>parameters</source>
    !#   <variable>beta</variable>
    !#   <defaultValue>1.69d0</defaultValue>
    !#   <description>The parameter $\beta$ appearing in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <inputParameter>
    !#   <name>scatter</name>
    !#   <source>parameters</source>
    !#   <variable>scatter</variable>
    !#   <defaultValue>0.0d0</defaultValue>
    !#   <description>The scatter (in dex) to assume in the halo concentration algorithm of \cite{diemer_universal_2014}.</description>
    !#   <type>real</type>
    !#   <cardinality>1</cardinality>
    !# </inputParameter>
    !# <objectBuilder class="cosmologyFunctions"       name="cosmologyFunctions_"       source="parameters"/>
    !# <objectBuilder class="cosmologyParameters"      name="cosmologyParameters_"      source="parameters"/>
    !# <objectBuilder class="criticalOverdensity"      name="criticalOverdensity_"      source="parameters"/>
    !# <objectBuilder class="cosmologicalMassVariance" name="cosmologicalMassVariance_" source="parameters"/>
    !# <objectBuilder class="powerSpectrum"            name="powerSpectrum_"            source="parameters"/>
     self=darkMatterProfileConcentrationDiemerKravtsov2014(kappa,phi0,phi1,eta0,eta1,alpha,beta,scatter,cosmologyFunctions_,cosmologyParameters_,criticalOverdensity_,cosmologicalMassVariance_,powerSpectrum_)
    !# <inputParametersValidate source="parameters"/>
    return
  end function diemerKravtsov2014ConstructorParameters
  
  function diemerKravtsov2014ConstructorInternal(kappa,phi0,phi1,eta0,eta1,alpha,beta,scatter,cosmologyFunctions_,cosmologyParameters_,criticalOverdensity_,cosmologicalMassVariance_,powerSpectrum_) result(self)
    !% Constructor for the {\normalfont \ttfamily diemerKravtsov2014} dark matter halo profile
    !% concentration class.
    use Galacticus_Error
    implicit none
    type            (darkMatterProfileConcentrationDiemerKravtsov2014)                        :: self
    double precision                                                  , intent(in   )         :: kappa                    , phi0, phi1, eta0, eta1, alpha, beta, scatter
    class           (cosmologyFunctionsClass                         ), intent(in   ), target :: cosmologyFunctions_
    class           (cosmologyParametersClass                        ), intent(in   ), target :: cosmologyParameters_     
    class           (criticalOverdensityClass                        ), intent(in   ), target :: criticalOverdensity_     
    class           (cosmologicalMassVarianceClass                   ), intent(in   ), target :: cosmologicalMassVariance_
    class           (powerSpectrumClass                              ), intent(in   ), target :: powerSpectrum_
    !# <constructorAssign variables="kappa, phi0, phi1, eta0, eta1, alpha, beta, scatter, *cosmologyFunctions_, *cosmologyParameters_, *criticalOverdensity_, *cosmologicalMassVariance_, *powerSpectrum_"/>

    self%timePrevious             =-1.0d0
    self%massPrevious             =-1.0d0
    self%concentrationMeanPrevious=-1.0d0
    return
  end function diemerKravtsov2014ConstructorInternal

  subroutine diemerKravtsov2014Destructor(self)
    !% Destructor for the {\normalfont \ttfamily diemerKravtsov2014} dark matter halo profile concentration class.
    implicit none
    type(darkMatterProfileConcentrationDiemerKravtsov2014), intent(inout) :: self
    
    !# <objectDestructor name="self%cosmologyFunctions_"       />
    !# <objectDestructor name="self%cosmologyParameters_"      />
    !# <objectDestructor name="self%criticalOverdensity_"      />
    !# <objectDestructor name="self%cosmologicalMassVariance_" />
    !# <objectDestructor name="self%powerSpectrum_"            />
    return
  end subroutine diemerKravtsov2014Destructor

  double precision function diemerKravtsov2014Concentration(self,node)
    !% Return the concentration of the dark matter halo profile of {\normalfont \ttfamily node}
    !% using the \cite{diemer_universal_2014} algorithm.
    implicit none
    class(darkMatterProfileConcentrationDiemerKravtsov2014), intent(inout), target :: self
    type (treeNode                                        ), intent(inout), target :: node

    ! Get the mean concentration.
    diemerKravtsov2014Concentration=self%concentrationMean(node)
    ! Add scatter if necessary.
    if (self%scatter > 0.0d0)                                            &
         &  diemerKravtsov2014Concentration                              &
         & =diemerKravtsov2014Concentration                              &
         & *10.0d0**(                                                    &
         &           +self%scatter                                       &
         &           *node%hostTree%randomNumberGenerator%normalSample() &
         &          )
    return
  end function diemerKravtsov2014Concentration

  double precision function diemerKravtsov2014ConcentrationMean(self,node)
    !% Return the mean concentration of the dark matter halo profile of {\normalfont \ttfamily node}
    !% using the \cite{diemer_universal_2014} algorithm.
    use Numerical_Constants_Math
    use Math_Exponentiation
    use Galacticus_Nodes        , only : nodeComponentBasic
    implicit none
    class           (darkMatterProfileConcentrationDiemerKravtsov2014), intent(inout)          :: self
    type            (treeNode                                        ), intent(inout), target  :: node
    class           (nodeComponentBasic                              )               , pointer :: basic
    double precision                                                                           :: radiusHaloLagrangian, peakHeight        , &
         &                                                                                        wavenumber          , powerSpectrumSlope, &
         &                                                                                        concentrationMinimum, peakHeightMinimum
    
    basic => node%basic()
    if     (                                   &
         &   basic%mass() /= self%massPrevious &
         &  .or.                               &
         &   basic%time() /= self%timePrevious &
         & ) then
        radiusHaloLagrangian         =+cubeRoot(                                                                        &
            &                                  +3.0d0                                                                   &
            &                                  *basic%mass()                                                            &
            &                                  /4.0d0                                                                   &
            &                                  /Pi                                                                      &
            &                                  /self%cosmologyParameters_%densityCritical()                             &
            &                                  /self%cosmologyParameters_%OmegaMatter    ()                             &
            &                                 )
       peakHeight                    =+self%criticalOverdensity_     %value       (time=basic%time(),mass=basic%mass()) &
            &                         /self%cosmologicalMassVariance_%rootVariance(                       basic%mass())
       wavenumber                    =+self%kappa                                                                       &
            &                         *2.0d0                                                                            &
            &                         *Pi                                                                               &
            &                         /radiusHaloLagrangian
       powerSpectrumSlope            =+self%powerSpectrum_%powerLogarithmicDerivative(wavenumber)
       concentrationMinimum          =+self%phi0                                                                        &
            &                         +self%phi1                                                                        &
            &                         *powerSpectrumSlope
       peakHeightMinimum             =+self%eta0                                                                        &
            &                         +self%eta1                                                                        &
            &                         *powerSpectrumSlope
       self%concentrationMeanPrevious=+0.5d0                                                                            &
            &                         *concentrationMinimum                                                             &
            &                         *(                                                                                &
            &                           +(peakHeight/peakHeightMinimum)**(-self%alpha)                                  &
            &                           +(peakHeight/peakHeightMinimum)**(+self%beta )                                  &
            &                          )
       self%massPrevious             = basic%mass()
       self%timePrevious             = basic%time()
    end if
    diemerKravtsov2014ConcentrationMean=self%concentrationMeanPrevious
    return
  end function diemerKravtsov2014ConcentrationMean

  function diemerKravtsov2014DensityContrastDefinition(self)
    !% Return a virial density contrast object defining that used in the definition of concentration in the
    !% \cite{diemer_universal_2014} algorithm.
    implicit none
    class  (virialDensityContrastClass                      ), pointer                     :: diemerKravtsov2014DensityContrastDefinition
    class  (darkMatterProfileConcentrationDiemerKravtsov2014), intent(inout)               :: self
    class  (virialDensityContrastClass                      ), allocatable  , target, save :: densityContrastDefinition
    logical                                                                         , save :: densityContrastDefinitionInitialized=.false.
    !$omp threadprivate(densityContrastDefinition,densityContrastDefinitionInitialized)
    !GCC$ attributes unused :: self
    
    if (.not.densityContrastDefinitionInitialized) then
       allocate(virialDensityContrastFixed :: densityContrastDefinition)
       select type (densityContrastDefinition)
       type is (virialDensityContrastFixed)
          densityContrastDefinition=virialDensityContrastFixed(200.0d0,fixedDensityTypeCritical,self%cosmologyFunctions_)
          call densityContrastDefinition%makeIndestructible()
       end select
       densityContrastDefinitionInitialized=.true.
    end if
    diemerKravtsov2014DensityContrastDefinition => densityContrastDefinition
    return
  end function diemerKravtsov2014DensityContrastDefinition

  function diemerKravtsov2014DarkMatterProfileDefinition(self)
    !% Return a dark matter density profile object defining that used in the definition of concentration in the
    !% \cite{diemer_universal_2014} algorithm.
    use Dark_Matter_Halo_Scales
    implicit none
    class  (darkMatterProfileClass                            ), pointer                     :: diemerKravtsov2014DarkMatterProfileDefinition
    class  (darkMatterProfileConcentrationDiemerKravtsov2014  ), intent(inout)               :: self
    class  (darkMatterHaloScaleVirialDensityContrastDefinition), pointer                     :: darkMatterHaloScaleDefinition
    class  (darkMatterProfileClass                            ), allocatable  , target, save :: densityProfileDefinition
    logical                                                                           , save :: densityProfileDefinitionInitialized=.false.
    !$omp threadprivate(densityProfileDefinition,densityProfileDefinitionInitialized)
    
    if (.not.densityProfileDefinitionInitialized) then
       allocate(darkMatterProfileNFW                               :: densityProfileDefinition     )
       allocate(darkMatterHaloScaleVirialDensityContrastDefinition :: darkMatterHaloScaleDefinition)
       select type (densityProfileDefinition)
       type is (darkMatterProfileNFW)
          select type (darkMatterHaloScaleDefinition)
          type is (darkMatterHaloScaleVirialDensityContrastDefinition)
             darkMatterHaloScaleDefinition=darkMatterHaloScaleVirialDensityContrastDefinition(self%cosmologyParameters_,self%cosmologyFunctions_,self%densityContrastDefinition())
             densityProfileDefinition     =darkMatterProfileNFW                              (darkMatterHaloScaleDefinition                                                      )
             call densityProfileDefinition%makeIndestructible()
          end select
       end select
       densityProfileDefinitionInitialized=.true.
    end if
    diemerKravtsov2014DarkMatterProfileDefinition => densityProfileDefinition
    return
  end function diemerKravtsov2014DarkMatterProfileDefinition
