!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!% Contains a module which outputs the structure of entire merger trees.

module Merger_Tree_Output_Structure
  !% Outputs the structure of entire merger trees.
  use IO_HDF5
  implicit none
  private
  public :: Merger_Tree_Structure_Output, Merger_Tree_Structure_PreClose

  ! Flag indicating if module is initialized.
  logical :: structureOutputModuleInitialized         =.false.

  ! Flag indicating if output is required.
  logical :: mergerTreeStructureOutput

  ! Flag indicating if virial quantities should be included in output.
  logical :: mergerTreeStructureOutputVirialQuantities

  ! Group to which merger tree structures are written.
  type(hdf5Object) :: structureGroup

contains

  !# <mergerTreePreEvolveTask>
  !#   <unitName>Merger_Tree_Structure_Output</unitName>
  !# </mergerTreePreEvolveTask>
  subroutine Merger_Tree_Structure_Output(tree)
    !% Output the structure of {\normalfont \ttfamily tree}.
    use Galacticus_Nodes
    use Input_Parameters
    use Memory_Management
    use Galacticus_HDF5
    use ISO_Varying_String
    use String_Handling
    use Dark_Matter_Halo_Scales
    use Numerical_Constants_Astronomical
    use Merger_Tree_Walkers
    !# <include directive="mergerTreeStructureOutputTask" type="moduleUse">
    include 'merger_trees.output_structure.tasks.modules.inc'
    !# </include>
    implicit none
    type            (mergerTree                   ), intent(in   ), target                :: tree
    type            (treeNode                     )                             , pointer :: node
    integer         (kind=kind_int8               ), allocatable  , dimension(:)          :: nodeIndex
    double precision                               , allocatable  , dimension(:)          :: nodeProperty
    class           (nodeComponentBasic           )                             , pointer :: basic
    type            (mergerTree                   )                             , pointer :: currentTree
    class           (darkMatterHaloScaleClass     )                             , pointer :: darkMatterHaloScale_
    integer                                                                               :: nodeCount
    type            (varying_string               )                                       :: groupComment        , groupName
    type            (hdf5Object                   )                                       :: nodeDataset         , treeGroup
    type            (mergerTreeWalkerIsolatedNodes)                                       :: treeWalker
    
    ! Check if module is initialized.
    if (.not.structureOutputModuleInitialized) then
       !$omp critical(structureOutputModuleInitialize)
       if (.not.structureOutputModuleInitialized) then
          ! Get parameter specifying if output is required.
          !# <inputParameter>
          !#   <name>mergerTreeStructureOutput</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not to output the structure of merger trees prior to evolution.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          !# <inputParameter>
          !#   <name>mergerTreeStructureOutputVirialQuantities</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether or not to output virial quantities (radius and velocity) when outputting the structure of merger trees prior to evolution.</description>
          !#   <group>output</group>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !# </inputParameter>
          ! Create an output group if necessary.
          !$ call hdf5Access%set()
          if (mergerTreeStructureOutput) structureGroup=galacticusOutputFile%openGroup('mergerTreeStructures','Pre-evolution structures of merger trees.')
          !$ call hdf5Access%unset()
          ! Flag that module is initialized.
          structureOutputModuleInitialized=.true.
       end if
       !$omp end critical(structureOutputModuleInitialize)
    end if

    ! Output the tree structure history.
    if (mergerTreeStructureOutput) then
       ! Get required classes.
       darkMatterHaloScale_ => darkMatterHaloScale()
       ! Iterate over trees.
       currentTree => tree
       do while (associated(currentTree))
          ! Count up number of nodes in the tree.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             nodeCount=nodeCount+1
          end do
          ! Allocate storage space.
          call allocateArray(nodeIndex   ,[nodeCount])
          call allocateArray(nodeProperty,[nodeCount])
          ! Create a group for this tree structure.
          groupName   ='mergerTree'
          groupName   =groupName//currentTree%index
          groupComment='Pre-evolution structure of merger tree.'
          !$ call hdf5Access%set()
          treeGroup   =structureGroup%openGroup(char(groupName),'Pre-evolution structure of merger tree.')
          !$ call hdf5Access%unset()

          ! Extract node indices and output to file.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             nodeCount=nodeCount+1
             nodeIndex(nodeCount)=node%index()
          end do
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeIndex,'nodeIndex','Index of the node.')
          !$ call hdf5Access%unset()

          ! Extract child node indices and output to file.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             nodeCount=nodeCount+1
             nodeIndex(nodeCount)=node%firstChild%index()
          end do
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeIndex,'childIndex','Index of the child node.')
          !$ call hdf5Access%unset()

          ! Extract parent node indices and output to file.
          nodeCount=0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             nodeCount=nodeCount+1
             nodeIndex(nodeCount)=node%parent%index()
          end do
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeIndex,'parentIndex','Index of the parent node.')
          !$ call hdf5Access%unset()

          ! Extract sibling node indices and output to file.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             nodeCount=nodeCount+1
             nodeIndex(nodeCount)=node%sibling%index()
          end do
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeIndex,'siblingIndex','Index of the sibling node.')
          !$ call hdf5Access%unset()

          ! Extract node masses and output to file.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             basic => node%basic()
             nodeCount=nodeCount+1
             nodeProperty(nodeCount)=basic%mass()
          end do
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeProperty,'nodeMass','Mass of node.',datasetReturned=nodeDataset)
          call nodeDataset%writeAttribute(massSolar,"unitsInSI")
          call nodeDataset%close()
          !$ call hdf5Access%unset()

          ! Extract node times and output to file.
          nodeCount =0
          treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
          do while (treeWalker%next(node))
             basic => node%basic()
             nodeCount=nodeCount+1
             nodeProperty(nodeCount)=basic%time()
          end do          
          !$ call hdf5Access%set()
          call treeGroup%writeDataset(nodeProperty,'nodeTime','Time at node.',datasetReturned=nodeDataset)
          call nodeDataset%writeAttribute(gigaYear,"unitsInSI")
          call nodeDataset%close()
          !$ call hdf5Access%unset()

          ! Check whether output of virial quantities is required.
          if (mergerTreeStructureOutputVirialQuantities) then

             ! Extract node virial radii and output to file.
             nodeCount =0
             treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
             do while (treeWalker%next(node))
                nodeCount=nodeCount+1
                nodeProperty(nodeCount)=darkMatterHaloScale_%virialRadius(node)                
             end do
             !$ call hdf5Access%set()
             call treeGroup%writeDataset(nodeProperty,'nodeVirialRadius','Virial radius of the node [Mpc].',datasetReturned=nodeDataset)
             call nodeDataset%writeAttribute(megaParsec,"unitsInSI")
             call nodeDataset%close()
             !$ call hdf5Access%unset()

             ! Extract node virial velocity and output to file.
             nodeCount =0
             treeWalker=mergerTreeWalkerIsolatedNodes(currentTree)
             do while (treeWalker%next(node))
                nodeCount=nodeCount+1
                nodeProperty(nodeCount)=darkMatterHaloScale_%virialVelocity(node)
             end do
             !$ call hdf5Access%set()
             call treeGroup%writeDataset(nodeProperty,'nodeVirialVelocity','Virial velocity of the node [km/s].',datasetReturned=nodeDataset)
             call nodeDataset%writeAttribute(kilo,"unitsInSI")
             call nodeDataset%close()
             !$ call hdf5Access%unset()

          end if

          !$ call hdf5Access%set()
          ! Call any subroutines that want to attach data to the merger tree output.
          !# <include directive="mergerTreeStructureOutputTask" type="functionCall" functionType="void">
          !#  <functionArgs>currentTree%baseNode,nodeProperty,treeGroup</functionArgs>
          include 'merger_trees.output_structure.tasks.inc'
          !# </include>

          ! Close output group.
          call treeGroup%close()
          !$ call hdf5Access%unset()

          ! Deallocate storage space.
          call deallocateArray(nodeIndex   )
          call deallocateArray(nodeProperty)
          ! Move to the next tree.
          currentTree => currentTree%nextTree
       end do
    end if

    return
  end subroutine Merger_Tree_Structure_Output

  !# <hdfPreCloseTask>
  !#  <unitName>Merger_Tree_Structure_PreClose</unitName>
  !# </hdfPreCloseTask>
  subroutine Merger_Tree_Structure_PreClose
    !% Close the merger tree structure group.
    implicit none

    if (mergerTreeStructureOutput) call structureGroup%close()
    return
  end subroutine Merger_Tree_Structure_PreClose

end module Merger_Tree_Output_Structure
