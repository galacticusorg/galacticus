!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implements calculations of satellite merging times using the \cite{boylan-kolchin_dynamical_2008} method.

  use Dark_Matter_Halo_Scales
  use Dark_Matter_Profiles
  
  !# <satelliteMergingTimescales name="satelliteMergingTimescalesBoylanKolchin2008">
  !#  <description>Computes the merging timescale using the method of \cite{boylan-kolchin_dynamical_2008}.</description>
  !# </satelliteMergingTimescales>
  type, extends(satelliteMergingTimescalesClass) :: satelliteMergingTimescalesBoylanKolchin2008
     !% A class implementing the \cite{boylan-kolchin_dynamical_2008} method for satellite merging timescales.
     private
     class           (darkMatterHaloScaleClass), pointer :: darkMatterHaloScale_
     class           (darkMatterProfileClass  ), pointer :: darkMatterProfile_
     double precision                                    :: timescaleMultiplier
   contains
     final     ::                     boylanKolchin2008Destructor
     procedure :: timeUntilMerging => boylanKolchin2008TimeUntilMerging
  end type satelliteMergingTimescalesBoylanKolchin2008

  interface satelliteMergingTimescalesBoylanKolchin2008
     !% Constructors for the {\normalfont \ttfamily boylanKolchin2008} satellite merging timescale class.
     module procedure boylanKolchin2008ConstructorParameters
     module procedure boylanKolchin2008ConstructorInternal
  end interface satelliteMergingTimescalesBoylanKolchin2008

contains

  function boylanKolchin2008ConstructorParameters(parameters) result(self)
    !% A constructor for the {\normalfont \ttfamily boylanKolchin2008} satellite merging timescale class which builds the object from a
    !% parameter set.
    use Input_Parameters
    implicit none
    type            (satelliteMergingTimescalesBoylanKolchin2008)                :: self
    type            (inputParameters                            ), intent(inout) :: parameters
    class           (darkMatterHaloScaleClass                   ), pointer       :: darkMatterHaloScale_
    class           (darkMatterProfileClass                     ), pointer       :: darkMatterProfile_
    double precision                                                             :: timescaleMultiplier
    
    !# <inputParameter>
    !#   <name>timescaleMultiplier</name>
    !#   <cardinality>1</cardinality>
    !#   <defaultValue>0.75d0</defaultValue>
    !#   <description>A multiplier for the merging timescale in dynamical friction timescale calculations.</description>
    !#   <source>parameters</source>
    !#   <type>real</type>
    !# </inputParameter>
    !# <objectBuilder class="darkMatterHaloScale" name="darkMatterHaloScale_" source="parameters"/>
    !# <objectBuilder class="darkMatterProfile"   name="darkMatterProfile_"   source="parameters"/>
    self=satelliteMergingTimescalesBoylanKolchin2008(timescaleMultiplier,darkMatterHaloScale_,darkMatterProfile_)
    !# <inputParametersValidate source="parameters"/>
    return
  end function boylanKolchin2008ConstructorParameters

  function boylanKolchin2008ConstructorInternal(timescaleMultiplier,darkMatterHaloScale_,darkMatterProfile_) result(self)
    !% Default constructor for the {\normalfont \ttfamily boylanKolchin2008} satellite merging timescale class.
    implicit none
    type            (satelliteMergingTimescalesBoylanKolchin2008)                        :: self
    double precision                                             , intent(in   )         :: timescaleMultiplier
    class           (darkMatterHaloScaleClass                   ), intent(in   ), target :: darkMatterHaloScale_
    class           (darkMatterProfileClass                     ), intent(in   ), target :: darkMatterProfile_
    !# <constructorAssign variables="timescaleMultiplier, *darkMatterHaloScale_, *darkMatterProfile_"/>

    return
  end function boylanKolchin2008ConstructorInternal

  subroutine boylanKolchin2008Destructor(self)
    !% Destructor for the {\normalfont \ttfamily boylanKolchin2008} satellite merging timescale class.
    implicit none
    type(satelliteMergingTimescalesBoylanKolchin2008), intent(inout) :: self

    !# <objectDestructor name="self%darkMatterHaloScale_"/>
    !# <objectDestructor name="self%darkMatterProfile_"  />
    return
  end subroutine boylanKolchin2008Destructor

  double precision function boylanKolchin2008TimeUntilMerging(self,node,orbit)
    !% Return the timescale for merging satellites using the \cite{boylan-kolchin_dynamical_2008} method.
    use Galacticus_Nodes
    use Galacticus_Error
    use Kepler_Orbits
    use Satellite_Orbits
    implicit none
    class           (satelliteMergingTimescalesBoylanKolchin2008), intent(inout) :: self
    type            (treeNode                                   ), intent(inout) :: node
    type            (keplerOrbit                                ), intent(inout) :: orbit
    type            (treeNode                                   ), pointer       :: nodeHost
    class           (nodeComponentBasic                         ), pointer       :: basicHost                            , basic
    logical                                                      , parameter     :: acceptUnboundOrbits          =.false.
    double precision                                             , parameter     :: expArgumentMaximum           =100.0d0
    double precision                                             , parameter     :: A                            =0.216d0, b                 =1.3d0, &  !   Fitting parameters from eqn. (6) of Boylan-Kolchin et al.
         &                                                                          c                            =1.9d0  , d                 =1.0d0
    double precision                                                             :: equivalentCircularOrbitRadius        , massRatio               , &
         &                                                                          orbitalCircularity                   , radialScale             , &
         &                                                                          velocityScale                        , expArgument
    integer                                                                      :: errorCode
    !GCC$ attributes unused :: self
    
    ! Find the host node.
    nodeHost => node%parent
    ! Get velocity scale.
    velocityScale=self%darkMatterHaloScale_%virialVelocity(nodeHost)
    radialScale  =self%darkMatterHaloScale_%virialRadius  (nodeHost)
    ! Get the equivalent circular orbit.
    equivalentCircularOrbitRadius=Satellite_Orbit_Equivalent_Circular_Orbit_Radius(nodeHost,orbit,self%darkMatterHaloScale_,self%darkMatterProfile_,errorCode)
    ! Check error codes.
    select case (errorCode)
    case (errorCodeOrbitUnbound     )
       orbitalCircularity               =1.0d0
       boylanKolchin2008TimeUntilMerging=satelliteMergeTimeInfinite
       return
    case (errorCodeNoEquivalentOrbit)
       ! Circularity is not defined. Assume instantaneous merging.
       orbitalCircularity               =1.0d0
       boylanKolchin2008TimeUntilMerging=0.0d0
       return
    case (errorCodeSuccess          )
       ! Compute orbital circularity.
       orbitalCircularity                                                                       &
            & =orbit%angularMomentum()                                                          &
            & /equivalentCircularOrbitRadius                                                    &
            & /self%darkMatterProfile_%circularVelocity(nodeHost,equivalentCircularOrbitRadius)
    case default
       orbitalCircularity=0.0d0
       call Galacticus_Error_Report('unrecognized error code'//{introspection:location})
    end select
    ! Compute mass ratio (mass in host [not including satellite] divided by mass in satellite).
    basic => node%basic()
    basicHost => nodeHost%basic()
    massRatio=basicHost%mass()/basic%mass()-1.0d0
    if (massRatio <= 0.0d0) then
       ! Assume zero merging time as the satellite is as massive as the host.
       boylanKolchin2008TimeUntilMerging=0.0d0
    else
       ! Compute dynamical friction timescale.
       expArgument=min(expArgumentMaximum,c*orbitalCircularity)
       boylanKolchin2008TimeUntilMerging=+self%timescaleMultiplier                               &
            &                            *self%darkMatterHaloScale_%dynamicalTimescale(nodeHost) &
            &                            *A                                                      &
            &                            *          massRatio**b                                 &
            &                            /log(1.0d0+massRatio   )                                &
            &                            *exp(expArgument)                                       &
            &                            *(equivalentCircularOrbitRadius/radialScale)**d
    end if
    return
  end function boylanKolchin2008TimeUntilMerging
