!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

!+    Contributions to this file made by:  Luiz Felippe S. Rodrigues.

  !% An implementation of the intergalactic medium state class in which state is computed using {\normalfont \scshape RecFast}.

  use File_Utilities

  !# <intergalacticMediumState name="intergalacticMediumStateRecFast">
  !#  <description>The intergalactic medium state is computed using {\normalfont \scshape RecFast}.</description>
  !# </intergalacticMediumState>
  type, extends(intergalacticMediumStateFile) :: intergalacticMediumStateRecFast
     !% An \gls{igm} state class which computes state using {\normalfont \scshape RecFast}.
     private
     type (lockDescriptor) :: fileLock
  end type intergalacticMediumStateRecFast
  
  interface intergalacticMediumStateRecFast
     !% Constructors for the {\normalfont \scshape RecFast} intergalactic medium state class.
     module procedure recFastConstructorParameters
     module procedure recFastConstructorInternal
  end interface intergalacticMediumStateRecFast

contains

  function recFastConstructorParameters(parameters) result(self)
    !% Default constructor for the {\normalfont \scshape RecFast} \gls{igm} state class.
    use Input_Parameters
    implicit none
    type (intergalacticMediumStateRecFast)                :: self
    type (inputParameters                ), intent(inout) :: parameters
    class(cosmologyFunctionsClass        ), pointer       :: cosmologyFunctions_
    class(cosmologyParametersClass       ), pointer       :: cosmologyParameters_
    class(linearGrowthClass              ), pointer       :: linearGrowth_

    ! Check and read parameters.
    !# <objectBuilder class="cosmologyFunctions"  name="cosmologyFunctions_"  source="parameters"/>
    !# <objectBuilder class="cosmologyParameters" name="cosmologyParameters_" source="parameters"/>
    !# <objectBuilder class="linearGrowth"        name="linearGrowth_"        source="parameters"/>
    self=intergalacticMediumStateRecFast(cosmologyFunctions_,cosmologyParameters_,linearGrowth_)
    !# <inputParametersValidate source="parameters"/>
    !# <objectDestructor name="cosmologyFunctions_" />
    !# <objectDestructor name="cosmologyParameters_"/>
    !# <objectDestructor name="linearGrowth_"       />
    return
  end function recFastConstructorParameters

  function recFastConstructorInternal(cosmologyFunctions_,cosmologyParameters_,linearGrowth_) result(self)
    !% Constructor for the {\normalfont \scshape RecFast} \gls{igm} state class.
    use Cosmology_Parameters            , only : hubbleUnitsStandard
    use System_Command
    use Numerical_Constants_Astronomical
    use Galacticus_Paths
    use Galacticus_Error
    use Galacticus_Display
    use File_Utilities
    use Dates_and_Times
    use IO_HDF5
    use Interfaces_RecFast
    implicit none
    type            (intergalacticMediumStateRecFast)                              :: self
    class           (cosmologyFunctionsClass        ), intent(in   ), target       :: cosmologyFunctions_
    class           (cosmologyParametersClass       ), intent(in   ), target       :: cosmologyParameters_
    class           (linearGrowthClass              ), intent(in   ), target       :: linearGrowth_
    double precision                                 , allocatable  , dimension(:) :: redshift            , electronFraction , &
         &                                                                            hIonizedFraction    , heIonizedFraction, &
         &                                                                            matterTemperature
    character       (len=32                         )                              :: parameterLabel
    type            (varying_string                 )                              :: parameterFile       , recFastFile      , &
         &                                                                            recfastPath         , recfastVersion
    double precision                                                               :: omegaDarkMatter
    integer                                                                        :: fileFormatVersion   , i                , &
         &                                                                            countRedshift       , parametersUnit   , &
         &                                                                            recFastUnit
    type            (hdf5Object                     )                              :: outputFile          , dataset          , &
         &                                                                            provenance          , recFastProvenance
    logical                                                                        :: buildFile
    !# <constructorAssign variables="*cosmologyFunctions_, *cosmologyParameters_, *linearGrowth_"/>

    ! Compute dark matter density.
    omegaDarkMatter=self%cosmologyParameters_%OmegaMatter()-self%cosmologyParameters_%OmegaBaryon()
    ! Construct the file name.
    self%fileName=char(galacticusPath(pathTypeDataDynamic))//'intergalacticMedium/recFast'
    write (parameterLabel,'(f6.4)') self%cosmologyParameters_%OmegaMatter    (                   )
    self%fileName=self%fileName//'_OmegaMatter'    //trim(parameterLabel)
    write (parameterLabel,'(f6.4)') self%cosmologyParameters_%OmegaDarkEnergy(                   )
    self%fileName=self%fileName//'_OmegaDarkEnergy'//trim(parameterLabel)
    write (parameterLabel,'(f6.4)') self%cosmologyParameters_%OmegaBaryon    (                   )
    self%fileName=self%fileName//'_OmegaBaryon'    //trim(parameterLabel)
    write (parameterLabel,'(f4.1)') self%cosmologyParameters_%HubbleConstant (hubbleUnitsStandard)
    self%fileName=self%fileName//'_HubbleConstant' //trim(parameterLabel)
    write (parameterLabel,'(f6.4)') self%cosmologyParameters_%temperatureCMB (                   )
    self%fileName=self%fileName//'_temperatureCMB' //trim(parameterLabel)
    write (parameterLabel,'(f4.2)') heliumByMassPrimordial
    self%fileName=self%fileName//'_YHe'            //trim(parameterLabel)
    self%fileName=self%fileName//'.hdf5'
    ! Create directory for output.
    call System_Command_Do('mkdir -p '//char(galacticusPath(pathTypeDataDynamic))//'intergalacticMedium')
    ! Lock file
    call File_Lock_Initialize(self%fileLock)
    call File_Lock(char(self%fileName),self%fileLock,lockIsShared=.true.)
    ! Check existance of file.
    buildFile=.false.
    if (File_Exists(char(self%fileName))) then
       ! Check file version number.
       !$ call hdf5Access%set()
       call outputFile%openFile     (char(self%fileName),overwrite=.false.          )
       call outputFile%readAttribute('fileFormat'       ,          fileFormatVersion)
       call outputFile%close        (                                               )
       !$ call hdf5Access%unset()
       buildFile=fileFormatVersion /= fileFormatVersionCurrent
    else
       buildFile=.true.
    end if
    ! Build file if necessary.
    if (buildFile) then
       call File_Unlock(                    self%fileLock                     )
       call File_Lock  (char(self%fileName),self%fileLock,lockIsShared=.false.)
       ! Initialize RecFast.
       call Interface_RecFast_Initialize(recfastPath,recfastVersion)
       ! Build RecFast parameter file.
       parameterFile=File_Name_Temporary("recFastParameters")
       recFastFile  =parameterFile//".out"
       open(newUnit=parametersUnit,file=char(parameterFile),status='unknown',form='formatted')
       write (parametersUnit,'(a)')  char(recFastFile)
       write (parametersUnit,*    )  self%cosmologyParameters_%OmegaBaryon   (),omegaDarkMatter                           ,self%cosmologyParameters_%OmegaDarkEnergy()
       write (parametersUnit,*    )  self%cosmologyParameters_%HubbleConstant(),self%cosmologyParameters_%temperatureCMB(),heliumByMassPrimordial
       write (parametersUnit,*    )  1
       write (parametersUnit,*    )  6
       close(parametersUnit)
       ! Run RecFast.
       call System_Command_Do(recfastPath//"recfast.exe < "//parameterFile)
       ! Parse the output file.
       countRedshift=Count_Lines_in_File(recFastFile)-1
       allocate(redshift         (countRedshift))
       allocate(electronFraction (countRedshift))
       allocate(hIonizedFraction (countRedshift))
       allocate(heIonizedFraction(countRedshift))
       allocate(matterTemperature(countRedshift))
       open(newUnit=recFastUnit,file=char(recFastFile),status='old',form='formatted')
       read (recFastUnit,*) ! Skip header line.
       do i=1,countRedshift
          read (recFastUnit,*) redshift(i),electronFraction(i),hIonizedFraction(i),heIonizedFraction(i),matterTemperature(i)
       end do
       close(recFastUnit)
       call File_Remove(char(recFastFile  ))
       call File_Remove(char(parameterFile))
       ! Create the output file.
       !$ call hdf5Access%set()
       call outputFile%openFile      (char(self%fileName),overwrite=.true.)
       call outputFile%writeDataset  (redshift           ,'redshift'         ,'Redshift'                                            )
       call outputFile%writeDataset  (electronFraction   ,'electronFraction' ,'Electron fraction'                                   )
       call outputFile%writeDataset  (hIonizedFraction   ,'hIonizedFraction' ,'Fraction of ionized hydrogen'                        )
       call outputFile%writeDataset  (heIonizedFraction  ,'heIonizedFraction','Fraction of ionized helium'                          )
       call outputFile%writeDataset  (matterTemperature  ,'matterTemperature','Temperature of matter'       ,datasetReturned=dataset)
       call dataset   %writeAttribute('Kelvin'           ,'units'                                                                   )
       call dataset   %writeAttribute(1.0d0              ,'unitsInSI'                                                               )
       call dataset   %close         (                                                                                              )
       ! Add description and provenance to output structure.
       call outputFile%writeAttribute('IGM ionization/thermal state computed using RecFast','description')
       call outputFile%writeAttribute(fileFormatVersionCurrent                             ,'fileFormat' )
       provenance=outputFile%openGroup('provenance')
       call provenance%writeAttribute(char(Formatted_Date_and_Time())                      ,'date'       )
       call provenance%writeAttribute('Galacticus via RecFast'                             ,'source'     )
       recFastProvenance=provenance%openGroup('recFast'                                                                                            )
       call recFastProvenance%writeAttribute(trim(recFastVersion)                                                                        ,'version')
       call recFastProvenance%writeAttribute('Includes modification of H recombination. Includes all modifications for HeI recombination','notes'  )
       call recFastProvenance%close         (                                                                                                      )
       call provenance       %close         (                                                                                                      )
       call outputFile       %close         (                                                                                                      )
       !$ call hdf5Access%unset()
    end if
    call File_Unlock(self%fileLock)
    return
  end function recFastConstructorInternal
