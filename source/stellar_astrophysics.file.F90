!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017, 2018,
!!           2019
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% Implements a stellar astrophysics class in which the stellar properties are read from file and interpolated.
  
  use Numerical_Interpolation_2D_Irregular

  !# <stellarAstrophysics name="stellarAstrophysicsFile">
  !#  <description>A stellar astrophysics class in which the stellar properties are read from file and interpolated.</description>
  !# </stellarAstrophysics>
  type, extends(stellarAstrophysicsClass) :: stellarAstrophysicsFile
     !% A stellar astrophysics class in which the stellar properties are read from file and interpolated.
     private
     type            (varying_string         )                              :: fileName
     double precision                         , allocatable, dimension(:  ) :: lifetimeLifetime                 , lifetimeMass                   , &
          &                                                                    lifetimeMetallicity
     double precision                         , allocatable, dimension(:  ) :: massEjectedMassEjected           , massEjectedMass                , &
          &                                                                    massEjectedMetallicity
     double precision                         , allocatable, dimension(:  ) :: yieldMetals                      , yieldMetalsMass                , &
          &                                                                    yieldMetalsMetallicity
     double precision                         , allocatable, dimension(:,:) :: yieldElement                     , yieldElementMass               , &
          &                                                                    yieldElementMetallicity
     integer                                  , allocatable, dimension(:  ) :: atomIndexMap                     , countYieldElement
     integer                                                                :: countElement
     type            (interp2dIrregularObject)                              :: interpolationWorkspaceMassInitial, interpolationWorkspaceLifetime , &
          &                                                                    interpolationWorkspaceMassEjected, interpolationWorkspaceMassYield
     logical                                                                :: interpolationResetMassInitial    , interpolationResetLifetime     , &
          &                                                                    interpolationResetMassEjected    , interpolationResetMassYield    , &
          &                                                                    readDone
  contains
     !@ <objectMethods>
     !@   <object>stellarAstrophysicsFile</object>
     !@   <objectMethod>
     !@     <method>read</method>
     !@     <arguments></arguments>
     !@     <type>\void</type>
     !@     <description>Read stellar astrophysics data from file.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     procedure :: massInitial => fileMassInitial
     procedure :: massEjected => fileMassEjected
     procedure :: massYield   => fileMassYield
     procedure :: lifetime    => fileLifetime
     procedure :: read        => fileRead
  end type stellarAstrophysicsFile
  
  interface stellarAstrophysicsFile
     !% Constructors for the {\normalfont \ttfamily file} stellar astrophysics class.
     module procedure fileConstructorParameters
     module procedure fileConstructorInternal
  end interface stellarAstrophysicsFile

  ! Current file format version.
  integer, parameter :: fileFormatVersionCurrent=1

contains

  function fileConstructorParameters(parameters) result(self)
    !% Constructor for the {\normalfont \ttfamily file} stellar astrophysics class which takes a parameter list as input.
    use Galacticus_Paths
    use Input_Parameters
    implicit none
    type(stellarAstrophysicsFile)                :: self
    type(inputParameters        ), intent(inout) :: parameters
    type(varying_string         )                :: fileName
    
    !# <inputParameter>
    !#   <name>fileName</name>
    !#   <defaultValue>galacticusPath(pathTypeDataStatic)//'stellarAstrophysics/Stellar_Properties_Compilation.xml'</defaultValue>
    !#   <description>The name of the XML file from which to read stellar properties (ejected masses, yields, etc.).</description>
    !#   <type>string</type>
    !#   <cardinality>1</cardinality>
    !#   <source>parameters</source>
    !# </inputParameter>
    self=stellarAstrophysicsFile(char(fileName))
    !# <inputParametersValidate source="parameters"/>
    return
  end function fileConstructorParameters

  function fileConstructorInternal(fileName) result(self)
    !% Internal constructor for the {\normalfont \ttfamily file} stellar astrophysics class.
    use Memory_Management
    use Atomic_Data
    implicit none
    type     (stellarAstrophysicsFile)                :: self
    character(len=*                  ), intent(in   ) :: fileName
    !# <constructorAssign variables="fileName"/>

    ! Allocate array to store number of entries in file for yield of each element.
    call allocateArray(self%countYieldElement,[Atomic_Data_Atoms_Count()])
    call allocateArray(self%atomIndexMap     ,[Atomic_Data_Atoms_Count()])
    self%interpolationResetMassInitial=.true.
    self%interpolationResetLifetime   =.true.
    self%interpolationResetMassEjected=.true.
    self%interpolationResetMassYield  =.true.
    self%readDone                     =.false.
    return
  end function fileConstructorInternal
  
  subroutine fileRead(self)
    !% Read stellar astrophysics data. This is not done during object construction since it can be slow---we only perform the read if the data is actually needed.
    use Memory_Management
    use FoX_DOM
    use Galacticus_Error
    use ISO_Varying_String
    use IO_XML
    use Atomic_Data
    implicit none
    class           (stellarAstrophysicsFile), intent(inout) :: self
    type            (node                   ), pointer       :: doc              , datum                   , &
         &                                                      star
    type            (nodeList               ), pointer       :: propertyList     , starList
    integer                                                  :: countMassEjected , countYieldElementMaximum, &
         &                                                      fileFormatVersion, iElement                , &
         &                                                      iStar            , ioErr                   , &
         &                                                      countLifetime    , mapToIndex              , &
         &                                                      countYieldMetals
    double precision                                         :: massInitial      , metallicity
    logical                                                  :: starHasElements

    if (self%readDone) return
    !$omp critical (FoX_DOM_Access)
    ! Open the XML file containing stellar properties.
    doc => parseFile(char(self%fileName),iostat=ioErr)
    if (ioErr /= 0) call Galacticus_Error_Report('Unable to parse stellar properties file'//{introspection:location})
    ! Check the file format version of the file.
    datum => XML_Get_First_Element_By_Tag_Name(doc,"fileFormat")
    call extractDataContent(datum,fileFormatVersion)
    if (fileFormatVersion /= fileFormatVersionCurrent) call Galacticus_Error_Report('file format version is out of date'//{introspection:location})
    ! Get a list of all stars.
    starList => getElementsByTagname(doc,"star")
    ! Count up number of stars with given properties.
    countLifetime         =0
    countMassEjected      =0
    countYieldMetals      =0
    self%countYieldElement=0
    do iStar=0,getLength(starList)-1
       star         => item(starList,iStar)
       propertyList => getElementsByTagname(star,"initialMass"    )
       if (getLength(propertyList) /= 1) call Galacticus_Error_Report('star must have precisely one initial mass'//{introspection:location})
       propertyList => getElementsByTagname(star,"metallicity")
       if (getLength(propertyList) /= 1) call Galacticus_Error_Report('star must have precisely one metallicity' //{introspection:location})
       propertyList => getElementsByTagname(star,"lifetime"       )
       if (getLength(propertyList) == 1) countLifetime=countLifetime      +1
       if (getLength(propertyList) >  1) call Galacticus_Error_Report('star has multiple lifetimes'              //{introspection:location})
       propertyList => getElementsByTagname(star,"ejectedMass"    )
       if (getLength(propertyList) == 1) countMassEjected=countMassEjected+1
       if (getLength(propertyList) >  1) call Galacticus_Error_Report('star has multiple ejected masses'         //{introspection:location})
       propertyList => getElementsByTagname(star,"metalYieldMass")
       if (getLength(propertyList) == 1) countYieldMetals=countYieldMetals+1
       if (getLength(propertyList) >  1) call Galacticus_Error_Report('star has multiple metal yield masses'     //{introspection:location})
       do iElement=1,size(self%countYieldElement)
          propertyList => getElementsByTagname(star,"elementYieldMass"//trim(Atomic_Short_Label(iElement)))
          if (getLength(propertyList) == 1) self%countYieldElement(iElement)=self%countYieldElement(iElement)+1
          if (getLength(propertyList) >  1) call Galacticus_Error_Report('star has multiple element yield masses'//{introspection:location})
       end do
    end do    
    ! Find number of elements for which some yield data is available.
    self%countElement       =count (self%countYieldElement > 0)
    countYieldElementMaximum=maxval(self%countYieldElement    )
    ! Create mapping of atomic index to our array space.
    mapToIndex=0
    do iElement=1,size(self%countYieldElement)
       if (self%countYieldElement(iElement) > 0) then
          mapToIndex=mapToIndex+1
          self%atomIndexMap(iElement)=mapToIndex
       else
          self%atomIndexMap(iElement)=-1
       end if
    end do
    ! Allocate arrays to store stellar properties.
    call allocateArray(self%lifetimeLifetime          ,[countLifetime                             ])
    call allocateArray(self%lifetimeMass              ,[countLifetime                             ])
    call allocateArray(self%lifetimeMetallicity       ,[countLifetime                             ])
    call allocateArray(self%massEjectedMassEjected    ,[countMassEjected                          ])
    call allocateArray(self%massEjectedMass           ,[countMassEjected                          ])
    call allocateArray(self%massEjectedMetallicity    ,[countMassEjected                          ])
    call allocateArray(self%yieldMetals               ,[countYieldMetals                          ])
    call allocateArray(self%yieldMetalsMass           ,[countYieldMetals                          ])
    call allocateArray(self%yieldMetalsMetallicity    ,[countYieldMetals                          ])
    call allocateArray(self%yieldElement              ,[countYieldElementMaximum,self%countElement])
    call allocateArray(self%yieldElementMass          ,[countYieldElementMaximum,self%countElement])
    call allocateArray(self%yieldElementMetallicity   ,[countYieldElementMaximum,self%countElement])
    ! Loop over stars to process their properties.
    countLifetime         =0
    countMassEjected      =0
    countYieldMetals      =0
    self%countYieldElement=0
    do iStar=0,getLength(starList)-1
       star         => item(starList,iStar)
       propertyList => getElementsByTagname(star,"initialMass")
       datum        => item(propertyList,0)
       call extractDataContent(datum,massInitial)
       propertyList => getElementsByTagname(star,"metallicity")
       datum        => item(propertyList,0)
       call extractDataContent(datum,metallicity)
       ! Process stellar lifetimes.
       propertyList => getElementsByTagname(star,"lifetime")
       if (getLength(propertyList) == 1) then
          datum => item(propertyList,0)
          countLifetime=countLifetime+1
          call extractDataContent(datum,self%lifetimeLifetime      (countLifetime   ))
          self%lifetimeMass       (countLifetime)=massInitial
          self%lifetimeMetallicity(countLifetime)=metallicity
       end if
       ! Process ejected masses.
       propertyList => getElementsByTagname(star,"ejectedMass")
       if (getLength(propertyList) == 1) then
          datum => item(propertyList,0)
          countMassEjected=countMassEjected+1
          call extractDataContent(datum,self%massEjectedMassEjected(countMassEjected))
          self%massEjectedMass       (countMassEjected)=massInitial
          self%massEjectedMetallicity(countMassEjected)=metallicity
       end if
       ! Process metal yields.
       propertyList => getElementsByTagname(star,"metalYieldMass")
       if (getLength(propertyList) == 1) then
          datum => item(propertyList,0)
          countYieldMetals=countYieldMetals+1
          call extractDataContent(datum,self%yieldMetals           (countYieldMetals))
          self%yieldMetalsMass       (countYieldMetals)=massInitial
          self%yieldMetalsMetallicity(countYieldMetals)=metallicity
       end if
       ! Process element yields.
       starHasElements=.false.
       do iElement=1,size(self%countYieldElement)
          propertyList => getElementsByTagname(star,"elementYieldMass"//trim(Atomic_Short_Label(iElement)))
          if (getLength(propertyList) == 1) then
             starHasElements=.true.
             datum => item(propertyList,0)
             self%countYieldElement(iElement)=self%countYieldElement(iElement)+1
             call extractDataContent(datum,self%yieldElement(self%countYieldElement(iElement),self%atomIndexMap(iElement)))
             self%yieldElementMass       (self%countYieldElement(iElement),self%atomIndexMap(iElement))=massInitial
             self%yieldElementMetallicity(self%countYieldElement(iElement),self%atomIndexMap(iElement))=metallicity
          end if
       end do
       ! Set any elements that were not included for this star to zero.
       if (starHasElements) then
          do iElement=1,size(self%countYieldElement)
             if (self%countYieldElement(iElement) < maxval(self%countYieldElement) .and. self%atomIndexMap(iElement) > 0) then
                self%countYieldElement(iElement)=self%countYieldElement(iElement)+1
                self%yieldElementMass       (self%countYieldElement(iElement),self%atomIndexMap(iElement))=massInitial
                self%yieldElementMetallicity(self%countYieldElement(iElement),self%atomIndexMap(iElement))=metallicity
                self%yieldElement           (self%countYieldElement(iElement),self%atomIndexMap(iElement))=0.0d0
             end if
          end do
       end if
    end do
    ! Destroy the document.
    call destroy(doc)
    !$omp end critical (FoX_DOM_Access)
    self%readDone=.true. 
    return
  end subroutine fileRead
  
  double precision function fileMassInitial(self,lifetime,metallicity)
    !% Return the initial mass of a star of given {\normalfont \ttfamily lifetime} and {\normalfont \ttfamily metallicity}.
    implicit none
    class           (stellarAstrophysicsFile), intent(inout) :: self
    double precision                         , intent(in   ) :: lifetime, metallicity

    call self%read()
    fileMassInitial=Interpolate_2D_Irregular(                                                            &
         &                                                       self%lifetimeLifetime                 , &
         &                                                       self%lifetimeMetallicity              , &
         &                                                       self%lifetimeMass                     , &
         &                                                            lifetime                         , &
         &                                                            metallicity                      , &
         &                                                       self%interpolationWorkspaceMassInitial, &
         &                                   reset              =self%interpolationResetMassInitial    , &
         &                                   numberComputePoints=     3                                  &
         &                                  )
    return
  end function fileMassInitial

  double precision function fileLifetime(self,massInitial,metallicity)
    !% Return the lifetime of a star (in Gyr) given an {\normalfont \ttfamily massInitial} and {\normalfont \ttfamily metallicity}.
    implicit none
    class           (stellarAstrophysicsFile), intent(inout) :: self
    double precision                         , intent(in   ) :: massInitial, metallicity

    call self%read()
    fileLifetime=Interpolate_2D_Irregular(                                           &
         &                                      self%lifetimeMass                  , &
         &                                      self%lifetimeMetallicity           , &
         &                                      self%lifetimeLifetime              , &
         &                                           massInitial                   , &
         &                                           metallicity                   , &
         &                                      self%interpolationWorkspaceLifetime, &
         &                                reset=self%interpolationResetLifetime      &
         &                               )
    return
  end function fileLifetime

  double precision function fileMassEjected(self,massInitial,metallicity)
    !% Return the mass ejected during the lifetime of a star of given {\normalfont \ttfamily massInitial} and {\normalfont \ttfamily metallicity}.
    implicit none
    class           (stellarAstrophysicsFile), intent(inout) :: self
    double precision                         , intent(in   ) :: massInitial, metallicity

    call self%read()
    fileMassEjected=max(                                                                       &
         &              Interpolate_2D_Irregular(                                              &
         &                                             self%massEjectedMass                  , &
         &                                             self%massEjectedMetallicity           , &
         &                                             self%massEjectedMassEjected           , &
         &                                                  massInitial                      , &
         &                                                  metallicity                      , &
         &                                             self%interpolationWorkspaceMassEjected, &
         &                                       reset=self%interpolationResetMassEjected      &
         &                                      )                                            , &
         &               0.0d0                                                                 &
         &             )
    return
  end function fileMassEjected

  double precision function fileMassYield(self,massInitial,metallicity,atomIndex)
    !% Return the mass of metals yielded by a star of given {\normalfont \ttfamily massInitial} and {\normalfont \ttfamily metallicity}.
    implicit none
    class           (stellarAstrophysicsFile), intent(inout)           :: self
    double precision                         , intent(in   )           :: massInitial , metallicity
    integer                                  , intent(in   ), optional :: atomIndex
    integer                                                            :: elementIndex

    call self%read()
    if (present(atomIndex)) then
       ! Compute the element mass yield.
       elementIndex =self%atomIndexMap(atomIndex)
       fileMassYield=max(                                                                                                                       &
            &            Interpolate_2D_Irregular(                                                                                              &
            &                                           self%yieldElementMass               (1:self%countYieldElement(atomIndex),elementIndex), &
            &                                           self%yieldElementMetallicity        (1:self%countYieldElement(atomIndex),elementIndex), &
            &                                           self%yieldElement                   (1:self%countYieldElement(atomIndex),elementIndex), &
            &                                                massInitial                                                                      , &
            &                                                metallicity                                                                      , &
            &                                           self%interpolationWorkspaceMassYield                                                  , &
            &                                     reset=self%interpolationResetMassYield                                                        &
            &                                    )                                                                                            , &
            &            0.0d0                                                                                                                  &
            &           )
    else
       ! Compute the metal mass yield.
       fileMassYield=max(                                                                     &
            &            Interpolate_2D_Irregular(                                            &
            &                                           self%yieldMetalsMass                , &
            &                                           self%yieldMetalsMetallicity         , &
            &                                           self%yieldMetals                    , &
            &                                                massInitial                    , &
            &                                                metallicity                    , &
            &                                           self%interpolationWorkspaceMassYield, &
            &                                     reset=self%interpolationResetMassYield      &
            &                                    )                                          , &
            &             0.0d0                                                               &
            &            )
    end if
    return
  end function fileMassYield
