!! Copyright 2009, 2010, 2011, 2012, 2013, 2014, 2015, 2016, 2017
!!    Andrew Benson <abenson@carnegiescience.edu>
!!
!! This file is part of Galacticus.
!!
!!    Galacticus is free software: you can redistribute it and/or modify
!!    it under the terms of the GNU General Public License as published by
!!    the Free Software Foundation, either version 3 of the License, or
!!    (at your option) any later version.
!!
!!    Galacticus is distributed in the hope that it will be useful,
!!    but WITHOUT ANY WARRANTY; without even the implied warranty of
!!    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!    GNU General Public License for more details.
!!
!!    You should have received a copy of the GNU General Public License
!!    along with Galacticus.  If not, see <http://www.gnu.org/licenses/>.

  !% An implementation of accretion from the \gls{igm} onto halos using simple truncation to
  !% mimic the effects of reionization.

  use Radiation_Structure

  !# <accretionHalo name="accretionHaloSimple">
  !#  <description>Accretion onto halos using simple truncation to mimic the effects of reionization.</description>
  !# </accretionHalo>

  type, extends(accretionHaloClass) :: accretionHaloSimple
     !% A halo accretion class using simple truncation to mimic the effects of reionization.
     private
     double precision                     :: reionizationSuppressionTime, reionizationSuppressionVelocity
     logical                              :: negativeAccretionAllowed   , accreteNewGrowthOnly
     type            (radiationStructure) :: radiation
   contains
     !@ <objectMethods>
     !@   <object>accretionHaloSimple</object>
     !@   <objectMethod>
     !@     <method>failedFraction</method>
     !@     <type>double precision</type>
     !@     <arguments>\textcolor{red}{\textless type(treeNode)\textgreater} *node\arginout</arguments>
     !@     <description>Returns the fraction of potential accretion onto a halo from the \gls{igm} which fails.</description>
     !@   </objectMethod>
     !@   <objectMethod>
     !@     <method>velocityScale</method>
     !@     <type>double precision</type>
     !@     <arguments>\textcolor{red}{\textless type(treeNode)\textgreater} *node\arginout</arguments>
     !@     <description>Returns the velocity scale to use for {\normalfont \ttfamily node}.</description>
     !@   </objectMethod>
     !@ </objectMethods>
     procedure :: branchHasBaryons       => simpleBranchHasBaryons
     procedure :: accretionRate          => simpleAccretionRate
     procedure :: accretedMass           => simpleAccretedMass
     procedure :: failedAccretionRate    => simpleFailedAccretionRate
     procedure :: failedAccretedMass     => simpleFailedAccretedMass
     procedure :: accretionRateMetals    => simpleAccretionRateMetals
     procedure :: accretedMassMetals     => simpleAccretedMassMetals
     procedure :: accretionRateChemicals => simpleAccretionRateChemicals
     procedure :: accretedMassChemicals  => simpleAccretedMassChemicals
     procedure :: velocityScale          => simpleVelocityScale
     procedure :: failedFraction         => simpleFailedFraction
  end type accretionHaloSimple

  interface accretionHaloSimple
     !% Constructors for the {\normalfont \ttfamily simple} halo accretion class.
     module procedure simpleConstructor
     module procedure simpleDefaultConstructor
  end interface accretionHaloSimple

  ! Parameters controlling when accretion is suppressed.
  double precision                     :: simpleReionizationSuppressionRedshift  , simpleReionizationSuppressionTime, &
       &                                  simpleReionizationSuppressionVelocity

  ! Options controlling accretion.
  logical                              :: simpleNegativeAccretionAllowed
  logical                              :: simpleAccreteNewGrowthOnly

  ! Index of Solar abundance pattern.
  integer                              :: simpleAbundanceIndexSolar

  ! Internal record of the number of chemicals being tracked.
  integer                              :: simpleChemicalsCount

  ! Initialization state.
  logical                              :: simpleInitialized=.false., simpleDefaultInitialized=.false.

contains

  function simpleDefaultConstructor()
    !% Default constructor for the {\normalfont \ttfamily simple} halo accretion class.
    use Intergalactic_Medium_State
    use Cosmology_Functions
    use Galacticus_Error
    use Input_Parameters
    implicit none
    type            (accretionHaloSimple          ), target  :: simpleDefaultConstructor
    class           (cosmologyFunctionsClass      ), pointer :: cosmologyFunctionsDefault
    class           (intergalacticMediumStateClass), pointer :: intergalacticMediumState_ 
    double precision                                         :: reionizationSuppressionOpticalDepth

    ! Get default parameters.
    if (.not.simpleDefaultInitialized) then
       !$omp critical(accretionHaloSimpleDefaultInitialize)
       if (.not.simpleDefaultInitialized) then
          if (globalParameters%isPresent("simpleReionizationSuppressionOpticalDepth")) then
             if (globalParameters%isPresent("simpleReionizationSuppressionRedshift")) call Galacticus_Error_Report("simpleDefaultConstructor","only one of [simpleReionizationSuppressionOpticalDepth] and [simpleReionizationSuppressionRedshift] should be specified")
             !# <inputParameter>
             !#   <name>reionizationSuppressionOpticalDepth</name>
             !#   <cardinality>1</cardinality>
             !#   <description>The optical depth to electron scattering below which baryonic accretion is suppressed.</description>
             !#   <source>globalParameters</source>
             !#   <type>real</type>
             !# </inputParameter>
             intergalacticMediumState_        => intergalacticMediumState()
             simpleReionizationSuppressionTime=intergalacticMediumState_%electronScatteringTime(reionizationSuppressionOpticalDepth&
                  &,assumeFullyIonized=.true.)
          else
             !# <inputParameter>
             !#   <name>reionizationSuppressionRedshift</name>
             !#   <cardinality>1</cardinality>
             !#   <defaultSource>(\citealt{hinshaw_nine-year_2012}; CMB$+H_0+$BAO)</defaultSource>
             !#   <defaultValue>9.97d0</defaultValue>
             !#   <description>The redshift below which baryonic accretion is suppressed.</description>
             !#   <source>globalParameters</source>
             !#   <type>real</type>
             !#   <variable>simpleReionizationSuppressionRedshift</variable>
             !# </inputParameter>
             cosmologyFunctionsDefault        => cosmologyFunctions()
             simpleReionizationSuppressionTime=cosmologyFunctionsDefault%cosmicTime(cosmologyFunctionsDefault%expansionFactorFromRedshift(simpleReionizationSuppressionRedshift))
          end if
          !# <inputParameter>
          !#   <name>reionizationSuppressionVelocity</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>35.0d0</defaultValue>
          !#   <description>The velocity scale below which baryonic accretion is suppressed.</description>
          !#   <source>globalParameters</source>
          !#   <type>real</type>
          !#   <variable>simpleReionizationSuppressionVelocity</variable>
          !# </inputParameter>
          !# <inputParameter>
          !#   <name>accretionHalosSimpleNegativeAccretionAllowed</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.true.</defaultValue>
          !#   <description>Specifies whether negative accretion (mass loss) is allowed in the simple halo accretion model.</description>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !#   <variable>simpleNegativeAccretionAllowed</variable>
          !# </inputParameter>
          !# <inputParameter>
          !#   <name>accretionHalosSimpleAccreteNewGrowthOnly</name>
          !#   <cardinality>1</cardinality>
          !#   <defaultValue>.false.</defaultValue>
          !#   <description>Specifies whether accretion from the \gls{igm} is allowed only when a halo is growing past its previous greatest mass.</description>
          !#   <source>globalParameters</source>
          !#   <type>boolean</type>
          !#   <variable>simpleAccreteNewGrowthOnly</variable>
          !# </inputParameter>
          ! Record that class is now initialized.
          simpleDefaultInitialized=.true.
       end if
       !$omp end critical(accretionHaloSimpleDefaultInitialize)
    end if
    simpleDefaultConstructor=simpleConstructor(simpleReionizationSuppressionTime,simpleReionizationSuppressionVelocity,simpleNegativeAccretionAllowed,simpleAccreteNewGrowthOnly)
    return
  end function simpleDefaultConstructor
       
  function simpleConstructor(reionizationSuppressionTime,reionizationSuppressionVelocity,negativeAccretionAllowed,accreteNewGrowthOnly)
    !% Default constructor for the {\normalfont \ttfamily simple} halo accretion class.
    use Input_Parameters
    use Galacticus_Nodes
    use Galacticus_Error
    use Atomic_Data
    use Chemical_Abundances_Structure
    implicit none
    type            (accretionHaloSimple), target        :: simpleConstructor
    double precision                     , intent(in   ) :: reionizationSuppressionTime, reionizationSuppressionVelocity
    logical                              , intent(in   ) :: negativeAccretionAllowed   , accreteNewGrowthOnly

    simpleConstructor%reionizationSuppressionTime    =reionizationSuppressionTime
    simpleConstructor%reionizationSuppressionVelocity=reionizationSuppressionVelocity
    simpleConstructor%negativeAccretionAllowed       =negativeAccretionAllowed
    simpleConstructor%accreteNewGrowthOnly           =accreteNewGrowthOnly
    call simpleConstructor%radiation%define([radiationTypeCMB])
    ! Check that required properties have required attributes.       
    if     (                                                                                                      &
         &   accreteNewGrowthOnly                                                                                 &
         &  .and.                                                                                                 &
         &   .not.defaultBasicComponent%massMaximumIsGettable()                                                   &
         & ) call Galacticus_Error_Report                                                                         &
         &   (                                                                                                    &
         &    'simpleConstructor'                                                                               , &
         &    'accreteNewGrowthOnly=true requires that the "massMaximum" '//                                      &
         &    'property of the basic component be gettable.'              //                                      &
         &    Galacticus_Component_List(                                                                          &
         &                              'basic'                                                                 , &
         &                               defaultBasicComponent%massMaximumAttributeMatch(requireGettable=.true.)  &
         &                             )                                                                          &
         &   )
    ! Perform class global initialization.
    if (.not.simpleInitialized) then
       !$omp critical(accretionHaloSimpleInitialize)
       if (.not.simpleInitialized) then
          simpleAbundanceIndexSolar=Abundance_Pattern_Lookup(abundanceName="solar")
          ! Get a count of the number of chemicals being tracked.
          simpleChemicalsCount     =Chemicals_Property_Count(                     )
          ! Record that initialization is completed.
          simpleInitialized=.true.
       end if
       !$omp end critical(accretionHaloSimpleInitialize)
    end if
    return
  end function simpleConstructor

  logical function simpleBranchHasBaryons(self,node)
    !% Returns true if this branch can accrete any baryons.
    use Galacticus_Nodes
    implicit none
    class(accretionHaloSimple), intent(inout)          :: self
    type (treeNode           ), intent(inout), target  :: node
    type (treeNode           )               , pointer :: branchNode

    simpleBranchHasBaryons=.false.
    branchNode => node
    do while (associated(branchNode))
       if (self%failedFraction(branchNode) < 1.0d0) then
          simpleBranchHasBaryons=.true.
          exit
       end if
       branchNode => branchNode%walkBranch(node)
    end do
    return
  end function simpleBranchHasBaryons

  double precision function simpleAccretionRate(self,node,accretionMode)
    !% Computes the baryonic accretion rate onto {\normalfont \ttfamily node}.
    use Galacticus_Nodes
    use Cosmology_Parameters
    use Accretion_Halo_Totals
    implicit none
    class           (accretionHaloSimple     ), intent(inout) :: self
    type            (treeNode                ), intent(inout) :: node
    integer                                   , intent(in   ) :: accretionMode
    class           (nodeComponentBasic      ), pointer       :: basic
    class           (nodeComponentHotHalo    ), pointer       :: hotHalo
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (accretionHaloTotalClass ), pointer       :: accretionHaloTotal_
    double precision                                          :: growthRate             , unaccretedMass, &
         &                                                       failedFraction

    simpleAccretionRate=0.0d0
    if (accretionMode      == accretionModeCold) return
    if (node%isSatellite()                     ) return
    ! Get the failed accretion fraction.
    failedFraction=self%failedFraction(node)
    ! Get the default cosmology.
    cosmologyParameters_ => cosmologyParameters()
    accretionHaloTotal_  => accretionHaloTotal ()
    basic                => node%basic         ()
    hotHalo              => node%hotHalo       ()
    simpleAccretionRate=(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretionRate(node)*(1.0d0-failedFraction)
    ! Test for negative accretion.
    if (.not.self%negativeAccretionAllowed.and.accretionHaloTotal_%accretionRate(node) < 0.0d0) then
       ! Accretion rate is negative, and not allowed. Return zero accretion rate.
       simpleAccretionRate=0.0d0
    else
       ! Return the standard accretion rate.
       unaccretedMass=hotHalo%unaccretedMass()
       growthRate=accretionHaloTotal_%accretionRate(node)/accretionHaloTotal_%accretedMass(node)
       simpleAccretionRate=simpleAccretionRate+unaccretedMass*growthRate*(1.0d0-failedFraction)
    end if
    ! If accretion is allowed only on new growth, check for new growth and shut off accretion if growth is not new.
    if (self%accreteNewGrowthOnly) then
       if (accretionHaloTotal_%accretedMass(node) < basic%massMaximum()) simpleAccretionRate=0.0d0
    end if
    return
  end function simpleAccretionRate

  double precision function simpleAccretedMass(self,node,accretionMode)
    !% Computes the mass of baryons accreted into {\normalfont \ttfamily node}.
    use Galacticus_Nodes
    use Cosmology_Parameters
    use Accretion_Halo_Totals
    implicit none
    class           (accretionHaloSimple     ), intent(inout) :: self
    type            (treeNode                ), intent(inout) :: node
    integer                                   , intent(in   ) :: accretionMode
    class           (nodeComponentBasic      ), pointer       :: basic
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (accretionHaloTotalClass ), pointer       :: accretionHaloTotal_
    double precision                                          :: failedFraction

    simpleAccretedMass=0.0d0
    if (accretionMode      == accretionModeCold) return
    if (node%isSatellite()                     ) return
    ! Get the failed accretion fraction.
    failedFraction=self%failedFraction(node)
    basic   => node%basic     ()
    ! Get the default cosmology.
    cosmologyParameters_ => cosmologyParameters()
    accretionHaloTotal_  => accretionHaloTotal ()
    simpleAccretedMass=(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretedMass(node)*(1.0d0-failedFraction)
    return
  end function simpleAccretedMass

  double precision function simpleFailedAccretionRate(self,node,accretionMode)
    !% Computes the baryonic accretion rate onto {\normalfont \ttfamily node}.
    use Galacticus_Nodes
    use Cosmology_Parameters
    use Dark_Matter_Halo_Scales
    use Accretion_Halo_Totals
    implicit none
    class           (accretionHaloSimple     ), intent(inout) :: self
    type            (treeNode                ), intent(inout) :: node
    integer                                   , intent(in   ) :: accretionMode
    class           (nodeComponentBasic      ), pointer       :: basic
    class           (nodeComponentHotHalo    ), pointer       :: hotHalo
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (accretionHaloTotalClass ), pointer       :: accretionHaloTotal_
    double precision                                          :: growthRate          , unaccretedMass, &
         &                                                       failedFraction

    simpleFailedAccretionRate=0.0d0
    if (accretionMode               == accretionModeCold) return
    if (node         %isSatellite()                     ) return
    ! Get the default cosmology.
    cosmologyParameters_ => cosmologyParameters      ()
    accretionHaloTotal_  => accretionHaloTotal       ()
    basic                => node               %basic()
    ! Get the failed fraction.
    failedFraction=self%failedFraction(node)
    hotHalo => node%hotHalo()
    ! Test for negative accretion.
    if (.not.self%negativeAccretionAllowed.and.accretionHaloTotal_%accretionRate(node) < 0.0d0) then
       simpleFailedAccretionRate=(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretionRate(node)
    else
       simpleFailedAccretionRate=(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretionRate(node)*failedFraction
       unaccretedMass=hotHalo%unaccretedMass()
       growthRate=accretionHaloTotal_%accretionRate(node)/accretionHaloTotal_%accretedMass(node)
       simpleFailedAccretionRate=simpleFailedAccretionRate-unaccretedMass*growthRate*(1.0d0-failedFraction)
    end if
    ! If accretion is allowed only on new growth, check for new growth and shut off accretion if growth is not new.
    if (self%accreteNewGrowthOnly) then
       if (accretionHaloTotal_%accretedMass(node) < basic%massMaximum()) simpleFailedAccretionRate=0.0d0
    end if
    return
  end function simpleFailedAccretionRate

  double precision function simpleFailedAccretedMass(self,node,accretionMode)
    !% Computes the mass of baryons accreted into {\normalfont \ttfamily node}.
    use Galacticus_Nodes
    use Cosmology_Parameters
    use Dark_Matter_Halo_Scales
    use Accretion_Halo_Totals
    implicit none
    class           (accretionHaloSimple     ), intent(inout) :: self
    type            (treeNode                ), intent(inout) :: node
    integer                                   , intent(in   ) :: accretionMode
    class           (nodeComponentBasic      ), pointer       :: basic
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (accretionHaloTotalClass ), pointer       :: accretionHaloTotal_
    double precision                                          :: failedFraction

    simpleFailedAccretedMass=0.0d0
    if (accretionMode      == accretionModeCold) return
    if (node%isSatellite()                     ) return
    ! Get the failed fraction.
    failedFraction=self%failedFraction(node)
    basic => node%basic()
    ! Get the default cosmology.
    cosmologyParameters_ => cosmologyParameters()
    accretionHaloTotal_     => accretionHaloTotal ()
    simpleFailedAccretedMass=(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretedMass(node)*failedFraction
    return
  end function simpleFailedAccretedMass
  
  function simpleAccretionRateMetals(self,node,accretionMode)
    !% Computes the rate of mass of abundance accretion (in $M_\odot/$Gyr) onto {\normalfont \ttfamily node} from the intergalactic medium.
    use Galacticus_Nodes
    implicit none
    type  (abundances         )                :: simpleAccretionRateMetals
    class (accretionHaloSimple), intent(inout) :: self
    type  (treeNode           ), intent(inout) :: node
    integer                    , intent(in   ) :: accretionMode
    !GCC$ attributes unused :: self, node, accretionMode
    
    ! Assume zero metallicity.
    simpleAccretionRateMetals=zeroAbundances
    return
  end function simpleAccretionRateMetals

  function simpleAccretedMassMetals(self,node,accretionMode)
    !% Computes the mass of abundances accreted (in $M_\odot$) onto {\normalfont \ttfamily node} from the intergalactic medium.
    use Galacticus_Nodes
    implicit none
    type   (abundances         )                :: simpleAccretedMassMetals
    class  (accretionHaloSimple), intent(inout) :: self
    type   (treeNode           ), intent(inout) :: node
    integer                     , intent(in   ) :: accretionMode
    !GCC$ attributes unused :: self, node, accretionMode

    ! Assume zero metallicity.
    simpleAccretedMassMetals=zeroAbundances
    return
  end function simpleAccretedMassMetals

  function simpleAccretionRateChemicals(self,node,accretionMode)
    !% Computes the rate of mass of chemicals accretion (in $M_\odot/$Gyr) onto {\normalfont \ttfamily node} from the intergalactic medium. Assumes a
    !% primordial mixture of hydrogen and helium and that accreted material is in collisional ionization equilibrium at the virial
    !% temperature.
    use Galacticus_Nodes
    use Chemical_Abundances_Structure
    implicit none
    type            (chemicalAbundances )                :: simpleAccretionRateChemicals
    class           (accretionHaloSimple), intent(inout) :: self
    type            (treeNode           ), intent(inout) :: node
    integer                              , intent(in   ) :: accretionMode
    double precision                                     :: massAccretionRate

    ! Ensure that chemicals are reset to zero.
    call simpleAccretionRateChemicals%reset()
    ! Return immediately if no chemicals are being tracked.
    if (simpleChemicalsCount == 0) return
    ! Get the total mass accretion rate onto the halo.
    massAccretionRate=simpleAccretionRate(self,node,accretionMode)
    ! Get the mass accretion rates.
    simpleAccretionRateChemicals=simpleChemicalMasses(self,node,massAccretionRate)
    return
  end function simpleAccretionRateChemicals

  function simpleAccretedMassChemicals(self,node,accretionMode)
    !% Computes the mass of chemicals accreted (in $M_\odot$) onto {\normalfont \ttfamily node} from the intergalactic medium.
    use Galacticus_Nodes
    use Chemical_Abundances_Structure
    implicit none
    type            (chemicalAbundances )                :: simpleAccretedMassChemicals
    class           (accretionHaloSimple), intent(inout) :: self
    type            (treeNode           ), intent(inout) :: node
    integer                              , intent(in   ) :: accretionMode
    double precision                                     :: massAccreted


    ! Ensure that chemicals are reset to zero.
    call simpleAccretedMassChemicals%reset()
    ! Return if no chemicals are being tracked.
    if (simpleChemicalsCount == 0) return
    ! Total mass of material accreted.
    massAccreted=simpleAccretedMass(self,node,accretionMode)
    ! Get the masses of chemicals accreted.
    simpleAccretedMassChemicals=simpleChemicalMasses(self,node,massAccreted)
    return
  end function simpleAccretedMassChemicals

  function simpleChemicalMasses(self,node,massAccreted)
    !% Compute the masses of chemicals accreted (in $M_\odot$) onto {\normalfont \ttfamily node} from the intergalactic medium.
    use Galacticus_Nodes
    use Cosmology_Parameters
    use Dark_Matter_Halo_Scales
    use Numerical_Constants_Astronomical
    use Chemical_States
    use Chemical_Abundances_Structure
    use Chemical_Reaction_Rates_Utilities
    use Accretion_Halo_Totals
    implicit none
    class           (accretionHaloSimple     ), intent(inout) :: self
    type            (chemicalAbundances      )                :: simpleChemicalMasses
    type            (treeNode                ), intent(inout) :: node
    double precision                          , intent(in   ) :: massAccreted
    class           (nodeComponentBasic      ), pointer       :: basic
    class           (cosmologyParametersClass), pointer       :: cosmologyParameters_
    class           (accretionHaloTotalClass )               , pointer :: accretionHaloTotal_
    class           (darkMatterHaloScaleClass), pointer       :: darkMatterHaloScale_
    class           (chemicalStateClass      ), pointer       :: chemicalState_
    type            (chemicalAbundances      ), save          :: chemicalDensities
    !$omp threadprivate(chemicalDensities)
    double precision                                          :: massToDensityConversion, numberDensityHydrogen, &
         &                                                       temperature

    ! Get required objects.
    cosmologyParameters_ => cosmologyParameters()
    accretionHaloTotal_  => accretionHaloTotal ()
    darkMatterHaloScale_ => darkMatterHaloScale()
    chemicalState_       => chemicalState      ()
    ! Compute coefficient in conversion of mass to density for this node.
    massToDensityConversion=Chemicals_Mass_To_Density_Conversion(darkMatterHaloScale_%virialRadius(node))/3.0d0
    ! Compute the temperature and density of accreting material, assuming accreted has is at the virial temperature and that the
    ! overdensity is one third of the mean overdensity of the halo.
    temperature          =darkMatterHaloScale_%virialTemperature(node)
    basic   => node%basic()
    numberDensityHydrogen=hydrogenByMassPrimordial*(cosmologyParameters_%OmegaBaryon()/cosmologyParameters_%OmegaMatter())*accretionHaloTotal_%accretedMass(node)*massToDensityConversion/atomicMassHydrogen
    ! Set the radiation field.
    call self%radiation%set(node)
    ! Get the chemical densities.
    call chemicalState_%chemicalDensities(chemicalDensities,numberDensityHydrogen,temperature,zeroAbundances,self%radiation)
    ! Convert from densities to masses.
    call chemicalDensities%numberToMass(simpleChemicalMasses)
    simpleChemicalMasses=simpleChemicalMasses*massAccreted*hydrogenByMassPrimordial/numberDensityHydrogen/atomicMassHydrogen
    return
  end function simpleChemicalMasses

  double precision function simpleVelocityScale(self,node)
    !% Returns the velocity scale to use for {\normalfont \ttfamily node}. Use the virial velocity.
    use Galacticus_Nodes
    use Dark_Matter_Halo_Scales
    implicit none
    class(accretionHaloSimple     ), intent(inout) :: self
    type (treeNode                ), intent(inout) :: node
    class(darkMatterHaloScaleClass), pointer       :: darkMatterHaloScale_
    !GCC$ attributes unused :: self

    darkMatterHaloScale_ => darkMatterHaloScale                (    )
    simpleVelocityScale  =  darkMatterHaloScale_%virialVelocity(node)
    return
  end function simpleVelocityScale

  double precision function simpleFailedFraction(self,node)
    !% Returns the fraction of potential accretion onto a halo from the \gls{igm} which fails.
    use Galacticus_Nodes
    implicit none
    class(accretionHaloSimple), intent(inout) :: self
    type (treeNode           ), intent(inout) :: node
    class(nodeComponentBasic ), pointer       :: basic

    basic => node%basic()
    if     (                                                                 &
         &  basic%time         (    ) > self%reionizationSuppressionTime     &
         &   .and.                                                           &
         &  self %velocityScale(node) < self%reionizationSuppressionVelocity &
         & ) then
       simpleFailedFraction=1.0d0
    else
       simpleFailedFraction=0.0d0
    end if
    return
  end function simpleFailedFraction
