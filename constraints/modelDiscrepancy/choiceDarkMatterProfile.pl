#!/usr/bin/env perl
my $galacticusPath;
if ( exists($ENV{"GALACTICUS_ROOT_V094"}) ) {
    $galacticusPath = $ENV{"GALACTICUS_ROOT_V094"};
    $galacticusPath .= "/" unless ( $galacticusPath =~ m/\/$/ );
} else {
    $galacticusPath = "./";
}
unshift(@INC, $galacticusPath."perl"); 
use strict;
use warnings;
use Clone qw(clone);
use PDL;
use PDL::NiceSlice;
use PDL::IO::HDF5;
require List::ExtraUtils;
require Galacticus::Constraints::Parameters;
require Galacticus::Constraints::DiscrepancySystematics;
require Galacticus::Launch::PBS;

# Run calculations to determine the model discrepancy arising from the choice
# of dark matter density profile.
# Andrew Benson (17-September-2014)

# Get arguments.
die("Usage: choiceDarkMatterProfile.pl <configFile> [options]")
    unless ( scalar(@ARGV) >= 1 );
my $configFile = $ARGV[0];
# Create a hash of named arguments.
my $iArg = -1;
my %arguments = 
    (
     make => "yes"
    );
while ( $iArg < $#ARGV ) {
    ++$iArg;
    if ( $ARGV[$iArg] =~ m/^\-\-(.*)/ ) {
	$arguments{$1} = $ARGV[$iArg+1];
	++$iArg;
    }
}

# Parse the Galacticus config file.
my $xml    = new XML::Simple;
my $config = $xml->XMLin($configFile, KeyAttr => 0);

# Validate the config file.
die("choiceDarkMatterProfile.pl: workDirectory must be specified in config file" )
    unless ( exists($config->{'likelihood'}->{'workDirectory' }) );
die("choiceDarkMatterProfile.pl: compilation must be specified in config file"   )
    unless ( exists($config->{'likelihood'}->{'compilation'   }) );
die("choiceDarkMatterProfile.pl: baseParameters must be specified in config file")
    unless ( exists($config->{'likelihood'}->{'baseParameters'}) );

# Determine the scratch and work directories.
my $workDirectory    = $config->{'likelihood'}->{'workDirectory'   };
my $scratchDirectory = $config->{'likelihood'}->{'workDirectory'   };
$scratchDirectory    = $config->{'likelihood'}->{'scratchDirectory'}
    if ( exists($config->{'likelihood'}->{'scratchDirectory'}) );

# Create the work and scratch directories.
system("mkdir -p ".$config->{'likelihood'}->{'workDirectory'});

# Determine base parameters to use.
my $baseParameters = exists($arguments{'baseParameters'}) ? $arguments{'baseParameters'} : $config->{'likelihood'}->{'baseParameters'};

# Ensure that Galacticus is built.
if ( $arguments{'make'} eq "yes" ) {
    system("make Galacticus.exe");
    die("choiceDarkMatterProfile.pl: failed to build Galacticus.exe")
	unless ( $? == 0 );
}

# Get a hash of the parameter values.
(my $constraintsRef, my $parameters) = 
    &Parameters::Compilation
    (
     $config->{'likelihood'}->{'compilation'},
     $baseParameters
    );
my @constraints = @{$constraintsRef};

# Switch off thread locking.
$parameters->{'treeEvolveThreadLock'}->{'value'} = "false";

# Initialize a stack for PBS models.
my @pbsStack;

# Specify models to run.
my @models = 
    (
     {
	 label      => "defaultDarkMatterProfile",
	 parameters =>
	     [
	      # Use the default concentration method.
	      {
		  name  => "darkMatterProfileMethod",
		  value => "NFW"
	      }
	     ]
     },
     {
	 label      => "alternativeDarkMatterProfile",
	 parameters => 
	     [
	      # Use an alternative dark matter profile method.
	      {
		  name  => "darkMatterProfileMethod",
		  value => "einasto"
	      },
	      {
		  name  => "treeNodeMethodDarkMatterProfile",
		  value => "scaleShape"
	      }
	     ]
     }
    );

# Iterate over models.
foreach my $model ( @models ) {
# Specify the output name.
    my $modelDirectory = $workDirectory."/modelDiscrepancy/choiceDarkMatterProfile/".$model->{'label'};
    system("mkdir -p ".$modelDirectory);
    my $galacticusFileName = $modelDirectory."/galacticus.hdf5";
    push(
	@{$model->{'parameters'}},
	{
	    name  => "galacticusOutputFileName",
	    value => $galacticusFileName
	}
	);
    my $newParameters = clone($parameters);
    $newParameters->{$_->{'name'}}->{'value'} = $_->{'value'}
        foreach ( @{$model->{'parameters'}} );
    # Adjust the number of trees to run if specified.
    if ( exists($arguments{'treesPerDecade'}) ) {
	# Must also specify that trees are to be built in this case.
	$newParameters->{'mergerTreeConstructMethod'    }->{'value'} = "build";
	$newParameters->{'mergerTreeBuildTreesPerDecade'}->{'value'} = $arguments{'treesPerDecade'};
    }
    # Run the model.
    unless ( -e $galacticusFileName ) {
	# Generate the parameter file.
	my $parameterFileName = $modelDirectory."/parameters.xml";
	&Parameters::Output($newParameters,$parameterFileName);
	# Create PBS job.
	my $command = "mpirun --bynode -np 1 Galacticus.exe ".$parameterFileName."\n";
	foreach my $constraint ( @constraints ) {
	    # Parse the definition file.
	    my $constraintDefinition = $xml->XMLin($constraint->{'definition'});
	    # Insert code to run the analysis code.
	    my $analysisCode = $constraintDefinition->{'analysis'};
	    $command .= $analysisCode." ".$galacticusFileName." --resultFile ".$modelDirectory."/".$constraintDefinition->{'label'}.".hdf5\n";
	}
	my %job =
	    (
	     launchFile => $modelDirectory."/launch.pbs",
	     label      => "choiceDarkMatterProfile".$model->{'label'},
	     logFile    => $modelDirectory."/launch.log",
	     command    => $command
	    );
	foreach ( 'ppn', 'walltime', 'memory' ) {
	    $job{$_} = $arguments{$_}
	    if ( exists($arguments{$_}) );
	}
	# Queue the calculation.
	push(
	    @pbsStack,
	    \%job
	    );   
    }
}
# Send jobs to PBS.
&PBS::SubmitJobs(\%arguments,@pbsStack)
    if ( scalar(@pbsStack) > 0 );
# Iterate over constraints.
foreach my $constraint ( @constraints ) {
    # Parse the definition file.
    my $constraintDefinition = $xml->XMLin($constraint->{'definition'});
    print "Computing discrepancy for constraint: ".$constraintDefinition->{'label'}."\n";    
    # Locate the model results.
    my $alternativeDarkMatterProfileResultFileName = 
	$workDirectory.
	"/modelDiscrepancy/choiceDarkMatterProfile/alternativeDarkMatterProfile/".
	$constraintDefinition->{'label'}.".hdf5";
    my $defaultDarkMatterProfileResultFileName     = 
	$workDirectory.
	"/modelDiscrepancy/choiceDarkMatterProfile/defaultDarkMatterProfile/"    .
	$constraintDefinition->{'label'}.".hdf5";
    # Read the results.
    my $alternativeDarkMatterProfileResult = new PDL::IO::HDF5($alternativeDarkMatterProfileResultFileName);
    my $defaultDarkMatterProfileResult     = new PDL::IO::HDF5($defaultDarkMatterProfileResultFileName   );
    # Extract the results.
    my $defaultX              = $defaultDarkMatterProfileResult    ->dataset('x'         )->get();
    my $defaultY              = $defaultDarkMatterProfileResult    ->dataset('y'         )->get();
    my $defaultCovariance     = $alternativeDarkMatterProfileResult->dataset('covariance')->get();
    my $alternativeY          = $alternativeDarkMatterProfileResult->dataset('y'         )->get();
    my $alternativeCovariance = $alternativeDarkMatterProfileResult->dataset('covariance')->get();
    # Apply any systematics models.
    my %systematicResults;
    foreach my $argument ( keys(%arguments) ) {
	if ( $argument =~ m/^systematic(.*)/ ) {
	    my $model = $1;
	    if 
		( 
		  exists($DiscrepancySystematics::models{$model}) 
		  && 
		  $arguments{$argument} eq "yes"
		  &&
		  exists($constraintDefinition->{$argument})
		  &&
		  $constraintDefinition->{$argument} eq "yes"
		) {
		%{$systematicResults{$model}} =
		    &{$DiscrepancySystematics::models{$model}}(
		    \%arguments           ,
		    $constraintDefinition ,
		    $defaultX             ,
		    $defaultY             ,
		    $defaultCovariance    ,
		    $alternativeY         ,
		    $alternativeCovariance
		);
	    }
	}
    }
    # Find the multiplicative discrepancy between these two models.
    (my $nonZero, my $zero)                      = which_both($defaultY > 0.0);
    my $modelDiscrepancyMultiplicative           = $alternativeY->copy();
    $modelDiscrepancyMultiplicative->($nonZero) /= $defaultY->($nonZero);
    $modelDiscrepancyMultiplicative->(   $zero) .= 1.0
	if ( nelem($zero) > 0 );
    # Compute the covariance.
    my $modelDiscrepancyCovarianceMultiplicative = 
	+outer($defaultY-$alternativeY,$defaultY-$alternativeY)
	*outer(      1.0/$alternativeY,      1.0/$alternativeY);
    # Output the model discrepancy to file.
    my $outputFile = 
	new PDL::IO::HDF5(
	    ">".
	    $workDirectory.
	    "/modelDiscrepancy/choiceDarkMatterProfile/discrepancy".
	    ucfirst($constraintDefinition->{'label'}).
	    ".hdf5"
	);
    $outputFile->dataset('multiplicativeCovariance')->set($modelDiscrepancyCovarianceMultiplicative);
    $outputFile->attrSet(
	description => "Model discrepancy for ".$constraintDefinition->{'name'}." due to choice of halo concentration-mass relation."
	);
    # Add results of systematics models.
    my $systematicGroup = $outputFile->group("systematicModels");
    foreach my $model ( keys(%systematicResults) ) {
	my $modelGroup = $systematicGroup->group($model);
	my %modelResults = %{$systematicResults{$model}};
	foreach my $parameter ( keys(%modelResults) ) {
	    $modelGroup->attrSet($parameter => $modelResults{$parameter});
	}
    }
}

exit;
